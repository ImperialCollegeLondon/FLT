/-
Copyright (c) 2025 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Andrew Yang, Matthew Jasper
-/
import FLT.Mathlib.Algebra.Algebra.Bilinear
import FLT.Mathlib.Algebra.Algebra.Pi
import FLT.Mathlib.Algebra.Module.Submodule.Basic
import FLT.Mathlib.NumberTheory.RamificationInertia.Basic
import FLT.Mathlib.Topology.Algebra.Module.Equiv
import FLT.Mathlib.Topology.Algebra.Module.ModuleTopology
import FLT.Mathlib.Topology.Algebra.UniformRing
import FLT.Mathlib.Topology.Algebra.Valued.ValuationTopology
import FLT.Mathlib.Topology.Algebra.Valued.WithVal
import FLT.Mathlib.RingTheory.TensorProduct.Basis
import FLT.Mathlib.RingTheory.Finiteness.Pi
import Mathlib.Algebra.Algebra.Subalgebra.Pi
import Mathlib.Algebra.BigOperators.Group.Finset.Basic
import Mathlib.Algebra.Group.Int.TypeTags
import Mathlib.Data.Int.WithZero
import Mathlib.Topology.Algebra.Algebra.Equiv
import Mathlib.Topology.Algebra.Module.ModuleTopology
import Mathlib.Topology.Algebra.Valued.NormedValued
import Mathlib.RingTheory.Valuation.RankOne
import Mathlib.RingTheory.Flat.Basic
import Mathlib.Topology.Algebra.Module.FiniteDimension
import FLT.DedekindDomain.AdicValuation
import FLT.DedekindDomain.IntegralClosure
import FLT.Hacks.RightActionInstances

/-!

# Base change of adic completions.

If `A` is a Dedekind domain with field of fractions `K`, if `L/K` is a finite extension
and if `B/A` is also finite then `L ‚äó_K K_v ‚âÖ ‚àè_{w|v} L_w` as `L`-algebras. Further this
map is continuous, `K_v`-linear and restricts to an isomorphism `B ‚äó_A ùìû_v ‚âÖ ‚àè_{w|v} ùìû_w`.

## Main definitions

* `IsDedekindDomain.HeightOneSpectrum.adicCompletionComapContinuousAlgEquiv` :
   `L ‚äó[K] K_v ‚âÉA[L] ‚àè_{w|v} L_w`

## Main theorems

* `IsDedekindDomain.HeightOneSpectrum.valued_adicCompletionComap A K L B v w pf` :
  If w|v are nonzero primes of B and A, and if x ‚àà K_v ‚äÜ L_w, then w(x)=v(x)^e
  where e is the global ramification index of w/v.

* `IsDedekindDomain.HeightOneSpectrum.tensorAdicCompletionComapAlgHom_bijective A K L B v` :
  The canonical map L ‚äó K·µ• ‚Üí ‚àè_{w|v} L_w is bijective.

## TODO

* Show that `B ‚äó_A ùìû_v ‚Üí L ‚äó_K K_v` is injective to finish the proof of `B ‚äó_A ùìû_v ‚âÖ ‚àè_{w|v} ùìû_w`.

-/

open scoped WithZero Valued

/-!

The general "AKLB" set-up. `K` is the field of fractions of the Dedekind domain `A`,
`L/K` is a finite separable extension, and `B` is the integral closure of `A` in `L`.

-/

variable (A K L B : Type*) [CommRing A] [CommRing B] [Algebra A B] [Field K] [Field L]
    [Algebra A K] [IsFractionRing A K] [Algebra B L] [IsDedekindDomain A]
    [Algebra K L] [Algebra A L] [IsScalarTower A B L] [IsScalarTower A K L]
    [IsIntegralClosure B A L] [FiniteDimensional K L]

/-!

Under these hypotheses and `[Algebra.IsSeparable K L]`, we can prove the following:

`[IsDomain B]`
`[Algebra.IsIntegral A B]`
`[Module.Finite A B]`
`[IsDedekindDomain B]`
`[IsFractionRing B L]`

However none of these facts are available to typeclass inference because they all use
variables such as `K` which are not mentioned in the statement so are unfindable by
Lean 4's typeclass system. We thus introduce them as variables when needed.

-/
example : IsDomain B := by
  have foo : Function.Injective (algebraMap B L) := IsIntegralClosure.algebraMap_injective' A
  have bar : IsDomain L := inferInstance
  exact Function.Injective.isDomain _ foo -- exact? failed

example : Algebra.IsIntegral A B := IsIntegralClosure.isIntegral_algebra A L
variable [Algebra.IsIntegral A B]

example [IsDomain B] [Algebra.IsSeparable K L] : IsDedekindDomain B :=
  IsIntegralClosure.isDedekindDomain A K L B
variable [IsDedekindDomain B]

example : IsFractionRing B L := IsIntegralClosure.isFractionRing_of_finite_extension A K L B
variable [IsFractionRing B L]

example [Algebra.IsSeparable K L] : Module.Finite A B :=
  have := IsIntegralClosure.isNoetherian A K L B
  Module.IsNoetherian.finite A B
-- variable [Module.Finite A B] -- we'll need this later

example : FaithfulSMul A B := FaithfulSMul.of_field_isFractionRing A B K L
-- variable [FaithfulSMul A B] -- we'll need this later

namespace IsDedekindDomain

namespace HeightOneSpectrum

variable (v : HeightOneSpectrum A)

/--
If we have an AKLB set-up, and `w` is a valuation on `L` extending `v` on `K`,
then `œÉ v w` is the ring homomorphism from (K with valuation v) to (L with valuation w).
More precisely, by (K with valuation v) we mean the
type synonym `WithVal (HeightOneSpectrum.valuation K v)`, which is `K` equipped with
the instance `Valued K Œì‚ÇÄ` coming from `v`. In particular this type synonym has
a canonical valuation, topology, and uniform addive group structure. It is shown
that `œÉ v w` is continuous.
-/
local notation "œÉ" => fun v w => algebraMap (WithVal (HeightOneSpectrum.valuation K v))
    (WithVal (HeightOneSpectrum.valuation L w))

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
lemma adicValued.continuous_algebraMap
    (v : HeightOneSpectrum A) (w : HeightOneSpectrum B) (hvw : w.comap A = v) :
    Continuous (œÉ v w) := by
  subst hvw
  refine continuous_of_continuousAt_zero (algebraMap _ _) ?hf
  delta ContinuousAt
  simp only [map_zero]
  rw [(@Valued.hasBasis_nhds_zero K _ _ _ (comap A w).adicValued).tendsto_iff
    (@Valued.hasBasis_nhds_zero L _ _ _ w.adicValued)]
  simp only [HeightOneSpectrum.adicValued_apply, Set.mem_setOf_eq, true_and, true_implies]
  rw [WithZero.unitsWithZeroEquiv.forall_congr_left, Multiplicative.forall]
  intro a
  rw [WithZero.unitsWithZeroEquiv.exists_congr_left, Multiplicative.exists]
  let m := Ideal.ramificationIdx (algebraMap A B) (comap A w).asIdeal w.asIdeal
  let e : ‚Ñ§ ‚âÉ ‚Ñ§·µê‚Å∞À£ := Multiplicative.ofAdd.trans OrderMonoidIso.unitsWithZero.symm.toEquiv
  have e_apply (a : ‚Ñ§) : e a = OrderMonoidIso.unitsWithZero.symm (Multiplicative.ofAdd a) := rfl
  have hm : m ‚â† 0 := by
    refine ramificationIdx_ne_zero A B ?_ w
    exact algebraMap_injective_of_field_isFractionRing A B K L
  refine ‚ü®a / m, fun x hx ‚Ü¶ ?_‚ü©
  erw [‚Üê valuation_comap A]
  calc
    (comap A w).valuation K x ^ m < e (a / ‚Üëm) ^ m := by gcongr; exacts [zero_le', hx]
  _ = e (m ‚Ä¢ (a / ‚Üëm)) := by
    dsimp [e]
    rfl
  _ ‚â§ e a := by
    simp only [nsmul_eq_mul, e_apply, Units.val_le_val, OrderIsoClass.map_le_map_iff]
    rw [mul_comm]
    exact Int.mul_le_of_le_ediv (by positivity) le_rfl

/-- If w of L divides v of K, `adicCompletionComapSemialgHom v w pf` is the canonical map
`K·µ• ‚Üí L_w` lying above `K ‚Üí L`. Here we actually use the type synonyms `WithVal K` and `WithVal L`.
-/
noncomputable def adicCompletionComapSemialgHom (v : HeightOneSpectrum A) (w : HeightOneSpectrum B)
    (hvw : w.comap A = v) :
    v.adicCompletion K ‚Üí‚Çõ‚Çê[œÉ v w] w.adicCompletion L :=
  UniformSpace.Completion.mapSemialgHom _ <|
  adicValued.continuous_algebraMap A K L B v w hvw

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
/-- The square with sides K ‚Üí K_v ‚Üí L_w and K ‚Üí L ‚Üí L_w commutes. -/
lemma adicCompletionComap_coe
    (v : HeightOneSpectrum A) (w : HeightOneSpectrum B) (hvw : w.comap A = v) (x : K) :
    adicCompletionComapSemialgHom A K L B v w hvw x = algebraMap K L x :=
  (adicCompletionComapSemialgHom A K L B v w hvw).commutes x

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
open WithZeroTopology in
/--
The local ramification index for the extension L_w/K_v is equal to the global ramification
index for the extension w/v. In other words, if x in K_v and i:K_v->L_w then w(i(x))=v(x)^e
where e is computed globally.
-/
lemma valued_adicCompletionComap
  (v : HeightOneSpectrum A) (w : HeightOneSpectrum B) (hvw : w.comap A = v) (x) :
    Valued.v (adicCompletionComapSemialgHom A K L B v w hvw x) = Valued.v x ^
      Ideal.ramificationIdx (algebraMap A B) (comap A w).asIdeal w.asIdeal := by
  revert x
  apply funext_iff.mp
  symm
  apply UniformSpace.Completion.ext
  ¬∑ exact Valued.continuous_valuation.pow _
  ¬∑ exact Valued.continuous_valuation.comp UniformSpace.Completion.continuous_extension
  intro a
  simp_rw [adicCompletionComap_coe, adicCompletion, Valued.valuedCompletion_apply,
    adicValued_apply']
  subst hvw
  rw [‚Üê valuation_comap A K L B w a]

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
/-- The canonical map K_v ‚Üí L_w sends ùìû_v to ùìû_w. -/
lemma adicCompletionComapSemialgHom.mapadicCompletionIntegers (v : HeightOneSpectrum A)
    (w : HeightOneSpectrum B) (hvw : w.comap A = v) :
    (adicCompletionComapSemialgHom A K L B v w hvw) '' (v.adicCompletionIntegers K) ‚â§
    w.adicCompletionIntegers L := by
  rintro y ‚ü®x, hx, rfl‚ü©
  rw [SetLike.mem_coe, mem_adicCompletionIntegers] at hx ‚ä¢
  rw [valued_adicCompletionComap A K L B v w hvw]
  rwa [pow_le_one_iff]
  exact ramificationIdx_ne_zero A B (algebraMap_injective_of_field_isFractionRing A B K L) w

/-- The K_v-algebra structure on L_w when w | v. -/
noncomputable
def comap_algebra {v : HeightOneSpectrum A} {w : HeightOneSpectrum B} (h : w.comap A = v) :
    Algebra (v.adicCompletion K) (w.adicCompletion L) :=
  (adicCompletionComapSemialgHom A K L B v w h).toAlgebra

/-- The canonical map `K_v ‚Üí ‚àè_{w|v} L_w` extending K ‚Üí L. -/
noncomputable def adicCompletionComapSemialgHom' (v : HeightOneSpectrum A) :
  v.adicCompletion K ‚Üí‚Çõ‚Çê[algebraMap K L] ‚àÄ w : v.Extension B, w.1.adicCompletion L :=
  Pi.semialgHom _ _ fun i ‚Ü¶ adicCompletionComapSemialgHom A K L B v i.1 i.2

/-- The K_v-algebra structure on ‚àè_{w|v} L_w. -/
noncomputable instance comap_pi_algebra (v : HeightOneSpectrum A) :
    Algebra (v.adicCompletion K) (Œ† (w : v.Extension B), w.1.adicCompletion L) :=
  (adicCompletionComapSemialgHom' A K L B v).toAlgebra

open scoped TensorProduct -- ‚äó notation for tensor product

/-- The canonical ring homomorphism `L ‚äó_K K_v ‚Üí ‚àè_{w|v} L_w` as an `L`-algebra map. -/
noncomputable def tensorAdicCompletionComapAlgHom (v : HeightOneSpectrum A) :
    L ‚äó[K] adicCompletion K v ‚Üí‚Çê[L] Œ† w : v.Extension B, w.1.adicCompletion L :=
  SemialgHom.baseChange_of_algebraMap (adicCompletionComapSemialgHom' A K L B v)

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
lemma tensorAdicCompletionComapAlgHom_tmul_apply (v : HeightOneSpectrum A) (x y w) :
  letI := comap_algebra A K L B w.prop
  tensorAdicCompletionComapAlgHom A K L B v (x ‚äó‚Çú y) w =
    (algebraMap _ (w.1.adicCompletion L) x) * (algebraMap _ (w.1.adicCompletion L) y) := by
  rfl

open scoped TensorProduct.RightActions in
/-- The canonical ring homomorphism `L ‚äó_K K_v ‚Üí ‚àè_{w|v} L_w` as an `K_v`-linear map. -/
noncomputable def tensorAdicCompletionComapLinearMap :
    letI := comap_pi_algebra A K L B v |>.toModule
    L ‚äó[K] adicCompletion K v ‚Üí‚Çó[adicCompletion K v]
    (w : Extension B v) ‚Üí adicCompletion L w.val :=
  SemialgHom.baseChangeRightOfAlgebraMap (adicCompletionComapSemialgHom' A K L B v)
    |>.toLinearMap

section ModuleTopology

open WithZeroMulInt Valued

-- Make (v.adicCompletion K) a normed field.
-- This exists for number fields in Mathlib, but not for general Dedekind Domains.
-- v.asIdeal.absNorm may be 0, so just use 2 as the base for the norm.
/-- The data of a rank 1 (‚Ñù-valued) valuation on K_v. -/
noncomputable local instance adicCompletion_RkOne :
    Valuation.RankOne (Valued.v : Valuation (adicCompletion K v) ‚Ñ§·µê‚Å∞) where
  hom := {
    toFun := toNNReal (by norm_num : (2 : NNReal) ‚â† 0)
    map_zero' := rfl
    map_one' := rfl
    map_mul' := MonoidWithZeroHom.map_mul (toNNReal (by norm_num))
  }
  strictMono' := toNNReal_strictMono (by norm_num)
  exists_val_nontrivial := by
    obtain ‚ü®x, hx1, hx2‚ü© := Submodule.exists_mem_ne_zero_of_ne_bot v.ne_bot
    use algebraMap A K x
    rw [valuedAdicCompletion_eq_valuation' v (algebraMap A K x)]
    constructor
    ¬∑ simpa only [ne_eq, map_eq_zero, FaithfulSMul.algebraMap_eq_zero_iff]
    ¬∑ apply ne_of_lt
      rwa [valuation_of_algebraMap, intValuation_lt_one_iff_mem]

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
/-- The map K_v ‚Üí L_w is continuous. -/
lemma adicCompletionComapSemialgHom_continuous
    (v : HeightOneSpectrum A) (w : HeightOneSpectrum B) (hvw : w.comap A = v) :
    Continuous (adicCompletionComapSemialgHom A K L B v w hvw) :=
  UniformSpace.Completion.continuous_extension (Œ≤ := (adicCompletion L w))

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
/-- The K_v-action on L_w is continuous. -/
lemma comap_algebra_continuousSmul (v : HeightOneSpectrum A) (w : HeightOneSpectrum B)
    (hvw : comap A w = v) :
    letI := comap_algebra A K L B hvw
    ContinuousSMul (adicCompletion K v) (adicCompletion L w) := by
  let inst_alg := comap_algebra A K L B hvw
  constructor
  have leftCts := adicCompletionComapSemialgHom_continuous A K L B v w hvw
  exact Continuous.mul (Continuous.fst' leftCts) continuous_snd

open scoped TensorProduct.RightActions in
omit [IsIntegralClosure B A L] in
/-- The canonical map `L ‚äó[K] K_v ‚Üí ‚àè_{w|v} L_w` is surjective. -/
lemma tensorAdicCompletionComapLinearMap_surjective (v : HeightOneSpectrum A) :
    Function.Surjective (tensorAdicCompletionComapLinearMap A K L B v) := by
  letI : Module (adicCompletion K v) ((w : Extension B v) ‚Üí adicCompletion L w.val) :=
    comap_pi_algebra A K L B v |>.toModule
  let _ (w : Extension B v) : SMul (adicCompletion K v) (adicCompletion L w.val) :=
    comap_algebra A K L B w.prop |>.toSMul
  let _ (w : Extension B v) : ContinuousSMul (adicCompletion K v) (adicCompletion L w.val) :=
    comap_algebra_continuousSmul A K L B v w.val w.prop
  let f' := tensorAdicCompletionComapLinearMap A K L B v
  let s : Submodule (adicCompletion K v) ((w : Extension B v) ‚Üí adicCompletion L w.val) :=
    LinearMap.range f'
  have hFinite : Module.Finite (adicCompletion K v) s := Module.Finite.range f'
  have isClosed : IsClosed s.carrier :=
    Submodule.closed_of_finiteDimensional (E := (w : Extension B v) ‚Üí adicCompletion L w.val) s
  rw [‚Üê LinearMap.range_eq_top, Submodule.eq_top_iff']
  simp_rw [‚Üê Submodule.mem_toAddSubmonoid, ‚Üê AddSubmonoid.mem_toSubsemigroup,
      ‚Üê AddSubsemigroup.mem_carrier]
  have denseL : DenseRange (algebraMap L ((w : Extension B v) ‚Üí adicCompletion L w.val)) := by
    have := Extension.finite A K L B v
    let _ := Fintype.ofFinite <| Extension B v
    exact denseRange_of_prodAlgebraMap _ Subtype.val_injective
  rw [‚Üê isClosed.closure_eq]
  apply Dense.mono _ denseL
  rintro _ ‚ü®l, rfl‚ü©
  use (l ‚äó‚Çú 1)
  ext w
  simp [tensorAdicCompletionComapLinearMap, f']

open scoped TensorProduct.RightActions in
omit [IsIntegralClosure B A L] in
/-- ‚àè_{w|v} L_w is a finite K_v-module. -/
theorem comap_pi_algebra_finite (v : HeightOneSpectrum A) :
    letI := comap_pi_algebra A K L B v |>.toModule
    Module.Finite (adicCompletion K v) (Œ† w : v.Extension B, w.1.adicCompletion L) :=
  letI := comap_pi_algebra A K L B v |>.toModule
  Module.Finite.of_surjective (tensorAdicCompletionComapLinearMap A K L B v)
    (tensorAdicCompletionComapLinearMap_surjective A K L B v)

omit [IsIntegralClosure B A L] in
/-- L_w is a finite K_v-module if w | v. -/
theorem comap_algebra_finite (v : HeightOneSpectrum A) (w : HeightOneSpectrum B)
    (hvw : comap A w = v) :
    letI := comap_algebra A K L B hvw
    Module.Finite (adicCompletion K v) (adicCompletion L w) :=
  have := comap_pi_algebra_finite A K L B v
  let _ (w : Extension B v) := comap_algebra A K L B w.prop |>.toModule
  Module.Finite.of_pi (fun (w : Extension B v) => w.1.adicCompletion L) ‚ü®w, hvw‚ü©

omit [IsIntegralClosure B A L] in
/-- L_w has the K_v-module topology. -/
lemma adicCompletionComap_isModuleTopology
    (v : HeightOneSpectrum A) (w : HeightOneSpectrum B) (hvw : w.comap A = v) :
    -- temporarily make L_w a K_v-algebra
    letI := comap_algebra A K L B hvw
    -- then claim that L_w has the module topology.
    IsModuleTopology (v.adicCompletion K) (w.adicCompletion L) := by
  let Kv := adicCompletion K v
  let Lw := adicCompletion L w
  let _ : Algebra Kv Lw := comap_algebra A K L B hvw
  have : ContinuousSMul Kv Lw := comap_algebra_continuousSmul A K L B v w hvw
  have : Module.Finite Kv Lw := comap_algebra_finite A K L B v w hvw
  let iso : ((Fin (Module.finrank Kv Lw)) ‚Üí Kv) ‚âÉL[Kv] Lw :=
    ContinuousLinearEquiv.ofFinrankEq (Module.finrank_fin_fun Kv)
  apply IsModuleTopology.iso iso

omit [IsIntegralClosure B A L] in
/-- ‚àè_{w|v} L_w has the K_v-module topology. -/
lemma prodAdicCompletionComap_isModuleTopology (v : HeightOneSpectrum A) :
    -- TODO: the `let _` in the statement below should not be required as it is an instance
    -- see mathlib PR #22488 for potential fix to this.
    -- Note that this one does not involve `adicCompletionIntegers` so the
    -- issue may not be to do with subtype vs. type implementation of
    -- `adicCompletionIntegers`.
    let _ := comap_pi_algebra A K L B v |>.toSMul
    -- the claim that L_w has the module topology.
    IsModuleTopology (v.adicCompletion K) (Œ† (w : v.Extension B), w.1.adicCompletion L) := by
  -- these are defs or lemmas so are required
  let _ (w : v.Extension B) := comap_algebra A K L B w.2 |>.toModule
  let _ (w : v.Extension B) := adicCompletionComap_isModuleTopology A K L B v w.1 w.2
  let _ := Extension.finite A K L B v
  exact IsModuleTopology.instPi

open scoped TensorProduct.RightActions in
omit [IsIntegralClosure B A L] in
/-- `tensorAdicCompletionComapLinearMap` is continuous, open and surjective.
  We later show that it's a homeomorphism. -/
lemma tensorAdicCompletionComapLinearMap_isOpenQuotientMap (v : HeightOneSpectrum A) :
    IsOpenQuotientMap (tensorAdicCompletionComapLinearMap A K L B v) := by
  have : T2Space (L ‚äó[K] adicCompletion K v) :=
    IsModuleTopology.t2Space' (K := (adicCompletion K v))
  let _ (w : Extension B v) : SMul (adicCompletion K v) (adicCompletion L w.val) :=
    comap_algebra A K L B w.prop |>.toSMul
  let _ (w : Extension B v) : ContinuousSMul (adicCompletion K v) (adicCompletion L w.val) :=
    comap_algebra_continuousSmul A K L B v w.val w.prop
  have := IsModuleTopology.topologicalAddGroup (adicCompletion K v) (L ‚äó[K] adicCompletion K v)
  have := comap_pi_algebra_finite A K L B v
  have hsurj := tensorAdicCompletionComapLinearMap_surjective A K L B v
  exact ‚ü®hsurj, LinearMap.continuous_of_finiteDimensional _,
    LinearMap.isOpenMap_of_finiteDimensional _ hsurj‚ü©

-- TODO : this local instance should not be required, see mathlib PR #22488 for potential fix
attribute [local instance 9999] SMulCommClass.of_commMonoid TensorProduct.isScalarTower_left
  IsScalarTower.right Algebra.toSMul

/-- The triangle A ‚Üí ùìû_v ‚Üí K_v commutes. -/
instance (R K : Type*) [CommRing R] [IsDedekindDomain R] [Field K]
    [Algebra R K] [IsFractionRing R K] (v : HeightOneSpectrum R) :
    IsScalarTower R (v.adicCompletionIntegers K) (v.adicCompletion K) :=
  ‚ü®fun x y z ‚Ü¶ by exact smul_mul_assoc x y.1 z‚ü©

/-- ùìû_v as an A-subalgebra of K_v. -/
noncomputable
def adicCompletionIntegersSubalgebra {R : Type*} (K : Type*) [CommRing R]
    [IsDedekindDomain R] [Field K] [Algebra R K] [IsFractionRing R K] (v : HeightOneSpectrum R) :
    Subalgebra R (HeightOneSpectrum.adicCompletion K v) where
  __ := HeightOneSpectrum.adicCompletionIntegers K v
  algebraMap_mem' r := coe_mem_adicCompletionIntegers v r

/-- The canonical B-algebra map `B ‚äó[A] ùìû_v ‚Üí L ‚äó[K] K_v` -/
noncomputable def tensorAdicCompletionIntegersTo (v : HeightOneSpectrum A) :
    B ‚äó[A] adicCompletionIntegers K v ‚Üí‚Çê[B] L ‚äó[K] adicCompletion K v :=
  Algebra.TensorProduct.lift
    ((Algebra.TensorProduct.includeLeft).comp (Algebra.ofId B L))
    ((Algebra.TensorProduct.includeRight.restrictScalars A).comp (IsScalarTower.toAlgHom _ _ _))
    (fun _ _ ‚Ü¶ .all _ _)

omit [IsIntegralClosure B A L] [FiniteDimensional K L] [Algebra.IsIntegral A B]
  [IsDedekindDomain B] [IsFractionRing B L] in
@[simp]
lemma tensorAdicCompletionIntegersTo_tmul (v : HeightOneSpectrum A) (b : B)
    (x : v.adicCompletionIntegers K) : tensorAdicCompletionIntegersTo A K L B v (b ‚äó‚Çú x) =
      (algebraMap B L b) ‚äó‚Çú x.val := by
  simp [tensorAdicCompletionIntegersTo]

omit [IsIntegralClosure B A L]
    [Algebra.IsIntegral A B] [IsDedekindDomain B]
    [IsFractionRing B L] in
open scoped TensorProduct.RightActions in
/-- The image of `B ‚äó[A] ùìû_v` in `L ‚äó[K] K_v` is contained in the closure of the image of `B`. -/
lemma tensorAdicCompletionIntegersToRange_subset_closureIntegers :
  (tensorAdicCompletionIntegersTo A K L B v).range.carrier ‚äÜ
      closure (algebraMap B (L ‚äó[K] adicCompletion K v)).range := by
  rintro _ ‚ü®s, rfl‚ü©
  induction s with
    | zero =>
        apply subset_closure
        use 0
        simp
    | add x y hx hy =>
        -- The closure of a subgroup is a subgroup
        rw [RingHom.map_add]
        apply map_mem_closure‚ÇÇ _ hx hy _
        ¬∑ exact (ModuleTopology.continuousAdd _ _).continuous_add
        intro _ ha _ hb
        exact add_mem ha hb
    | tmul b a' =>
        -- Rewrite `tensorAdicCompletionIntegersTo (b ‚äó‚Çú a')` to `b ‚Ä¢ (1 ‚äó‚Çú a')`
        simp only [RingHom.coe_range, tensorAdicCompletionIntegersTo, Algebra.comp_ofId,
          AlgHom.toRingHom_eq_coe, RingHom.coe_coe, Algebra.TensorProduct.lift_tmul,
          AlgHom.coe_comp, AlgHom.coe_restrictScalars', IsScalarTower.coe_toAlgHom',
          Function.comp_apply, ValuationSubring.algebraMap_apply,
          Algebra.TensorProduct.includeRight_apply]
        -- Now, `f : a' ‚Ü¶ b ‚Ä¢ (1 ‚äó‚Çú a')` is continuous
        let f (y : ‚Ü•(adicCompletionIntegers K v)) : (L ‚äó[K] adicCompletion K v) :=
          (Algebra.ofId B (L ‚äó[K] adicCompletion K v)) b * (1 : L) ‚äó‚Çú[K] (y : adicCompletion K v)
        have hfval : f = fun (y : ‚Ü•(adicCompletionIntegers K v)) =>
              (y : adicCompletion K v) ‚Ä¢ (Algebra.ofId B (L ‚äó[K] adicCompletion K v)) b := by
          ext y
          unfold f
          rw [Algebra.smul_def]
          exact mul_comm _ _
        have hcf : ContinuousAt f a' := by
          apply Continuous.continuousAt
          rw [hfval]
          apply Continuous.smul continuous_subtype_val continuous_const
        -- So, because `A` is dense in `ùí™_v`, `b ‚Ä¢ (1 ‚äó‚Çú a') ‚àà f '' closure A ‚äÜ closure f '' A`
        have hy : a' ‚àà closure (Set.range (algebraMap A _)) := by
          apply denseRange_of_integerAlgebraMap
        apply mem_closure_image hcf hy
        constructor
        ¬∑ exact isClosed_closure
        -- Finally, `b ‚Ä¢ (1 ‚äó‚Çú a) = (b * a) ‚Ä¢ (1 ‚äó‚Çú 1)`, so `f '' A ‚äÜ algebraMap '' B`
        rintro u ‚ü®_, ‚ü®a, rfl‚ü©, rfl‚ü©
        apply subset_closure
        use algebraMap A B a * b
        unfold f
        rw [Algebra.algebraMap_eq_smul_one (A := (adicCompletionIntegers K v)) a,
          coe_smul_adicCompletionIntegers, ‚Üê TensorProduct.smul_tmul, Algebra.ofId_apply,
          Algebra.TensorProduct.algebraMap_apply, RingHom.map_mul, ‚Üê Algebra.smul_def]
        simp

open TensorProduct.AlgebraTensorModule in
open scoped TensorProduct.RightActions in
omit [Algebra.IsIntegral A B] [IsDedekindDomain B] [IsFractionRing B L]  in
/-- The image of `B ‚äó[A] ùìû_v` in `L ‚äó[K] K_v` is clopen. -/
lemma tensorAdicCompletionIsClopenRange :
    IsClopen (SetLike.coe (tensorAdicCompletionIntegersTo A K L B v).range) := by
  -- `B ‚äó[A] ùí™_v` is a subgroup of `L ‚äó[K] K_v`, so we can show it's closed
  -- by showing that it's open. **TODO** split into IsOpen + IsClosed lemmas?
  rw [‚Üê Subalgebra.coe_toSubring, ‚Üê Subring.coe_toAddSubgroup]
  have := ModuleTopology.continuousAdd (adicCompletion K v) (L ‚äó[K] adicCompletion K v)
  refine OpenAddSubgroup.isClopen ‚ü®_, ?_‚ü©
  -- Further, we can show `B ‚äó[A] ùí™_v` is open by showing that it contains an
  -- open neighbourhood of 0.
  apply AddSubgroup.isOpen_of_zero_mem_interior
  rw [mem_interior, Subring.coe_toAddSubgroup, Subalgebra.coe_toSubring]

  -- Take a basis `b` of `L` over `K` with elements in `B` and use it to
  -- get a basis `b'` of `L ‚äó[K] K_v` over `K_v`.
  obtain ‚ü®Œπ, b, hb‚ü© := FiniteDimensional.exists_is_basis_integral A K L
  let b' : Module.Basis Œπ (adicCompletion K v) (L ‚äó[K] (adicCompletion K v)) := by
    classical
    exact b.rightBaseChange L
  -- Use the basis to get a continuous equivalence from `L ‚äó[K] K_v` to `Œπ ‚Üí K_v`.
  let equiv : L ‚äó[K] (adicCompletion K v) ‚âÉL[K] (Œπ ‚Üí adicCompletion K v) :=
    IsModuleTopology.continuousLinearEquiv (b'.equivFun) |>.restrictScalars K

  -- Use the preimage of `‚àè ùí™_v` as the open neighbourhood.
  use equiv.symm '' (Set.pi Set.univ (fun _ => SetLike.coe (adicCompletionIntegers K v)))
  refine ‚ü®?_, ?_, by simp‚ü©
  ¬∑ intro t ‚ü®g, hg, ht‚ü©
    -- We have `t = equiv g = ‚àë i, b i ‚äó g i`, since `g in ‚àè ùí™_v` and
    -- `b i ‚àà (algebraMap B L).range`, this is `tensorAdicCompletionIntegersTo`
    -- of some element of `B ‚äó[A] ùí™_v`
    have hf : ‚àÄ (i : Œπ), ‚àÉ (w : B), (algebraMap B L w) = (b i) := by
      intro i
      apply IsIntegralClosure.isIntegral_iff.mp (hb i)
    choose f hf_prop using hf
    use ‚àë (i : Œπ), (f i) ‚äó‚Çú ‚ü®g i, hg i trivial‚ü©
    let _ : NonAssocSemiring (B ‚äó[A] (adicCompletionIntegers K v)) :=
      Algebra.TensorProduct.instNonAssocSemiring
    let _ : AddMonoidHomClass (B ‚äó[A] (adicCompletionIntegers K v) ‚Üí+* L ‚äó[K] adicCompletion K v)
        (B ‚äó[A] (adicCompletionIntegers K v)) (L ‚äó[K] adicCompletion K v) :=
      RingHomClass.toAddMonoidHomClass
    rw [map_sum, ‚Üê ht]
    unfold equiv
    rw [ContinuousLinearEquiv.restrictScalars_symm_apply,
      IsModuleTopology.continuousLinearEquiv_symm_apply,
      Module.Basis.equivFun_symm_apply]
    apply Finset.sum_congr rfl
    intro x
    have : (algebraMap _ (L ‚äó[K] adicCompletion K v)) (g x) = 1 ‚äó‚Çú[K] (g x) := rfl
    simp [Algebra.smul_def, tensorAdicCompletionIntegersTo_tmul, hf_prop, b', this]
  ¬∑ rw [ContinuousLinearEquiv.image_symm_eq_preimage]
    apply IsOpen.preimage equiv.continuous
    apply isOpen_set_pi Set.finite_univ
    rintro i -
    exact Valued.isOpen_valuationSubring (v.adicCompletion K)

omit [Algebra.IsIntegral A B] [IsDedekindDomain B] [IsFractionRing B L] in
open scoped TensorProduct.RightActions in
/-- The image of `B ‚äó[A] ùìû_v` in `L ‚äó[K] K_v` is the closure of the image of `B`. -/
lemma tensorAdicCompletionIntegersToRange_eq_closureIntegers :
    SetLike.coe (tensorAdicCompletionIntegersTo A K L B v).range =
        closure (algebraMap B (L ‚äó[K] adicCompletion K v)).range := by
  apply Set.Subset.antisymm
  ¬∑ apply tensorAdicCompletionIntegersToRange_subset_closureIntegers
  ¬∑ apply closure_minimal
    ¬∑ rintro _ ‚ü®b, rfl‚ü©
      apply algebraMap_mem
    ¬∑ apply IsClopen.isClosed
      apply tensorAdicCompletionIsClopenRange

omit [Algebra A L] [IsScalarTower A B L] [IsIntegralClosure B A L] in
/-- The `B`-subalgebra `‚àè_{w|v} ùìû_w` of `‚àè_{w|v} L_w` is the closure of the image of `B`. -/
lemma prodAdicCompletionsIntegers_eq_closureIntegers :
    SetLike.coe (Subalgebra.pi (R := B) Set.univ
      (fun (w : Extension B v) ‚Ü¶ adicCompletionIntegersSubalgebra L w.1)) =
    closure (SetLike.coe (algebraMap B _).range) := by
  rw [Subalgebra.coe_pi]
  let _ (w : Extension B v) : Module B (adicCompletion L w.val) :=
    UniformSpace.Completion.instModule
  change SetLike.coe (Submodule.pi _ _) = _
  rw [Submodule.coe_pi]
  let val := (fun (w : Extension B v) ‚Ü¶ w.1)
  have hinj : Function.Injective val :=
    (Set.injective_codRestrict Subtype.property).mp fun _ _ a ‚Ü¶ a
  rw [closureAlgebraMapIntegers_eq_prodIntegers L val hinj]
  rfl

open scoped TensorProduct.RightActions in
/-- The image of `B ‚äó[A] ùìû_v` (the closure of `B`) in `‚àè_w L_w` is closed. -/
lemma adicCompletionComapAlgHom_map_closure_is_closed :
    IsClosed ((tensorAdicCompletionComapAlgHom A K L B v) ''
        closure (algebraMap B (L ‚äó[K] adicCompletion K v)).range) := by
  let S := AddSubgroup.map
      (tensorAdicCompletionComapAlgHom A K L B v).toAddMonoidHom
      (tensorAdicCompletionIntegersTo A K L B v).range.toSubring.toAddSubgroup
  have hSclosed : IsClosed S.carrier := by
    apply AddSubgroup.isClosed_of_isOpen
    apply (tensorAdicCompletionComapLinearMap_isOpenQuotientMap A K L B v).isOpenMap
    apply (tensorAdicCompletionIsClopenRange A K L B v).isOpen
  suffices h : (tensorAdicCompletionComapAlgHom A K L B v) ''
    closure ‚Üë(algebraMap B (L ‚äó[K] adicCompletion K v)).range = S.carrier by
    rwa [h]
  rw [‚Üê tensorAdicCompletionIntegersToRange_eq_closureIntegers]
  rfl

instance : MulActionHomClass
    (L ‚äó[K] adicCompletion K v ‚Üí‚Çê[L] (w : Extension B v) ‚Üí adicCompletion L w.1) B
    (L ‚äó[K] adicCompletion K v) ((w : Extension B v) ‚Üí adicCompletion L w.1) where
  map_smul‚Çõ‚Çó œÜ b x := by
    rw [‚Üê IsScalarTower.algebraMap_smul L, AlgHom.map_smul_of_tower,
      IsScalarTower.algebraMap_smul, id_def]

instance : OneHomClass
    (L ‚äó[K] adicCompletion K v ‚Üí‚Çê[L] (w : Extension B v) ‚Üí adicCompletion L w.1)
    (L ‚äó[K] adicCompletion K v) ((w : Extension B v) ‚Üí adicCompletion L w.1) where
  map_one f := f.toRingHom.map_one

open scoped TensorProduct.RightActions in
/-- The image of `B ‚äó[A] ùìû_v` in `‚àè_w L_w` is `‚àè_w ùìû_w`. -/
theorem adicCompletionComapAlgEquiv_integral :
    AlgHom.range (((tensorAdicCompletionComapAlgHom A K L B v).restrictScalars B).comp
      (tensorAdicCompletionIntegersTo A K L B v)) =
    Subalgebra.pi Set.univ (fun _ ‚Ü¶ adicCompletionIntegersSubalgebra _ _) := by
  have hlhs := tensorAdicCompletionIntegersToRange_eq_closureIntegers
  have hrhs := prodAdicCompletionsIntegers_eq_closureIntegers
  have hrange :
    SetLike.coe (algebraMap B ((w : Extension B v) ‚Üí adicCompletion L w.1)).range =
      (tensorAdicCompletionComapAlgHom A K L B v) ''
      SetLike.coe (algebraMap B (L ‚äó[K] adicCompletion K v)).range := by
    ext x
    simp [Algebra.algebraMap_eq_smul_one]
  have hrange' := adicCompletionComapAlgHom_map_closure_is_closed A K L B v
  rw [AlgHom.range_comp, ‚Üê SetLike.coe_set_eq, Subalgebra.coe_map, AlgHom.coe_restrictScalars',
      hlhs, hrhs, hrange, ‚Üê IsClosed.closure_eq hrange']
  exact closure_image_closure
    (tensorAdicCompletionComapLinearMap_isOpenQuotientMap A K L B v).continuous

attribute [local instance 9999] SMulCommClass.of_commMonoid TensorProduct.isScalarTower_left
  IsScalarTower.right

-- **TODO**: Mathlib#23590 will provide these instances in more generality.
/-- The restriction of `adicCompletionComapSemialgHom` to a map `ùìû_v ‚Üí ùìû_w`. -/
noncomputable def adicCompletionComapIntegerRingHom (v : HeightOneSpectrum A)
    (w : HeightOneSpectrum B) (h : w.comap A = v) :
    v.adicCompletionIntegers K ‚Üí+* w.adicCompletionIntegers L where
  toFun x :=
    letI := FaithfulSMul.of_field_isFractionRing A B K L
    ‚ü®adicCompletionComapSemialgHom A K L B v w h x,
     adicCompletionComapSemialgHom.mapadicCompletionIntegers A K L B v w h ‚ü®x, x.prop, rfl‚ü©‚ü©
  map_zero' := by ext; simp
  map_one' := by ext; simp
  map_add' x y := by ext; simp
  map_mul' x y := by ext; simp

/-- If `w` is an extension of `v`, then `ùìû_w` is naturally an `ùìû_v`-algebra. -/
noncomputable
def comap_integer_algebra {v : HeightOneSpectrum A} {w : HeightOneSpectrum B} (h : w.comap A = v) :
    Algebra (v.adicCompletionIntegers K) (w.adicCompletionIntegers L) :=
  (adicCompletionComapIntegerRingHom A K L B v w h).toAlgebra

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
lemma comap_integer_algebraMap {v : HeightOneSpectrum A} {w : HeightOneSpectrum B}
    (h : w.comap A = v) (x : v.adicCompletionIntegers K) :
    letI := comap_integer_algebra A K L B h
    algebraMap (v.adicCompletionIntegers K) (w.adicCompletionIntegers L) x =
    (adicCompletionComapSemialgHom A K L B v w h) x.val :=
  rfl

variable {v} in
/-- If `w` is an extension of `v`, then `L_w` is naturally an `ùìû_v`-algebra. -/
noncomputable def comap_integer_algebra' {w : HeightOneSpectrum B} (hvw : comap A w = v) :=
  letI := comap_algebra A K L B hvw
  RingHom.toAlgebra <| (algebraMap (adicCompletion K v) (adicCompletion L w)).comp
    (algebraMap (adicCompletionIntegers K v) (adicCompletion K v))

open scoped TensorProduct.RightActions in
/-- `tensorAdicCompletionIntegersTo` as an ùìû_v-linear map. -/
noncomputable def tensorAdicCompletionIntegersToLinearMap :
    (B ‚äó[A] adicCompletionIntegers K v) ‚Üí‚Çó[adicCompletionIntegers K v]
      (L ‚äó[K] adicCompletion K v) where
  __ := tensorAdicCompletionIntegersTo A K L B v
  map_smul' x y := by
    simp only [tensorAdicCompletionIntegersTo_tmul, AlgHom.toRingHom_eq_coe,
      RingHom.toMonoidHom_eq_coe, AlgHom.toRingHom_toMonoidHom, Algebra.smul_def,
      TensorProduct.RightActions.algebraMap_eval, OneHom.toFun_eq_coe, MonoidHom.toOneHom_coe,
      MonoidHom.coe_coe, map_mul, map_one, RingHom.id_apply]
    rw [Subsemiring.smul_def, Algebra.smul_def]
    rfl

open scoped TensorProduct.RightActions in
/-- The map `B ‚äó ùìû_v ‚Üí L_w` for `w` an extension of `v` given by the algebra maps. -/
noncomputable def adicCompletionComapIntegerLinearMap (v : HeightOneSpectrum A)
    (w : HeightOneSpectrum B) (hvw : comap A w = v) :
    letI := comap_algebra A K L B hvw
    letI := RingHom.toAlgebra <| (algebraMap (adicCompletion K v) (adicCompletion L w)).comp
      (algebraMap (adicCompletionIntegers K v) (adicCompletion K v))
    B ‚äó[A] (adicCompletionIntegers K v) ‚Üí‚Çó[adicCompletionIntegers K v] (adicCompletion L w) :=
  letI := comap_integer_algebra A K L B hvw
  letI (w' : Extension B v) := comap_algebra A K L B w'.prop
  let alg (w' : Extension B v) : Algebra _ _ :=
    RingHom.toAlgebra <| (algebraMap (adicCompletion K v) (adicCompletion L w'.val)).comp
      (algebraMap (adicCompletionIntegers K v) (adicCompletion K v))

  have : LinearMap.CompatibleSMul (L ‚äó[K] adicCompletion K v)
      ((w : Extension B v) ‚Üí adicCompletion L w.val)
      (adicCompletionIntegers K v) (adicCompletion K v) := {
    map_smul f x y := by
      obtain ‚ü®x, hx‚ü© := x
      exact LinearMap.map_smul f x y
  }
  let f : L ‚äó[K] adicCompletion K v ‚Üí‚Çó[adicCompletionIntegers K v]
      (w' : Extension B v) ‚Üí adicCompletion L w'.val :=
    (tensorAdicCompletionComapLinearMap A K L B v).restrictScalars (adicCompletionIntegers K v)
  let g := LinearMap.comp f (tensorAdicCompletionIntegersToLinearMap A K L B v)
  @LinearMap.proj _ _ _ (fun (w : Extension B v) ‚Ü¶ adicCompletion L w.val) _
    (fun w => (alg w).toModule) ‚ü®w, hvw‚ü© |>.comp g

open scoped TensorProduct.RightActions in
/-- The range of `adicCompletionComapIntegerLinearMap` is `ùìû_w`. -/
lemma adicCompletionComapIntegerLinearMap_range_eq_integers (v : HeightOneSpectrum A)
    (w : HeightOneSpectrum B) (hvw : comap A w = v) :
    letI := comap_algebra A K L B hvw
    Set.range (adicCompletionComapIntegerLinearMap A K L B v w hvw) =
    adicCompletionIntegers L w := by
  ext x
  have memrange : (Set.range _) = _ :=
    congr_arg SetLike.coe (adicCompletionComapAlgEquiv_integral A K L B v)
  rw [Subalgebra.coe_pi, Submodule.carrier_eq_coe, Submodule.coe_pi, Set.ext_iff] at memrange
  let extension : Extension B v := ‚ü®w, hvw‚ü©
  constructor
  ¬∑ rintro ‚ü®y, rfl‚ü©
    exact (memrange _).mp (Set.mem_range_self y) extension trivial
  ¬∑ intro hx
    classical
    set x' : (w : Extension B v) ‚Üí adicCompletion L w.val := Pi.single extension x with hx'
    obtain ‚ü®y, (hy : _ = x')‚ü© : x' ‚àà Set.range _ := by
      rw [memrange x', Set.mem_pi]
      intro w _
      by_cases h : extension = w
      ¬∑ rw [‚Üê h, hx', Pi.single_eq_same, SetLike.mem_coe]
        exact hx
      ¬∑ rw [hx', Pi.single_eq_of_ne' h]
        apply Subalgebra.zero_mem
    use y
    simpa [hx'] using congr_fun hy extension


open scoped TensorProduct.RightActions in
/-- `ùìû_w` is finite over `ùìû_v`. -/
-- This can be proved for finite extensions of complete discretely valued fields without
-- reference to underlying fields being completed, but this is sufficient for our
-- purposes.
theorem comap_integer_algebra_finite [Module.Finite A B] (v : HeightOneSpectrum A)
    (w : HeightOneSpectrum B) (hvw : comap A w = v) :
    letI := comap_integer_algebra A K L B hvw
    Module.Finite (adicCompletionIntegers K v) (adicCompletionIntegers L w) := by
  letI := comap_integer_algebra A K L B hvw
  letI := comap_algebra A K L B hvw
  let hom : (adicCompletionIntegers K v) ‚Üí+* (adicCompletion L w) :=
    (algebraMap (adicCompletion K v) (adicCompletion L w)).comp
      (algebraMap (adicCompletionIntegers K v) (adicCompletion K v))
  letI := RingHom.toAlgebra hom

  let integerSubmodule : Submodule (adicCompletionIntegers K v) (adicCompletion L w) :=
    have : IsScalarTower (adicCompletionIntegers K v) (adicCompletionIntegers L w)
        (adicCompletion L w) := by
      apply IsScalarTower.of_algebraMap_smul fun _ _ ‚Ü¶ rfl
    (adicCompletionIntegers L w).toSubring.toSubmodule.restrictScalars
      (adicCompletionIntegers K v)

  have heq : (LinearMap.range (adicCompletionComapIntegerLinearMap A K L B v w hvw)) =
      integerSubmodule := by
    ext x
    apply adicCompletionComapIntegerLinearMap_range_eq_integers A K L B v w hvw |> Set.ext_iff.mp
  have := Module.Finite.range (adicCompletionComapIntegerLinearMap A K L B v w hvw)
  apply Module.Finite.equiv <|
    LinearEquiv.ofEq (LinearMap.range (adicCompletionComapIntegerLinearMap A K L B v w hvw)) _ heq

end ModuleTopology

section RamificationInertia

lemma WithZero.ofAdd_neg_ofNat_pow (n : ‚Ñï) :
    (WithZero.coe (Multiplicative.ofAdd (-n : ‚Ñ§))) = (Multiplicative.ofAdd (-1 : ‚Ñ§)) ^ n := by
  congr
  rw [‚Üê ofAdd_nsmul, nsmul_eq_mul, Int.mul_neg_one]

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
theorem adicCompletion.ramificationIdx_eq_ramificationIdx (w : HeightOneSpectrum B)
    (hvw : w.comap A = v) :
    letI := comap_integer_algebra A K L B hvw
    Ideal.ramificationIdx (algebraMap _ _) (v.completionIdeal K) (w.completionIdeal L)
      = Ideal.ramificationIdx (algebraMap A B) v.asIdeal w.asIdeal := by
  apply Ideal.ramificationIdx_spec
  ¬∑ rw [Ideal.map_le_iff_le_comap]
    intro x hx
    rw [mem_completionIdeal_iff'] at hx
    rw [Ideal.mem_comap, adicCompletion.mem_completionIdeal_pow, comap_integer_algebraMap,
      valued_adicCompletionComap]
    rw [WithZero.ofAdd_neg_ofNat_pow, hvw]
    apply pow_le_pow_left' hx
  ¬∑ obtain ‚ü®œñ, hœñ‚ü© := adicCompletion.exists_uniformizer K v
    have hœñ' : œñ ‚àà v.completionIdeal K := by
      rw [mem_completionIdeal_iff, hœñ]
      decide
    rw [Ideal.map_le_iff_le_comap]
    intro h
    have hcomap := h hœñ'
    rw [Ideal.mem_comap, adicCompletion.mem_completionIdeal_pow, comap_integer_algebraMap,
      valued_adicCompletionComap, hœñ, ‚Üê WithZero.ofAdd_neg_ofNat_pow,
      WithZero.coe_le_coe, Multiplicative.ofAdd_le, hvw] at hcomap
    simp at hcomap

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
theorem adicCompletion.inertiaDeg_eq_inertiaDeg (w : HeightOneSpectrum B) (hvw : w.comap A = v) :
    letI := comap_integer_algebra A K L B hvw
    v.asIdeal.inertiaDeg w.asIdeal = (v.completionIdeal K).inertiaDeg (w.completionIdeal L) :=
  letI := Algebra.compHom (adicCompletionIntegers L w) (algebraMap A B)
  letI := comap_integer_algebra A K L B hvw
  have : IsScalarTower A B (adicCompletionIntegers L w) :=
    IsScalarTower.of_algebraMap_eq fun _ ‚Ü¶ rfl
  have : IsScalarTower A (adicCompletionIntegers K v) (adicCompletionIntegers L w) := by
    apply IsScalarTower.of_algebraMap_eq
    intro x
    ext
    rw [Algebra.compHom_algebraMap_eq, RingHom.coe_comp, Function.comp_apply,
      algebraMap_completionIntegers, comap_integer_algebraMap, algebraMap_completionIntegers,
      IsScalarTower.algebraMap_apply B L (adicCompletion L w),
      ‚Üê IsScalarTower.algebraMap_apply A B L, IsScalarTower.algebraMap_apply A K L]
    symm
    apply SemialgHom.commutes
  have : w.asIdeal.LiesOver v.asIdeal := ‚ü®hvw ‚ñ∏ rfl‚ü©
  have : (completionIdeal L w).LiesOver (completionIdeal K v) := {
    over := by
      rw [Ideal.under_def]
      ext x
      rw [Ideal.mem_comap, mem_completionIdeal_iff, mem_completionIdeal_iff,
        comap_integer_algebraMap, valued_adicCompletionComap, pow_lt_one_iff]
      exact ramificationIdx_ne_zero A B (algebraMap_injective_of_field_isFractionRing A B K L) w
  }
  calc v.asIdeal.inertiaDeg w.asIdeal
      = v.asIdeal.inertiaDeg (w.completionIdeal L) := by
        rw [Ideal.inertiaDeg_algebra_tower v.asIdeal w.asIdeal (w.completionIdeal L),
          inertiaDeg_asIdeal_completionIdeal, mul_one]
    _ = (v.completionIdeal K).inertiaDeg (w.completionIdeal L) := by
        rw [Ideal.inertiaDeg_algebra_tower v.asIdeal (v.completionIdeal K) (w.completionIdeal L),
          inertiaDeg_asIdeal_completionIdeal, one_mul]

-- We use Ideal.sum_ramification_inertia_of_isLocalRing here to show this, but we could make use
-- of the more general results in BGR:
-- - in general e * f <= degree (Prop 3.1.3.2)
-- - equality holds for L/K if L is K-cartesian (Prop 3.6.2.4)
-- - so for example if K is complete and discretely-valued (Cor 2.4.3.11).
attribute [local instance 9999] Algebra.toModule Algebra.toSMul in
theorem ramification_mul_inertia_eq_finrank_completion [Module.Finite A B] (w : HeightOneSpectrum B)
    (hvw : w.comap A = v) :
    letI := comap_algebra A K L B hvw |>.toModule
    Ideal.ramificationIdx (algebraMap A B) v.asIdeal w.asIdeal * v.asIdeal.inertiaDeg w.asIdeal =
    Module.finrank (adicCompletion K v) (adicCompletion L w) := by
  letI := comap_algebra A K L B hvw
  letI := comap_integer_algebra A K L B hvw
  have := comap_integer_algebra_finite A K L B v w hvw
  let hom : (adicCompletionIntegers K v) ‚Üí+* (adicCompletion L w) :=
    (algebraMap (adicCompletion K v) (adicCompletion L w)).comp
      (algebraMap (adicCompletionIntegers K v) (adicCompletion K v))
  let alg := RingHom.toAlgebra hom
  letI := alg.toSMul
  have : IsScalarTower (adicCompletionIntegers K v) (adicCompletionIntegers L w)
      (adicCompletion L w) := by
    exact IsScalarTower.of_algebraMap_smul fun _ _ ‚Ü¶ rfl
  have : IsScalarTower (adicCompletionIntegers K v) (adicCompletion K v)
      (adicCompletion L w) := by
    apply IsScalarTower.of_algebraMap_smul fun _ _ ‚Ü¶ rfl
  rw [‚Üê Ideal.sum_ramification_inertia_of_isLocalRing (adicCompletionIntegers L w)
    (v.completionIdeal K) (adicCompletion K v) (adicCompletion L w) (v.completionIdeal_ne_bot K),
    adicCompletion.ramificationIdx_eq_ramificationIdx A K L B v w hvw,
    adicCompletion.inertiaDeg_eq_inertiaDeg A K L B v w hvw]

end RamificationInertia

variable [Module.Finite A B]

open scoped TensorProduct.RightActions in
attribute [local instance 9999] Algebra.toModule in
/-- `L ‚äó[K] K_v` and `‚àè_{w|v} L_w` have equal dimensions -/
lemma finrank_tensorProduct_adicCompletion_eq_finrank_pi_adicCompletion :
    letI := comap_pi_algebra A K L B v
    Module.finrank (adicCompletion K v) (L ‚äó[K] adicCompletion K v) =
    Module.finrank (adicCompletion K v) ((w : Extension B v) ‚Üí adicCompletion L w.val) :=
  letI := Extension.fintype A K L B v
  letI (w : Extension B v) := comap_algebra A K L B w.prop
  letI (w : Extension B v) : Module.Finite (adicCompletion K v) (adicCompletion L w.val) :=
    comap_algebra_finite A K L B v w.val w.prop
  letI (w : Extension B v) : Module.Free (adicCompletion K v) (adicCompletion L w.val) :=
    inferInstance
  calc Module.finrank (adicCompletion K v) (L ‚äó[K] adicCompletion K v)
    _ = Module.finrank K L := by rw [TensorProduct.finrank_rightAlgebra]
    _ = ‚àë (w : Extension B v), Ideal.ramificationIdx (algebraMap A B) v.asIdeal w.val.asIdeal *
        Ideal.inertiaDeg v.asIdeal w.val.asIdeal := by
        rw [Ideal.sum_ramification_inertia_extensions]
    _ = ‚àë (w : Extension B v), Module.finrank (adicCompletion K v) (adicCompletion L w.val) :=
        Finset.sum_congr rfl fun w _ ‚Ü¶
          ramification_mul_inertia_eq_finrank_completion A K L B v w.val w.prop
    _ = Module.finrank (adicCompletion K v) ((w : Extension B v) ‚Üí adicCompletion L w.val) := by
        rw [Module.finrank_pi_fintype (adicCompletion K v)]

open scoped TensorProduct.RightActions in
/-- The canonical map `L ‚äó[K] K_v ‚Üí ‚àè_{w|v} L_w` is bijective. -/
theorem tensorAdicCompletionComapAlgHom_bijective (v : HeightOneSpectrum A) :
    Function.Bijective (tensorAdicCompletionComapAlgHom A K L B v) := by
  change Function.Bijective (tensorAdicCompletionComapLinearMap A K L B v)
  have hsurj := tensorAdicCompletionComapLinearMap_surjective A K L B v
  refine ‚ü®?_, hsurj‚ü©
  have hfin := comap_pi_algebra_finite A K L B v
  have hrank := finrank_tensorProduct_adicCompletion_eq_finrank_pi_adicCompletion A K L B v
  rwa [LinearMap.injective_iff_surjective_of_finrank_eq_finrank hrank]

/-- The L-algebra isomorphism `L ‚äó[K] K_v ‚âÖ ‚àè_{w|v} L_w`. -/
noncomputable def adicCompletionComapAlgEquiv (v : HeightOneSpectrum A) :
    L ‚äó[K] v.adicCompletion K ‚âÉ‚Çê[L] (‚àÄ w : v.Extension B, w.1.adicCompletion L) :=
  AlgEquiv.ofBijective (tensorAdicCompletionComapAlgHom A K L B v) <|
    tensorAdicCompletionComapAlgHom_bijective A K L B v

open scoped TensorProduct.RightActions in
/-- The continuous K_v-algebra isomorphism `L ‚äó[K] K_v ‚âÖ ‚àè_{w|v} L_w`. -/
noncomputable def adicCompletionComapRightContinuousAlgEquiv (v : HeightOneSpectrum A) :
    L ‚äó[K] v.adicCompletion K ‚âÉA[v.adicCompletion K] (‚àÄ w : v.Extension B, w.1.adicCompletion L) :=
  let _ := comap_pi_algebra A K L B v |>.toSMul
  have : IsModuleTopology (v.adicCompletion K) (‚àÄ w : v.Extension B, w.1.adicCompletion L) :=
    prodAdicCompletionComap_isModuleTopology A K L B v
  IsModuleTopology.continuousAlgEquivOfAlgEquiv {
    __ := SemialgHom.baseChangeRightOfAlgebraMap (adicCompletionComapSemialgHom' A K L B v)
    __ := adicCompletionComapAlgEquiv A K L B v
  }

open scoped TensorProduct.RightActions in
/-- The continuous L-algebra isomorphism `L ‚äó[K] K_v ‚âÖ ‚àè_{w|v} L_w`. -/
noncomputable def adicCompletionComapContinuousAlgEquiv (v : HeightOneSpectrum A) :
    L ‚äó[K] v.adicCompletion K ‚âÉA[L] ‚àÄ w : v.Extension B, w.1.adicCompletion L :=
  {
    __ := adicCompletionComapAlgEquiv A K L B v
    __ := adicCompletionComapRightContinuousAlgEquiv A K L B v
  }

/-- The A-module isomorphism `B ‚äó[A] K_v ‚âÖ ‚àè_{w|v} L_w`. -/
noncomputable def adicCompletionComapIntegerLinearEquiv (v : HeightOneSpectrum A) :
    B ‚äó[A] v.adicCompletion K ‚âÉ‚Çó[A] ‚àÄ w : v.Extension B, w.1.adicCompletion L :=
  (linearEquivTensorProductModule A K L B (v.adicCompletion K)).symm.trans
    ((adicCompletionComapAlgEquiv A K L B v).toLinearEquiv.restrictScalars A)

@[simp]
lemma adicCompletionComapIntegerLinearEquiv_tmul_apply (v : HeightOneSpectrum A) (b : B)
    (x : v.adicCompletion K) (w : Extension B v) :
    letI := comap_algebra A K L B w.prop
    adicCompletionComapIntegerLinearEquiv A K L B v (b ‚äó‚Çú[A] x) w =
    (algebraMap B _ b) * (algebraMap _ _ x) := by
  rw [adicCompletionComapIntegerLinearEquiv, LinearEquiv.trans_apply,
    linearEquivTensorProductModule_symm_tmul]
  rfl

/-- The canonical A-linear map `B ‚äó[A] ùìû_v ‚Üí B ‚äó[A] K_v`. -/
noncomputable def adicCompletionTensorIntegerCoe :
    B ‚äó[A] (v.adicCompletionIntegers K) ‚Üí‚Çó[A] B ‚äó[A] (v.adicCompletion K) :=
  (Algebra.algHom A (adicCompletionIntegers K v) (adicCompletion K v)).toLinearMap.lTensor B

omit [Algebra.IsIntegral A B] [IsDedekindDomain B] [Module.Finite A B] in
@[simp]
lemma adicCompletionTensorIntegerCoe_tmul (b : B) (x : v.adicCompletionIntegers K) :
    adicCompletionTensorIntegerCoe A K B v (b ‚äó‚Çú x) = b ‚äó‚Çú x.val :=
  rfl

/-- `ùìû_v` as an `A`-submodule of `K_v`. -/
noncomputable def integerSubmodule (v : HeightOneSpectrum A) : Submodule A (adicCompletion K v) :=
  let s : Submodule (adicCompletionIntegers K v) _ := (adicCompletionIntegers K v).toSubmodule
  s.restrictScalars A

theorem adicCompletionComapIntegerLinearEquiv_bijOn (v : HeightOneSpectrum A) :
    Set.BijOn (adicCompletionComapIntegerLinearEquiv A K L B v)
    (LinearMap.range <| adicCompletionTensorIntegerCoe A K B v)
    (Submodule.pi Set.univ fun (w : Extension B v) ‚Ü¶ integerSubmodule B L w.val) := by
  suffices h : ((adicCompletionComapIntegerLinearEquiv A K L B v).toEquiv ''
      (LinearMap.range (adicCompletionTensorIntegerCoe A K B v))) =
      Submodule.pi Set.univ
      (fun (w : Extension B v) ‚Ü¶ (integerSubmodule B L w.val).restrictScalars A) from
    h ‚ñ∏ Equiv.bijOn_image (adicCompletionComapIntegerLinearEquiv A K L B v).toEquiv
  apply Eq.trans _ congr(SetLike.coe $(adicCompletionComapAlgEquiv_integral A K L B v))
  rw [LinearMap.range_coe, ‚Üê Set.range_comp, AlgHom.coe_range,
    ‚Üê AlgHom.coe_restrictScalars' (R:=A), ‚Üê AlgHom.coe_toLinearMap,
    LinearEquiv.coe_toEquiv, ‚Üê LinearEquiv.coe_toLinearMap, ‚Üê LinearMap.coe_comp]
  congr
  refine TensorProduct.ext' (fun x y ‚Ü¶ ?_)
  ext w
  simp [‚Üê IsScalarTower.algebraMap_apply, tensorAdicCompletionComapAlgHom_tmul_apply]

-- Mathlib #26783
instance [NoZeroSMulDivisors A B] : Module.Flat A B := by
  sorry

end IsDedekindDomain.HeightOneSpectrum
