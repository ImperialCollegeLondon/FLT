/-
Copyright (c) 2025 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Matthew Jasper
-/
import FLT.DedekindDomain.Completion.BaseChange
import FLT.DedekindDomain.FiniteAdeleRing.TensorRestrictedProduct
import FLT.Mathlib.RingTheory.DedekindDomain.FiniteAdeleRing
import FLT.Mathlib.Topology.Algebra.RestrictedProduct.Module
import Mathlib.RingTheory.Flat.TorsionFree

/-!

# Base change of adele rings.

If `A` is a Dedekind domain with field of fractions `K`, if `L/K` is a finite separable
extension and if `B` is the integral closure of `A` in `L`, then `B` is also a Dedekind
domain. Hence the rings of finite adeles `ğ”¸_K^âˆ` and `ğ”¸_L^âˆ` (defined using `A` and `B`)
are defined. In this file we define the natural `K`-algebra map `ğ”¸_K^âˆ â†’ ğ”¸_L^âˆ` and
the natural `L`-algebra map `ğ”¸_K^âˆ âŠ—[K] L â†’ ğ”¸_L^âˆ`, and show that the latter map
is an isomorphism.

## Main definitions

* `FiniteAdeleRing.baseChangeAlgEquiv : L âŠ—[K] FiniteAdeleRing A K â‰ƒâ‚[L] FiniteAdeleRing B L`

## Main theorems

* `FiniteAdeleRing B L` has the `FiniteAdeleRing A K`-module topology, shown as an instance.

-/

variable (A K L B : Type*) [CommRing A] [CommRing B] [Algebra A B] [Field K] [Field L]
    [Algebra A K] [IsFractionRing A K] [Algebra B L] [IsDedekindDomain A]
    [Algebra K L] [Algebra A L] [IsScalarTower A B L] [IsScalarTower A K L]
    [IsIntegralClosure B A L] [FiniteDimensional K L] [Module.Finite A B]
    [IsDedekindDomain B] [IsFractionRing B L]

namespace IsDedekindDomain

open IsDedekindDomain HeightOneSpectrum

open scoped TensorProduct -- âŠ— notation for tensor product

lemma tendsTo_comap_cofinite [FaithfulSMul A B] :
    Filter.Tendsto (comap A (B:=B)) Filter.cofinite Filter.cofinite :=
  have : FaithfulSMul A (FractionRing B) := FractionRing.instFaithfulSMul A B
  letI : Algebra (FractionRing A) (FractionRing B) :=
    FractionRing.liftAlgebra A (FractionRing B)
  (Filter.Tendsto.cofinite_of_finite_preimage_singleton <|
    Extension.finite A (FractionRing A) (FractionRing B) B)

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
lemma cofinite_mapsTo_adicCompletionComapSemialgHom :
    âˆ€á¶  (w : HeightOneSpectrum B) in Filter.cofinite,
    Set.MapsTo (adicCompletionComapSemialgHom A K L B (comap A w) w rfl)
      (adicCompletionIntegers K (comap A w)) (adicCompletionIntegers L w) := by
  apply Filter.Eventually.of_forall
  intro w
  have := adicCompletionComapSemialgHom.mapadicCompletionIntegers A K L B (comap A w) w rfl
  exact Set.image_subset_iff.1 this

/-- The ring homomorphism `ğ”¸_K^âˆ â†’ ğ”¸_L^âˆ` for `L/K` an extension of number fields. -/
noncomputable def FiniteAdeleRing.mapRingHom :
    FiniteAdeleRing A K â†’+* FiniteAdeleRing B L :=
  have : FaithfulSMul A B := FaithfulSMul.of_field_isFractionRing A B K L
  RestrictedProduct.mapAlongRingHom
    (fun (v : HeightOneSpectrum A) â†¦ v.adicCompletion K)
    (fun (w : HeightOneSpectrum B) â†¦ w.adicCompletion L)
    (HeightOneSpectrum.comap A)
    (tendsTo_comap_cofinite A B)
    (fun w â†¦ adicCompletionComapSemialgHom A K L B (w.comap A) w rfl)
    (cofinite_mapsTo_adicCompletionComapSemialgHom A K L B)

/-- The ring homomorphism `ğ”¸_K^âˆ â†’ ğ”¸_L^âˆ` for `L/K` an extension of number fields,
as a morphism lying over the canonical map `K â†’ L`. -/
noncomputable def FiniteAdeleRing.mapSemialgHom :
    FiniteAdeleRing A K â†’â‚›â‚[algebraMap K L] FiniteAdeleRing B L where
      __ := FiniteAdeleRing.mapRingHom A K L B
      map_smul' k a := by
        ext w
        simpa only [Algebra.smul_def'] using
          (adicCompletionComapSemialgHom A K L B (comap A w) w rfl).map_smul' k (a (comap A w))

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
lemma FiniteAdeleRing.mapSemialgHom_apply (x : FiniteAdeleRing A K) (w : HeightOneSpectrum B) :
    letI := comap_algebra A K L B (Eq.refl (comap A w))
    mapSemialgHom A K L B x w = algebraMap _ _ (x (comap A w)) := rfl

open scoped TensorProduct.RightActions

noncomputable
instance BaseChange.algebra : Algebra (FiniteAdeleRing A K) (FiniteAdeleRing B L) :=
  RingHom.toAlgebra (FiniteAdeleRing.mapRingHom A K L B)

attribute [local instance 9999] Algebra.toSMul in
noncomputable instance baseChangeAlgebra : Algebra K (FiniteAdeleRing B L) :=
  Algebra.compHom (FiniteAdeleRing B L) (algebraMap K L)

instance : IsScalarTower K L (FiniteAdeleRing B L) :=
  IsScalarTower.of_algebraMap_eq' rfl

-- this instance creates a nasty diamond for
-- `IsScalarTower K (FiniteAdeleRing A K) (FiniteAdeleRing B L)` when K = L A = B, and
-- should probably be scoped (or even removed and statements changed so that they
-- don't need it).
attribute [local instance 9999] Algebra.toSMul in
instance : IsScalarTower K (FiniteAdeleRing A K) (FiniteAdeleRing B L) := by
  apply IsScalarTower.of_algebraMap_eq
  intro x
  nth_rw 2 [RingHom.algebraMap_toAlgebra]
  exact SemialgHom.commutes (FiniteAdeleRing.mapSemialgHom A K L B) x |>.symm

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
lemma BaseChange.algebraMap_apply (w : HeightOneSpectrum B) (x : FiniteAdeleRing A K) :
    letI := comap_algebra A K L B (w := w) rfl
    algebraMap _ (FiniteAdeleRing B L) x w = algebraMap _ _ (x (comap A w)) :=
  rfl

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
lemma FiniteAdeleRing.mapSemialgHom_continuous : Continuous (mapSemialgHom A K L B) :=
  have : FaithfulSMul A B := FaithfulSMul.of_field_isFractionRing A B K L
  RestrictedProduct.mapAlong_continuous _ _ _ (tendsTo_comap_cofinite A B) _
    (cofinite_mapsTo_adicCompletionComapSemialgHom A K L B)
    fun w â†¦ adicCompletionComapSemialgHom_continuous A K L B _ w rfl

attribute [instance 100] RestrictedProduct.instSMulCoeOfSMulMemClass
-- otherwise
-- #synth SMul (FiniteAdeleRing A K) (FiniteAdeleRing B L)
-- spends 2 seconds failing to find `SMul (FiniteAdeleRing A K) (adicCompletion L w)

noncomputable section bijection

instance baseChangeIntegerAlgebra : Algebra A (FiniteAdeleRing B L) :=
  Algebra.compHom (FiniteAdeleRing B L) (algebraMap A B)

omit [Module.Finite A B] [IsDedekindDomain B] in
theorem range_adicCompletionTensorIntegerCoe_eq_lTensorRestriction (v : HeightOneSpectrum A) :
    LinearMap.range (adicCompletionTensorIntegerCoe A K B v) =
    RestrictedProduct.rangeLTensor A B (adicCompletion K) (integerSubmodule A K) v := by
  rfl

/-- The canonical linear isomorphism `L âŠ—[K] ğ”¸_K^âˆ â‰… B âŠ—[A] ğ”¸_K^âˆ`. -/
def FiniteAdeleRing.tensorEquivTensor :
    L âŠ—[K] FiniteAdeleRing A K â‰ƒâ‚—[A] B âŠ—[A] FiniteAdeleRing A K := by
  exact linearEquivTensorProductModule A K L B (FiniteAdeleRing A K)

omit [Module.Finite A B] [IsDedekindDomain B] [IsFractionRing B L] in
lemma FiniteAdeleRing.tensorEquivTensor_tmul (b : B) (x : FiniteAdeleRing A K) :
    tensorEquivTensor A K L B ((algebraMap B L b) âŠ—â‚œ[K] x) = b âŠ—â‚œ[A] x := by
  simp [tensorEquivTensor, linearEquivTensorProductModule_tmul]

open scoped RestrictedProduct in
/-- The `A`-linear isomorphism `Ï† : B âŠ—[K] ğ”¸_K^âˆ â‰… âˆ'_v [B âŠ—[A] K_v, B âŠ—[A] ğ“_v]`
given by `Ï† (b âŠ— x) v = b âŠ— (x v)`. -/
def FiniteAdeleRing.tensorEquivRestrictedProduct :
    B âŠ—[A] FiniteAdeleRing A K â‰ƒâ‚—[A]
      Î Ê³ v, [B âŠ—[A] (adicCompletion K v), RestrictedProduct.rangeLTensor A
      B (adicCompletion K) (integerSubmodule A K) v]:= by
  have := Module.finitePresentation_of_finite A B
  have := noZeroSMulDivisors A K L B
  let map :=
    RestrictedProduct.lTensorEquiv A B (adicCompletion K) Filter.cofinite (integerSubmodule A K)
  apply LinearEquiv.trans (TensorProduct.congr (LinearEquiv.refl A B) _) map
  exact {
    __ := AddEquiv.refl _
    map_smul' a x := by
      ext v
      exact Algebra.smul_def a (x v) |>.symm
  }

omit [IsIntegralClosure B A L] [FiniteDimensional K L] [IsFractionRing B L] in
lemma FiniteAdeleRing.tensorEquivRestrictedProduct_tmul (b : B) (x : FiniteAdeleRing A K)
    (v : HeightOneSpectrum A) :
    tensorEquivRestrictedProduct A K L B (b âŠ—â‚œ[A] x) v = b âŠ—â‚œ[A] (x v) := rfl

open scoped RestrictedProduct in
/-- The `A`-linear isomorphism `âˆ'_v [B âŠ—[A] K_v, B âŠ—[A] ğ“_v] â‰… âˆ'_v [âˆ_{w|v} L_w, âˆ_{w|v} ğ“_w]`
given by `adicCompletionComapIntegerLinearEquiv`. -/
def FiniteAdeleRing.restrictedProduct_tensorProduct_equiv_restrictedProduct_prod :
    Î Ê³ v, [B âŠ—[A] (adicCompletion K v), RestrictedProduct.rangeLTensor A
      B (adicCompletion K) (integerSubmodule A K) v] â‰ƒâ‚—[A]
    Î Ê³ (v : HeightOneSpectrum A), [(w : Extension B v) â†’ adicCompletion L w.val,
      Submodule.pi Set.univ fun w : Extension B v â†¦ (integerSubmodule B L w.val).restrictScalars A]
    :=
  LinearEquiv.restrictedProductCongrRight
    (adicCompletionComapIntegerLinearEquiv A K L B)
    (Filter.Eventually.of_forall <| adicCompletionComapIntegerLinearEquiv_bijOn A K L B)

lemma FiniteAdeleRing.restrictedProduct_tensorProduct_equiv_restrictedProduct_prod_apply
    (f) (v : HeightOneSpectrum A) :
     FiniteAdeleRing.restrictedProduct_tensorProduct_equiv_restrictedProduct_prod A K L B f v =
     adicCompletionComapIntegerLinearEquiv A K L B v (f v) := rfl

open scoped RestrictedProduct in
/-- The `A`-linear isomorphism `âˆ'_v [âˆ_{w|v} L_w, âˆ_{w|v} ğ“_w] â†’ ğ”¸_L^âˆ` given by
`RestrictedProduct.flatten_equiv'`. -/
def FiniteAdeleRing.restrictedProduct_prod_equiv :
    Î Ê³ (v : HeightOneSpectrum A), [(w : Extension B v) â†’ adicCompletion L w.val,
    Submodule.pi Set.univ fun w : Extension B v â†¦ (integerSubmodule B L w.val).restrictScalars A]
    â‰ƒâ‚—[A] FiniteAdeleRing B L :=
  have : FaithfulSMul A B := FaithfulSMul.of_field_isFractionRing A B K L
  {
    __ := RestrictedProduct.flatten_equiv'
      (fun w : HeightOneSpectrum B â†¦ SetLike.coe <| w.adicCompletionIntegers L)
      (tendsTo_comap_cofinite A B)
    map_add' x y := rfl
    map_smul' a x := by
      ext w
      change a â€¢ (x (comap A w) âŸ¨w, rflâŸ©) = _
      simp only [Submodule.coe_pi, Submodule.coe_restrictScalars, Algebra.smul_def,
        RingHom.id_apply, Equiv.toFun_as_coe,
        IsScalarTower.algebraMap_apply A B (w.adicCompletion L)]
      rfl
  }

omit [IsIntegralClosure B A L] [FiniteDimensional K L] in
lemma FiniteAdeleRing.restrictedProduct_prod_equiv_apply (f) (w : HeightOneSpectrum B) :
  restrictedProduct_prod_equiv A K L B f w = f (comap A w) âŸ¨w, rflâŸ© := rfl

/-- The `K`-linear isomorphism `L âŠ— A_K^âˆ â‰… A_L^âˆ` given by composing the previous four maps. -/
def FiniteAdeleRing.baseChangeLinearEquiv :
    L âŠ—[K] FiniteAdeleRing A K â‰ƒâ‚—[K] FiniteAdeleRing B L :=
  have : IsScalarTower A K (FiniteAdeleRing B L) := by
    have : IsScalarTower A B (FiniteAdeleRing B L) := IsScalarTower.of_algebraMap_eq' rfl
    apply IsScalarTower.of_algebraMap_eq
    intro x
    nth_rw 2 [RingHom.algebraMap_toAlgebra]
    rw [RingHom.comp_apply, IsScalarTower.algebraMap_apply A B (FiniteAdeleRing B L),
      â† IsScalarTower.algebraMap_apply A K L, IsScalarTower.algebraMap_apply A B L]
    rfl
  let f := (FiniteAdeleRing.tensorEquivTensor A K L B) â‰ªâ‰«â‚—
    (FiniteAdeleRing.tensorEquivRestrictedProduct A K L B) â‰ªâ‰«â‚—
    (FiniteAdeleRing.restrictedProduct_tensorProduct_equiv_restrictedProduct_prod A K L B) â‰ªâ‰«â‚—
    (FiniteAdeleRing.restrictedProduct_prod_equiv A K L B).restrictScalars A
  LinearEquiv.extendScalarsOfIsLocalization (nonZeroDivisors A) K f

@[simp]
lemma algebraMap_apply (x : K) (v : HeightOneSpectrum A) :
    algebraMap K (FiniteAdeleRing A K) x v = algebraMap K (v.adicCompletion K) x := by
  rfl

@[simp]
lemma FiniteAdeleRing.baseChangeLinearEquiv_tmul (b : B) (x : FiniteAdeleRing A K) :
    FiniteAdeleRing.baseChangeLinearEquiv A K L B ((algebraMap B L b) âŠ—â‚œ x) =
    (algebraMap _ (FiniteAdeleRing B L) b) * (algebraMap _ (FiniteAdeleRing B L) x) := by
  ext w
  simp [baseChangeLinearEquiv, restrictedProduct_prod_equiv_apply, tensorEquivTensor_tmul,
    restrictedProduct_tensorProduct_equiv_restrictedProduct_prod_apply,
    tensorEquivRestrictedProduct_tmul, adicCompletionComapIntegerLinearEquiv_tmul_apply,
    BaseChange.algebraMap_apply, IsScalarTower.algebraMap_apply B L (FiniteAdeleRing B L),
    IsScalarTower.algebraMap_apply B L (w.adicCompletion L), -Submodule.coe_pi]

open scoped RestrictedProduct in
theorem FiniteAdeleRing.baseChange_bijective :
    Function.Bijective (SemialgHom.baseChange_of_algebraMap <|
      FiniteAdeleRing.mapSemialgHom A K L B) := by
  suffices â‡‘(SemialgHom.baseChange_of_algebraMap <| FiniteAdeleRing.mapSemialgHom A K L B) =
      â‡‘(FiniteAdeleRing.baseChangeLinearEquiv A K L B) by
      rw [this]
      exact (FiniteAdeleRing.baseChangeLinearEquiv A K L B).bijective
  have : IsScalarTower K L (FiniteAdeleRing B L) := by
    apply IsScalarTower.of_algebraMap_eq' rfl
  rw [â† AlgHom.coe_restrictScalars' (R:=K), â† AlgHom.coe_toLinearMap, â† LinearEquiv.coe_toLinearMap]
  apply congr_arg
  have := IsIntegralClosure.isLocalizedModule A K L B
  apply IsLocalization.tensorProduct_ext (nonZeroDivisors A) B
  intro b x
  ext w
  simp [SemialgHom.baseChange_of_algebraMap_tmul, mapSemialgHom_apply, BaseChange.algebraMap_apply,
    IsScalarTower.algebraMap_apply B L (FiniteAdeleRing B L)]

/-- The `L`-algebra isomorphism `L âŠ—_K ğ”¸_K^âˆ â‰… ğ”¸_L^âˆ`. -/
def FiniteAdeleRing.baseChangeAlgEquiv :
    L âŠ—[K] FiniteAdeleRing A K â‰ƒâ‚[L] FiniteAdeleRing B L :=
  AlgEquiv.ofBijective
    (SemialgHom.baseChange_of_algebraMap <| FiniteAdeleRing.mapSemialgHom A K L B)
    (FiniteAdeleRing.baseChange_bijective A K L B)

/-- The `ğ”¸_K^âˆ`-algebra isomorphism `L âŠ—_K ğ”¸_K^âˆ â‰… ğ”¸_L^âˆ`. -/
def FiniteAdeleRing.baseChangeAdeleAlgEquiv :
    L âŠ—[K] FiniteAdeleRing A K â‰ƒâ‚[FiniteAdeleRing A K] FiniteAdeleRing B L where
  __ := SemialgHom.baseChangeRightOfAlgebraMap <| FiniteAdeleRing.mapSemialgHom A K L B
  __ := FiniteAdeleRing.baseChangeAlgEquiv A K L B

instance : Module.Finite (FiniteAdeleRing A K) (FiniteAdeleRing B L) :=
  Module.Finite.equiv (FiniteAdeleRing.baseChangeAdeleAlgEquiv A K L B).toLinearEquiv

end bijection

section moduleTopology

open scoped RestrictedProduct

attribute [local instance 9999] comap_pi_algebra Algebra.toSMul

/-- `ğ“_v`-module structure on `âˆ L_w` from restricting the scalars of the `K_v`-module structure. -/
noncomputable local instance (v : HeightOneSpectrum A) : Module (adicCompletionIntegers K v)
    ((w : Extension B v) â†’ adicCompletion L w.val) :=
  Module.compHom _ (algebraMap (adicCompletionIntegers K v) (adicCompletion K v))

/-- SMul instance from the module structure. -/
noncomputable local instance (v : HeightOneSpectrum A) : SMul (adicCompletionIntegers K v)
    ((w : Extension B v) â†’ adicCompletion L w.val) :=
  Module.toDistribMulAction.toDistribSMul.toSMul

/-- `âˆ_{wâˆ£v} ğ“_w` as an `ğ“_v`-submodule of `âˆ_{wâˆ£v} L_w` -/
noncomputable def piAdicIntegerSubmodule (v : HeightOneSpectrum A) :
    Submodule (adicCompletionIntegers K v) ((w : Extension B v) â†’ adicCompletion L w.val) :=
  letI (w : Extension B v) := comap_algebra A K L B w.prop
  letI (w : Extension B v) := comap_integer_algebra A K L B w.prop
  letI (w : Extension B v) : Module (adicCompletionIntegers K v) (adicCompletion L w.val) :=
    Module.compHom (adicCompletion L w.val)
      (algebraMap (adicCompletionIntegers K v) (adicCompletion K v))
  Submodule.pi Set.univ fun (w : Extension B v) â†¦
    letI : MulAction (adicCompletionIntegers L w.val) (adicCompletion L w.val) :=
      Algebra.toModule.toMulAction
    have : IsScalarTower (adicCompletionIntegers K v) (adicCompletionIntegers L w.val)
        (adicCompletion L w.val) :=
      IsScalarTower.of_algebraMap_smul fun _ _ â†¦ rfl
    let s := (adicCompletionIntegers L w.val).toSubmodule
    s.restrictScalars (adicCompletionIntegers K v)

-- Help instance synthesis
private noncomputable local instance (priority := 9999) (v : HeightOneSpectrum A) :
    Module (adicCompletion K v) ((w : Extension B v) â†’ adicCompletion L w.val) :=
  Algebra.toModule

/-- An auxiliary ğ”¸_K-module structure on restricted product over v of (product of w's dividing v
of L_w wrt ğ“_w). Only used in this file to compare L âŠ— ğ”¸_K and ğ”¸_L.
-/
noncomputable local instance : Module (FiniteAdeleRing A K)
    Î Ê³ (v : HeightOneSpectrum A), [(w : Extension B v) â†’ adicCompletion L w.1,
    â†‘(piAdicIntegerSubmodule A K L B v)] :=
  inferInstanceAs <| Module
      (Î Ê³ v : HeightOneSpectrum A, [v.adicCompletion K, v.adicCompletionIntegers K])
      Î Ê³ (v : HeightOneSpectrum A), [(w : Extension B v) â†’ adicCompletion L w.1,
    â†‘(piAdicIntegerSubmodule A K L B v)]

open scoped RestrictedProduct in
/-- The continuous `ğ”¸ K`-Linear equivalence between `âˆ'_v âˆ_{wâˆ£v} L_w` and `ğ”¸ L` given by
reaindexing the elements. -/
noncomputable def FiniteAdeleRing.restrictedProduct_pi_equiv :
    Î Ê³ (v : HeightOneSpectrum A), [(w : Extension B v) â†’ adicCompletion L w.val,
      piAdicIntegerSubmodule A K L B v] â‰ƒL[FiniteAdeleRing A K] FiniteAdeleRing B L :=
  have := FaithfulSMul.of_field_isFractionRing A B K L
  let f : _ â‰ƒâ‚œ (FiniteAdeleRing B L) := RestrictedProduct.flatten_homeomorph'
    (G := adicCompletion L) (fun w â†¦ adicCompletionIntegers L w) (tendsTo_comap_cofinite A B)
  {
    __ := f
    map_add' x y := rfl
    map_smul' r x := by
      ext w
      letI (w : HeightOneSpectrum B) := comap_algebra A K L B (w := w) rfl
      rw [RingHom.id_apply, Algebra.smul_def, RestrictedProduct.mul_apply,
        BaseChange.algebraMap_apply, â† Algebra.smul_def]
      rfl
  }

omit [IsIntegralClosure B A L] in
lemma FiniteAdeleRing.restrictedProduct_pi_isModuleTopology : IsModuleTopology (FiniteAdeleRing A K)
    (Î Ê³ (v : HeightOneSpectrum A), [(w : Extension B v) â†’ adicCompletion L w.val,
      piAdicIntegerSubmodule A K L B v]) := by
  have :=
    Module.Finite.equiv (FiniteAdeleRing.restrictedProduct_pi_equiv A K L B).symm.toLinearEquiv
  unfold FiniteAdeleRing at this
  have := prodAdicCompletionComap_isModuleTopology A K L B
  apply RestrictedProduct.isModuleTopology
  Â· exact fun v â†¦ Valued.isOpen_integer (adicCompletion K v)
  Â· intro v
    simp only [piAdicIntegerSubmodule, Submodule.coe_pi, Submodule.coe_restrictScalars]
    apply isOpen_set_pi _ (fun _ _ â†¦ Valued.isOpen_integer _)
    rw [Set.finite_univ_iff]
    exact Extension.finite A K L B v

omit [IsIntegralClosure B A L] in
instance : IsModuleTopology (FiniteAdeleRing A K) (FiniteAdeleRing B L) :=
  have := FiniteAdeleRing.restrictedProduct_pi_isModuleTopology A K L B
  IsModuleTopology.iso (FiniteAdeleRing.restrictedProduct_pi_equiv A K L B)

end moduleTopology

/-- The continuous `ğ”¸_K^âˆ`-algebra isomorphism `L âŠ—_K ğ”¸_K^âˆ â‰… ğ”¸_L^âˆ` -/
noncomputable def FiniteAdeleRing.baseChangeAdeleContinuousAlgEquiv :
    L âŠ—[K] FiniteAdeleRing A K â‰ƒA[FiniteAdeleRing A K] FiniteAdeleRing B L :=
  IsModuleTopology.continuousAlgEquivOfAlgEquiv <| baseChangeAdeleAlgEquiv A K L B

/-- The continuous `L`-algebra isomorphism `L âŠ—_K ğ”¸_K^âˆ â‰… ğ”¸_L^âˆ` -/
noncomputable def FiniteAdeleRing.baseChangeContinuousAlgEquiv :
    L âŠ—[K] FiniteAdeleRing A K â‰ƒA[L] FiniteAdeleRing B L where
  __ := FiniteAdeleRing.baseChangeAlgEquiv A K L B
  __ := FiniteAdeleRing.baseChangeAdeleContinuousAlgEquiv A K L B

end IsDedekindDomain
