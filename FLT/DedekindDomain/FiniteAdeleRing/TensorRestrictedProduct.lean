/-
Copyright (c) 2025 Matthew Jasper. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matthew Jasper
-/

import FLT.DedekindDomain.FiniteAdeleRing.TensorPi
import FLT.DedekindDomain.FiniteAdeleRing.IsDirectLimitRestricted
import Mathlib.RingTheory.Flat.Basic

namespace RestrictedProduct

open TensorProduct

variable (R M : Type*) [CommRing R] [AddCommGroup M] [Module R M]
  {Œπ : Type*} (N : Œπ ‚Üí Type*) [‚àÄ i, AddCommGroup (N i)]
  [‚àÄ i, Module R (N i)]

variable (‚Ñ± : Filter Œπ) (L : ‚àÄ i, Submodule R (N i))

/-- `M ‚äó_R (L i)` as a submodule of `M ‚äó_R (N i)`. -/
def rangeLTensor (i : Œπ) : Submodule R (M ‚äó[R] N i) :=
  LinearMap.range (LinearMap.lTensor M ((L i).subtype))

/-- The `R`-linear map `œÜ : M ‚äó_R ‚àè'_i [N i, L i]_[ùìï] ‚Üí ‚àè'_i [M ‚äó_R (N i), M ‚äó_R (L i)]_[ùìï]`
given by `œÜ (m ‚äó n) i = m ‚äó (n i)`. -/
def lTensor :
    M ‚äó[R] Œ† ≥ i, [N i, L i]_[‚Ñ±] ‚Üí‚Çó[R] Œ† ≥ i, [M ‚äó[R] N i, rangeLTensor R M N L i]_[‚Ñ±] :=
  have hmap : ‚àÄ (m : M), ‚àÄ·∂† (j : Œπ) in ‚Ñ±, Set.MapsTo
      (TensorProduct.mk R M (N j) m) (L j) (rangeLTensor R M N L j) := by
    intro m
    filter_upwards with i n hn using ‚ü®m ‚äó‚Çú[R] ‚ü®n, hn‚ü©, rfl‚ü©
  TensorProduct.lift {
    toFun m := mapAlongLinearMap N _ id Filter.tendsto_id
        (fun i ‚Ü¶ TensorProduct.mk R M (N i) m) (hmap m)
    map_add' m n := by ext; simp
    map_smul' a m := by ext; simp
  }

@[simp]
lemma lTensor_tmul (m : M) (f : Œ† ≥ i, [N i, L i]_[‚Ñ±]) (i : Œπ) :
    lTensor R M N ‚Ñ± L (m ‚äó‚Çú f) i = m ‚äó‚Çú (f i) :=
  rfl

variable (S : Set Œπ) [Module.FinitePresentation R M] [Module.Flat R M]

/-- `R`-Linear isomorphism between `M ‚äó_R (L i)` and `rangeLTensor R M N L i`. -/
noncomputable def tmulEquivRangeLTensor (i : Œπ) : M ‚äó[R] (L i) ‚âÉ‚Çó[R] rangeLTensor R M N L i :=
  LinearEquiv.ofInjective (LinearMap.lTensor M (Submodule.subtype <| L i))
    (Module.Flat.lTensor_preserves_injective_linearMap (L i).subtype
      (Submodule.injective_subtype (L i)))

open scoped Filter in
/-- `R`-Linear isomorphism that's propositionally equal to `lTensor`. -/
noncomputable def lTensorPrincipalEquiv :
    M ‚äó[R] Œ† ≥ i, [N i, L i]_[ùìü S] ‚âÉ‚Çó[R] Œ† ≥ i, [M ‚äó[R] N i, rangeLTensor R M N L i]_[ùìü S] :=
  open scoped Classical in
  let N' (i : Œπ) := if i ‚àà S then L i else (‚ä§ : Submodule R (N i))
  let f : Œ† ≥ i, [N i, L i]_[ùìü S] ‚âÉ‚Çó[R] (Œ† i, N' i) := {
    toFun x i := ‚ü®x i, by
      by_cases h : i ‚àà S
      ¬∑ simpa [N', h] using x.property h
      ¬∑ simp [N', h]‚ü©
    invFun x := ‚ü®fun i ‚Ü¶ x i, by
      rw [Filter.eventually_principal]
      intro y hy
      simpa only [N', hy, ‚ÜìreduceIte] using (x y).prop‚ü©
    map_add' x y := by ext; simp
    map_smul' a x := by ext; simp
  }
  let g1 : M ‚äó[R] Œ† ≥ i, [N i, L i]_[ùìü S] ‚âÉ‚Çó[R] M ‚äó[R] (Œ† i, N' i) := LinearEquiv.lTensor M f
  let g2 : M ‚äó[R] (Œ† i, N' i) ‚âÉ‚Çó[R] Œ† i, M ‚äó[R] N' i :=
    tensorPi_equiv_piTensor' R M fun i ‚Ü¶ ‚Ü•(N' i)
  let gEquiv (i : Œπ) (h : i ‚àà S) : M ‚äó[R] (N' i) ‚âÉ‚Çó[R] rangeLTensor R M N L i :=
    (LinearEquiv.lTensor M (LinearEquiv.ofEq _ _ (by simp [N', h]))) ‚â™‚â´‚Çó
      (tmulEquivRangeLTensor R M N L i)
  let gEquiv' (i : Œπ) (h : i ‚àâ S) : M ‚äó[R] (N' i) ‚âÉ‚Çó[R] M ‚äó[R] N i :=
    LinearEquiv.lTensor M <| LinearEquiv.ofTop (N' i) (by simp [N', h])
  let g3 : (Œ† i, M ‚äó[R] N' i) ‚âÉ‚Çó[R] Œ† ≥ i, [M ‚äó[R] N i, rangeLTensor R M N L i]_[ùìü S] := {
    toFun x := ‚ü®
      fun i ‚Ü¶ if h : i ‚àà S then gEquiv i h (x i) else gEquiv' i h (x i),
      by
        rw [Filter.eventually_principal]
        intro i h
        simp [h]‚ü©
    invFun x i := if h : i ‚àà S then
        gEquiv i h |>.symm ‚ü®(x i), by simpa using x.property h‚ü©
      else
        gEquiv' i h |>.symm (x i)
    left_inv x := by
      ext i
      by_cases h : i ‚àà S <;> simp [h]
    right_inv x := by
      ext i
      by_cases h : i ‚àà S <;> simp [h]
    map_add' x y := by
      ext i
      by_cases h : i ‚àà S <;> simp [h]
    map_smul' a x := by
      ext i
      by_cases h : i ‚àà S <;> simp [h]
  }
  g1 ‚â™‚â´‚Çó g2 ‚â™‚â´‚Çó g3

open scoped Filter in
lemma lTensorPrincipalEquiv_tmul (m : M) (x : Œ† ≥ i, [N i, L i]_[ùìü S]) (i : Œπ) :
    lTensorPrincipalEquiv R M N L S (m ‚äó‚Çú x) i = m ‚äó‚Çú x i := by
  simp [lTensorPrincipalEquiv, tensorPi_equiv_piTensor'_apply, tmulEquivRangeLTensor,
      rangeLTensor]

open scoped Filter in
lemma lTensor_bijective : Function.Bijective (lTensor R M N ‚Ñ± L) := by
  let comp1 := TensorProduct.directLimitRight (inclusion_module (C:=L) (‚Ñ± := ‚Ñ±)) M (R:=R)
  let comp := IsDirectLimit.linearEquiv_of_isDirectLimit
    (inclusion_module (‚Ñ± := ‚Ñ±)) (Œ† ≥ i, [N i, L i]_[‚Ñ±])
    (Module.DirectLimit (mem_A_away_from_S L) (inclusion_module (C:=L) (‚Ñ± := ‚Ñ±)))
    (inclusion_to_restricted_product_module) (Module.DirectLimit.of R _
    (mem_A_away_from_S L) (inclusion_module (‚Ñ± := ‚Ñ±)))
  let comp2 : Œ† ≥ (i : Œπ), [M ‚äó[R] N i, (rangeLTensor R M N L  i)]_[‚Ñ±] ‚âÉ‚Çó[R]
    Module.DirectLimit (mem_A_away_from_S (fun i ‚Ü¶ rangeLTensor R M N L i)) inclusion_module :=
    IsDirectLimit.linearEquiv_of_isDirectLimit (inclusion_module (‚Ñ± := ‚Ñ±))
      Œ† ≥ i, [M ‚äó[R] N i, rangeLTensor R M N L i]_[‚Ñ±]
      (Module.DirectLimit (mem_A_away_from_S
        (fun i ‚Ü¶ rangeLTensor R M N L i)) (inclusion_module (‚Ñ± := ‚Ñ±)))
      (inclusion_to_restricted_product_module) (Module.DirectLimit.of R _
        (mem_A_away_from_S
          (fun i ‚Ü¶ rangeLTensor R M N L i)) (inclusion_module (‚Ñ± := ‚Ñ±)))
  let comp4 :=  (LinearEquiv.lTensor M comp) ‚â™‚â´‚Çó comp1
  let comp5 : Module.DirectLimit (fun (S: ‚Ñ±.complement) ‚Ü¶
    M ‚äó[R] Œ† ≥ (i : Œπ), [N i, L i]_[ùìü S·∂ú]) (fun i j h ‚Ü¶
    LinearMap.lTensor M (inclusion_module i j h)) ‚âÉ‚Çó[R] Module.DirectLimit (fun (S : ‚Ñ±.complement)
    ‚Ü¶ Œ† ≥ (i : Œπ), [M ‚äó[R] N i, (rangeLTensor R M N L i)]_[ùìü (S)·∂ú]) inclusion_module :=
    Module.DirectLimit.congr (fun (S : ‚Ñ±.complement) ‚Ü¶
      (RestrictedProduct.lTensorPrincipalEquiv R M N L (S·∂ú : Set Œπ))) (by
        intro i j hij
        refine TensorProduct.ext' (fun x y ‚Ü¶ ?_)
        ext k
        simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply,
          LinearMap.lTensor_tmul, lTensorPrincipalEquiv_tmul]
        change x ‚äó‚Çú[R] y k = ((lTensorPrincipalEquiv R M N L i·∂ú) (x ‚äó‚Çú[R] y)) k
        rw [lTensorPrincipalEquiv_tmul])
  let tensor_comm' := comp4 ‚â™‚â´‚Çó comp5 ‚â™‚â´‚Çó comp2.symm
  have : RestrictedProduct.lTensor R M N ‚Ñ± L  = tensor_comm' := by
    ext m x i
    simp only [TensorProduct.AlgebraTensorModule.curry_apply, TensorProduct.curry_apply,
      LinearMap.coe_restrictScalars, lTensor_tmul, LinearEquiv.coe_coe, tensor_comm', comp2,
      comp4, comp5, comp, comp1, LinearEquiv.trans_apply, LinearEquiv.lTensor_tmul]
    obtain ‚ü®j, x', hjx'‚ü© :=
      Module.DirectLimit.exists_of (IsDirectLimit.linearEquiv_of_isDirectLimit inclusion_module
        Œ† ≥ (i : Œπ), [N i, L i]_[‚Ñ±] (Module.DirectLimit (mem_A_away_from_S L) inclusion_module)
        inclusion_to_restricted_product_module
        (Module.DirectLimit.of R (‚Ñ±.complement) (mem_A_away_from_S L) inclusion_module) x)
    rw [‚Üê hjx', TensorProduct.directLimitRight_tmul_of]
    unfold mem_A_away_from_S
    rw [Module.DirectLimit.congr_apply_of]
    simp only [rangeLTensor, lTensorPrincipalEquiv, Set.mem_compl_iff, tmulEquivRangeLTensor,
      LinearEquiv.trans_apply, LinearEquiv.ofInjective_apply, LinearEquiv.trans_symm,
      LinearEquiv.lTensor_tmul, LinearEquiv.coe_mk, LinearMap.coe_mk, AddHom.coe_mk,
      tensorPi_equiv_piTensor'_apply, LinearMap.lTensor_tmul, Submodule.subtype_apply,
      LinearEquiv.coe_ofEq_apply, LinearEquiv.ofTop_apply, dite_eq_ite, ite_self,
      IsDirectLimit.iso_of_isDirectLimit_symm_apply, inclusion_to_restricted_product_module,
      inclusion_to_restrictedProduct, inclusion_apply, mk_apply]
    apply_fun (IsDirectLimit.linearEquiv_of_isDirectLimit inclusion_module
      Œ† ≥ (i : Œπ), [N i, L i]_[‚Ñ±]
      (Module.DirectLimit (mem_A_away_from_S L) inclusion_module)
      inclusion_to_restricted_product_module
      (Module.DirectLimit.of R (‚Üë‚Ñ±.complement) (mem_A_away_from_S L) inclusion_module)).symm at hjx'
    simp only [IsDirectLimit.iso_of_isDirectLimit_symm_apply,
      inclusion_to_restricted_product_module, inclusion_to_restrictedProduct, LinearMap.coe_mk,
      AddHom.coe_mk, LinearEquiv.symm_apply_apply] at hjx'
    rw [‚Üê congrFun (congrArg DFunLike.coe hjx') i, inclusion_apply]
  rw [this]
  exact tensor_comm'.bijective



/-- The `R`-linear isomorphism given by `lTensor` when `M` is a finite flat `R`-module. -/
noncomputable def lTensorEquiv :
    M ‚äó[R] Œ† ≥ i, [N i, L i]_[‚Ñ±] ‚âÉ‚Çó[R] Œ† ≥ i, [M ‚äó[R] N i, rangeLTensor R M N L i]_[‚Ñ±] :=
  LinearEquiv.ofBijective (lTensor R M N ‚Ñ± L) (lTensor_bijective R M N ‚Ñ± L)

@[simp]
lemma lTensorEquiv_tmul (m : M) (f : Œ† ≥ i, [N i, L i]_[‚Ñ±]) (i : Œπ) :
    lTensorEquiv R M N ‚Ñ± L (m ‚äó‚Çú f) i = m ‚äó‚Çú (f i) :=
  rfl

end RestrictedProduct
