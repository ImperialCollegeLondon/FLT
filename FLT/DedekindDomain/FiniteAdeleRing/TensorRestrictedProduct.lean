/-
Copyright (c) 2025 Matthew Jasper. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matthew Jasper
-/

import FLT.DedekindDomain.FiniteAdeleRing.TensorPi
import FLT.DedekindDomain.FiniteAdeleRing.IsDirectLimitRestricted
import Mathlib.RingTheory.Flat.Basic

namespace RestrictedProduct

open TensorProduct

variable (R M : Type*) [CommRing R] [AddCommGroup M] [Module R M]
  {Î¹ : Type*} (N : Î¹ â†’ Type*) [âˆ€ i, AddCommGroup (N i)]
  [âˆ€ i, Module R (N i)]

variable (â„± : Filter Î¹) (L : âˆ€ i, Submodule R (N i))

/-- `M âŠ—_R (L i)` as a submodule of `M âŠ—_R (N i)`. -/
def rangeLTensor (i : Î¹) : Submodule R (M âŠ—[R] N i) :=
  LinearMap.range (LinearMap.lTensor M ((L i).subtype))

/-- The `R`-linear map `Ï† : M âŠ—_R âˆ'_i [N i, L i]_[ğ“•] â†’ âˆ'_i [M âŠ—_R (N i), M âŠ—_R (L i)]_[ğ“•]`
given by `Ï† (m âŠ— n) i = m âŠ— (n i)`. -/
def lTensor :
    M âŠ—[R] Î Ê³ i, [N i, L i]_[â„±] â†’â‚—[R] Î Ê³ i, [M âŠ—[R] N i, rangeLTensor R M N L i]_[â„±] :=
  have hmap : âˆ€ (m : M), âˆ€á¶  (j : Î¹) in â„±, Set.MapsTo
      (TensorProduct.mk R M (N j) m) (L j) (rangeLTensor R M N L j) := by
    intro m
    filter_upwards with i n hn using âŸ¨m âŠ—â‚œ[R] âŸ¨n, hnâŸ©, rflâŸ©
  TensorProduct.lift {
    toFun m := mapAlongLinearMap N _ id Filter.tendsto_id
        (fun i â†¦ TensorProduct.mk R M (N i) m) (hmap m)
    map_add' m n := by ext; simp
    map_smul' a m := by ext; simp
  }

@[simp]
lemma lTensor_tmul (m : M) (f : Î Ê³ i, [N i, L i]_[â„±]) (i : Î¹) :
    lTensor R M N â„± L (m âŠ—â‚œ f) i = m âŠ—â‚œ (f i) :=
  rfl

variable (S : Set Î¹) [Module.FinitePresentation R M] [Module.Flat R M]

/-- `R`-Linear isomorphism between `M âŠ—_R (L i)` and `rangeLTensor R M N L i`. -/
noncomputable def tmulEquivRangeLTensor (i : Î¹) : M âŠ—[R] (L i) â‰ƒâ‚—[R] rangeLTensor R M N L i :=
  LinearEquiv.ofInjective (LinearMap.lTensor M (Submodule.subtype <| L i))
    (Module.Flat.lTensor_preserves_injective_linearMap (L i).subtype
      (Submodule.injective_subtype (L i)))

open scoped Filter in
/-- `R`-Linear isomorphism that's propositionally equal to `lTensor`. -/
noncomputable def lTensorPrincipalEquiv :
    M âŠ—[R] Î Ê³ i, [N i, L i]_[ğ“Ÿ S] â‰ƒâ‚—[R] Î Ê³ i, [M âŠ—[R] N i, rangeLTensor R M N L i]_[ğ“Ÿ S] :=
  open scoped Classical in
  let N' (i : Î¹) := if i âˆˆ S then L i else (âŠ¤ : Submodule R (N i))
  let f : Î Ê³ i, [N i, L i]_[ğ“Ÿ S] â‰ƒâ‚—[R] (Î  i, N' i) := {
    toFun x i := âŸ¨x i, by
      by_cases h : i âˆˆ S
      Â· simpa [N', h] using x.property h
      Â· simp [N', h]âŸ©
    invFun x := âŸ¨fun i â†¦ x i, by
      rw [Filter.eventually_principal]
      intro y hy
      simpa only [N', hy, â†“reduceIte] using (x y).propâŸ©
    map_add' x y := by ext; simp
    map_smul' a x := by ext; simp
  }
  let g1 : M âŠ—[R] Î Ê³ i, [N i, L i]_[ğ“Ÿ S] â‰ƒâ‚—[R] M âŠ—[R] (Î  i, N' i) := LinearEquiv.lTensor M f
  let g2 : M âŠ—[R] (Î  i, N' i) â‰ƒâ‚—[R] Î  i, M âŠ—[R] N' i :=
    tensorPi_equiv_piTensor' R M fun i â†¦ â†¥(N' i)
  let gEquiv (i : Î¹) (h : i âˆˆ S) : M âŠ—[R] (N' i) â‰ƒâ‚—[R] rangeLTensor R M N L i :=
    (LinearEquiv.lTensor M (LinearEquiv.ofEq _ _ (by simp [N', h]))) â‰ªâ‰«â‚—
      (tmulEquivRangeLTensor R M N L i)
  let gEquiv' (i : Î¹) (h : i âˆ‰ S) : M âŠ—[R] (N' i) â‰ƒâ‚—[R] M âŠ—[R] N i :=
    LinearEquiv.lTensor M <| LinearEquiv.ofTop (N' i) (by simp [N', h])
  let g3 : (Î  i, M âŠ—[R] N' i) â‰ƒâ‚—[R] Î Ê³ i, [M âŠ—[R] N i, rangeLTensor R M N L i]_[ğ“Ÿ S] := {
    toFun x := âŸ¨
      fun i â†¦ if h : i âˆˆ S then gEquiv i h (x i) else gEquiv' i h (x i),
      by
        rw [Filter.eventually_principal]
        intro i h
        simp [h]âŸ©
    invFun x i := if h : i âˆˆ S then
        gEquiv i h |>.symm âŸ¨(x i), by simpa using x.property hâŸ©
      else
        gEquiv' i h |>.symm (x i)
    left_inv x := by
      ext i
      by_cases h : i âˆˆ S <;> simp [h]
    right_inv x := by
      ext i
      by_cases h : i âˆˆ S <;> simp [h]
    map_add' x y := by
      ext i
      by_cases h : i âˆˆ S <;> simp [h]
    map_smul' a x := by
      ext i
      by_cases h : i âˆˆ S <;> simp [h]
  }
  g1 â‰ªâ‰«â‚— g2 â‰ªâ‰«â‚— g3

open scoped Filter in
lemma lTensorPrincipalEquiv_tmul (m : M) (x : Î Ê³ i, [N i, L i]_[ğ“Ÿ S]) (i : Î¹) :
    lTensorPrincipalEquiv R M N L S (m âŠ—â‚œ x) i = m âŠ—â‚œ x i := by
  simp [lTensorPrincipalEquiv, tensorPi_equiv_piTensor'_apply, tmulEquivRangeLTensor,
      rangeLTensor]

open scoped Filter in
lemma lTensor_bijective : Function.Bijective (lTensor R M N â„± L) := by
  classical
  let comp1 := TensorProduct.directLimitRight (inclusionLinearMap (C:=L) (â„±:=â„±)) M (R:=R)
  let comp := IsDirectLimit.Module.linearEquiv
     (Î Ê³ i, [N i, L i]_[â„±])
    (Module.DirectLimit (fun (S : â„±.setsáµ’áµˆ) â†¦ Î Ê³ i, [N i, L i]_[ğ“Ÿ S.1])
    (inclusionLinearMap (C:=L) (â„±:=â„±))) (inclusionLinearMap (â„±:=â„±))
    (coeLinearMap) (Module.DirectLimit.of R _
    (fun (S : â„±.setsáµ’áµˆ) â†¦ Î Ê³ i, [N i, L i]_[ğ“Ÿ S.1]) (inclusionLinearMap (â„± := â„±)))
  let comp2 : Î Ê³ (i : Î¹), [M âŠ—[R] N i, (rangeLTensor R M N L i)]_[â„±] â‰ƒâ‚—[R]
    Module.DirectLimit
      (fun (S : â„±.setsáµ’áµˆ) â†¦ Î Ê³ i, [(M âŠ—[R] N i), rangeLTensor R M N L i]_[ğ“Ÿ S.1])
      inclusionLinearMap := IsDirectLimit.Module.linearEquiv  Î Ê³ i,
    [M âŠ—[R] N i, rangeLTensor R M N L i]_[â„±]
    (Module.DirectLimit (fun (S : â„±.setsáµ’áµˆ) â†¦ Î Ê³ i, [M âŠ—[R] N i, rangeLTensor R M N L i]_[ğ“Ÿ S.1])
    (inclusionLinearMap (â„±:=â„±))) (inclusionLinearMap (â„±:=â„±)) (coeLinearMap)
    (Module.DirectLimit.of R _
    (fun (S : â„±.setsáµ’áµˆ) â†¦ Î Ê³ i, [(M âŠ—[R] N i), rangeLTensor R M N L i]_[ğ“Ÿ S.1])
    (inclusionLinearMap (â„±:=â„±)))
  let comp4 :=  (LinearEquiv.lTensor M comp) â‰ªâ‰«â‚— comp1
  let comp5 : Module.DirectLimit (fun (S : â„±.setsáµ’áµˆ) â†¦
    M âŠ—[R] Î Ê³ (i : Î¹), [N i, L i]_[ğ“Ÿ S.1]) (fun i j h â†¦
    LinearMap.lTensor M (inclusionLinearMap i j h)) â‰ƒâ‚—[R] Module.DirectLimit (fun (S : â„±.setsáµ’áµˆ)
    â†¦ Î Ê³ (i : Î¹), [M âŠ—[R] N i, (rangeLTensor R M N L i)]_[ğ“Ÿ (S.1)]) inclusionLinearMap :=
    Module.DirectLimit.congr (fun (S : â„±.setsáµ’áµˆ) â†¦
      (RestrictedProduct.lTensorPrincipalEquiv R M N L (S.1 : Set Î¹))) (by
        intro i j hij
        refine TensorProduct.ext' (fun x y â†¦ ?_)
        ext k
        simp only [LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply,
          LinearMap.lTensor_tmul, lTensorPrincipalEquiv_tmul]
        change x âŠ—â‚œ[R] y k = ((lTensorPrincipalEquiv R M N L i.1) (x âŠ—â‚œ[R] y)) k
        rw [lTensorPrincipalEquiv_tmul])
  let tensor_comm' := comp4 â‰ªâ‰«â‚— comp5 â‰ªâ‰«â‚— comp2.symm
  have : RestrictedProduct.lTensor R M N â„± L  = tensor_comm' := by
    ext m x i
    simp only [TensorProduct.AlgebraTensorModule.curry_apply, TensorProduct.curry_apply,
      LinearMap.coe_restrictScalars, lTensor_tmul, LinearEquiv.coe_coe, tensor_comm', comp2,
      comp4, comp5, comp, comp1, LinearEquiv.trans_apply, LinearEquiv.lTensor_tmul]
    obtain âŸ¨j, x', hjx'âŸ© :=
      Module.DirectLimit.exists_of (IsDirectLimit.Module.linearEquiv
        Î Ê³ (i : Î¹), [N i, L i]_[â„±] (Module.DirectLimit
        (fun (S : â„±.setsáµ’áµˆ) â†¦ Î Ê³ i, [N i, L i]_[ğ“Ÿ S.1]) inclusionLinearMap)
        inclusionLinearMap coeLinearMap
        (Module.DirectLimit.of R (â„±.setsáµ’áµˆ) (fun (S : â„±.setsáµ’áµˆ) â†¦ Î Ê³ i, [N i, L i]_[ğ“Ÿ S.1])
        inclusionLinearMap) x)
    rw [â† hjx', TensorProduct.directLimitRight_tmul_of]
    rw [Module.DirectLimit.congr_apply_of]
    simp only [rangeLTensor, lTensorPrincipalEquiv, tmulEquivRangeLTensor,
      LinearEquiv.trans_apply, LinearEquiv.ofInjective_apply, LinearEquiv.trans_symm,
      LinearEquiv.lTensor_tmul, LinearEquiv.coe_mk, LinearMap.coe_mk, AddHom.coe_mk,
      tensorPi_equiv_piTensor'_apply, LinearMap.lTensor_tmul, Submodule.subtype_apply,
      LinearEquiv.coe_ofEq_apply, LinearEquiv.ofTop_apply, dite_eq_ite, ite_self,
      IsDirectLimit.Module.linearEquiv_symm_apply,
      coeLinearMap]
    apply_fun (IsDirectLimit.Module.linearEquiv Î Ê³ (i : Î¹), [N i, L i]_[â„±]
      (Module.DirectLimit (fun (S : â„±.setsáµ’áµˆ) â†¦ Î Ê³ i, [N i, L i]_[ğ“Ÿ S.1]) inclusionLinearMap)
      inclusionLinearMap coeLinearMap
      (Module.DirectLimit.of R (â„±.setsáµ’áµˆ) (fun (S : â„±.setsáµ’áµˆ) â†¦ Î Ê³ i, [N i, L i]_[ğ“Ÿ S.1])
      inclusionLinearMap)).symm at hjx'
    simp only [IsDirectLimit.Module.linearEquiv_symm_apply, coeLinearMap,
      LinearMap.coe_mk, AddHom.coe_mk,
      LinearEquiv.symm_apply_apply] at hjx'
    rw [â† congrFun (congrArg DFunLike.coe hjx') i, inclusion_apply]
    rfl
  rw [this]
  exact tensor_comm'.bijective

/-- The `R`-linear isomorphism given by `lTensor` when `M` is a finite flat `R`-module. -/
noncomputable def lTensorEquiv :
    M âŠ—[R] Î Ê³ i, [N i, L i]_[â„±] â‰ƒâ‚—[R] Î Ê³ i, [M âŠ—[R] N i, rangeLTensor R M N L i]_[â„±] :=
  LinearEquiv.ofBijective (lTensor R M N â„± L) (lTensor_bijective R M N â„± L)

@[simp]
lemma lTensorEquiv_tmul (m : M) (f : Î Ê³ i, [N i, L i]_[â„±]) (i : Î¹) :
    lTensorEquiv R M N â„± L (m âŠ—â‚œ f) i = m âŠ—â‚œ (f i) :=
  rfl

end RestrictedProduct
