/-
Copyright (c) 2025 Madison Crim. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Madison Crim
-/

import FLT.Mathlib.Topology.Algebra.RestrictedProduct.Basic
import FLT.DedekindDomain.FiniteAdeleRing.TensorPi
import FLT.Mathlib.Algebra.IsDirectLimit

namespace RestrictedProduct

open Set Filter

section directedSystem

variable {Î¹ : Type*} {ğ“• : Filter Î¹}

instance : Nonempty ğ“•.setsáµ’áµˆ := âŸ¨âŠ¤, by simpâŸ©

variable {R : Î¹ â†’ Type*} {A : Î¹ â†’ Type*} [Î  i, SetLike (A i) (R i)] {C : Î  i, A i}

instance instDirectedSystem :
    DirectedSystem (fun (S : ğ“•.setsáµ’áµˆ) â†¦ Î Ê³ i, [R i, C i]_[ğ“Ÿ S.1])
      (fun _ _ h â†¦ RestrictedProduct.inclusion _ _ <| monotone_principal h) where
  map_self _ _ := rfl
  map_map _ _ _ _ _ _ := rfl

end directedSystem

section inclusion

open scoped RestrictedProduct TensorProduct IsDirectLimit

variable {A : Type*} [CommRing A] {Î¹ : Type*} {R : Î¹ â†’ Type*} {â„± : Filter Î¹}
  [Î  i, AddCommGroup (R i)] [âˆ€ i, Module A (R i)] {C : âˆ€ i, Submodule A (R i)} {M : Type*}
  [AddCommGroup M] [Module A M] [Module.FinitePresentation A M] (S : â„±.setsáµ’áµˆ)

open Set Filter RestrictedProduct

/-- A linear map version of `RestrictedProduct.inclusion` :
if `ğ“• â‰¤ ğ“–` then there's a linear map
`Î Ê³ i, [R i, C i]_[ğ“–] â†’â‚—[A] Î Ê³ i, [R i, C i]_[ğ“•]` where the `R i`
are `A`-modules and the `C i` are submodules.
-/
def inclusionLinearMap {ğ“• ğ“– : Filter Î¹} (h : ğ“• â‰¤ ğ“–) :
    Î Ê³ i, [R i, C i]_[ğ“–] â†’â‚—[A] Î Ê³ i, [R i, C i]_[ğ“•] :=
  mapAlongLinearMap R R id h (fun _ â†¦ .id)
  (Filter.Eventually.of_forall <| fun _ _ â†¦ id)

lemma inclusionLinearMap_apply {ğ“• ğ“– : Filter Î¹} (h : ğ“• â‰¤ ğ“–) (x : Î Ê³ i, [R i, C i]_[ğ“–]) :
  inclusionLinearMap h x = âŸ¨x.1, x.2.filter_mono hâŸ© := rfl

instance : DirectedSystem (fun (S : â„±.setsáµ’áµˆ) â†¦ Î Ê³ i, [R i, C i]_[ğ“Ÿ S.1])
    fun _ _ h â†¦ (inclusionLinearMap <| monotone_principal h) :=
  RestrictedProduct.instDirectedSystem

instance directed : IsDirected (â„±.setsáµ’áµˆ) (Â· â‰¤ Â·) where
    directed Si Sj := by
      obtain âŸ¨Si, hiâŸ© := Si
      obtain âŸ¨Sj, hjâŸ© := Sj
      use âŸ¨Si âˆ© Sj, â„±.inter_sets hi hjâŸ©, inter_subset_left, inter_subset_right

end inclusion

variable {Î¹ : Type*} (R : Î¹ â†’ Type*) (S : Set Î¹) [âˆ€ i, Decidable (i âˆˆ S)] (A : (i : Î¹) â†’ Set (R i))

open scoped Filter

section type

/-- The canonical isomorphism between `Î Ê³ i, [R i, A i]_[ğ“Ÿ S]` and
`(Î  i âˆˆ S, R i) Ã— (Î  i âˆ‰ S, A i)`
-/
def principalEquivProd : Î Ê³ i, [R i, A i]_[ğ“Ÿ S] â‰ƒ
    (Î  i : {i // i âˆˆ S}, A i) Ã— (Î  i : {i // i âˆ‰ S}, R i) where
  toFun x := (fun i â†¦ âŸ¨x i, x.property i.propertyâŸ©, fun i â†¦ x i)
  invFun y := âŸ¨fun i â†¦ if hi : i âˆˆ S then y.1 âŸ¨i, hiâŸ© else y.2 âŸ¨i, hiâŸ©,
  Filter.eventually_principal.mpr (fun i hi â†¦ by simp only [hi]; exact (y.1 âŸ¨i, hiâŸ©).2)âŸ©
  left_inv x := by
    ext
    simp
  right_inv x := by aesop

end type

variable {T : Î¹ â†’ Type*} [Î  i, SetLike (T i) (R i)] {A : Î  i, T i}

section monoid

-- TODO move to FLT/Mathlib
/-- Monoid equivalence version of `principalEquivProd`. -/
@[to_additive /-- Additive monoid equivalence of principalEquivProd. -/]
def principalMulEquivProd [Î  i, Monoid (R i)] [âˆ€ i, SubmonoidClass (T i) (R i)] :
    Î Ê³ i, [R i, A i]_[ğ“Ÿ S] â‰ƒ* (Î  i : {i // i âˆˆ S}, A i) Ã— (Î  i : {i // i âˆ‰ S}, R i) where
  __ := principalEquivProd R S _
  map_mul' _ _ := rfl

end monoid

variable {Î¹ : Type*} (R : Î¹ â†’ Type*) {â„± : Filter Î¹} (A : Type*) [CommRing A]

open scoped RestrictedProduct TensorProduct

open Filter

section module

-- TODO move to FLT/Mathlib
/-- Module equivalence version of `principalEquivProd`. -/
noncomputable def principalLinearEquivProd [Î  i, AddCommGroup (R i)]
    [âˆ€ i, Module A (R i)] {C : âˆ€ i, Submodule A (R i)}
    (S : Set Î¹) [âˆ€ i, Decidable (i âˆˆ S)] :
    (Î Ê³ i, [R i, C i]_[ğ“Ÿ S]) â‰ƒâ‚—[A] ((Î  i : {i // i âˆˆ S}, C i) Ã—
      (Î  i : {i // i âˆ‰ S}, R i)) where
  __ := principalAddEquivSum R S (A := C)
  map_smul' _ _ := rfl

variable [Î  i, AddCommGroup (R i)] [âˆ€ i, Module A (R i)] {C : âˆ€ i, Submodule A (R i)}

/-- If `ğ“• â‰¤ ğ“–`, the restricted product `Î Ê³ i, [R i, A i]_[ğ“–]` is naturally included in
`Î Ê³ i, [R i, A i]_[ğ“•]`. This is the corresponding map. -/
def linclusion
    {Î¹ : Type*} {Râ‚€ : Type*} (R : Î¹ â†’ Type*) [Semiring Râ‚€] [âˆ€ i, AddCommMonoid (R i)]
    [âˆ€ i, Module Râ‚€ (R i)] (A : (i : Î¹) â†’ Submodule Râ‚€ (R i)) {â„± ğ“– : Filter Î¹}
    (h : â„± â‰¤ ğ“–) : Î Ê³ i, [R i, A i]_[ğ“–] â†’â‚—[Râ‚€] Î Ê³ i, [R i, A i]_[â„±] where
  toFun := inclusion R (A Â·) h
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

instance {I : Type*} [Preorder I] (ğ“– : I â†’ Filter Î¹) (hğ“– : Antitone ğ“–) :
    DirectedSystem (fun x â†¦ Î Ê³ (i : Î¹), [R i, â†‘(C i)]_[ğ“– x]) (linclusion _ _ <| @hğ“– Â· Â· Â·) where
  map_self _ _ := rfl
  map_map _ _ _ _ _ _ := rfl

instance instIsDirectLimit {I : Type*} [Preorder I] [Nonempty I] [IsDirected I (Â· â‰¤ Â·)]
    (ğ“– : I â†’ Filter Î¹) (hğ“– : Antitone ğ“–) (hâ„± : â„± = iInf ğ“–) :
    IsDirectLimit (linclusion R C <| @hğ“– Â· Â· Â·)
    (linclusion R C <| hâ„±.trans_le <| iInf_le ğ“– Â·) where
  inj Sáµ¢ Sâ±¼ mi mj hmij := by
    obtain âŸ¨Sâ‚–, hik, hjkâŸ© := @directed_of _ (Â· â‰¤ Â·) _ Sáµ¢ Sâ±¼
    refine âŸ¨Sâ‚–, hik, hjk, ?_âŸ©
    apply Subtype.ext
    injection hmij
  surj r := by
    dsimp [inclusionLinearMap]
    have : { i : Î¹ | r.1 i âˆˆ (C i : Set (R i)) } âˆˆ (iInf ğ“–) := hâ„± â–¸ r.property
    obtain âŸ¨j, hjâŸ© := (Filter.mem_iInf_of_directed hğ“–.directed_ge _).mp this
    use j
    apply RestrictedProduct.exists_inclusion_eq_of_eventually _ _ _ hj
    rw [hâ„±]
    exact iInf_le_iff.mpr fun b a â†¦ a j
  compatibility i j hij x := by
    dsimp [inclusionLinearMap, RestrictedProduct.inclusion, RestrictedProduct.inclusion]
    exact Subtype.ext rfl

instance instIsDirectLimit' : IsDirectLimit (M := fun (S : â„±.setsáµ’áµˆ) â†¦ Î Ê³ i, [R i, C i]_[ğ“Ÿ S.1])
    ((fun _ _ h â†¦ inclusionLinearMap <| monotone_principal h))
    (fun S â†¦ inclusionLinearMap <| Filter.le_principal_iff.2 S.2) := by
  apply instIsDirectLimit
  Â· intro i j hij
    simpa only [le_principal_iff, mem_principal]
  Â· exact eq_iInf_of_mem_iff_exists_mem (fun {s} â†¦ âŸ¨fun h â†¦ âŸ¨âŸ¨s, hâŸ©, subset_refl sâŸ©,
      fun âŸ¨i, hiâŸ© â†¦ Filter.mem_of_superset i.2 hiâŸ©)

end module

end RestrictedProduct
#lint
