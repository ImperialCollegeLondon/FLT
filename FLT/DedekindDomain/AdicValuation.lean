/-
Copyright (c) 2025 Matthew Jasper. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matthew Jasper
-/
import FLT.Mathlib.Topology.Algebra.Valued.ValuationTopology
import FLT.Mathlib.RingTheory.Valuation.ValuationSubring
import FLT.Mathlib.RingTheory.LocalRing.MaximalIdeal.Basic
import Mathlib.Algebra.Order.GroupWithZero.Canonical
import Mathlib.RingTheory.DedekindDomain.AdicValuation
import Mathlib.Algebra.Group.Int.TypeTags
import Mathlib.NumberTheory.RamificationInertia.Basic
import Mathlib.RingTheory.PrincipalIdealDomainOfPrime
import Mathlib.RingTheory.DiscreteValuationRing.Basic

/-!

# Adic Completions

If `A` is a valued ring with field of fractions `K` there are two different
complete rings containing `A` one might define, the first is
`ğ’ª_v = {x âˆˆ K_v | v x â‰¤ 1}` (defined in Lean as `adicCompletionIntegers K v`)
and the second is the `v-adic` completion of `A`. In the case when `A` is a
Dedekind domain these definitions give isomorphic topological `A`-algebras.
This file makes some progress towards this.

## Main theorems/defs

* `IsDedekindDomain.HeightOneSpectrum.closureAlgebraMapIntegers_eq_integers` : The closure of
    `A` in `K_v` is `ğ’ª_v`.
* `IsDedekindDomain.HeightOneSpectrum.ResidueFieldEquivCompletionResidueField` : The canonical
  isomorphism `A â§¸ v â‰… ğ“áµ¥ / v`.
* `IsDedekindDomain.HeightOneSpectrum.closureAlgebraMapIntegers_eq_prodIntegers` : If `s` is
    a set of primes of `A`, then the closure of `A` in `âˆ_{v âˆˆ s} K_v` is `âˆ_{v âˆˆ s} ğ’ª_v`.
* `IsDedekindDomain.HeightOneSpectrum.denseRange_of_prodAlgebraMap` : If `s` is a finite set
    of primes of `A`, then `K` is dense in `âˆ_{v âˆˆ s} K_v`.
* We show (as an unnamed instance) `IsDiscreteValuationRing (ğ’ª[v.adicCompletion K])`
-/

namespace IsDedekindDomain.HeightOneSpectrum

section Multiplicative

open scoped WithZero
lemma exists_ofAdd_natCast_of_le_one {x : â„¤áµâ°} (hx : x â‰  0) (hx' : x â‰¤ 1) :
    âˆƒ (k : â„•), (Multiplicative.ofAdd (-(k : â„¤))) = x := by
  lift x to Multiplicative â„¤ using hx
  norm_cast at hx'
  obtain âŸ¨k, hkâŸ© := Int.eq_ofNat_of_zero_le (Int.neg_nonneg_of_nonpos hx')
  use k
  rw [â† hk, Int.neg_neg]
  rfl

lemma exists_ofAdd_natCast_lt {x : â„¤áµâ°} (hx : x â‰  0) :
    âˆƒ (k : â„•), (Multiplicative.ofAdd (-(k : â„¤))) < x := by
  obtain âŸ¨y, hnz, hyxâŸ© := WithZero.exists_ne_zero_and_lt hx
  lift y to Multiplicative â„¤ using hnz
  use y.natAbs
  apply lt_of_le_of_lt _ hyx
  norm_cast
  exact inv_mabs_le y

end Multiplicative

variable {A : Type*} (K : Type*) [CommRing A] [Field K] [Algebra A K] [IsFractionRing A K]
    [IsDedekindDomain A] (v : HeightOneSpectrum A)

lemma ne_zero_of_some_le_intValuation {a : A} {m : Multiplicative â„¤} (h : m â‰¤ v.intValuation a)
    : a â‰  0 := by
  rintro rfl
  simp at h

lemma intValuation_eq_coe_neg_multiplicity {a : A} (hnz : a â‰  0) :
    v.intValuation a =
    (Multiplicative.ofAdd (-(multiplicity v.asIdeal (Ideal.span {a}): â„¤))) := by
  classical
  have hnb : Ideal.span {a} â‰  âŠ¥ := by
    rwa [ne_eq, Ideal.span_singleton_eq_bot]
  rw [intValuation_if_neg _ hnz, count_associates_factors_eq hnb v.isPrime v.ne_bot]
  nth_rw 1 [â† normalize_eq v.asIdeal]
  congr
  symm
  apply multiplicity_eq_of_emultiplicity_eq_some
  rw [â† UniqueFactorizationMonoid.emultiplicity_eq_count_normalizedFactors v.irreducible hnb]

lemma emultiplicity_eq_of_valuation_eq_ofAdd {a : A} {k : â„•}
    (hv : v.intValuation a = (Multiplicative.ofAdd (-(k : â„¤)))) :
    emultiplicity v.asIdeal (Ideal.span {a}) = k := by
  classical
  have hnz : a â‰  0 := ne_zero_of_some_le_intValuation _ (le_of_eq hv.symm)
  have hnb : Ideal.span {a} â‰  âŠ¥ := by
    rwa [ne_eq, Ideal.span_singleton_eq_bot]
  simp only [intValuation_if_neg _ hnz, ofAdd_neg, WithZero.coe_inv, inv_inj, WithZero.coe_inj,
    EmbeddingLike.apply_eq_iff_eq, Nat.cast_inj] at hv
  rw [â† hv, UniqueFactorizationMonoid.emultiplicity_eq_count_normalizedFactors v.irreducible hnb,
    count_associates_factors_eq hnb v.isPrime v.ne_bot, normalize_eq]

/-- Given `a, b âˆˆ A` and `v b â‰¤ v a` we can find `y in A` such that `y` is close to `a / b` by
    the valuation v. -/
lemma exists_adicValued_mul_sub_le {a b : A} {Î³ : WithZero (Multiplicative â„¤)} (hÎ³ : Î³ â‰  0)
    (hle : Î³ â‰¤ v.intValuation a)
    (hle' : v.intValuation b â‰¤ v.intValuation a) :
    âˆƒ y, v.intValuation (y * a - b) â‰¤ Î³ := by
  -- Find `n` such that `Î³ = Multiplicative.ofAdd (-(n : â„¤))`
  have hÎ³' : Î³ â‰¤ 1 := by
    apply hle.trans
    apply intValuation_le_one
  obtain âŸ¨n, hnâŸ© := exists_ofAdd_natCast_of_le_one hÎ³ hÎ³'
  rw [â† hn] at hle âŠ¢
  have hnz : a â‰  0 := ne_zero_of_some_le_intValuation _ hle
  have hnb : Ideal.span {a} â‰  âŠ¥ := by
    rwa [ne_eq, Ideal.span_singleton_eq_bot]
  -- Rewrite the statements to involve multiplicity rather than valuations
  rw [intValuation_eq_coe_neg_multiplicity _ hnz, WithZero.coe_le_coe, Multiplicative.ofAdd_le,
    neg_le_neg_iff, Int.ofNat_le] at hle
  have hm : emultiplicity v.asIdeal (Ideal.span {a}) â‰¤ n :=
    le_of_eq_of_le
      (emultiplicity_eq_of_valuation_eq_ofAdd v <| intValuation_eq_coe_neg_multiplicity v hnz)
      (ENat.coe_le_coe.mpr hle)
  have hb : b âˆˆ v.asIdeal ^ multiplicity v.asIdeal (Ideal.span {a}) := by
    rwa [â† intValuation_le_pow_iff_mem, â† intValuation_eq_coe_neg_multiplicity _ hnz]
  -- Now make use of
  -- `v.asIdeal ^ multiplicity v.asIdeal (Ideal.span {a}) = v.asIdeal ^ n âŠ” Ideal.span {a}`
  -- (this is where we need `IsDedekindDomain A`)
  rw [â† irreducible_pow_sup_of_ge hnb (irreducible v) n hm] at hb
  -- Extract y by writing b as a general term of the sum of the two ideals.
  obtain âŸ¨x, hx, z, hz, hxzâŸ© := Submodule.mem_sup.mp hb
  obtain âŸ¨y, hyâŸ© := Ideal.mem_span_singleton'.mp hz
  use y
  -- And again prove the result about valuations by turning into one about ideals.
  rwa [hy, â† hxz, sub_add_cancel_right, intValuation_le_pow_iff_mem, neg_mem_iff]

lemma exists_adicValued_sub_lt_of_adicValued_le_one {x : (WithVal (v.valuation K))}
    (Î³ : (WithZero (Multiplicative â„¤))Ë£) (hx : Valued.v x â‰¤ 1) :
    âˆƒa, Valued.v ((algebraMap A K a) - (x : v.adicCompletion K)) < Î³.val := by
  -- Write `x = n / d`
  obtain âŸ¨âŸ¨n, d, hdâŸ©, hndâŸ© := IsLocalization.surj (nonZeroDivisors A) x
  dsimp only at hnd
  -- Show `v n â‰¤ v d`
  have hnd' := congr_arg Valued.v hnd
  simp only [adicValued_apply', map_mul] at hnd'
  have hge : Valued.v ((algebraMap A (WithVal (v.valuation K))) d) â‰¥
      Valued.v ((algebraMap A (WithVal (v.valuation K))) n) :=
    calc Valued.v ((algebraMap A (WithVal (v.valuation K))) d)
          â‰¥ (valuation K v) x *
            (valuation K v) ((algebraMap A (WithVal (v.valuation K))) d) :=
                mul_le_of_le_one_left' hx
        _ = Valued.v ((algebraMap A (WithVal (v.valuation K))) n) := hnd'
  simp only [ge_iff_le, WithVal, adicValued_apply, valuation_of_algebraMap] at hge
  have hdz : (algebraMap A (WithVal (v.valuation K)) d) â‰  0 :=
    IsLocalization.to_map_ne_zero_of_mem_nonZeroDivisors _ (fun _ â†¦ id) hd
  -- Find a suitable `Î³` for the bound in `exists_adicValued_mul_sub_le`
  have hv : Valued.v ((algebraMap A (WithVal (v.valuation K)) d)) â‰  0 := by
    rw [Valuation.ne_zero_iff]
    exact hdz
  let hu : Valued.v ((algebraMap A (WithVal (v.valuation K)) d)) * Î³.val â‰  0 := by
    rw [mul_ne_zero_iff]
    exact âŸ¨hv, Î³.ne_zeroâŸ©
  obtain âŸ¨Î³', hÎ³, hÎ³u, hÎ³vâŸ© := WithZero.exists_ne_zero_and_lt_and_lt hu hv
  simp only [WithVal, adicValued_apply, valuation_of_algebraMap] at hÎ³v
  -- Now can apply `exists_adicValued_mul_sub_le` to get the approximation of `x`.
  obtain âŸ¨a, hvalâŸ© := exists_adicValued_mul_sub_le v hÎ³ hÎ³v.le hge
  use a
  rw [â† eq_div_iff_mul_eq hdz] at hnd
  rw [â† UniformSpace.Completion.coe_sub,
      valuedAdicCompletion_eq_valuation',
      hnd, sub_div' hdz, map_divâ‚€]
  unfold WithVal at hdz âŠ¢
  rw [â† Valuation.pos_iff (valuation K v)] at hdz
  rw [â† map_mul, â† map_sub, div_lt_iffâ‚€' hdz, valuation_of_algebraMap]
  exact lt_of_le_of_lt hval hÎ³u

/-- The closure of `A` in `K_v` is `ğ’ª_v`. -/
theorem closureAlgebraMapIntegers_eq_integers :
    closure (algebraMap A (v.adicCompletion K)).range =
    SetLike.coe (v.adicCompletionIntegers K) := by
  apply subset_antisymm
  -- We know `closure A âŠ† ğ’ª_v` because `ğ’ª_v` is closed and `A âŠ† ğ’ª_v`
  Â· apply closure_minimal _ (Valued.isClosed_valuationSubring _)
    rintro b âŸ¨a, rflâŸ©
    exact coe_mem_adicCompletionIntegers v a
  -- Show `ğ’ª_v âŠ† closure A` from `ğ’ª_v âŠ† closure O_[K]` and `closure O_[K] âŠ† closure A`
  Â· let f := fun (k : WithVal (v.valuation K)) => (k : v.adicCompletion K)
    suffices h : closure (f '' (f â»Â¹' (adicCompletionIntegers K v))) âŠ†
        closure (algebraMap A (adicCompletion K v)).range by
      apply Set.Subset.trans _ h
      exact DenseRange.subset_closure_image_preimage_of_isOpen
        UniformSpace.Completion.denseRange_coe (Valued.isOpen_valuationSubring _)
    -- Unfold the topological definitions until we get the result from the previous lemma
    apply closure_minimal _ isClosed_closure
    rintro k âŸ¨x, hx, rflâŸ©
    unfold f at hx
    rw [Set.mem_preimage, SetLike.mem_coe, mem_adicCompletionIntegers,
        Valued.valuedCompletion_apply, adicValued_apply'] at hx
    rw [mem_closure_iff_nhds_zero]
    intro U hU
    rw [Valued.mem_nhds] at hU
    obtain âŸ¨Î³, hÎ³âŸ© := hU
    obtain âŸ¨a, haâŸ© := exists_adicValued_sub_lt_of_adicValued_le_one K v Î³ hx
    use algebraMap A K a
    constructor
    Â· use a
      rfl
    Â· apply hÎ³
      simpa

/-- `A` is dense in `ğ’ª_v`. -/
theorem denseRange_of_integerAlgebraMap :
    DenseRange (algebraMap A (v.adicCompletionIntegers K)) := by
  rw [denseRange_iff_closure_range, Set.eq_univ_iff_forall]
  intro x
  rw [closure_subtype]
  suffices h : Subtype.val ''
      Set.range ((algebraMap A â†¥(adicCompletionIntegers K v))) =
      (algebraMap A (v.adicCompletion K)).range by
    rw [h, closureAlgebraMapIntegers_eq_integers K v]
    exact Subtype.coe_prop x
  simp only [RingHom.coe_range, â† Set.range_comp']
  rfl

/-- An element of `ğ’ª_v` can be approximated by an element of `A`. -/
theorem exists_adicValued_sub_lt_of_adicCompletionInteger (x : v.adicCompletionIntegers K)
    (Î³ : (WithZero (Multiplicative â„¤))Ë£) :
    âˆƒa, Valued.v ((algebraMap A K a) - (x : v.adicCompletion K)) < Î³.val := by
  have h := closureAlgebraMapIntegers_eq_integers K v
  rw [Set.ext_iff] at h
  specialize h x
  simp_rw [RingHom.coe_range, Subtype.coe_prop, iff_true, mem_closure_iff_nhds] at h
  specialize h { y | Valued.v (y  - (x : v.adicCompletion K)) < Î³.val }
  have hn : {y | Valued.v (y - (x : v.adicCompletion K)) < Î³.val} âˆˆ nhds x.val := by
    rw [Valued.mem_nhds]
    use Î³
  obtain âŸ¨z, âŸ¨hz, a, haâŸ©âŸ© := h hn
  use a
  rw [algebraMap_adicCompletion, Function.comp_apply] at ha
  rwa [ha]

/-- The maximal ideal of the integers of the completion of `v`. -/
noncomputable abbrev completionIdeal : Ideal (v.adicCompletionIntegers K) :=
  IsLocalRing.maximalIdeal (adicCompletionIntegers K v)

lemma mem_completionIdeal_iff (x : v.adicCompletionIntegers K) :
    x âˆˆ completionIdeal K v â†” Valued.v x.val < 1 :=
  Valuation.mem_maximalIdeal_iff _ _

lemma algebraMap_completionIntegers (x : A) :
    (algebraMap A (v.adicCompletionIntegers K) x) = (algebraMap A (v.adicCompletion K) x) :=
  rfl

instance : (v.completionIdeal K).LiesOver v.asIdeal where
  over := by
    rw [Ideal.under_def]
    ext x
    simp only [Ideal.mem_comap, mem_completionIdeal_iff, algebraMap_completionIntegers,
      valuedAdicCompletion_eq_valuation, valuation_lt_one_iff_mem]

open IsLocalRing in
/-- The canonical ring homomorphism from A / v to ğ“áµ¥ / v, where ğ“áµ¥ is the integers of the
completion Káµ¥ of the field of fractions of A. -/
noncomputable def ResidueFieldToCompletionResidueField :
    A â§¸ v.asIdeal â†’+* ResidueField (v.adicCompletionIntegers K) :=
  Ideal.quotientMap _ (algebraMap _ _) <| le_of_eq Ideal.LiesOver.over

open IsLocalRing in
/-- The canonical isomorphism from A / v to ğ“áµ¥ / v, where ğ“áµ¥ is the integers of the
completion Káµ¥ of the field of fractions K of A. -/
noncomputable def ResidueFieldEquivCompletionResidueField :
    A â§¸ v.asIdeal â‰ƒ+* ResidueField (v.adicCompletionIntegers K) := by
  apply RingEquiv.ofBijective (ResidueFieldToCompletionResidueField K v)
    âŸ¨Ideal.quotientMap_injective' <| ge_of_eq Ideal.LiesOver.over, ?_âŸ©
  intro z
  obtain âŸ¨x, hxâŸ© :=
    Submodule.Quotient.mk_surjective (p := maximalIdeal â†¥(adicCompletionIntegers K v)) z
  rw [â† hx, Ideal.Quotient.mk_eq_mk]
  suffices âˆƒ a : A, (ResidueFieldToCompletionResidueField K v) a = Ideal.Quotient.mk _ x by
    obtain âŸ¨a, haâŸ© := this
    refine âŸ¨a, haâŸ©
  change âˆƒ a, Ideal.Quotient.mk (maximalIdeal (v.adicCompletionIntegers K)) _ = _
  simp_rw [Ideal.Quotient.mk_eq_mk_iff_sub_mem, mem_maximalIdeal, mem_nonunits_iff]
  -- TODO - figure out why this can't be 'simp_rw/simp'
  conv =>
    pattern Â¬(IsUnit _)
    rw [Valuation.Integer.not_isUnit_iff_valuation_lt_one]
  exact exists_adicValued_sub_lt_of_adicCompletionInteger K v x 1

theorem inertiaDeg_asIdeal_completionIdeal :
    Ideal.inertiaDeg v.asIdeal (v.completionIdeal K) = 1 := by
  rw [Ideal.inertiaDeg_algebraMap]
  have f : (A â§¸ v.asIdeal) â‰ƒâ‚—[A â§¸ v.asIdeal]
      ((adicCompletionIntegers K v) â§¸ completionIdeal K v) := {
    __ := ResidueFieldEquivCompletionResidueField K v
    map_smul' := by
      intro x y
      rw [Algebra.smul_def, Algebra.smul_def]
      exact map_mul (ResidueFieldEquivCompletionResidueField K v) x y
  }
  rw [â† LinearEquiv.finrank_eq f]
  exact Module.finrank_self _

/-- An element of `âˆ_{v âˆˆ s} ğ’ª_v`, with `s` finite, can be approximated by an element of `A`.
-/
theorem exists_forall_adicValued_sub_lt {Î¹ : Type*} (s : Finset Î¹)
    (e : Î¹ â†’ (WithZero (Multiplicative â„¤))Ë£) (valuation : Î¹ â†’ HeightOneSpectrum A)
    (injective : Function.Injective valuation)
    (x : (i : Î¹) â†’ (valuation i).adicCompletionIntegers K) :
    âˆƒ a, âˆ€ i âˆˆ s, Valued.v ((algebraMap A K a) - (x i).val) < (e i).val := by
  -- Approximate elements of `ğ’ª_v` with elements of `A` using the previous theorem.
  choose f hf using fun (i : s) =>
    exists_adicValued_sub_lt_of_adicCompletionInteger K (valuation i) (x i) (e i)

  -- Convert the hypotheses from being about valuations to being about ideals, so
  -- that we can apply (a suitable corollary of) the Chinese remainder theorem.
  have hexists_e' : âˆ€ (i : Î¹), âˆƒ (e' : â„•), (Multiplicative.ofAdd (-(e' : â„¤))) < (e i).val := by
    intro i
    apply exists_ofAdd_natCast_lt (e i).ne_zero
  choose e' he' using hexists_e'
  have hinj : âˆ€ i âˆˆ s, âˆ€ j âˆˆ s, i â‰  j â†’
      (fun i â†¦ (valuation i).asIdeal) i â‰  (fun i â†¦ (valuation i).asIdeal) j := by
    intro _ _ _ _
    exact mt <| fun hij â†¦ injective (HeightOneSpectrum.ext hij)

  -- Use Chinese remainder theorem to get a single approximation for `f i` for all `i âˆˆ s`.
  obtain âŸ¨a, haâŸ© := IsDedekindDomain.exists_forall_sub_mem_ideal (s := s)
    (fun i => (valuation i).asIdeal) e' (fun i hi => (valuation i).prime) hinj f
  use a
  intro i hi
  specialize ha i hi
  specialize hf âŸ¨i, hiâŸ©
  rw [â† intValuation_le_pow_iff_mem, â† valuation_of_algebraMap (K := K),
    â† valuedAdicCompletion_eq_valuation, algebraMap.coe_sub] at ha
  refine lt_of_le_of_lt ?_ (Valuation.map_add_lt _ (ha.trans_lt (he' i)) hf)
  apply le_of_eq
  congr
  rw [add_sub, sub_eq_sub_iff_add_eq_add, add_right_cancel_iff,
    add_comm_sub, add_sub, eq_sub_iff_add_eq]
  rfl

/-- The closure of `A` in `âˆ_{v âˆˆ s} K_v` is `âˆ_{v âˆˆ s} ğ’ª_v`. `s` may be infinite. -/
theorem closureAlgebraMapIntegers_eq_prodIntegers {Î¹ : Type*}
    (valuation : Î¹ â†’ HeightOneSpectrum A) (injective : Function.Injective valuation) :
    closure (SetLike.coe (algebraMap A ((i : Î¹) â†’ (valuation i).adicCompletion K)).range) =
    (Set.pi Set.univ (fun (i : Î¹) â†¦ ((valuation i).adicCompletionIntegers K).carrier)) := by
  apply Set.Subset.antisymm
  Â· apply closure_minimal
    Â· rintro c âŸ¨a, haâŸ© i -
      rw [â† ha]
      simp only [Pi.algebraMap_apply]
      exact coe_mem_adicCompletionIntegers (valuation i) a
    Â· apply isClosed_set_pi
      rintro w -
      apply Valued.isClosed_valuationSubring
  Â· intro f hf
    rw [mem_closure_iff_nhds_zero]
    intro U hU
    rw [Pi.zero_def, nhds_pi, Filter.mem_pi'] at hU
    obtain âŸ¨I, t, htn, htsâŸ© := hU
    choose g hg using fun w => (Valued.is_topological_valuation (t w)).mp (htn w)
    obtain âŸ¨a, haâŸ© :=
      exists_forall_adicValued_sub_lt K I g valuation injective (fun w => âŸ¨f w, hf w âŸ¨âŸ©âŸ©)
    use algebraMap A _ a
    constructor
    Â· rw [RingHom.coe_range]
      exact Set.mem_range_self a
    Â· exact hts fun w hw â†¦ hg w <| ha w hw

lemma adicCompletion.eq_mul_nonZeroDivisor_inv_adicCompletionIntegers (v : HeightOneSpectrum A)
    (x : v.adicCompletion K) :
    âˆƒa âˆˆ nonZeroDivisors A, âˆƒb âˆˆ v.adicCompletionIntegers K, x = (algebraMap A K a)â»Â¹ â€¢ b := by
  obtain âŸ¨a, hz, haâŸ© :=
    adicCompletion.mul_nonZeroDivisor_mem_adicCompletionIntegers v x
  use a, hz, (algebraMap A K a) â€¢ x
  constructor
  Â· rwa [Algebra.smul_def, â† IsScalarTower.algebraMap_apply, mul_comm]
  Â· rw [smul_smul, inv_mul_cancelâ‚€, one_smul]
    exact IsLocalization.to_map_ne_zero_of_mem_nonZeroDivisors K (fun _ â†¦ id) hz

lemma adicCompletion.eq_mul_pi_adicCompletionIntegers {Î¹ : Type*} [Fintype Î¹]
    (valuation : Î¹ â†’ HeightOneSpectrum A) (x : (i : Î¹) â†’ (valuation i).adicCompletion K) :
      âˆƒk : K, âˆƒy âˆˆ Set.pi Set.univ (fun (i : Î¹) â†¦ ((valuation i).adicCompletionIntegers K).carrier),
      x = k â€¢ y := by
  classical
  choose f hf using fun (i : Î¹) =>
    eq_mul_nonZeroDivisor_inv_adicCompletionIntegers K (valuation i) (x i)
  use (algebraMap A K (âˆ i : Î¹, f i))â»Â¹, (algebraMap A K (âˆ i : Î¹, f i)) â€¢ x
  have hz : âˆ€ (i : Î¹), (algebraMap A K) (f i) â‰  0 := fun i =>
    IsLocalization.to_map_ne_zero_of_mem_nonZeroDivisors K (fun _ â†¦ id) (hf i).left
  constructor
  Â· rintro i -
    obtain âŸ¨b, hb, hxâŸ© := (hf i).right
    beta_reduce
    rw [Pi.smul_apply, algebraMap_smul, Subsemiring.coe_carrier_toSubmonoid,
        Subring.coe_toSubsemiring, SetLike.mem_coe, ValuationSubring.mem_toSubring, hx,
        â† Finset.prod_erase_mul _ f (Finset.mem_univ i), mul_smul,
        â† IsScalarTower.smul_assoc (f i), Algebra.smul_def (f i), mul_inv_cancelâ‚€ (hz i), one_smul,
        Algebra.smul_def]
    apply mul_mem (coe_mem_adicCompletionIntegers _ _) hb
  Â· rw [smul_smul, inv_mul_cancelâ‚€, one_smul]
    simp [Finset.prod_ne_zero_iff, hz]

/-- If `s` is finite then `K` in dense in `âˆ_{v âˆˆ s} K_v`. -/
theorem denseRange_of_prodAlgebraMap {Î¹ : Type*} [Fintype Î¹]
    {valuation : Î¹ â†’ HeightOneSpectrum A} (injective : Function.Injective valuation) :
    DenseRange (algebraMap K ((i : Î¹) â†’ (valuation i).adicCompletion K)) := by
  rw [denseRange_iff_closure_range, Set.eq_univ_iff_forall]
  let S := Set.range (algebraMap K ((i : Î¹) â†’ (valuation i).adicCompletion K))
  -- We've already shown that the closure of `A` is `âˆ_{v âˆˆ s} ğ’ª_v`, so
  -- the closure of `K` at least contains this set.
  have hint : Set.pi Set.univ (fun (i : Î¹) â†¦ ((valuation i).adicCompletionIntegers K).carrier)
      âŠ† closure S := by
    rw [â† closureAlgebraMapIntegers_eq_prodIntegers _ _ injective]
    apply closure_mono
    exact fun _ âŸ¨a, haâŸ© â†¦ âŸ¨algebraMap A K a, haâŸ©
  -- Next, the closure of `K` is closed under multiplication by `K` because
  -- scalar multiplication by a constant is continuous.
  have hmul : âˆ€x, x âˆˆ closure S â†’ âˆ€k : K, k â€¢ x âˆˆ closure S := by
    intro x h k
    let f := fun (z : (i : Î¹) â†’ (valuation i).adicCompletion K) â†¦ k â€¢ z
    have hf : ContinuousAt f x := Continuous.continuousAt (continuous_const_smul k)
    apply closure_mono _ <| mem_closure_image hf h
    rintro x âŸ¨_, âŸ¨z, rflâŸ©, rflâŸ©
    use k â€¢ algebraMap K _ z
    ext i
    simp [Algebra.smul_def, f]
  -- Finally, `âˆ_{v âˆˆ s} K_v = K â€¢ âˆ_{v âˆˆ s} ğ’ª_v`
  intro x
  obtain âŸ¨k, y, hy, hxâŸ© := adicCompletion.eq_mul_pi_adicCompletionIntegers K valuation x
  exact hx â–¸ hmul y (hint hy) k

namespace adicCompletion

open scoped algebraMap in
theorem exists_uniformizer (v : HeightOneSpectrum A) :
    âˆƒ Ï€ : v.adicCompletionIntegers K, Valued.v Ï€.1 = Multiplicative.ofAdd (- 1 : â„¤) := by
  obtain âŸ¨Ï€, hÏ€âŸ© := v.intValuation_exists_uniformizer
  use Ï€
  rw [â† hÏ€, â† ValuationSubring.algebraMap_apply, â† IsScalarTower.algebraMap_apply,
    v.valuedAdicCompletion_eq_valuation, v.valuation_of_algebraMap]

variable {K} in
theorem uniformizer_ne_zero {v : HeightOneSpectrum A}
    {Ï€ : v.adicCompletionIntegers K} (hÏ€ : Valued.v Ï€.1 = Multiplicative.ofAdd (-1 : â„¤)) :
    Ï€ â‰  0 := by
  contrapose! hÏ€
  simp [hÏ€]

variable {K} in
open scoped Multiplicative in
theorem uniformizer_not_isUnit {Ï€ : v.adicCompletionIntegers K}
    (hÏ€ : Valued.v Ï€.1 = Multiplicative.ofAdd (-1 : â„¤)) :
    Â¬IsUnit (Ï€ : v.adicCompletionIntegers K) := by
  rw [ValuationSubring.isUnit_iff_valued_eq_one, â† WithZero.coe_one, â† ofAdd_zero, hÏ€]
  apply ne_of_lt
  rw [WithZero.coe_lt_coe, Multiplicative.ofAdd_lt]
  omega

theorem eq_pow_uniformizer_mul_unit {x : v.adicCompletionIntegers K} (hx : x â‰  0)
    {Ï€ : v.adicCompletionIntegers K} (hÏ€ : Valued.v Ï€.1 = Multiplicative.ofAdd (-1 : â„¤)) :
    âˆƒ (n : â„•) (u : (v.adicCompletionIntegers K)Ë£), x = Ï€ ^ n * u := by
  have hx' : Valued.v x.1 â‰  0 := by simp [hx]
  let m := - Multiplicative.toAdd (WithZero.unzero hx')
  have hmâ‚€ : 0 â‰¤ m := by
    simp_rw [m, Right.nonneg_neg_iff, â† toAdd_one, Multiplicative.toAdd_le]
    rw [â† WithZero.coe_le_coe]; exact (WithZero.coe_unzero _).symm â–¸ x.2
  have hpow : Valued.v (Ï€ ^ (-m) * x.val) = 1 := by
    rw [Valued.v.map_mul, map_zpowâ‚€, hÏ€, ofAdd_neg, WithZero.coe_inv,
      inv_zpow', neg_neg, â† WithZero.coe_zpow, â† Int.ofAdd_mul, one_mul, ofAdd_neg, ofAdd_toAdd,
      WithZero.coe_inv, WithZero.coe_unzero, inv_mul_cancelâ‚€ hx']
  let a : v.adicCompletionIntegers K := âŸ¨Ï€ ^ (-m) * x.val, le_of_eq hpowâŸ©
  refine âŸ¨m.toNat, (ValuationSubring.isUnit_of_valued_eq_one a hpow).unit, Subtype.ext ?_âŸ©
  simp only [zpow_neg, IsUnit.unit_spec, MulMemClass.coe_mul, SubmonoidClass.coe_pow, a,
    â† zpow_natCast, m.toNat_of_nonneg hmâ‚€, â† mul_assoc]
  rw [mul_inv_cancelâ‚€ (zpow_ne_zero _ <| (by simp [uniformizer_ne_zero hÏ€])), one_mul]

open scoped algebraMap in
theorem maximalIdeal_eq_span_uniformizer {Ï€ : v.adicCompletionIntegers K}
    (hÏ€ : Valued.v Ï€.1 = Multiplicative.ofAdd (-1 : â„¤)) :
    IsLocalRing.maximalIdeal (v.adicCompletionIntegers K) =
      Ideal.span {(Ï€ : v.adicCompletionIntegers K)} := by
  refine (IsLocalRing.maximalIdeal.isMaximal _).eq_of_le
    (Ideal.span_singleton_ne_top (uniformizer_not_isUnit v hÏ€)) (fun x hx => ?_)
  by_cases hxâ‚€ : x = 0
  Â· simp only [hxâ‚€, Ideal.zero_mem]
  Â· obtain âŸ¨n, âŸ¨u, huâŸ©âŸ© := eq_pow_uniformizer_mul_unit K v hxâ‚€ hÏ€
    have hn : Â¬(IsUnit x) := fun h =>
      (IsLocalRing.maximalIdeal.isMaximal _).ne_top (Ideal.eq_top_of_isUnit_mem _ hx h)
    replace hn : n â‰  0 := fun h => by {rw [hu, h, pow_zero, one_mul] at hn; exact hn u.isUnit}
    simpa [Ideal.mem_span_singleton, hu, IsUnit.dvd_mul_right, Units.isUnit] using dvd_pow_self _ hn

instance : Ring.DimensionLEOne (v.adicCompletionIntegers K) where
  maximalOfPrime {ğ”­} hğ”­_ne_bot hğ”­_prime := by
    let âŸ¨x, hxâŸ© := Submodule.exists_mem_ne_zero_of_ne_bot hğ”­_ne_bot
    let âŸ¨Ï€, hÏ€âŸ© := exists_uniformizer K v
    obtain âŸ¨n, âŸ¨u, rflâŸ©âŸ© := eq_pow_uniformizer_mul_unit K v hx.2 hÏ€
    simp only [Units.isUnit, Ideal.mul_unit_mem_iff_mem, ne_eq, mul_eq_zero, pow_eq_zero_iff',
      Units.ne_zero, or_false, not_and, Decidable.not_not] at hx
    by_cases hn : n = 0
    Â· simp only [hn, pow_zero, â† ğ”­.eq_top_iff_one, implies_true, and_true] at hx
      exact hğ”­_prime.ne_top hx |>.elim
    Â· rw [hğ”­_prime.pow_mem_iff_mem n (by omega), â† ğ”­.span_singleton_le_iff_mem,
        â† maximalIdeal_eq_span_uniformizer K v hÏ€] at hx
      exact IsLocalRing.maximalIdeal_le hğ”­_prime.ne_top hx.1

open scoped algebraMap in
instance : IsPrincipalIdealRing (v.adicCompletionIntegers K) := by
  apply IsPrincipalIdealRing.of_prime
  intro P hP
  by_cases hP_bot : P = âŠ¥
  Â· exact hP_bot â–¸ bot_isPrincipal
  Â· let âŸ¨Ï€, hÏ€âŸ© := exists_uniformizer K v
    use Ï€
    rw [IsLocalRing.eq_maximalIdeal (hP.isMaximal hP_bot)]
    exact maximalIdeal_eq_span_uniformizer K v hÏ€

instance : IsDiscreteValuationRing (v.adicCompletionIntegers K) where
  __ := inferInstanceAs (IsPrincipalIdealRing (v.adicCompletionIntegers K))
  not_a_field' := by
    let âŸ¨Ï€, hÏ€âŸ© := exists_uniformizer K v
    rw [maximalIdeal_eq_span_uniformizer K v hÏ€]
    intro h
    simp only [Ideal.span_singleton_eq_bot] at h
    exact uniformizer_ne_zero hÏ€ h

open scoped Valued in
instance : IsDiscreteValuationRing (ğ’ª[v.adicCompletion K]) :=
  inferInstanceAs (IsDiscreteValuationRing (v.adicCompletionIntegers K))

lemma mem_completionIdeal_pow {n : â„•} (x : v.adicCompletionIntegers K) :
    x âˆˆ (v.completionIdeal K) ^ n â†” Valued.v x.val â‰¤ â†‘(Multiplicative.ofAdd (-(n : â„¤))) := by
  obtain âŸ¨Ï€, hÏ€âŸ© := exists_uniformizer K v
  unfold completionIdeal
  rw [maximalIdeal_eq_span_uniformizer K v hÏ€, Ideal.span_singleton_pow, Ideal.mem_span_singleton']
  have hvalÏ€_pow : (Valued.v Ï€.val) ^ n = (Multiplicative.ofAdd (-n : â„¤)) := by
    rw [hÏ€]
    norm_num
    norm_cast
    rw [â† ofAdd_nsmul, Nat.smul_one_eq_cast]
  constructor
  Â· rintro âŸ¨a, rflâŸ©
    simp only [MulMemClass.coe_mul, SubmonoidClass.coe_pow, map_mul, map_pow, ofAdd_neg,
      WithZero.coe_inv]
    apply mul_le_of_le_one_of_le a.prop <| le_of_eq hvalÏ€_pow
  Â· intro hx
    set a := x.val / (Ï€ ^ n) with ha'
    have ha : Valued.v a â‰¤ 1 := by
      rwa [ha', Valuation.map_div, Valuation.map_pow, hvalÏ€_pow,
        div_le_oneâ‚€ (WithZero.zero_lt_coe _)]
    use âŸ¨a, haâŸ©
    apply Subtype.val_injective
    simp only [MulMemClass.coe_mul, SubmonoidClass.coe_pow, ha']
    rw [div_mul_eq_mul_divâ‚€, mul_div_cancel_rightâ‚€]
    apply pow_ne_zero n
    norm_cast
    exact uniformizer_ne_zero hÏ€

end adicCompletion

lemma mem_completionIdeal_iff' (x : v.adicCompletionIntegers K) :
    x âˆˆ v.completionIdeal K â†” Valued.v x.val â‰¤ â†‘(Multiplicative.ofAdd (-(1 : â„¤))) := by
  rw [â† Submodule.pow_one (v.completionIdeal K), adicCompletion.mem_completionIdeal_pow,
    Int.natCast_one]

lemma completionIdeal_ne_bot : completionIdeal K v â‰  âŠ¥ := IsDiscreteValuationRing.not_a_field _

end IsDedekindDomain.HeightOneSpectrum
