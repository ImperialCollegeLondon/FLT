/-
Copyright (c) 2025 Matthew Jasper. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matthew Jasper
-/
import FLT.Mathlib.Topology.Algebra.Valued.ValuationTopology
import FLT.Mathlib.RingTheory.Valuation.ValuationSubring
import FLT.Mathlib.RingTheory.LocalRing.MaximalIdeal.Basic
import FLT.Mathlib.Algebra.Order.GroupWithZero
import Mathlib.RingTheory.DedekindDomain.AdicValuation
import Mathlib.Algebra.Group.Int.TypeTags
import Mathlib.NumberTheory.RamificationInertia.Basic
import Mathlib.RingTheory.PrincipalIdealDomainOfPrime
import Mathlib.RingTheory.DiscreteValuationRing.Basic

/-!

# Adic Completions

If `A` is a valued ring with field of fractions `K` there are two different
complete rings containing `A` one might define, the first is
`ùí™_v = {x ‚àà K_v | v x ‚â§ 1}` (defined in Lean as `adicCompletionIntegers K v`)
and the second is the `v-adic` completion of `A`. In the case when `A` is a
Dedekind domain these definitions give isomorphic topological `A`-algebras.
This file makes some progress towards this.

## Main theorems

* `IsDedekindDomain.HeightOneSpectrum.closureAlgebraMapIntegers_eq_integers` : The closure of
    `A` in `K_v` is `ùí™_v`.
* `IsDedekindDomain.HeightOneSpectrum.closureAlgebraMapIntegers_eq_prodIntegers` : If `s` is
    a set of primes of `A`, then the closure of `A` in `‚àè_{v ‚àà s} K_v` is `‚àè_{v ‚àà s} ùí™_v`.
* `IsDedekindDomain.HeightOneSpectrum.denseRange_of_prodAlgebraMap` : If `s` is a finite set
    of primes of `A`, then `K` is dense in `‚àè_{v ‚àà s} K_v`.
-/

namespace IsDedekindDomain.HeightOneSpectrum

section Multiplicative

open scoped Multiplicative
lemma exists_ofAdd_natCast_of_le_one {x : ‚Ñ§‚Çò‚ÇÄ} (hx : x ‚â† 0) (hx' : x ‚â§ 1):
    ‚àÉ (k : ‚Ñï), (Multiplicative.ofAdd (-(k : ‚Ñ§))) = x := by
  lift x to Multiplicative ‚Ñ§ using hx
  norm_cast at hx'
  obtain ‚ü®k, hk‚ü© := Int.eq_ofNat_of_zero_le (Int.neg_nonneg_of_nonpos hx')
  use k
  rw [‚Üê hk, Int.neg_neg]
  rfl

lemma exists_ofAdd_natCast_lt {x : ‚Ñ§‚Çò‚ÇÄ} (hx : x ‚â† 0) :
    ‚àÉ (k : ‚Ñï), (Multiplicative.ofAdd (-(k : ‚Ñ§))) < x := by
  obtain ‚ü®y, hnz, hyx‚ü© := WithZero.exists_ne_zero_and_lt hx
  lift y to Multiplicative ‚Ñ§ using hnz
  use y.natAbs
  apply lt_of_le_of_lt _ hyx
  norm_cast
  exact inv_mabs_le y

end Multiplicative

variable { A : Type* } ( K : Type* ) [CommRing A] [Field K] [Algebra A K] [IsFractionRing A K]
    [IsDedekindDomain A] (v : HeightOneSpectrum A)

lemma ne_zero_of_some_le_intValuation {a : A} {m : Multiplicative ‚Ñ§} (h : m ‚â§ v.intValuation a)
    : a ‚â† 0 := by
  rintro rfl
  simp at h

lemma intValuation_eq_coe_neg_multiplicity {a : A} (hnz : a ‚â† 0) :
    v.intValuation a =
    (Multiplicative.ofAdd (-(multiplicity v.asIdeal (Ideal.span {a}): ‚Ñ§))) := by
  classical
  have hnb : Ideal.span {a} ‚â† ‚ä• := by
    rwa [ne_eq, Ideal.span_singleton_eq_bot]
  rw [intValuation_apply, intValuationDef_if_neg _ hnz,
    count_associates_factors_eq hnb v.isPrime v.ne_bot]
  nth_rw 1 [‚Üê normalize_eq v.asIdeal]
  congr
  symm
  apply multiplicity_eq_of_emultiplicity_eq_some
  rw [‚Üê UniqueFactorizationMonoid.emultiplicity_eq_count_normalizedFactors v.irreducible hnb]

lemma emultiplicity_eq_of_valuation_eq_ofAdd {a : A} {k : ‚Ñï}
    (hv : v.intValuation a = (Multiplicative.ofAdd (-(k : ‚Ñ§)))) :
    emultiplicity v.asIdeal (Ideal.span {a}) = k := by
  classical
  have hnz : a ‚â† 0 := ne_zero_of_some_le_intValuation _ (le_of_eq hv.symm)
  have hnb : Ideal.span {a} ‚â† ‚ä• := by
    rwa [ne_eq, Ideal.span_singleton_eq_bot]
  simp only [intValuation_apply,  intValuationDef_if_neg _ hnz,
    ofAdd_neg, WithZero.coe_inv, inv_inj, WithZero.coe_inj, EmbeddingLike.apply_eq_iff_eq,
    Nat.cast_inj] at hv
  rw [‚Üê hv, UniqueFactorizationMonoid.emultiplicity_eq_count_normalizedFactors v.irreducible hnb,
    count_associates_factors_eq hnb v.isPrime v.ne_bot, normalize_eq]

/-- Given `a, b ‚àà A` and `v b ‚â§ v a` we can find `y in A` such that `y` is close to `a / b` by
    the valuation v. -/
lemma exists_adicValued_mul_sub_le {a b : A} {Œ≥ : WithZero (Multiplicative ‚Ñ§)} (hŒ≥ : Œ≥ ‚â† 0)
    (hle : Œ≥ ‚â§ v.intValuation a)
    (hle' : v.intValuation b ‚â§ v.intValuation a) :
    ‚àÉ y, v.intValuation (y * a - b) ‚â§ Œ≥ := by
  -- Find `n` such that `Œ≥ = Multiplicative.ofAdd (-(n : ‚Ñ§))`
  have hŒ≥' : Œ≥ ‚â§ 1 := by
    apply hle.trans
    rw [intValuation_apply]
    apply intValuation_le_one
  obtain ‚ü®n, hn‚ü© := exists_ofAdd_natCast_of_le_one hŒ≥ hŒ≥'
  rw [‚Üê hn] at hle ‚ä¢
  have hnz : a ‚â† 0 := ne_zero_of_some_le_intValuation _ hle
  have hnb : Ideal.span {a} ‚â† ‚ä• := by
    rwa [ne_eq, Ideal.span_singleton_eq_bot]
  -- Rewrite the statements to involve multiplicity rather than valuations
  rw [intValuation_eq_coe_neg_multiplicity _ hnz, WithZero.coe_le_coe, Multiplicative.ofAdd_le,
    neg_le_neg_iff, Int.ofNat_le] at hle
  have hm : emultiplicity v.asIdeal (Ideal.span {a}) ‚â§ n :=
    le_of_eq_of_le
      (emultiplicity_eq_of_valuation_eq_ofAdd v <| intValuation_eq_coe_neg_multiplicity v hnz)
      (ENat.coe_le_coe.mpr hle)
  have hb : b ‚àà v.asIdeal ^ multiplicity v.asIdeal (Ideal.span {a}) := by
    rwa [‚Üê Ideal.dvd_span_singleton, ‚Üê intValuation_le_pow_iff_dvd,
        ‚Üê intValuation_eq_coe_neg_multiplicity _ hnz]
  -- Now make use of
  -- `v.asIdeal ^ multiplicity v.asIdeal (Ideal.span {a}) = v.asIdeal ^ n ‚äî Ideal.span {a}`
  -- (this is where we need `IsDedekindDomain A`)
  rw [‚Üê irreducible_pow_sup_of_ge hnb (irreducible v) n hm] at hb
  -- Extract y by writing b as a general term of the sum of the two ideals.
  obtain ‚ü®x, hx, z, hz, hxz‚ü© := Submodule.mem_sup.mp hb
  obtain ‚ü®y, hy‚ü© := Ideal.mem_span_singleton'.mp hz
  use y
  -- And again prove the result about valuations by turning into one about ideals.
  rwa [hy, ‚Üê hxz, sub_add_cancel_right, intValuation_apply,
      intValuation_le_pow_iff_dvd, Ideal.dvd_span_singleton, neg_mem_iff]

lemma exists_adicValued_sub_lt_of_adicValued_le_one {x : (WithVal (v.valuation K))}
    (Œ≥ : (WithZero (Multiplicative ‚Ñ§))À£) (hx : Valued.v x ‚â§ 1) :
    ‚àÉa, Valued.v ((algebraMap A K a) - (x : v.adicCompletion K)) < Œ≥.val := by
  -- Write `x = n / d`
  obtain ‚ü®‚ü®n, d, hd‚ü©, hnd‚ü© := IsLocalization.surj (nonZeroDivisors A) x
  dsimp only at hnd
  -- Show `v n ‚â§ v d`
  have hnd' := congr_arg Valued.v hnd
  simp only [adicValued_apply', map_mul] at hnd'
  have hge : Valued.v ((algebraMap A (WithVal (v.valuation K))) d) ‚â•
      Valued.v ((algebraMap A (WithVal (v.valuation K))) n) :=
    calc Valued.v ((algebraMap A (WithVal (v.valuation K))) d)
          ‚â• (valuation K v) x *
            (valuation K v) ((algebraMap A (WithVal (v.valuation K))) d) :=
                mul_le_of_le_one_left' hx
        _ = Valued.v ((algebraMap A (WithVal (v.valuation K))) n) := hnd'
  simp only [adicValued_apply', ge_iff_le,
    WithVal, adicValued_apply,
    valuation_of_algebraMap] at hge
  have hdz : (algebraMap A (WithVal (v.valuation K)) d) ‚â† 0 :=
    IsLocalization.to_map_ne_zero_of_mem_nonZeroDivisors _ (fun _ ‚Ü¶ id) hd
  -- Find a suitable `Œ≥` for the bound in `exists_adicValued_mul_sub_le`
  have hv : Valued.v ((algebraMap A (WithVal (v.valuation K)) d)) ‚â† 0 := by
    rw [Valuation.ne_zero_iff]
    exact hdz
  let hu : Valued.v ((algebraMap A (WithVal (v.valuation K)) d)) * Œ≥.val ‚â† 0 := by
    rw [mul_ne_zero_iff]
    exact ‚ü®hv, Œ≥.ne_zero‚ü©
  obtain ‚ü®Œ≥', hŒ≥, hŒ≥u, hŒ≥v‚ü© := WithZero.exists_ne_zero_and_lt_and_lt hu hv
  simp only [WithVal, adicValued_apply,
    valuation_of_algebraMap, intValuation_apply] at hŒ≥v
  -- Now can apply `exists_adicValued_mul_sub_le` to get the approximation of `x`.
  obtain ‚ü®a, hval‚ü© := exists_adicValued_mul_sub_le v hŒ≥ hŒ≥v.le hge
  use a
  rw [‚Üê eq_div_iff_mul_eq hdz] at hnd
  rw [‚Üê UniformSpace.Completion.coe_sub,
      valuedAdicCompletion_eq_valuation',
      hnd, sub_div' hdz, map_div‚ÇÄ]
  unfold WithVal at hdz ‚ä¢
  rw [‚Üê Valuation.pos_iff (valuation K v)] at hdz
  rw [‚Üê map_mul, ‚Üê map_sub, div_lt_iff‚ÇÄ' hdz, valuation_of_algebraMap,
      intValuation_apply]
  exact lt_of_le_of_lt hval hŒ≥u

/-- The closure of `A` in `K_v` is `ùí™_v`. -/
theorem closureAlgebraMapIntegers_eq_integers :
    closure (algebraMap A (v.adicCompletion K)).range =
    SetLike.coe (v.adicCompletionIntegers K) := by
  apply subset_antisymm
  -- We know `closure A ‚äÜ ùí™_v` because `ùí™_v` is closed and `A ‚äÜ ùí™_v`
  . apply closure_minimal _ Valued.valuationSubring_isClosed
    rintro b ‚ü®a, rfl‚ü©
    exact coe_mem_adicCompletionIntegers v a
  -- Show `ùí™_v ‚äÜ closure A` from `ùí™_v ‚äÜ closure O_[K]` and `closure O_[K] ‚äÜ closure A`
  . let f := fun (k : WithVal (v.valuation K)) => (k : v.adicCompletion K)
    suffices h : closure (f '' (f ‚Åª¬π' (adicCompletionIntegers K v))) ‚äÜ
        closure (algebraMap A (adicCompletion K v)).range by
      apply Set.Subset.trans _ h
      exact DenseRange.subset_closure_image_preimage_of_isOpen
        UniformSpace.Completion.denseRange_coe (Valued.valuationSubring_isOpen _)
    -- Unfold the topological definitions until we get the result from the previous lemma
    apply closure_minimal _ isClosed_closure
    rintro k ‚ü®x, hx, rfl‚ü©
    unfold f at hx
    rw [Set.mem_preimage, SetLike.mem_coe, mem_adicCompletionIntegers,
        Valued.valuedCompletion_apply, adicValued_apply'] at hx
    rw [mem_closure_iff_nhds_zero]
    intro U hU
    rw [Valued.mem_nhds] at hU
    obtain ‚ü®Œ≥, hŒ≥‚ü© := hU
    obtain ‚ü®a, ha‚ü© := exists_adicValued_sub_lt_of_adicValued_le_one K v Œ≥ hx
    use algebraMap A K a
    constructor
    . use a
      rfl
    . apply hŒ≥
      simpa

/-- `A` is dense in `ùí™_v`. -/
theorem denseRange_of_integerAlgebraMap :
    DenseRange (algebraMap A (v.adicCompletionIntegers K)) := by
  rw [denseRange_iff_closure_range, Set.eq_univ_iff_forall]
  intro x
  rw [closure_subtype]
  suffices h : Subtype.val ''
      Set.range ((algebraMap A ‚Ü•(adicCompletionIntegers K v))) =
      (algebraMap A (v.adicCompletion K)).range by
    rw [h, closureAlgebraMapIntegers_eq_integers K v]
    exact Subtype.coe_prop x
  simp only [RingHom.coe_range, ‚Üê Set.range_comp']
  rfl

/-- An element of `ùí™_v` can be approximated by an element of `A`. -/
theorem exists_adicValued_sub_lt_of_adicCompletionInteger ( x : v.adicCompletionIntegers K )
    ( Œ≥ : (WithZero (Multiplicative ‚Ñ§))À£ ) :
    ‚àÉa, Valued.v ((algebraMap A K a) - (x : v.adicCompletion K)) < Œ≥.val := by
  have h := closureAlgebraMapIntegers_eq_integers K v
  rw [Set.ext_iff] at h
  specialize h x
  simp_rw [RingHom.coe_range, Subtype.coe_prop, iff_true, mem_closure_iff_nhds] at h
  specialize h { y | Valued.v (y  - (x : v.adicCompletion K)) < Œ≥.val }
  have hn : {y | Valued.v (y - (x : v.adicCompletion K)) < Œ≥.val} ‚àà nhds x.val := by
    rw [Valued.mem_nhds]
    use Œ≥
  obtain ‚ü®z, ‚ü®hz, a, ha‚ü©‚ü© := h hn
  use a
  rw [algebraMap_adicCompletion, Function.comp_apply] at ha
  rwa [ha]

/-- The maximal ideal of the integers of the completion of `v`. -/
noncomputable abbrev completionIdeal : Ideal (v.adicCompletionIntegers K) :=
  IsLocalRing.maximalIdeal (adicCompletionIntegers K v)

lemma mem_maximalIdeal_completion_iff (x : v.adicCompletionIntegers K) :
    x ‚àà IsLocalRing.maximalIdeal (adicCompletionIntegers K v) ‚Üî Valued.v x.val < 1 :=
  Valuation.mem_maximalIdeal_iff _ _

lemma algebraMap_completionIntegers (x : A) :
    (algebraMap A (v.adicCompletionIntegers K) x) = (algebraMap A (v.adicCompletion K) x) :=
  rfl

instance : (v.completionIdeal K).LiesOver v.asIdeal where
  over := by
    rw [Ideal.under_def]
    ext x
    simp only [Ideal.mem_comap, completionIdeal, mem_maximalIdeal_completion_iff,
      algebraMap_completionIntegers, valuedAdicCompletion_eq_valuation,
      valuation_eq_intValuationDef, intValuation_lt_one_iff_dvd, Ideal.dvd_span_singleton]

open IsLocalRing in
/-- The canonical ring homomorphism from A / v to ùìû·µ• / v, where ùìû·µ• is the integers of the
completion K·µ• of the field of fractions of A. -/
noncomputable def ResidueFieldToCompletionResidueField :
    A ‚ß∏ v.asIdeal ‚Üí+* ResidueField (v.adicCompletionIntegers K) :=
  Ideal.quotientMap _ (algebraMap _ _) <| le_of_eq Ideal.LiesOver.over

open IsLocalRing in
/-- The canonical isomorphism from A / v to ùìû·µ• / v, where ùìû·µ• is the integers of the
completion K·µ• of the field of fractions K of A. -/
noncomputable def ResidueFieldEquivCompletionResidueField :
    A ‚ß∏ v.asIdeal ‚âÉ+* ResidueField (v.adicCompletionIntegers K) :=
  RingEquiv.ofBijective (ResidueFieldToCompletionResidueField K v)
  ‚ü®Ideal.quotientMap_injective' <| ge_of_eq Ideal.LiesOver.over,
    by
      rintro ‚ü®x‚ü©
      obtain ‚ü®a, ha‚ü© := exists_adicValued_sub_lt_of_adicCompletionInteger K v x 1
      use a
      apply Ideal.Quotient.eq.mpr
      rwa [mem_maximalIdeal_completion_iff]‚ü©

theorem inertiaDeg_asIdeal_completionIdeal :
    Ideal.inertiaDeg v.asIdeal (v.completionIdeal K) = 1 := by
  rw [Ideal.inertiaDeg_algebraMap]
  have f : (A ‚ß∏ v.asIdeal) ‚âÉ‚Çó[A ‚ß∏ v.asIdeal]
      ((adicCompletionIntegers K v) ‚ß∏ completionIdeal K v) := {
    __ := ResidueFieldEquivCompletionResidueField K v
    map_smul' := by
      rintro ‚ü®x‚ü© y
      rw [Algebra.smul_def, Algebra.smul_def]
      apply map_mul (ResidueFieldEquivCompletionResidueField K v)
  }
  rw [‚Üê LinearEquiv.finrank_eq f]
  exact Module.finrank_self _

/-- An element of `‚àè_{v ‚àà s} ùí™_v`, with `s` finite, can be approximated by an element of `A`.
-/
theorem exists_forall_adicValued_sub_lt {Œπ : Type*} (s : Finset Œπ)
    (e : Œπ ‚Üí (WithZero (Multiplicative ‚Ñ§))À£ ) (valuation : Œπ ‚Üí HeightOneSpectrum A)
    (injective : Function.Injective valuation)
    (x : (i : Œπ) ‚Üí (valuation i).adicCompletionIntegers K) :
    ‚àÉ a, ‚àÄ i ‚àà s, Valued.v ((algebraMap A K a) - (x i).val) < (e i).val := by
  -- Approximate elements of `ùí™_v` with elements of `A` using the previous theorem.
  choose f hf using fun (i : s) =>
    exists_adicValued_sub_lt_of_adicCompletionInteger K (valuation i) (x i) (e i)

  -- Convert the hypotheses from being about valuations to being about ideals, so
  -- that we can apply (a suitable corollary of) the Chinese remainder theorem.
  have hexists_e' : ‚àÄ (i : Œπ), ‚àÉ (e' : ‚Ñï), (Multiplicative.ofAdd (-(e' : ‚Ñ§))) < (e i).val := by
    intro i
    apply exists_ofAdd_natCast_lt (e i).ne_zero
  choose e' he' using hexists_e'
  have hinj : ‚àÄ i ‚àà s, ‚àÄ j ‚àà s, i ‚â† j ‚Üí
      (fun i ‚Ü¶ (valuation i).asIdeal) i ‚â† (fun i ‚Ü¶ (valuation i).asIdeal) j := by
    intro _ _ _ _
    exact mt <| fun hij ‚Ü¶ injective (HeightOneSpectrum.ext hij)

  -- Use Chinese remainder theorem to get a single approximation for `f i` for all `i ‚àà s`.
  obtain ‚ü®a, ha‚ü© := IsDedekindDomain.exists_forall_sub_mem_ideal (s := s)
    (fun i => (valuation i).asIdeal) e' (fun i hi => (valuation i).prime) hinj f
  use a
  intro i hi
  specialize ha i hi
  specialize hf ‚ü®i, hi‚ü©
  rw [‚Üê Ideal.dvd_span_singleton, ‚Üê intValuation_le_pow_iff_dvd,
      ‚Üê intValuation_apply, ‚Üê valuation_of_algebraMap (K := K),
      ‚Üê valuedAdicCompletion_eq_valuation, algebraMap.coe_sub] at ha
  refine lt_of_le_of_lt ?_ (Valuation.map_add_lt _ (ha.trans_lt (he' i)) hf)
  apply le_of_eq
  congr
  rw [add_sub, sub_eq_sub_iff_add_eq_add, add_right_cancel_iff,
    add_comm_sub, add_sub, eq_sub_iff_add_eq]
  rfl

/-- The closure of `A` in `‚àè_{v ‚àà s} K_v` is `‚àè_{v ‚àà s} ùí™_v`. `s` may be infinite. -/
theorem closureAlgebraMapIntegers_eq_prodIntegers {Œπ : Type*}
    (valuation : Œπ ‚Üí HeightOneSpectrum A) (injective : Function.Injective valuation) :
    closure (SetLike.coe (algebraMap A ((i : Œπ) ‚Üí (valuation i).adicCompletion K)).range) =
    (Set.pi Set.univ (fun (i : Œπ) ‚Ü¶ ((valuation i).adicCompletionIntegers K).carrier)) := by
  apply Set.Subset.antisymm
  . apply closure_minimal
    . rintro c ‚ü®a, ha‚ü© i -
      rw [‚Üê ha]
      simp only [Pi.algebraMap_apply, SetLike.mem_coe]
      exact coe_mem_adicCompletionIntegers (valuation i) a
    . apply isClosed_set_pi
      rintro w -
      exact Valued.valuationSubring_isClosed
  . intro f hf
    rw [mem_closure_iff_nhds_zero]
    intro U hU
    rw [Pi.zero_def, nhds_pi, Filter.mem_pi'] at hU
    obtain ‚ü®I, t, htn, hts‚ü© := hU
    choose g hg using fun w => (Valued.is_topological_valuation (t w)).mp (htn w)
    obtain ‚ü®a, ha‚ü© :=
      exists_forall_adicValued_sub_lt K I g valuation injective (fun w => ‚ü®f w, hf w ‚ü®‚ü©‚ü©)
    use algebraMap A _ a
    constructor
    . rw [RingHom.coe_range]
      exact Set.mem_range_self a
    . exact hts fun w hw ‚Ü¶ hg w <| ha w hw

lemma adicCompletion.eq_mul_nonZeroDivisor_inv_adicCompletionIntegers (v : HeightOneSpectrum A)
    (x : v.adicCompletion K) :
    ‚àÉa ‚àà nonZeroDivisors A, ‚àÉb ‚àà v.adicCompletionIntegers K, x = (algebraMap A K a)‚Åª¬π ‚Ä¢ b := by
  obtain ‚ü®a, hz, ha‚ü© :=
    adicCompletion.mul_nonZeroDivisor_mem_adicCompletionIntegers v x
  use a, hz, (algebraMap A K a) ‚Ä¢ x
  constructor
  . rwa [Algebra.smul_def, ‚Üê IsScalarTower.algebraMap_apply, mul_comm]
  . rw [smul_smul, inv_mul_cancel‚ÇÄ, one_smul]
    exact IsLocalization.to_map_ne_zero_of_mem_nonZeroDivisors K (fun _ ‚Ü¶ id) hz

lemma adicCompletion.eq_mul_pi_adicCompletionIntegers {Œπ : Type*} [Fintype Œπ]
    (valuation : Œπ ‚Üí HeightOneSpectrum A) (x : (i : Œπ) ‚Üí (valuation i).adicCompletion K) :
      ‚àÉk : K, ‚àÉy ‚àà Set.pi Set.univ (fun (i : Œπ) ‚Ü¶ ((valuation i).adicCompletionIntegers K).carrier),
      x = k ‚Ä¢ y := by
  classical
  choose f hf using fun (i : Œπ) =>
    eq_mul_nonZeroDivisor_inv_adicCompletionIntegers K (valuation i) (x i)
  use (algebraMap A K (‚àè i : Œπ, f i))‚Åª¬π, (algebraMap A K (‚àè i : Œπ, f i)) ‚Ä¢ x
  have hz : ‚àÄ (i : Œπ), (algebraMap A K) (f i) ‚â† 0 := fun i =>
    IsLocalization.to_map_ne_zero_of_mem_nonZeroDivisors K (fun _ ‚Ü¶ id) (hf i).left
  constructor
  . rintro i -
    obtain ‚ü®b, hb, hx‚ü© := (hf i).right
    beta_reduce
    rw [Pi.smul_apply, algebraMap_smul, Subsemiring.coe_carrier_toSubmonoid,
        Subring.coe_toSubsemiring, SetLike.mem_coe, ValuationSubring.mem_toSubring, hx,
        ‚Üê Finset.prod_erase_mul _ f (Finset.mem_univ i), mul_smul,
        ‚Üê IsScalarTower.smul_assoc (f i), Algebra.smul_def (f i), mul_inv_cancel‚ÇÄ (hz i), one_smul,
        Algebra.smul_def]
    apply mul_mem (coe_mem_adicCompletionIntegers _ _) hb
  . rw [smul_smul, inv_mul_cancel‚ÇÄ, one_smul]
    simp [Finset.prod_ne_zero_iff, hz]

/-- If `s` is finite then `K` in dense in `‚àè_{v ‚àà s} K_v`. -/
theorem denseRange_of_prodAlgebraMap {Œπ : Type*} [Fintype Œπ]
    {valuation : Œπ ‚Üí HeightOneSpectrum A} (injective : Function.Injective valuation) :
    DenseRange (algebraMap K ((i : Œπ) ‚Üí (valuation i).adicCompletion K)) := by
  rw [denseRange_iff_closure_range, Set.eq_univ_iff_forall]
  let S := Set.range (algebraMap K ((i : Œπ) ‚Üí (valuation i).adicCompletion K))
  -- We've already shown that the closure of `A` is `‚àè_{v ‚àà s} ùí™_v`, so
  -- the closure of `K` at least contains this set.
  have hint : Set.pi Set.univ (fun (i : Œπ) ‚Ü¶ ((valuation i).adicCompletionIntegers K).carrier)
      ‚äÜ closure S := by
    rw [‚Üê closureAlgebraMapIntegers_eq_prodIntegers _ _ injective]
    apply closure_mono
    exact fun _ ‚ü®a, ha‚ü© ‚Ü¶ ‚ü®algebraMap A K a, ha‚ü©
  -- Next, the closure of `K` is closed under multiplication by `K` because
  -- scalar multiplication by a constant is continuous.
  have hmul : ‚àÄx, x ‚àà closure S ‚Üí ‚àÄk : K, k ‚Ä¢ x ‚àà closure S := by
    intro x h k
    let f := fun (z : (i : Œπ) ‚Üí (valuation i).adicCompletion K) ‚Ü¶ k ‚Ä¢ z
    have hf : ContinuousAt f x := Continuous.continuousAt (continuous_const_smul k)
    apply closure_mono _ <| mem_closure_image hf h
    rintro x ‚ü®_, ‚ü®z, rfl‚ü©, rfl‚ü©
    use k ‚Ä¢ algebraMap K _ z
    ext i
    simp [Algebra.smul_def, f]
  -- Finally, `‚àè_{v ‚àà s} K_v = K ‚Ä¢ ‚àè_{v ‚àà s} ùí™_v`
  intro x
  obtain ‚ü®k, y, hy, hx‚ü© := adicCompletion.eq_mul_pi_adicCompletionIntegers K valuation x
  exact hx ‚ñ∏ hmul y (hint hy) k

namespace adicCompletion

open scoped algebraMap in
theorem exists_uniformizer (v : HeightOneSpectrum A) :
    ‚àÉ œÄ : v.adicCompletionIntegers K, Valued.v œÄ.1 = Multiplicative.ofAdd (- 1 : ‚Ñ§) := by
  obtain ‚ü®œÄ, hœÄ‚ü© := v.intValuation_exists_uniformizer
  use œÄ
  rw [‚Üê hœÄ, ‚Üê ValuationSubring.algebraMap_apply, ‚Üê IsScalarTower.algebraMap_apply,
    v.valuedAdicCompletion_eq_valuation, v.valuation_eq_intValuationDef]

variable {K} in
theorem uniformizer_ne_zero {v : HeightOneSpectrum A}
    {œÄ : v.adicCompletionIntegers K} (hœÄ : Valued.v œÄ.1 = Multiplicative.ofAdd (-1 : ‚Ñ§)) :
    œÄ ‚â† 0 := by
  contrapose! hœÄ
  simp [hœÄ]

variable {K} in
open scoped Multiplicative in
theorem uniformizer_not_isUnit {œÄ : v.adicCompletionIntegers K}
    (hœÄ : Valued.v œÄ.1 = Multiplicative.ofAdd (-1 : ‚Ñ§)) :
    ¬¨IsUnit (œÄ : v.adicCompletionIntegers K) := by
  rw [ValuationSubring.isUnit_iff_valued_eq_one, ‚Üê WithZero.coe_one, ‚Üê ofAdd_zero, hœÄ]
  apply ne_of_lt
  rw [WithZero.coe_lt_coe, Multiplicative.ofAdd_lt]
  omega

theorem eq_pow_uniformizer_mul_unit {x : v.adicCompletionIntegers K} (hx : x ‚â† 0)
    {œÄ : v.adicCompletionIntegers K} (hœÄ : Valued.v œÄ.1 = Multiplicative.ofAdd (-1 : ‚Ñ§)) :
    ‚àÉ (n : ‚Ñï) (u : (v.adicCompletionIntegers K)À£), x = œÄ ^ n * u := by
  have hx' : Valued.v x.1 ‚â† 0 := by simp [hx]
  let m := - Multiplicative.toAdd (WithZero.unzero hx')
  have hm‚ÇÄ : 0 ‚â§ m := by
    simp_rw [m, Right.nonneg_neg_iff, ‚Üê toAdd_one, Multiplicative.toAdd_le]
    rw [‚Üê WithZero.coe_le_coe]; exact (WithZero.coe_unzero _).symm ‚ñ∏ x.2
  have hpow : Valued.v (œÄ ^ (-m) * x.val) = 1 := by
    rw [Valued.v.map_mul, map_zpow‚ÇÄ, hœÄ, ofAdd_neg, WithZero.coe_inv,
      inv_zpow', neg_neg, ‚Üê WithZero.coe_zpow, ‚Üê Int.ofAdd_mul, one_mul, ofAdd_neg, ofAdd_toAdd,
      WithZero.coe_inv, WithZero.coe_unzero, inv_mul_cancel‚ÇÄ hx']
  let a : v.adicCompletionIntegers K := ‚ü®œÄ ^ (-m) * x.val, le_of_eq hpow‚ü©
  refine ‚ü®m.toNat, (ValuationSubring.isUnit_of_valued_eq_one a hpow).unit, Subtype.ext ?_‚ü©
  simp only [zpow_neg, IsUnit.unit_spec, MulMemClass.coe_mul, SubmonoidClass.coe_pow, a,
    ‚Üê zpow_natCast, m.toNat_of_nonneg hm‚ÇÄ, ‚Üê mul_assoc]
  rw [mul_inv_cancel‚ÇÄ (zpow_ne_zero _ <| (by simp [uniformizer_ne_zero hœÄ])), one_mul]

open scoped algebraMap in
theorem maximalIdeal_eq_span_uniformizer {œÄ : v.adicCompletionIntegers K}
    (hœÄ : Valued.v œÄ.1 = Multiplicative.ofAdd (-1 : ‚Ñ§)) :
    IsLocalRing.maximalIdeal (v.adicCompletionIntegers K) =
      Ideal.span {(œÄ : v.adicCompletionIntegers K)} := by
  refine (IsLocalRing.maximalIdeal.isMaximal _).eq_of_le
    (Ideal.span_singleton_ne_top (uniformizer_not_isUnit v hœÄ)) (fun x hx => ?_)
  by_cases hx‚ÇÄ : x = 0
  ¬∑ simp only [ZeroMemClass.coe_eq_zero] at hx‚ÇÄ
    simp only [hx‚ÇÄ, Ideal.zero_mem]
  ¬∑ obtain ‚ü®n, ‚ü®u, hu‚ü©‚ü© := eq_pow_uniformizer_mul_unit K v hx‚ÇÄ hœÄ
    have hn : ¬¨(IsUnit x) := fun h =>
      (IsLocalRing.maximalIdeal.isMaximal _).ne_top (Ideal.eq_top_of_isUnit_mem _ hx h)
    replace hn : n ‚â† 0 := fun h => by {rw [hu, h, pow_zero, one_mul] at hn; exact hn u.isUnit}
    simpa [Ideal.mem_span_singleton, hu, IsUnit.dvd_mul_right, Units.isUnit] using dvd_pow_self _ hn

instance : Ring.DimensionLEOne (v.adicCompletionIntegers K) where
  maximalOfPrime {ùî≠} hùî≠_ne_bot hùî≠_prime := by
    let ‚ü®x, hx‚ü© := Submodule.exists_mem_ne_zero_of_ne_bot hùî≠_ne_bot
    let ‚ü®œÄ, hœÄ‚ü© := exists_uniformizer K v
    obtain ‚ü®n, ‚ü®u, rfl‚ü©‚ü© := eq_pow_uniformizer_mul_unit K v hx.2 hœÄ
    simp only [Units.isUnit, Ideal.mul_unit_mem_iff_mem, ne_eq, mul_eq_zero, pow_eq_zero_iff',
      FaithfulSMul.algebraMap_eq_zero_iff, Units.ne_zero, or_false, not_and,
      Decidable.not_not] at hx
    by_cases hn : n = 0
    ¬∑ simp only [hn, pow_zero, ‚Üê ùî≠.eq_top_iff_one, implies_true, and_true] at hx
      exact hùî≠_prime.ne_top hx |>.elim
    ¬∑ rw [hùî≠_prime.pow_mem_iff_mem n (by omega), ‚Üê ùî≠.span_singleton_le_iff_mem,
        ‚Üê maximalIdeal_eq_span_uniformizer K v hœÄ] at hx
      exact IsLocalRing.maximalIdeal_le hùî≠_prime.ne_top hx.1

open scoped algebraMap in
instance : IsPrincipalIdealRing (v.adicCompletionIntegers K) := by
  apply IsPrincipalIdealRing.of_prime
  intro P hP
  by_cases hP_bot : P = ‚ä•
  ¬∑ exact hP_bot ‚ñ∏ bot_isPrincipal
  ¬∑ let ‚ü®œÄ, hœÄ‚ü© := exists_uniformizer K v
    use œÄ
    rw [IsLocalRing.eq_maximalIdeal (hP.isMaximal hP_bot)]
    exact maximalIdeal_eq_span_uniformizer K v hœÄ

instance : IsDiscreteValuationRing (v.adicCompletionIntegers K) where
  __ := inferInstanceAs (IsPrincipalIdealRing (v.adicCompletionIntegers K))
  not_a_field' := by
    let ‚ü®œÄ, hœÄ‚ü© := exists_uniformizer K v
    rw [maximalIdeal_eq_span_uniformizer K v hœÄ]
    intro h
    simp only [Ideal.span_singleton_eq_bot, FaithfulSMul.algebraMap_eq_zero_iff] at h
    exact uniformizer_ne_zero hœÄ h

open scoped Valued in
instance : IsDiscreteValuationRing (ùí™[v.adicCompletion K]) :=
  inferInstanceAs (IsDiscreteValuationRing (v.adicCompletionIntegers K))

end adicCompletion


lemma completion_ne_bot : completionIdeal K v ‚â† ‚ä• := IsDiscreteValuationRing.not_a_field _

/-- The unique element of the HeightOneSpectrum of the integers of the completion of `v`. -/
noncomputable abbrev to_completion :
    HeightOneSpectrum (v.adicCompletionIntegers K) where
  asIdeal := completionIdeal K v
  isPrime := Ideal.IsMaximal.isPrime' _
  ne_bot := completion_ne_bot K v


end IsDedekindDomain.HeightOneSpectrum
