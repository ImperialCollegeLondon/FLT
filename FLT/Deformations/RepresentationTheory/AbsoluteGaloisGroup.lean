import FLT.Deformations.RepresentationTheory.Frobenius
import FLT.Deformations.RepresentationTheory.IntegralClosure
import FLT.NumberField.Completion.Finite
import Mathlib.Analysis.Normed.Unbundled.SpectralNorm
import Mathlib.FieldTheory.AbsoluteGaloisGroup
import Mathlib.FieldTheory.Galois.Infinite
import Mathlib.NumberTheory.NumberField.FinitePlaces

variable {K L : Type*} [Field K] [Field L]
variable {A B : Type*} [CommRing A] [TopologicalSpace A] [CommRing B] [TopologicalSpace B]
variable {M N : Type*} [AddCommGroup M] [Module A M] [AddCommGroup N] [Module A N]
variable {n : Type*} [Fintype n] [DecidableEq n]

open NumberField

variable [NumberField K]

variable (v : IsDedekindDomain.HeightOneSpectrum (ğ“ K))

local notation3 "Î“" K:max => Field.absoluteGaloisGroup K
local notation3 K:max "áµƒË¡áµ" => AlgebraicClosure K
local notation3 "ğ”ª" => IsLocalRing.maximalIdeal
local notation3 "Îº" => IsLocalRing.ResidueField
local notation "Î©" K => IsDedekindDomain.HeightOneSpectrum (ğ“ K)
local notation "Káµ¥" => IsDedekindDomain.HeightOneSpectrum.adicCompletion K v
local notation "ğ’ªáµ¥" => IsDedekindDomain.HeightOneSpectrum.adicCompletionIntegers K v

/-- Given a field extension, this is a map between its absolute galois group.
Note that this relies on an arbitrarily chosen embedding of the algebraic closures -/
noncomputable
def Field.absoluteGaloisGroup.mapAux (f : K â†’+* L) : Î“ L â†’* Î“ K where
  toFun Ïƒ :=
    letI := f.toAlgebra
    letI := (AlgebraicClosure.map f).toAlgebra
    ((Ïƒ.restrictScalars K).toAlgHom.comp
      (IsAlgClosed.lift : KáµƒË¡áµ â†’â‚[K] LáµƒË¡áµ)).restrictNormal' (KáµƒË¡áµ)
  map_one' := by
    letI := f.toAlgebra
    letI := (AlgebraicClosure.map f).toAlgebra
    apply AlgEquiv.ext fun i â†¦ ?_
    apply (IsAlgClosed.lift : KáµƒË¡áµ â†’â‚[K] LáµƒË¡áµ).injective
    refine (AlgHom.restrictNormal_commutes _ _ _).trans (by simp)
  map_mul' Ïƒâ‚ Ïƒâ‚‚ := by
    letI := f.toAlgebra
    letI := (AlgebraicClosure.map f).toAlgebra
    apply AlgEquiv.ext fun i â†¦ ?_
    apply (AlgebraicClosure.map f).injective
    refine (AlgHom.restrictNormal_commutes _ _ _).trans ?_
    refine ((AlgHom.restrictNormal_commutes _ _ _).trans ?_).symm
    simpa [absoluteGaloisGroup] using AlgHom.restrictNormal_commutes _ _ _

/-- Given a field extension, this is a continuous map between its absolute galois group.
Note that this relies on an arbitrarily chosen embedding of the algebraic closures -/
noncomputable
def Field.absoluteGaloisGroup.map (f : K â†’+* L) : Î“ L â†’â‚œ* Î“ K where
  __ := Field.absoluteGaloisGroup.mapAux f
  continuous_toFun := by
    classical
    letI := f.toAlgebra
    let F : KáµƒË¡áµ â†’â‚[K] LáµƒË¡áµ := IsAlgClosed.lift
    letI := F.toRingHom.toAlgebra
    apply continuous_of_continuousAt_one (Field.absoluteGaloisGroup.mapAux f)
    rw [ContinuousAt, map_one]
    refine ((galGroupBasis L (LáµƒË¡áµ)).nhds_one_hasBasis.tendsto_iff
      (galGroupBasis K (KáµƒË¡áµ)).nhds_one_hasBasis).mpr ?_
    rintro _ âŸ¨_, âŸ¨K', hK', rflâŸ©, rflâŸ©
    refine âŸ¨_, âŸ¨_, âŸ¨.adjoin _ (K'.map F), ?_, rflâŸ©, rflâŸ©, fun Ïƒ hÏƒ x â†¦ ?_âŸ©
    Â· have : FiniteDimensional _ _ := hK'
      obtain âŸ¨s, hsâŸ© := K'.fg_iff_finiteType.mpr (inferInstanceAs (Algebra.FiniteType K K'))
      obtain rfl := IntermediateField.eq_adjoin_of_eq_algebra_adjoin _ _ _ hs.symm
      simp only [IntermediateField.adjoin_map, IntermediateField.adjoin_adjoin_right,
        â† Finset.coe_image]
      refine IntermediateField.finiteDimensional_adjoin fun _ _ â†¦ Algebra.IsIntegral.isIntegral _
    Â· exact F.injective ((AlgHom.restrictNormal_commutes _ _ _).trans
        (hÏƒ âŸ¨F x, IntermediateField.subset_adjoin _ _ âŸ¨_, x.2, rflâŸ©âŸ©))

set_option allowUnsafeReducibility true in
attribute [reducible] Field.absoluteGaloisGroup

lemma Field.absoluteGaloisGroup.lift_map (f : K â†’+* L) (Ïƒ : Î“ L) (x : KáµƒË¡áµ) :
    AlgebraicClosure.map f (map f Ïƒ x) = Ïƒ (AlgebraicClosure.map f x) := by
  letI := f.toAlgebra
  letI := (AlgebraicClosure.map f).toAlgebra
  exact AlgHom.restrictNormal_commutes _ _ _

instance finiteIndex_fixingSubgroup {K L : Type*} [Field K] [Field L] [Algebra K L]
    (E : IntermediateField K L) [FiniteDimensional K E] : E.fixingSubgroup.FiniteIndex := by
  let f : (L â‰ƒâ‚[K] L) â§¸ E.fixingSubgroup â†’ E â†’â‚[K] L := Quotient.lift
    (fun f â†¦ f.toAlgHom.comp E.val)
    (by rintro _ Ï„ âŸ¨Ïƒ, rflâŸ©; ext x; exact DFunLike.congr_arg Ï„ (Ïƒ.2 x))
  have : Function.Injective f := by
    rintro âŸ¨ÏƒâŸ© âŸ¨Ï„âŸ© (H : Ïƒ.toAlgHom.comp E.val = Ï„.toAlgHom.comp E.val)
    refine Quotient.sound âŸ¨âŸ¨.op (Ï„â»Â¹ * Ïƒ), fun x â†¦ ?_âŸ©, by simpâŸ©
    simpa [AlgEquiv.aut_inv, AlgEquiv.symm_apply_eq] using DFunLike.congr_fun H x
  have := Finite.of_injective _ this
  exact Subgroup.finiteIndex_of_finite_quotient

open IntermediateField in
instance {K L : Type*} [Field K] [Field L] [Algebra K L] [Algebra.IsAlgebraic K L] :
    CompactSpace (L â‰ƒâ‚[K] L) := by
  classical
  letI := IsTopologicalGroup.toUniformSpace (L â‰ƒâ‚[K] L)
  rw [â† isCompact_univ_iff, isCompact_iff_totallyBounded_isComplete]
  refine âŸ¨IsTopologicalGroup.totallyBounded fun s hs â†¦ ?_, ?_âŸ©
  Â· obtain âŸ¨E, hE, HâŸ© := (krullTopology_mem_nhds_one_iff _ _ _).mp hs
    refine âŸ¨_, inferInstance, HâŸ©
  Â· rintro f hf -
    have := hf.1
    have (x : L) :
        âˆƒ Ïƒâ‚€ : L â‰ƒâ‚[K] L, âˆƒ t âˆˆ f, âˆ€ Ïƒ âˆˆ t, âˆ€ Ï„ : L â‰ƒâ‚[K] L, Ïƒ (Ï„ x) = Ïƒâ‚€ (Ï„ x) := by
      have : FiniteDimensional K KâŸ®xâŸ¯ :=
        adjoin.finiteDimensional (Algebra.IsIntegral.isIntegral _)
      obtain âŸ¨t, htf, HâŸ© := ((Filter.HasBasis.cauchy_iff
        (by exact (galGroupBasis K L).nhds_one_hasBasis.comap _)).mp hf).2 _ (by
            exact âŸ¨_, âŸ¨normalClosure K KâŸ®xâŸ¯ L, inferInstanceAs (FiniteDimensional K _), rflâŸ©, rflâŸ©)
      obtain âŸ¨Ïƒ, hÏƒâŸ© := f.nonempty_of_mem htf
      refine âŸ¨Ïƒ, t, htf, fun Ï„ hÏ„ Ï„â‚€ â†¦ ?_âŸ©
      have : Ïƒ (Ï„.symm (Ï„ (Ï„â‚€ x))) = Ï„ (Ï„â‚€ x) := H Ï„ hÏ„ Ïƒ hÏƒ âŸ¨Ï„ (Ï„â‚€ x), by
        refine SetLike.le_def.mp (le_iSup _ (Ï„.toAlgHom.comp <| Ï„â‚€.toAlgHom.comp (val _))) ?_
        exact âŸ¨âŸ¨_, subset_adjoin _ _ (by simp)âŸ©, rflâŸ©âŸ©
      simpa using this.symm
    choose Ïƒâ‚€ t htf H using this
    have H' (s Ïƒ hÏƒ) := H s Ïƒ hÏƒ .refl
    dsimp at H'
    let F : L â‰ƒâ‚[K] L :=
    { toFun x := Ïƒâ‚€ x x
      invFun x := (Ïƒâ‚€ x).symm x
      left_inv x := by
        obtain âŸ¨Ïƒ, hÏƒâ‚, hÏƒâ‚‚âŸ© := f.nonempty_of_mem (f.inter_mem (htf x) (htf (Ïƒâ‚€ x x)))
        dsimp
        have H' := H' _ _ hÏƒâ‚
        have : Ïƒ x = (Ïƒâ‚€ (Ïƒâ‚€ x x) x) := by simpa using H _ _ hÏƒâ‚‚ (Ïƒâ‚€ x).symm
        rw [â† H', AlgEquiv.symm_apply_eq, H', â† this, H']
      right_inv x := by
        obtain âŸ¨Ïƒ, hÏƒâ‚, hÏƒâ‚‚âŸ© := f.nonempty_of_mem (f.inter_mem (htf x) (htf ((Ïƒâ‚€ x).symm x)))
        dsimp
        replace H := H _ _ hÏƒâ‚ Ïƒ.symm
        simp only [AlgEquiv.apply_symm_apply, â† AlgEquiv.symm_apply_eq, AlgEquiv.symm_symm] at H
        rw [â† H' _ _ hÏƒâ‚‚, H]
      map_mul' x y := by
        obtain âŸ¨Ïƒ, hÏƒx, hÏƒy, hÏƒxyâŸ© :=
          f.nonempty_of_mem (f.inter_mem (htf x) (f.inter_mem (htf y) (htf (x * y))))
        rw [â† H' _ _ hÏƒxy, â† H' _ _ hÏƒx, â† H' _ _ hÏƒy, map_mul]
      map_add' x y := by
        obtain âŸ¨Ïƒ, hÏƒx, hÏƒy, hÏƒxyâŸ© :=
          f.nonempty_of_mem (f.inter_mem (htf x) (f.inter_mem (htf y) (htf (x + y))))
        rw [â† H' _ _ hÏƒxy, â† H' _ _ hÏƒx, â† H' _ _ hÏƒy, map_add]
      commutes' := by simp }
    refine âŸ¨F, Set.mem_univ _, ?_âŸ©
    rw [((galGroupBasis K L).nhds_hasBasis F).ge_iff]
    rintro _ âŸ¨_, âŸ¨E, hE, rflâŸ©, rflâŸ©
    simp only [Set.image_mul_left]
    have âŸ¨s, hsâŸ© := E.toSubmodule.fg_iff_finiteDimensional.mpr hE
    refine f.mem_of_superset ((Filter.biInter_finset_mem s).mpr fun i _ â†¦ htf i) ?_
    rintro Ïƒ hÏƒ âŸ¨x, hxâŸ©
    change F.symm (Ïƒ x) = x
    induction hs.ge hx using Submodule.span_induction with
    | zero | add | smul => simp_all
    | mem x h =>
      rw [AlgEquiv.symm_apply_eq]
      simp [F, â† H' _ _ (Set.mem_iInterâ‚‚.mp hÏƒ _ h)]

open scoped IntermediateField in
instance {K L : Type*} [Field K] [Field L] [Algebra K L] [Algebra.IsAlgebraic K L] :
    ContinuousSMulDiscrete (L â‰ƒâ‚[K] L) L := by
  constructor
  intro x y
  rw [isOpen_iff_forall_mem_open]
  rintro Ïƒ (hÏƒ : _ = _)
  have : FiniteDimensional K KâŸ®xâŸ¯ := IntermediateField.adjoin.finiteDimensional
      (Algebra.IsAlgebraic.isAlgebraic (R := K) x).isIntegral
  refine âŸ¨_, ?_, KâŸ®xâŸ¯.fixingSubgroup_isOpen.smul Ïƒ, 1, one_mem _, by simpâŸ©
  rintro _ âŸ¨Ï„, hÏ„, rflâŸ©
  have := (mem_fixingSubgroup_iff _).mp hÏ„ x (IntermediateField.mem_adjoin_simple_self K x)
  simp only [smul_eq_mul, Set.mem_setOf_eq, mul_smul, this, hÏƒ]

instance {K L : Type*} [Field K] [Field L] [Algebra K L] [IsGalois K L] :
    Algebra.IsInvariant K L (L â‰ƒâ‚[K] L) :=
  âŸ¨fun _ H â†¦ (InfiniteGalois.fixedField_fixingSubgroup
    (âŠ¥ : IntermediateField K L)).le fun _ â†¦ H _âŸ©

attribute [local instance 100000]
  instAlgebraSubtypeMemValuationSubring_fLT IntermediateField.algebra'
  Algebra.toSMul Subalgebra.toCommRing Algebra.toModule
  Subalgebra.toRing Ring.toAddCommGroup AddCommGroup.toAddGroup
  ValuationSubring.smulCommClass IntermediateField.toAlgebra
  IntermediateField.smulCommClass_of_normal
  mulSemiringActionIntegralClosure
  Subalgebra.algebra
  CommRing.toCommSemiring
  Valued.toIsUniformAddGroup

attribute [local instance] Valued.toNormedField in
lemma isIntegral_of_spectralNorm_le_one
    {K L Î“â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“â‚€] [Field K] [Field L]
    [Valued K Î“â‚€] [(Valued.v : Valuation K Î“â‚€).RankOne] [Algebra K L] [Algebra.IsAlgebraic K L]
    {x : L} (hx : spectralNorm K L x â‰¤ 1) : IsIntegral (Valued.v : Valuation K Î“â‚€).integer x := by
  have : minpoly K x âˆˆ Polynomial.lifts (Valued.v : Valuation K Î“â‚€).integer.subtype := by
    refine (Polynomial.lifts_iff_coeff_lifts _).mpr fun i â†¦ ?_
    have := (ciSup_le_iff (spectralValueTerms_bddAbove ..)).mp hx i
    simp only [spectralValueTerms] at this
    split_ifs at this with h
    Â· conv_rhs at this => rw [â† Real.one_rpow (1 / (â†‘(minpoly K x).natDegree - â†‘i) : â„)]
      rw [Real.rpow_le_rpow_iff (by positivity) (by positivity) (by aesop)] at this
      simpa [Valuation.mem_integer_iff] using this
    obtain h | h := (le_of_not_lt h).eq_or_lt
    Â· simp [â† h, minpoly.monic (Algebra.IsAlgebraic.isAlgebraic x).isIntegral, one_mem]
    Â· simp [Polynomial.coeff_eq_zero_of_natDegree_lt h, zero_mem]
  obtain âŸ¨P, hP, _, hP'âŸ© := Polynomial.lifts_and_degree_eq_and_monic this
    (minpoly.monic (Algebra.IsAlgebraic.isAlgebraic x).isIntegral)
  refine âŸ¨P, hP', ?_âŸ©
  rw [â† Polynomial.aeval_def, â† Polynomial.aeval_map_algebraMap K,
    Subring.algebraMap_def, hP, minpoly.aeval]

lemma spectralAlgNorm_mul {K : Type*} [NontriviallyNormedField K]
    {L : Type*} [Field L] [Algebra K L]
    [Algebra.IsAlgebraic K L] [CompleteSpace K] [IsUltrametricDist K] (x y : L) :
    spectralAlgNorm K L (x * y) = spectralAlgNorm K L x * spectralAlgNorm K L y := by
  sorry -- This is proved in https://github.com/leanprover-community/mathlib4/pull/23333

lemma spectralNorm_inv
    {K L : Type*} [NontriviallyNormedField K] [Field L] [Algebra K L] [IsUltrametricDist K]
    [CompleteSpace K] [Algebra.IsAlgebraic K L] (x : L) :
    spectralNorm K L (xâ»Â¹) = (spectralNorm K L x)â»Â¹ := by
  by_cases H : x = 0
  Â· simp [H, spectralNorm_zero]
  refine eq_inv_of_mul_eq_one_right ?_
  rw [â† spectralAlgNorm_def, â† spectralAlgNorm_def, â† spectralAlgNorm_mul (K := K) x xâ»Â¹,
    mul_inv_cancelâ‚€ H, spectralAlgNorm_one]

noncomputable instance : NontriviallyNormedField Káµ¥ := Valued.toNontriviallyNormedField

instance valuationRing_integralClosure
    {L : Type*} [Field L] [Algebra Káµ¥ L] [Algebra.IsAlgebraic Káµ¥ L] :
    ValuationRing (IntegralClosure ğ’ªáµ¥ L) := by
  refine ValuationSubring.instValuationRingSubtypeMem âŸ¨(integralClosure ğ’ªáµ¥ L).toSubring, ?_âŸ©
  intro x
  obtain hx | hx := le_total (spectralNorm Káµ¥ L x) 1
  Â· exact .inl (isIntegral_of_spectralNorm_le_one hx)
  Â· have := inv_le_one_of_one_leâ‚€ hx
    rw [â† spectralNorm_inv] at this
    exact .inr (isIntegral_of_spectralNorm_le_one this)

/-- The local inertia subgroup of a number field at a prime, defined as a subgroup
of the local galois group. -/
noncomputable
def localInertiaGroup : Subgroup (Î“ Káµ¥) :=
  (ğ”ª (IntegralClosure ğ’ªáµ¥ (Káµ¥áµƒË¡áµ))).toAddSubgroup.inertia (Î“ Káµ¥)

open IntermediateField in
/-- The subgroup of the local galois group which is the kernel of the canonical map `Iáµ¥ â†’ k(v)Ë£`.
Note that this definition is somewhat cheating, abusing the fact that the field corresponding
to this subgroup is `Káµ˜Ê³(áµ–â»Â¹âˆšÏ–)` (where `p` is `#k(v)` and not the characteristic)
and that all units in `Káµ˜Ê³` have `p-1`-th roots.

TODO: show that this is indeed the right group. -/
noncomputable
def localTameAbelianInertiaGroup : Subgroup (Î“ Káµ¥) where
  carrier := { Ïƒ | âˆ€ x, x ^ (Nat.card (Îº ğ’ªáµ¥) - 1) âˆˆ fixedField (localInertiaGroup v) â†’ Ïƒ x = x }
  mul_mem' {Ïƒ Ï„} hÏƒ hÏ„ x hx := by dsimp; rw [hÏ„ x hx, hÏƒ x hx]
  one_mem' _ _ := rfl
  inv_mem' {Ïƒ} hÏƒ x hx := by
    conv_lhs => rw [â† hÏƒ x hx]
    simp [AlgEquiv.aut_inv]

instance : CharZero Káµ¥ :=
  ((algebraMap K Káµ¥).charZero_iff (algebraMap K Káµ¥).injective).mp inferInstance

instance {K L : Type*} [Field K] [Field L] [Algebra K L] [IsGalois K L] :
    Algebra.IsInvariant K L (L â‰ƒâ‚[K] L) :=
  âŸ¨fun _ H â†¦ (InfiniteGalois.fixedField_fixingSubgroup
    (âŠ¥ : IntermediateField K L)).le fun _ â†¦ H _âŸ©

instance : Finite (IsLocalRing.ResidueField ğ’ªáµ¥) := inferInstance

instance finite_adicCompletionIntegers_quotient
    {I : Ideal ğ’ªáµ¥} [I.IsPrime] [NeZero I] : Finite (ğ’ªáµ¥ â§¸ I) := by
  obtain rfl := ((IsDiscreteValuationRing.iff_pid_with_one_nonzero_prime ğ’ªáµ¥).mp
      inferInstance).2.unique âŸ¨NeZero.ne _, â€¹I.IsPrimeâ€ºâŸ© âŸ¨IsDiscreteValuationRing.not_a_field ğ’ªáµ¥,
      inferInstanceAs (ğ”ª _).IsPrimeâŸ©
  exact inferInstanceAs <| Finite (IsLocalRing.ResidueField _)

instance neZero_maximalIdeal_integralClosure :
    NeZero (ğ”ª (IntegralClosure ğ’ªáµ¥ (Káµ¥áµƒË¡áµ))) := by
  have : ğ’ªáµ¥ â‰  âŠ¤ := by
    refine fun h â†¦ IsDiscreteValuationRing.not_isField ğ’ªáµ¥ (h â–¸ ?_)
    exact (Subring.topEquiv (R := Káµ¥)).isField _ (Semifield.toIsField Káµ¥)
  exact âŸ¨(Ideal.bot_lt_of_maximal (ğ”ª _)
    (not_isField_integralClosure (L := Káµ¥áµƒË¡áµ) _ this)).ne'âŸ©

/-- An arbitrary choice of an (arithmetic) frobenious element of a local galois group. -/
noncomputable
def Field.AbsoluteGaloisGroup.adicArithFrob : Î“ Káµ¥ :=
  arithFrobAt' ğ’ªáµ¥ (Î“ Káµ¥) (ğ”ª (IntegralClosure ğ’ªáµ¥ (Káµ¥áµƒË¡áµ)))

local notation "Frobáµ¥" => Field.AbsoluteGaloisGroup.adicArithFrob v

lemma Field.AbsoluteGaloisGroup.isArithFrobAt_adicArithFrob :
    IsArithFrobAt ğ’ªáµ¥ Frobáµ¥ (ğ”ª (IntegralClosure ğ’ªáµ¥ (Káµ¥áµƒË¡áµ))) :=
  .arithFrobAt' ğ’ªáµ¥ (Î“ Káµ¥) (ğ”ª (IntegralClosure ğ’ªáµ¥ (Káµ¥áµƒË¡áµ)))
