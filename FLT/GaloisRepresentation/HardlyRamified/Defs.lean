/-
Copyright (c) 2025 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard
-/
import FLT.Deformations.Categories
import FLT.Deformations.RepresentationTheory.GaloisRep
import FLT.Mathlib.RingTheory.DedekindDomain.Ideal.Lemmas
import FLT.Assumptions.KnownIn1980s
import Mathlib.Algebra.Lie.OfAssociative
import Mathlib.NumberTheory.Cyclotomic.CyclotomicCharacter
import Mathlib.NumberTheory.Padics.Complex
import Mathlib.Order.CompletePartialOrder
import Mathlib.RingTheory.SimpleRing.Principal
import Mathlib.Topology.Algebra.Localization
/-

# Hardly ramified representations

Let `R` be a complete local Noetherian ring with residue charactestic `p>2` (or a slightly
more general class of topological ring) and let
$\rho:Gal(\overline{\mathbb{Q}}/\mathbb{Q})\to GL_2(R)$ be a continuous Galois
representation. We say that `œÅ` is *hardly ramified* if it has cyclotomic determinant, is
unramified outside `2p`, is flat at `p` and (possibly after conjugation) is
upper-triangular at 2 with unramified quotient of order 1 or 2.

The reason this definition is useful to us is that the `p`-torsion in the Frey
curve is hardly ramified (this is standard, although the full proof is long and needs the
theory of the Tate curve, as well as many standard facts about elliptic curves such as the
Weil pairing). Serre's conjecture says that such a representation cannot be irreducible
(as it should be modular of weight 2 and level 2).

Furthermore, mod `p` hardly ramified representations can be lifted to characteristic zero,
and `p`-adic hardly ramified representations can be put into compatible families (these are
hard theorems). Finally, 3-adic hardly ramified representations can be classified
(this is also a hard theorem).

## More details

Say `‚Ñì ‚â• 3` is a prime, `k` is a finite field of characteristic `‚Ñì` and `R` is a projective
limit of Artin local rings with residue field `k` along local ring maps which induce
the identity on `k`. Give the Artin local rings the discrete topology and `R` the
projective limit topology, so that `R` is profinite.

Let `V` be an `R`-module, free of rank 2 and with the product topology
(i.e., the `R`-module topology). A representation `œÅ : G_Q ‚Üí GL_R(V)` is said to be
*hardly ramified* if

1) `det œÅ` is the mod `‚Ñì` cyclotomic character;
2) `œÅ` is unramified outside `2‚Ñì`;
3) `œÅ|_{G_‚Ñì}` is flat (this means that for every open ideal `I` of `R`, the representation
`G_Q ‚Üí GL_(R/I)(V/I)` come from finite flat group schemes; note that `V/I` is a finite set); and
4) there is a `G_2`-stable exact sequence `0 ‚Üí K ‚Üí V ‚Üí W ‚Üí 0` with `K` and `W` `R`-free of
rank 1, and where `œÅ` acts on `W` via an unramified character whose square is trivial.

-/

open IsDedekindDomain
open scoped NumberField TensorProduct

namespace GaloisRepresentation

local notation3 "Œì" K:max => Field.absoluteGaloisGroup K
local notation3 K:max "·µÉÀ°·µç" => AlgebraicClosure K
local notation3 "ùî™" => IsLocalRing.maximalIdeal

universe u

/-- Z2bar is the ring of integers of `‚Ñö_[2]·µÉÀ°·µç`. -/
noncomputable abbrev Z2bar : ValuationSubring (‚Ñö_[2]·µÉÀ°·µç) := Valued.v.valuationSubring

instance : MulAction (Œì ‚Ñö_[2]) Z2bar where
  smul g z := ‚ü®g z, by
    obtain ‚ü®z, hz‚ü© := z
    rw [Valuation.mem_valuationSubring_iff] at hz ‚ä¢
    convert hz using 1
    apply NNReal.coe_injective
    exact (spectralNorm_eq_of_equiv g z).symm‚ü©
  one_smul z := rfl
  mul_smul g h z := rfl

/-- Let `R` be a compact Hausdorff local topological ring (for example any complete Noetherian
local ring with the maximal ideal-adic topology) having finite residue field of
characteristic `‚Ñì > 2`, and let `œÅ : Gal(Qbar/Q) ‚Üí GL_2(R)` be a continuous 2-dimensional
representation. We say that `œÅ` is *hardly ramified* if it has cyclotomic determinant, is
unramified outside `2‚Ñì`, flat at `‚Ñì` and upper-triangular at 2 with a 1-dimensional quotient which
is unramified and whose square is trivial. -/
structure IsHardlyRamified {‚Ñì : ‚Ñï} [Fact ‚Ñì.Prime] (h‚ÑìOdd : Odd ‚Ñì)
    {R : Type u} [CommRing R] [TopologicalSpace R] [IsTopologicalRing R] [IsLocalRing R]
    [Algebra ‚Ñ§_[‚Ñì] R] --[IsLocalHom (algebraMap ‚Ñ§_[‚Ñì] R)] -- a convenient way of saying "residue
    -- field has char ell"
    -- Rather than GL_2(R) we use the automorphisms of a finite free rank 2 `R`-module `V`.
    {V : Type*} [AddCommGroup V] [Module R V]
    [Module.Finite R V] [Module.Free R V] (hdim : Module.rank R V = 2)
  -- Let `œÅ` be a continuous action of the absolute Galois group of `‚Ñö` on `V`.
    (œÅ : GaloisRep ‚Ñö R V) : Prop where
  -- We say `œÅ` is *hardly ramified* if
  -- `det(œÅ)` is the ell-adic cyclotomic character;
  det : ‚àÄ g, œÅ.det g = algebraMap ‚Ñ§_[‚Ñì] R (cyclotomicCharacter (‚Ñö ·µÉÀ°·µç) ‚Ñì g.toRingEquiv)
  -- `œÅ` is unramified outside `2` and `‚Ñì`;
  isUnramified : ‚àÄ p (hp : p.Prime), p ‚â† 2 ‚àß p ‚â† ‚Ñì ‚Üí
    œÅ.IsUnramifiedAt hp.toHeightOneSpectrumRingOfIntegersRat
  -- œÅ is flat at ‚Ñì;
  isFlat : œÅ.IsFlatAt (Nat.Prime.toHeightOneSpectrumRingOfIntegersRat (Fact.out : ‚Ñì.Prime))
  -- and œÅ has a 1-dimensional quotient œÄ : œÅ ‚Üí Œ¥ such that
  isTameAtTwo : ‚àÉ (œÄ : V ‚Üí‚Çó[R] R) (_ : Function.Surjective œÄ) (Œ¥ : GaloisRep ‚Ñö_[2] R R),
    ‚àÄ g : Œì ‚Ñö_[2], ‚àÄ v : V, œÄ (œÅ.map (algebraMap ‚Ñö ‚Ñö_[2]) g v) = Œ¥ g (œÄ v) ‚àß
    -- Œ¥ is unramified and
    (AddSubgroup.inertia ((ùî™ Z2bar).toAddSubgroup : AddSubgroup Z2bar) (Œì ‚Ñö_[2]) ‚â§ Œ¥.ker) ‚àß
    -- Œ¥¬≤ = 1.
    (‚àÄ g : Œì ‚Ñö_[2], Œ¥ g * Œ¥ g = 1)

theorem baseChange_hardlyRamified {‚Ñì : ‚Ñï} [Fact ‚Ñì.Prime] (h‚ÑìOdd : Odd ‚Ñì)
    {R : Type u} [CommRing R] [TopologicalSpace R] [IsTopologicalRing R] [IsLocalRing R]
    [Algebra ‚Ñ§_[‚Ñì] R]
    (S : Type u) [CommRing S] [TopologicalSpace S] [IsTopologicalRing S] [IsLocalRing S]
    [Algebra R S] [Algebra ‚Ñ§_[‚Ñì] S] [ContinuousSMul R S] [IsScalarTower ‚Ñ§_[‚Ñì] R S]
    {V : Type*} [AddCommGroup V] [Module R V]
    [Module.Finite R V] [Module.Free R V] (hdim : Module.rank R V = 2)
    (œÅ : GaloisRep ‚Ñö R V) : IsHardlyRamified h‚ÑìOdd hdim œÅ ‚Üí
      IsHardlyRamified h‚ÑìOdd (by rw [Module.rank_baseChange, hdim]; exact Cardinal.lift_two)
      (GaloisRep.baseChange S œÅ) := sorry

theorem conj_hardlyRamified {‚Ñì : ‚Ñï} [Fact ‚Ñì.Prime] (h‚ÑìOdd : Odd ‚Ñì)
    {R : Type u} [CommRing R] [TopologicalSpace R] [IsTopologicalRing R] [IsLocalRing R]
    [Algebra ‚Ñ§_[‚Ñì] R] {V : Type*} [AddCommGroup V] [Module R V]
    [Module.Finite R V] [Module.Free R V] (hdimV : Module.rank R V = 2)
    {W : Type*} [AddCommGroup W] [Module R W]
    [Module.Finite R W] [Module.Free R W] (hdimW : Module.rank R W = 2)
    (e : V ‚âÉ‚Çó[R] W) (œÅ : GaloisRep ‚Ñö R V) : IsHardlyRamified h‚ÑìOdd hdimV œÅ ‚Üî
    IsHardlyRamified h‚ÑìOdd hdimW (GaloisRep.conj œÅ e) := sorry

instance {R : Type*} [CommRing R] [TopologicalSpace R] [IsTopologicalRing R]
    [IsDomain R] : ContinuousSMul R (FractionRing R) := by
      apply continuousSMul_of_algebraMap R (FractionRing R)
      exact RingTopology.coinduced_continuous ‚áë(algebraMap R (FractionRing R))

set_option linter.unusedVariables false in
theorem hardlyRamified_of_hardlyRamified_isogenous {‚Ñì : ‚Ñï} [Fact ‚Ñì.Prime] (h‚ÑìOdd : Odd ‚Ñì)
    {R : Type u} [CommRing R] [TopologicalSpace R] [IsTopologicalRing R] [IsLocalRing R]
    [IsDomain R] [Algebra ‚Ñ§_[‚Ñì] R]
    {V : Type*} [AddCommGroup V] [Module R V]
    [Module.Finite R V] [Module.Free R V] (hdimV : Module.rank R V = 2)
    {W : Type*} [AddCommGroup W] [Module R W]
    [Module.Finite R W] [Module.Free R W] (hdimW : Module.rank R W = 2)
    (œÅ : GaloisRep ‚Ñö R V) (œÉ : GaloisRep ‚Ñö R W)
    (e : (FractionRing R) ‚äó[R] V ‚âÉ‚Çó[FractionRing R] (FractionRing R) ‚äó[R] W)
    (he : GaloisRep.conj (GaloisRep.baseChange (FractionRing R) œÅ) e =
      (GaloisRep.baseChange (FractionRing R) œÉ)) :
    IsHardlyRamified h‚ÑìOdd hdimV œÅ ‚Üî IsHardlyRamified h‚ÑìOdd hdimW œÉ := knownin1980s


noncomputable def complexConjugationReal : Œì ‚Ñù := sorry

theorem complexConjugationReal_order_two : orderOf complexConjugationReal = 2 := sorry

noncomputable def complexConjugation : Œì ‚Ñö := (Field.absoluteGaloisGroup.mapAux (Rat.castHom ‚Ñù))
  complexConjugationReal

theorem complexConjugation_order_two : orderOf complexConjugation = 2 := by
  rw [orderOf_eq_prime_iff]
  constructor
  ¬∑ unfold complexConjugation
    rw [‚Üê map_pow, ‚Üê complexConjugationReal_order_two, pow_orderOf_eq_one, map_one]
  ¬∑ sorry

theorem odd_of_hardlyRamified {‚Ñì : ‚Ñï} [Fact ‚Ñì.Prime] (h‚ÑìOdd : Odd ‚Ñì)
    {R : Type u} [CommRing R] [TopologicalSpace R] [IsTopologicalRing R] [IsLocalRing R]
    [Algebra ‚Ñ§_[‚Ñì] R] {V : Type*} [AddCommGroup V] [Module R V]
    [Module.Finite R V] [Module.Free R V] (hdim : Module.rank R V = 2)
    (œÅ : GaloisRep ‚Ñö R V) (hœÅ : IsHardlyRamified h‚ÑìOdd hdim œÅ) : GaloisRep.det œÅ complexConjugation
    = -1 := sorry

theorem isAbsolutelyIrreducible_of_irreducible_odd {R : Type*} [TopologicalSpace R] [Field R]
    [IsTopologicalRing R] {V : Type*} [AddCommGroup V] [Module R V] [Module.Finite R V]
    (hV : Module.rank R V = 2) (œÅ : GaloisRep ‚Ñö R V) (œÅodd : GaloisRep.det œÅ complexConjugation
    = -1) (hœÅ : GaloisRep.IsIrreducible œÅ) : GaloisRep.IsAbsolutelyIrreducible œÅ := sorry

end GaloisRepresentation
