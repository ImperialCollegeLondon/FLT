/-
Copyright (c) 2025 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard
-/
import FLT.Deformations.Categories
import FLT.Deformations.RepresentationTheory.Basic
import Mathlib.Algebra.Lie.OfAssociative
import Mathlib.NumberTheory.Cyclotomic.CyclotomicCharacter
import Mathlib.NumberTheory.Padics.Complex
import Mathlib.Order.CompletePartialOrder
import Mathlib.RingTheory.SimpleRing.Principal
/-

# Hardly ramified representations

Let `R` be a complete local Noetherian ring with residue charactestic `p>2` (or a slightly
more general class of topological ring) and let
$\rho:Gal(\overline{\mathbb{Q}}/\mathbb{Q})\to GL_2(R)$ be a continuous Galois
representation. We say that `Ï` is *hardly ramified* if it has cyclotomic determinant, is
unramified outside `2p`, is flat at `p` and (possibly after conjugation) is
upper-triangular at 2 with unramified quotient of order 1 or 2.

The reason this definition is useful to us is that the `p`-torsion in the Frey
curve is hardly ramified (this is standard, although the full proof is long and needs the
theory of the Tate curve, as well as many standard facts about elliptic curves such as the
Weil pairing). Serre's conjecture says that such a representation cannot be irreducible
(as it should be modular of weight 2 and level 2).

Furthermore, mod `p` hardly ramified representations can be lifted to characteristic zero,
and `p`-adic hardly ramified representations can be put into compatible families (these are
hard theorems). Finally, 3-adic hardly ramified representations can be classified
(this is also a hard theorem).

## More details

Say `â„“ â‰¥ 3` is a prime, `k` is a finite field of characteristic `â„“` and `R` is a projective
limit of Artin local rings with residue field `k` along local ring maps which induce
the identity on `k`. Give the Artin local rings the discrete topology and `R` the
projective limit topology, so that `R` is profinite.

Let `V` be an `R`-module, free of rank 2 and with the product topology
(i.e., the `R`-module topology). A representation `Ï : G_Q â†’ GL_R(V)` is said to be
*hardly ramified* if

1) `det Ï` is the mod `â„“` cyclotomic character;
2) `Ï` is unramified outside `2â„“`;
3) `Ï|_{G_â„“}` is flat (this means that for every open ideal `I` of `R`, the representation
`G_Q â†’ GL_(R/I)(V/I)` come from finite flat group schemes; note that `V/I` is a finite set); and
4) there is a `G_2`-stable exact sequence `0 â†’ K â†’ V â†’ W â†’ 0` with `K` and `W` `R`-free of
rank 1, and where `Ï` acts on `W` via an unramified character whose square is trivial.

-/

open IsDedekindDomain
open scoped NumberField

def RingEquiv.heightOneSpectrum_comap {A B : Type*} [CommRing A] [CommRing B] (e : A â‰ƒ+* B)
    (P : HeightOneSpectrum B) : HeightOneSpectrum A :=
  {
    asIdeal := .comap e P.asIdeal
    isPrime := P.asIdeal.comap_isPrime e
    ne_bot h := P.ne_bot <| Ideal.comap_injective_of_surjective e e.surjective <| by
      rw [h, Ideal.comap_bot_of_injective e e.injective]
  }

open IsDedekindDomain in
def RingEquiv.heightOneSpectrum {A B : Type*} [CommRing A] [CommRing B] (e : A â‰ƒ+* B) :
    HeightOneSpectrum A â‰ƒ HeightOneSpectrum B where
      toFun := e.symm.heightOneSpectrum_comap
      invFun := e.heightOneSpectrum_comap
      left_inv P := by
        ext1
        convert Ideal.comap_comap e.toRingHom e.symm.toRingHom
        simp
      right_inv Q := by
        ext1
        convert Ideal.comap_comap e.symm.toRingHom e.toRingHom
        simp

def Nat.Prime.toHeightOneSpectrumInt {p : â„•} (hp : p.Prime) : HeightOneSpectrum â„¤ where
  asIdeal := .span {(p : â„¤)}
  isPrime := by
    rwa [Ideal.span_singleton_prime (Int.ofNat_ne_zero.mpr hp.ne_zero), â† prime_iff_prime_int]
  ne_bot := mt Submodule.span_singleton_eq_bot.mp (Int.ofNat_ne_zero.mpr hp.ne_zero)

noncomputable def Nat.Prime.toHeightOneSpectrumRingOfIntegersRat {p : â„•} (hp : p.Prime) :
    IsDedekindDomain.HeightOneSpectrum (ğ“ â„š) :=
  Rat.ringOfIntegersEquiv.symm.heightOneSpectrum <| hp.toHeightOneSpectrumInt

namespace GaloisRepresentation

local notation3 "Î“" K:max => Field.absoluteGaloisGroup K
local notation3 K:max "áµƒË¡áµ" => AlgebraicClosure K
local notation3 "ğ”ª" => IsLocalRing.maximalIdeal

universe u

noncomputable abbrev Z2bar : ValuationSubring (â„š_[2]áµƒË¡áµ) := Valued.v.valuationSubring
noncomputable example : Z2bar â†’+ â„š_[2]áµƒË¡áµ := by exact Z2bar.subtype.toAddMonoidHom

instance : MulAction (Î“ â„š_[2]) Z2bar where
  smul g z := âŸ¨g z, by
    obtain âŸ¨z, hzâŸ© := z
    rw [Valuation.mem_valuationSubring_iff] at hz âŠ¢
    convert hz using 1
    apply NNReal.coe_injective
    exact (spectralNorm_eq_of_equiv g z).symmâŸ©
  one_smul z := rfl
  mul_smul g h z := rfl

/-- Let `R` be a "local pro-artinian algebra" (for example any complete Noetherian local ring
with the maximal ideal-adic topology) having finite residue field of characteristic `â„“ > 2`,
and let `Ï : Gal(Qbar/Q) â†’ GL_2(R)` be a continuous 2-dimensional representation.
We say that `Ï` is *hardly ramified* if it has cyclotomic determinant, is unramified outside `2â„“`,
flat at `â„“` and upper-triangular at 2 with a 1-dimensional quotient which is unramified and
whose square is trivial. -/
structure IsHardlyRamified {â„“ : â„•} [Fact â„“.Prime] (hâ„“Odd : Odd â„“)
    -- In applications `ğ“` will be the integers of a finite extension of `â„š_[â„“]`;
    -- we assume `ğ“` acts on the coefficient ring `R` as it is technically convenient
    -- to build in this extra action.
    (ğ’ª : Type u) [CommRing ğ’ª] [Algebra â„¤_[â„“] ğ’ª] [IsLocalHom (algebraMap â„¤_[â„“] ğ’ª)]
    (R : Type u) [CommRing R] [TopologicalSpace R]
    [Algebra ğ’ª R] [Algebra â„¤_[â„“] R] [IsScalarTower â„¤_[â„“] ğ’ª R]
    [Deformation.IsLocalProartinianAlgebra ğ’ª R]
    -- Rather than GL_2(R) we use the automorphisms of a finite free rank 2 `R`-module.
    {V : Type*} [AddCommGroup V] [Module R V]
    [Module.Finite R V] [Module.Free R V] (hdim : Module.rank R V = 2)
  -- Let `Ï` be a continuous action of the absolute Galois group of `â„š` on V.
    (Ï : GaloisRep â„š R V) : Prop where
  -- We define *IsHardlyRamified* to mean:
  -- det(Ï) is the ell-adic cyclotomic character;
  det : âˆ€ g, Ï.det g = algebraMap â„¤_[â„“] R (cyclotomicCharacter (â„š áµƒË¡áµ) â„“ g.toRingEquiv)
  -- Ï is unramified outside 2 and â„“;
  isUnramified : âˆ€ p (hp : p.Prime), p â‰  2 âˆ§ p â‰  â„“ â†’
    Ï.IsUnramifiedAt hp.toHeightOneSpectrumRingOfIntegersRat
  -- Ï is flat at â„“;
  isFlat : Ï.IsFlatAt (Nat.Prime.toHeightOneSpectrumRingOfIntegersRat (Fact.out : â„“.Prime))
  -- and Ï has a 1-dimensional quotient Ï€ : Ï â†’ Î´ such that
  isTameAtTwo : âˆƒ (Ï€ : V â†’â‚—[R] R) (_ : Function.Surjective Ï€) (Î´ : GaloisRep â„š_[2] R R),
    âˆ€ g : Î“ â„š_[2], âˆ€ v : V, Ï€ (Ï.map (algebraMap â„š â„š_[2]) g v) = Î´ g (Ï€ v) âˆ§
    -- Î´ is unramified and
    (AddSubgroup.inertia ((ğ”ª Z2bar).toAddSubgroup : AddSubgroup Z2bar) (Î“ â„š_[2]) â‰¤ Î´.ker) âˆ§
    -- Î´Â² = 1.
    (âˆ€ g : Î“ â„š_[2], Î´ g * Î´ g = 1)

end GaloisRepresentation
