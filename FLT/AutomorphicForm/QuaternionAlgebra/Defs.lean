/-
Copyright (c) 2024 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard
-/
import FLT.DivisionAlgebra.Finiteness

/-

# Definition of automorphic forms on a totally definite quaternion algebra

-/

suppress_compilation

variable (F : Type*) [Field F] [NumberField F] --[NumberField.IsTotallyReal F]

variable (D : Type*) [Ring D] [Algebra F D] --[IsCentralSimple F D] [FiniteDimensional F D]

namespace TotallyDefiniteQuaternionAlgebra

-- noncomputable example : D â†’+* (D âŠ—[F] FiniteAdeleRing (ğ“ F) F) :=
--   Algebra.TensorProduct.includeLeftRingHom

open scoped TensorProduct NumberField

open DedekindDomain

abbrev Dfx := (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£

variable {F D} in
noncomputable abbrev inclâ‚ : DË£ â†’* Dfx F D :=
  Units.map Algebra.TensorProduct.includeLeftRingHom.toMonoidHom

noncomputable abbrev inclâ‚‚ : (FiniteAdeleRing (ğ“ F) F)Ë£ â†’* Dfx F D :=
  Units.map Algebra.TensorProduct.rightAlgebra.algebraMap.toMonoidHom

/-!
This definition is made in mathlib-generality but is *not* the definition of an automorphic
form unless DË£ is compact mod centre at infinity. This hypothesis will be true if `D` is a
totally definite quaternion algebra.
-/
structure AutomorphicForm
  -- defined over R
  (R : Type*) [CommRing R]
  -- of weight W
  (W : Type*) [AddCommGroup W] [Module R W] [MulAction DË£ W]
  -- and level U
  (U : Subgroup (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£)
  -- and character Ï‡
  (Ï‡ : (FiniteAdeleRing (ğ“ F) F)Ë£ â†’* R) where
  -- definition
  toFun : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£ â†’ W
  left_invt : âˆ€ (Î´ : DË£) (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£),
    toFun (inclâ‚ Î´ * g) = Î´ â€¢ (toFun g)
  has_character : âˆ€ (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) (z : (FiniteAdeleRing (ğ“ F) F)Ë£),
    toFun (g * inclâ‚‚ F D z) = Ï‡ z â€¢ toFun g
  right_invt : âˆ€ (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£),
    âˆ€ u âˆˆ U, toFun (g * u) = toFun g

namespace AutomorphicForm

-- defined over R
variable  (R : Type*) [CommRing R]
  -- weight
  (W : Type*) [AddCommGroup W] [Module R W] [DistribMulAction DË£ W]
  -- NB actions of DË£ and R should commute
  -- level
  (U : Subgroup (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) -- subgroup should be compact and open
  -- character
  (Ï‡ : (FiniteAdeleRing (ğ“ F) F)Ë£ â†’* R)

variable {F D R W U Ï‡}

instance : CoeFun (AutomorphicForm F D R W U Ï‡) (fun _ â†¦ Dfx F D â†’ W) where
  coe := toFun

attribute [coe] AutomorphicForm.toFun

@[ext]
theorem ext (Ï† Ïˆ : AutomorphicForm F D R W U Ï‡) (h : âˆ€ x, Ï† x = Ïˆ x) : Ï† = Ïˆ := by
  cases Ï†; cases Ïˆ; simp only [mk.injEq]; ext; apply h

def zero : (AutomorphicForm F D R W U Ï‡) where
  toFun := 0
  left_invt Î´ _ := by simp
  has_character _ z := by simp
  right_invt _ u _ := by simp

instance : Zero (AutomorphicForm F D R W U Ï‡) where
  zero := zero

@[simp]
theorem zero_apply (x : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
    (0 : AutomorphicForm F D R W U Ï‡) x = 0 := rfl

def neg (Ï† : AutomorphicForm F D R W U Ï‡) : AutomorphicForm F D R W U Ï‡ where
  toFun x := - Ï† x
  left_invt Î´ g := by simp [left_invt]
  has_character g z := by simp [has_character]
  right_invt g u hu := by simp_all [right_invt]

instance : Neg (AutomorphicForm F D R W U Ï‡) where
  neg := neg

@[simp, norm_cast]
theorem neg_apply (Ï† : AutomorphicForm F D R W U Ï‡) (x : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
    (-Ï† : AutomorphicForm F D R W U Ï‡) x = -(Ï† x) := rfl

def add (Ï† Ïˆ : AutomorphicForm F D R W U Ï‡) : AutomorphicForm F D R W U Ï‡ where
  toFun x := Ï† x + Ïˆ x
  left_invt := by simp [left_invt]
  has_character := by simp [has_character]
  right_invt := by simp_all [right_invt]

instance : Add (AutomorphicForm F D R W U Ï‡) where
  add := add

@[simp, norm_cast]
theorem add_apply (Ï† Ïˆ : AutomorphicForm F D R W U Ï‡) (x : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
    (Ï† + Ïˆ) x = (Ï† x) + (Ïˆ x) := rfl

instance addCommGroup : AddCommGroup (AutomorphicForm F D R W U Ï‡) where
  add := (Â· + Â·)
  add_assoc := by intros; ext; simp [add_assoc];
  zero := 0
  zero_add := by intros; ext; simp
  add_zero := by intros; ext; simp
  nsmul := nsmulRec
  neg := (-Â·)
  zsmul := zsmulRec
  neg_add_cancel := by intros; ext; simp
  add_comm := by intros; ext; simp [add_comm]

-- from this point we need the DË£-action on W to be R-linear
variable [SMulCommClass R DË£ W]

def smul (r : R) (Ï† : AutomorphicForm F D R W U Ï‡) :
    AutomorphicForm F D R W U Ï‡ where
      toFun g := r â€¢ Ï† g
      left_invt := by simp [left_invt, smul_comm]
      has_character g z := by simp only [has_character, smul_comm r]
      right_invt := by simp_all [right_invt]

instance : SMul R (AutomorphicForm F D R W U Ï‡) where
  smul := smul

lemma smul_apply (r : R) (Ï† : AutomorphicForm F D R W U Ï‡) (g : (D âŠ—[F] FiniteAdeleRing (ğ“ F) F)Ë£) :
    (r â€¢ Ï†) g = r â€¢ (Ï† g) := rfl

instance module : Module R (AutomorphicForm F D R W U Ï‡) where
  one_smul g := by ext; simp [smul_apply]
  mul_smul r s g := by ext; simp [smul_apply, mul_smul]
  smul_zero r := by ext; simp [smul_apply]
  smul_add r f g := by ext; simp [smul_apply]
  add_smul r s g := by ext; simp [smul_apply, add_smul]
  zero_smul g := by ext; simp [smul_apply]

end TotallyDefiniteQuaternionAlgebra.AutomorphicForm
