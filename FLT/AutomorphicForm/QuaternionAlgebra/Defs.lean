/-
Copyright (c) 2024 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard
-/
import FLT.Mathlib.Algebra.IsQuaternionAlgebra
import Mathlib.RingTheory.DedekindDomain.FiniteAdeleRing
import Mathlib.Topology.Algebra.Module.ModuleTopology
import FLT.Mathlib.Algebra.FixedPoints.Basic
import Mathlib.Order.CompletePartialOrder
import Mathlib.Algebra.Lie.OfAssociative
import Mathlib.NumberTheory.NumberField.FinitePlaces
import FLT.Hacks.RightActionInstances

/-

# Definition of automorphic forms on a totally definite quaternion algebra

## Main definitions

In the `TotallyDefiniteQuaternionAlgebra` namespace:

* `WeightTwoAutomorphicForm F D R` -- weight 2
  R-valued automorphic forms for the totally definite quaternion algebra `D` over
  the totally real field `F`. Defined as locally-constant functions
  `Ï† : DË£ \ (D âŠ— ğ”¸_F^âˆ)Ë£ â†’ R` which are right-invariant by some compact open subgroup
  (i.e. âˆƒ U_Ï† such that `Ï†(gu)=Ï†(g)` for all `u âˆˆ U`) and have trivial central character
  (i.e. `Ï†(zg)=Ï†(g)` for all `z âˆˆ (ğ”¸_F^âˆ)Ë£`).

* `WeightTwoAutomorphicFormOfLevel U R` -- weight 2 R-valued automorphic forms of
  level `U`, i.e. `U`-invariant elements of `WeightTwoAutomorphicForm F D R`.
  It is a nontrivial theorem that if `U` is open and `R` is Noetherian then this space
  is a finitely-generated `R`-module; this follows from Fujisaki's lemma.

## Implementation notes

This file is slow, for reasons I don't understand properly.
-/

suppress_compilation

set_option maxSynthPendingDepth 1 -- otherwise things are even slower, for some reason which
-- I never quite got to the bottom of

variable (F : Type*) [Field F] [NumberField F] -- if F isn't totally real all the definitions
-- below are garbage mathematically but they typecheck.

variable (D : Type*) [Ring D] [Algebra F D] [FiniteDimensional F D]
  -- If D isn't totally definite then all the
  -- definitions below are garbage mathematically but they typecheck.

namespace TotallyDefiniteQuaternionAlgebra

open scoped TensorProduct NumberField

open IsDedekindDomain

instance : CommRing (FiniteAdeleRing (ğ“ F) F) := inferInstance
instance : Ring (D âŠ—[F] FiniteAdeleRing (ğ“ F) F) := inferInstance

/-- `Dfx` is an abbreviation for $(D\otimes_F\mathbb{A}_F^\infty)^\times.$ -/
abbrev Dfx := (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£

/-- inclâ‚ is an abbreviation for the inclusion
$D^\times\to(D\otimes_F\mathbb{A}_F^\infty)^\times.$ Remark: I wrote the `inclâ‚`
docstring in LaTeX and the `inclâ‚‚` one in unicode. Which is better? -/
noncomputable abbrev inclâ‚ : DË£ â†’* Dfx F D :=
  Units.map (Algebra.TensorProduct.includeLeftRingHom.toMonoidHom)

open scoped TensorProduct.RightActions in
/-- `inclâ‚‚` is he inclusion `ğ”¸_F^âˆË£ â†’ (D âŠ— ğ”¸_F^âˆË£)`. Remark: I wrote the `inclâ‚`
docstring in LaTeX and the `inclâ‚‚` one in unicode. Which is better? -/
noncomputable abbrev inclâ‚‚ : (FiniteAdeleRing (ğ“ F) F)Ë£ â†’* Dfx F D :=
  Units.map (algebraMap _ _).toMonoidHom

-- it's actually equal but âŠ† is all we need, and equality is harder
lemma range_inclâ‚‚_le_center : MonoidHom.range (inclâ‚‚ F D) â‰¤ Subgroup.center (Dfx F D) := by
  sorry

open scoped TensorProduct.RightActions in
/--
This definition is made in mathlib-generality but is *not* the definition of a
weight 2 automorphic form unless `DË£` is compact mod centre at infinity.
This hypothesis will be true if `D` is a totally definite quaternion algebra
over a totally real field.
-/
structure WeightTwoAutomorphicForm
  -- defined over R
  (R : Type*) [AddCommMonoid R] where
  /-- The function underlying an automorphic form. -/
  toFun : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£ â†’ R
  left_invt : âˆ€ (Î´ : DË£) (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£),
    toFun (inclâ‚ F D Î´ * g) = (toFun g)
  right_invt : âˆƒ (U : Subgroup (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£),
    IsOpen (U : Set (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) âˆ§
    âˆ€ (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£),
    âˆ€ u âˆˆ U, toFun (g * u) = toFun g
  trivial_central_char (z : (FiniteAdeleRing (ğ“ F) F)Ë£)
      (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
      toFun (g * inclâ‚‚ F D z) = toFun g

variable {F D}

namespace WeightTwoAutomorphicForm

section add_comm_group

variable {R : Type*} [AddCommGroup R]

instance : CoeFun (WeightTwoAutomorphicForm F D R) (fun _ â†¦ Dfx F D â†’ R) where
  coe := toFun

attribute [coe] WeightTwoAutomorphicForm.toFun

@[ext]
theorem ext (Ï† Ïˆ : WeightTwoAutomorphicForm F D R) (h : âˆ€ x, Ï† x = Ïˆ x) : Ï† = Ïˆ := by
  cases Ï†; cases Ïˆ; simp only [mk.injEq]; ext; apply h

/-- The zero automorphic form for a totally definite quaterion algebra. -/
def zero : (WeightTwoAutomorphicForm F D R) where
  toFun := 0
  left_invt Î´ _ := by simp
  -- this used to be `by simp` but now it times out doing some crazy typeclass search for
  -- `DiscreteTopology (D âŠ—[F] FiniteAdeleRing (ğ“ F) F)Ë£`
  right_invt := âŸ¨âŠ¤, by simp only [Subgroup.coe_top, isOpen_univ, Subgroup.mem_top,
    Pi.zero_apply, imp_self, implies_true, and_self]âŸ©
  trivial_central_char _ z := by simp

instance : Zero (WeightTwoAutomorphicForm F D R) where
  zero := zero

@[simp]
theorem zero_apply (x : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
    (0 : WeightTwoAutomorphicForm F D R) x = 0 := rfl

/-- Negation on the space of automorphic forms over a totally definite quaternion algebra. -/
def neg (Ï† : WeightTwoAutomorphicForm F D R) : WeightTwoAutomorphicForm F D R where
  toFun x := - Ï† x
  left_invt Î´ g := by simp [left_invt]
  right_invt := by
    obtain âŸ¨U, hUâŸ© := Ï†.right_invt
    simp_all only [neg_inj, right_invt]
  trivial_central_char g z := by simp [trivial_central_char]

instance : Neg (WeightTwoAutomorphicForm F D R) where
  neg := neg

@[simp, norm_cast]
theorem neg_apply (Ï† : WeightTwoAutomorphicForm F D R) (x : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
    (-Ï† : WeightTwoAutomorphicForm F D R) x = -(Ï† x) := rfl

/-- Addition on the space of automorphic forms over a totally definite quaternion algebra. -/
def add (Ï† Ïˆ : WeightTwoAutomorphicForm F D R) : WeightTwoAutomorphicForm F D R where
  toFun x := Ï† x + Ïˆ x
  left_invt := by simp [left_invt]
  right_invt := by
    obtain âŸ¨U, hUâŸ© := Ï†.right_invt
    obtain âŸ¨V, hVâŸ© := Ïˆ.right_invt
    use U âŠ“ V
    simp_all only [Subgroup.coe_inf, IsOpen.inter, Subgroup.mem_inf, implies_true, and_self]
  trivial_central_char := by simp [trivial_central_char]

instance : Add (WeightTwoAutomorphicForm F D R) where
  add := add

@[simp, norm_cast]
theorem add_apply (Ï† Ïˆ : WeightTwoAutomorphicForm F D R) (x : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
    (Ï† + Ïˆ) x = (Ï† x) + (Ïˆ x) := rfl

instance addCommGroup : AddCommGroup (WeightTwoAutomorphicForm F D R) where
  add := (Â· + Â·)
  add_assoc := by intros; ext; simp [add_assoc];
  zero := 0
  zero_add := by intros; ext; simp
  add_zero := by intros; ext; simp
  nsmul := nsmulRec
  neg := (-Â·)
  zsmul := zsmulRec
  neg_add_cancel := by intros; ext; simp
  add_comm := by intros; ext; simp [add_comm]

open scoped Pointwise

-- this should be in mathlib
lemma _root_.ConjAct.isOpen_smul {G : Type*} [Group G] [TopologicalSpace G]
    [IsTopologicalGroup G] {U : Subgroup G} (hU : IsOpen (U : Set G)) (g : ConjAct G) :
    IsOpen ((g â€¢ U : Subgroup G) : Set G) := by
  sorry

open ConjAct

variable [IsQuaternionAlgebra F D]

open scoped TensorProduct.RightActions in
/-- The adelic group action on the space of automorphic forms over a totally definite
quaternion algebra. -/
def group_smul (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) (Ï† : WeightTwoAutomorphicForm F D R) :
    WeightTwoAutomorphicForm F D R where
  toFun x := Ï† (x * g)
  left_invt Î´ x := by simp [left_invt, mul_assoc]
  right_invt := by
    obtain âŸ¨U, hUâŸ© := Ï†.right_invt
    refine âŸ¨(toConjAct g) â€¢ U, ?_, ?_âŸ©
    Â· replace hU := hU.1
      exact isOpen_smul hU (toConjAct g)
    Â· rintro k x âŸ¨u, hu, rflâŸ©
      simp only [MulDistribMulAction.toMonoidEnd_apply, MulDistribMulAction.toMonoidHom_apply,
        smul_def, ofConjAct_toConjAct, â† hU.2 (k * g) u hu]
      group
  trivial_central_char z x := by
    simp only [mul_assoc]
    have := range_inclâ‚‚_le_center F D âŸ¨z, rflâŸ©
    rw [Subgroup.mem_center_iff] at this
    rw [â† this, â† mul_assoc, trivial_central_char]

instance : SMul (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£ (WeightTwoAutomorphicForm F D R) where
  smul := group_smul

omit [IsQuaternionAlgebra F D] in
@[simp]
lemma group_smul_apply (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£)
    (Ï† : WeightTwoAutomorphicForm F D R) (x : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
    (g â€¢ Ï†) x = Ï† (x * g) := rfl

attribute [instance low] Units.instMulAction

instance mulAction :
    MulAction (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£ (WeightTwoAutomorphicForm F D R) where
  smul := group_smul
  one_smul Ï† := by ext; simp only [group_smul_apply, mul_one]
  mul_smul g h Ï† := by ext; simp only [group_smul_apply, mul_assoc]

instance distribMulAction : DistribMulAction (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£
    (WeightTwoAutomorphicForm F D R) where
  __ := mulAction
  smul_zero g := by ext; simp only [group_smul_apply, zero_apply]
  smul_add g Ï† Ïˆ := by ext; simp only [group_smul_apply, add_apply]

end add_comm_group

section comm_ring

variable {R : Type*} [CommRing R]

/-- The scalar action on the space of weight 2 automorphic forms on a totally definite
quaternion algebra. -/
def ring_smul (r : R) (Ï† : WeightTwoAutomorphicForm F D R) :
    WeightTwoAutomorphicForm F D R where
      toFun g := r â€¢ Ï† g
      left_invt := by simp [left_invt]
      right_invt := by
        obtain âŸ¨U, hUâŸ© := Ï†.right_invt
        use U
        simp_all only [smul_eq_mul, implies_true, and_self]
      trivial_central_char g z := by simp only [trivial_central_char]

instance : SMul R (WeightTwoAutomorphicForm F D R) where
  smul := ring_smul

lemma smul_apply (r : R) (Ï† : WeightTwoAutomorphicForm F D R)
    (g : (D âŠ—[F] FiniteAdeleRing (ğ“ F) F)Ë£) :
    (r â€¢ Ï†) g = r â€¢ (Ï† g) := rfl

instance module : Module R (WeightTwoAutomorphicForm F D R) where
  one_smul g := by ext; simp [smul_apply]
  mul_smul r s g := by ext; simp [smul_apply, mul_assoc]
  smul_zero r := by ext; simp [smul_apply]
  smul_add r f g := by ext; simp [smul_apply, mul_add]
  add_smul r s g := by ext; simp [smul_apply, add_mul]
  zero_smul g := by ext; simp [smul_apply]

variable [IsQuaternionAlgebra F D]

instance : SMulCommClass (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£ R
    (WeightTwoAutomorphicForm F D R) where
  smul_comm r g Ï† := by
    ext x
    simp [smul_apply]

end comm_ring

end WeightTwoAutomorphicForm

section finite_level

variable [IsQuaternionAlgebra F D]

/--
`WeightTwoAutomorphicFormOfLevel U R` is the `R`-valued weight 2 automorphic forms of a fixed
level `U` for a totally definite quaternion algebra over a totally real field.
-/
def WeightTwoAutomorphicFormOfLevel (U : Subgroup (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£)
    (R : Type*) [CommRing R] : Type _ := MulAction.FixedPoints U (WeightTwoAutomorphicForm F D R)

variable (U : Subgroup (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) (R : Type*) [CommRing R]

instance : AddCommGroup (WeightTwoAutomorphicFormOfLevel U R) := inferInstanceAs <|
  AddCommGroup (MulAction.FixedPoints U (WeightTwoAutomorphicForm F D R))

instance : Module R (WeightTwoAutomorphicFormOfLevel U R) := inferInstanceAs <|
  Module R (MulAction.FixedPoints U (WeightTwoAutomorphicForm F D R))

end finite_level

end TotallyDefiniteQuaternionAlgebra
