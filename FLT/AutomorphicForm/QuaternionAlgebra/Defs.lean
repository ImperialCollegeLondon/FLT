/-
Copyright (c) 2024 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard
-/
import FLT.Mathlib.Algebra.IsQuaternionAlgebra
import FLT.Mathlib.RingTheory.TensorProduct.Finite
import Mathlib.RingTheory.DedekindDomain.FiniteAdeleRing
import Mathlib.Topology.Algebra.Module.ModuleTopology

/-

# Definition of automorphic forms on a totally definite quaternion algebra

-/

suppress_compilation

variable (F : Type*) [Field F] [NumberField F] --[NumberField.IsTotallyReal F]

variable (D : Type*) [Ring D] [Algebra F D]

namespace TotallyDefiniteQuaternionAlgebra

open scoped TensorProduct NumberField

open DedekindDomain


abbrev Dfx := (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£

noncomputable abbrev inclâ‚ : DË£ â†’* Dfx F D :=
  Units.map Algebra.TensorProduct.includeLeftRingHom.toMonoidHom

/-- The inclusion ğ”¸_F^âˆË£ â†’ (D âŠ— ğ”¸_F^âˆË£) -/
noncomputable abbrev inclâ‚‚ : (FiniteAdeleRing (ğ“ F) F)Ë£ â†’* Dfx F D :=
  Units.map Algebra.TensorProduct.rightAlgebra.algebraMap.toMonoidHom

-- it's actually equal but âŠ† is all we need, and equality is harder
lemma range_inclâ‚‚_le_center : MonoidHom.range (inclâ‚‚ F D) â‰¤ Subgroup.center (Dfx F D) := by
  sorry

attribute [local instance] Algebra.TensorProduct.rightAlgebra in
instance : TopologicalSpace (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F)) :=
  moduleTopology (FiniteAdeleRing (ğ“ F) F) _

attribute [local instance] Algebra.TensorProduct.rightAlgebra in
instance : IsModuleTopology (FiniteAdeleRing (ğ“ F) F) (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F)) :=
  âŸ¨rflâŸ©

variable [IsQuaternionAlgebra F D] in
attribute [local instance] Algebra.TensorProduct.rightAlgebra in
instance : Module.Finite (FiniteAdeleRing (ğ“ F) F) (D âŠ—[F] FiniteAdeleRing (ğ“ F) F) :=
  Module.Finite.base_change_right

variable [IsQuaternionAlgebra F D] in
attribute [local instance] Algebra.TensorProduct.rightAlgebra in
instance : IsTopologicalRing (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F)) :=
  IsModuleTopology.isTopologicalRing (FiniteAdeleRing (ğ“ F) F) _
/-!
This definition is made in mathlib-generality but is *not* the definition of a
weight 2 automorphic form unless DË£ is compact mod centre at infinity.
This hypothesis will be true if `D` is a totally definite quaternion algebra.
-/
structure WeightTwoAutomorphicForm
  -- defined over R
  (R : Type*) [AddCommMonoid R] where
  -- definition
  toFun : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£ â†’ R
  left_invt : âˆ€ (Î´ : DË£) (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£),
    toFun (inclâ‚ F D Î´ * g) = (toFun g)
--  (U : Subgroup (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£)
  right_invt : âˆƒ (U : Subgroup (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£),
    IsOpen (U : Set (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) âˆ§
    âˆ€ (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£),
    âˆ€ u âˆˆ U, toFun (g * u) = toFun g
  trivial_central_char (z : (FiniteAdeleRing (ğ“ F) F)Ë£)
      (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
      toFun (g * inclâ‚‚ F D z) = toFun g

variable {F D}

namespace WeightTwoAutomorphicForm

section add_comm_group

variable {R : Type*} [AddCommGroup R]

instance : CoeFun (WeightTwoAutomorphicForm F D R) (fun _ â†¦ Dfx F D â†’ R) where
  coe := toFun

attribute [coe] WeightTwoAutomorphicForm.toFun

@[ext]
theorem ext (Ï† Ïˆ : WeightTwoAutomorphicForm F D R) (h : âˆ€ x, Ï† x = Ïˆ x) : Ï† = Ïˆ := by
  cases Ï†; cases Ïˆ; simp only [mk.injEq]; ext; apply h

def zero : (WeightTwoAutomorphicForm F D R) where
  toFun := 0
  left_invt Î´ _ := by simp
  right_invt := âŸ¨âŠ¤, by simp only [Subgroup.coe_top, isOpen_univ, Subgroup.mem_top,
    Pi.zero_apply, imp_self, implies_true, and_self]âŸ©
  trivial_central_char _ z := by simp

instance : Zero (WeightTwoAutomorphicForm F D R) where
  zero := zero

@[simp]
theorem zero_apply (x : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
    (0 : WeightTwoAutomorphicForm F D R) x = 0 := rfl

def neg (Ï† : WeightTwoAutomorphicForm F D R) : WeightTwoAutomorphicForm F D R where
  toFun x := - Ï† x
  left_invt Î´ g := by simp [left_invt]
  right_invt := by
    obtain âŸ¨U, hUâŸ© := Ï†.right_invt
    simp_all only [neg_inj, right_invt]
  trivial_central_char g z := by simp [trivial_central_char]

instance : Neg (WeightTwoAutomorphicForm F D R) where
  neg := neg

@[simp, norm_cast]
theorem neg_apply (Ï† : WeightTwoAutomorphicForm F D R) (x : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
    (-Ï† : WeightTwoAutomorphicForm F D R) x = -(Ï† x) := rfl

def add (Ï† Ïˆ : WeightTwoAutomorphicForm F D R) : WeightTwoAutomorphicForm F D R where
  toFun x := Ï† x + Ïˆ x
  left_invt := by simp [left_invt]
  right_invt := by
    obtain âŸ¨U, hUâŸ© := Ï†.right_invt
    obtain âŸ¨V, hVâŸ© := Ïˆ.right_invt
    use U âŠ“ V
    simp_all only [Subgroup.coe_inf, IsOpen.inter, Subgroup.mem_inf, implies_true, and_self]
  trivial_central_char := by simp [trivial_central_char]

instance : Add (WeightTwoAutomorphicForm F D R) where
  add := add

@[simp, norm_cast]
theorem add_apply (Ï† Ïˆ : WeightTwoAutomorphicForm F D R) (x : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
    (Ï† + Ïˆ) x = (Ï† x) + (Ïˆ x) := rfl

instance addCommGroup : AddCommGroup (WeightTwoAutomorphicForm F D R) where
  add := (Â· + Â·)
  add_assoc := by intros; ext; simp [add_assoc];
  zero := 0
  zero_add := by intros; ext; simp
  add_zero := by intros; ext; simp
  nsmul := nsmulRec
  neg := (-Â·)
  zsmul := zsmulRec
  neg_add_cancel := by intros; ext; simp
  add_comm := by intros; ext; simp [add_comm]

open scoped Pointwise

-- this should be in mathlib
lemma _root_.ConjAct.isOpen_smul {G : Type*} [Group G] [TopologicalSpace G]
    [IsTopologicalGroup G] {U : Subgroup G} (hU : IsOpen (U : Set G)) (g : ConjAct G) :
    IsOpen ((g â€¢ U : Subgroup G) : Set G) := by
  sorry

open ConjAct

variable [IsQuaternionAlgebra F D]

def group_smul (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) (Ï† : WeightTwoAutomorphicForm F D R) :
    WeightTwoAutomorphicForm F D R where
  toFun x := Ï† (x * g)
  left_invt Î´ x := by simp [left_invt, mul_assoc]
  right_invt := by
    obtain âŸ¨U, hUâŸ© := Ï†.right_invt
    refine âŸ¨(toConjAct g) â€¢ U, ?_, ?_âŸ©
    Â· replace hU := hU.1
      exact isOpen_smul hU (toConjAct g)
    Â· rintro k x âŸ¨u, hu, rflâŸ©
      simp only [MulDistribMulAction.toMonoidEnd_apply, MulDistribMulAction.toMonoidHom_apply,
        smul_def, ofConjAct_toConjAct, â† hU.2 (k * g) u hu]
      group
  trivial_central_char z x := by
    simp only [mul_assoc]
    have := range_inclâ‚‚_le_center F D âŸ¨z, rflâŸ©
    rw [Subgroup.mem_center_iff] at this
    rw [â† this, â† mul_assoc, trivial_central_char]

instance : SMul (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£ (WeightTwoAutomorphicForm F D R) where
  smul := group_smul

@[simp]
lemma group_smul_apply (g : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£)
    (Ï† : WeightTwoAutomorphicForm F D R) (x : (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) :
    (g â€¢ Ï†) x = Ï† (x * g) := rfl

-- ?! TODO
set_option synthInstance.maxHeartbeats 40000 in
instance distribMulAction : DistribMulAction (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£
    (WeightTwoAutomorphicForm F D R) where
  smul := group_smul
  one_smul Ï† := by ext; simp
  mul_smul g h Ï† := by ext; simp [mul_assoc]
  smul_zero g := by ext; simp
  smul_add g Ï† Ïˆ := by ext; simp

end add_comm_group

section comm_ring

variable {R : Type*} [CommRing R]

def ring_smul (r : R) (Ï† : WeightTwoAutomorphicForm F D R) :
    WeightTwoAutomorphicForm F D R where
      toFun g := r â€¢ Ï† g
      left_invt := by simp [left_invt, smul_comm]
      right_invt := by
        obtain âŸ¨U, hUâŸ© := Ï†.right_invt
        use U
        simp_all only [smul_eq_mul, implies_true, and_self]
      trivial_central_char g z := by simp only [trivial_central_char, smul_comm r]

instance : SMul R (WeightTwoAutomorphicForm F D R) where
  smul := ring_smul

lemma smul_apply (r : R) (Ï† : WeightTwoAutomorphicForm F D R)
    (g : (D âŠ—[F] FiniteAdeleRing (ğ“ F) F)Ë£) :
    (r â€¢ Ï†) g = r â€¢ (Ï† g) := rfl

instance module : Module R (WeightTwoAutomorphicForm F D R) where
  one_smul g := by ext; simp [smul_apply]
  mul_smul r s g := by ext; simp [smul_apply, mul_smul, mul_assoc]
  smul_zero r := by ext; simp [smul_apply]
  smul_add r f g := by ext; simp [smul_apply, mul_add]
  add_smul r s g := by ext; simp [smul_apply, add_mul]
  zero_smul g := by ext; simp [smul_apply]

variable [IsQuaternionAlgebra F D]

instance : SMulCommClass (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£ R
    (WeightTwoAutomorphicForm F D R) where
  smul_comm g r Ï† := by
    ext x
    simp [smul_apply]

end comm_ring

end WeightTwoAutomorphicForm

section finite_level

variable [IsQuaternionAlgebra F D]

def WeightTwoAutomorphicFormOfLevel_aux (U : Subgroup (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£)
    (R : Type*) [CommRing R] : Submodule R (WeightTwoAutomorphicForm F D R) where
  carrier := {Ï† | âˆ€ u âˆˆ U, u â€¢ Ï† = Ï†}
  add_mem' {a b} ha hb := by simp_all
  zero_mem' := by simp_all
  smul_mem' c {x} hx := by simp_all [smul_comm]

def WeightTwoAutomorphicFormOfLevel (U : Subgroup (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£)
    (R : Type*) [CommRing R] : Type _ := WeightTwoAutomorphicFormOfLevel_aux U R

variable (U : Subgroup (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£) (R : Type*) [CommRing R]

instance : AddCommGroup (WeightTwoAutomorphicFormOfLevel U R) :=
  AddSubgroup.toAddCommGroup (WeightTwoAutomorphicFormOfLevel_aux U R).toAddSubgroup

instance : Module R (WeightTwoAutomorphicFormOfLevel U R) :=
  SMulMemClass.toModule (WeightTwoAutomorphicFormOfLevel_aux U R)

end finite_level

end TotallyDefiniteQuaternionAlgebra
