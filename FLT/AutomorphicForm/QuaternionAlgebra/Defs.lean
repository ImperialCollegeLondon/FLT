/-
Copyright (c) 2024 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard
-/
import Mathlib.RingTheory.DedekindDomain.FiniteAdeleRing
import Mathlib.Topology.Algebra.Module.ModuleTopology
import FLT.Mathlib.Algebra.FixedPoints.Basic
import Mathlib.Order.CompletePartialOrder
import Mathlib.Algebra.Lie.OfAssociative
import Mathlib.NumberTheory.NumberField.FinitePlaces
import FLT.Hacks.RightActionInstances
import Mathlib.GroupTheory.GroupAction.Defs
import FLT.Mathlib.Algebra.IsQuaternionAlgebra

/-

# Definition of automorphic forms on a totally definite quaternion algebra

## Main definitions

In the `TotallyDefiniteQuaternionAlgebra` namespace:

* `WeightTwoAutomorphicForm F D R` -- weight 2
  R-valued automorphic forms for the totally definite quaternion algebra `D` over
  the totally real field `F`. Defined as locally-constant functions
  `œÜ : DÀ£ \ (D ‚äó ùî∏_F^‚àû)À£ ‚Üí R` which are right-invariant by some compact open subgroup
  (i.e. ‚àÉ U_œÜ such that `œÜ(gu)=œÜ(g)` for all `u ‚àà U`) and have trivial central character
  (i.e. `œÜ(zg)=œÜ(g)` for all `z ‚àà (ùî∏_F^‚àû)À£`).

* `WeightTwoAutomorphicFormOfLevel U R` -- weight 2 R-valued automorphic forms of
  level `U`, i.e. `U`-invariant elements of `WeightTwoAutomorphicForm F D R`.
  It is a nontrivial theorem that if `U` is open and `R` is Noetherian then this space
  is a finitely-generated `R`-module; this follows from Fujisaki's lemma.

## Implementation notes

This file is slow, for reasons I don't understand properly.
-/

suppress_compilation

set_option maxSynthPendingDepth 1 -- otherwise things are even slower, for some reason which
-- I never quite got to the bottom of

variable (F : Type*) [Field F] [NumberField F] -- if F isn't totally real all the definitions
-- below are garbage mathematically but they typecheck.

variable (D : Type*) [Ring D] [Algebra F D] [FiniteDimensional F D]
  -- If D isn't totally definite then all the
  -- definitions below are garbage mathematically but they typecheck.

namespace TotallyDefiniteQuaternionAlgebra

open scoped TensorProduct NumberField

open IsDedekindDomain

instance : CommRing (FiniteAdeleRing (ùìû F) F) := inferInstance
instance : Ring (D ‚äó[F] FiniteAdeleRing (ùìû F) F) := inferInstance

/-- `Dfx` is an abbreviation for $(D\otimes_F\mathbb{A}_F^\infty)^\times.$ -/
abbrev Dfx := (D ‚äó[F] (FiniteAdeleRing (ùìû F) F))À£

/-- incl‚ÇÅ is an abbreviation for the inclusion
$D^\times\to(D\otimes_F\mathbb{A}_F^\infty)^\times.$ Remark: I wrote the `incl‚ÇÅ`
docstring in LaTeX and the `incl‚ÇÇ` one in unicode. Which is better? -/
noncomputable abbrev incl‚ÇÅ : DÀ£ ‚Üí* Dfx F D :=
  Units.map (Algebra.TensorProduct.includeLeftRingHom.toMonoidHom)

open scoped TensorProduct.RightActions in
/-- `incl‚ÇÇ` is he inclusion `ùî∏_F^‚àûÀ£ ‚Üí (D ‚äó ùî∏_F^‚àûÀ£)`. Remark: I wrote the `incl‚ÇÅ`
docstring in LaTeX and the `incl‚ÇÇ` one in unicode. Which is better? -/
noncomputable abbrev incl‚ÇÇ : (FiniteAdeleRing (ùìû F) F)À£ ‚Üí* Dfx F D :=
  Units.map (algebraMap _ _).toMonoidHom

-- it's actually equal but ‚äÜ is all we need, and equality is harder
open scoped TensorProduct.RightActions in
omit [FiniteDimensional F D] in
lemma range_incl‚ÇÇ_le_center : MonoidHom.range (incl‚ÇÇ F D) ‚â§ Subgroup.center (Dfx F D) := by
  rintro x ‚ü®y, rfl‚ü©
  refine Subgroup.mem_center_iff.mpr fun g ‚Ü¶ Units.ext ?_
  exact (Algebra.commutes _ _).symm

open scoped TensorProduct.RightActions in
/--
This definition is made in mathlib-generality but is *not* the definition of a
weight 2 automorphic form unless `DÀ£` is compact mod centre at infinity.
This hypothesis will be true if `D` is a totally definite quaternion algebra
over a totally real field.
-/
structure WeightTwoAutomorphicForm
  -- defined over R
  (R : Type*) [AddCommMonoid R] where
  /-- The function underlying an automorphic form. -/
  toFun : Dfx F D ‚Üí R
  left_invt : ‚àÄ (Œ¥ : DÀ£) (g : Dfx F D),
    toFun (incl‚ÇÅ F D Œ¥ * g) = (toFun g)
  right_invt : ‚àÉ (U : Subgroup (Dfx F D)),
    IsOpen (U : Set (Dfx F D)) ‚àß
    ‚àÄ (g : Dfx F D),
    ‚àÄ u ‚àà U, toFun (g * u) = toFun g
  trivial_central_char (z : (FiniteAdeleRing (ùìû F) F)À£)
      (g : Dfx F D) :
      toFun (g * incl‚ÇÇ F D z) = toFun g

variable {F D}

namespace WeightTwoAutomorphicForm

section add_comm_group

variable {R : Type*} [AddCommGroup R]

instance : CoeFun (WeightTwoAutomorphicForm F D R) (fun _ ‚Ü¶ Dfx F D ‚Üí R) where
  coe := toFun

attribute [coe] WeightTwoAutomorphicForm.toFun

@[ext]
theorem ext (œÜ œà : WeightTwoAutomorphicForm F D R) (h : ‚àÄ x, œÜ x = œà x) : œÜ = œà := by
  cases œÜ; cases œà; simp only [mk.injEq]; ext; apply h

/-- The zero automorphic form for a totally definite quaterion algebra. -/
def zero : (WeightTwoAutomorphicForm F D R) where
  toFun := 0
  left_invt Œ¥ _ := by simp
  -- this used to be `by simp` but now it times out doing some crazy typeclass search for
  -- `DiscreteTopology (D ‚äó[F] FiniteAdeleRing (ùìû F) F)À£`
  right_invt := ‚ü®‚ä§, by simp only [Subgroup.coe_top, isOpen_univ, Subgroup.mem_top,
    Pi.zero_apply, imp_self, implies_true, and_self]‚ü©
  trivial_central_char _ z := by simp

instance : Zero (WeightTwoAutomorphicForm F D R) where
  zero := zero

@[simp]
theorem zero_apply (x : Dfx F D) :
    (0 : WeightTwoAutomorphicForm F D R) x = 0 := rfl

/-- Negation on the space of automorphic forms over a totally definite quaternion algebra. -/
def neg (œÜ : WeightTwoAutomorphicForm F D R) : WeightTwoAutomorphicForm F D R where
  toFun x := - œÜ x
  left_invt Œ¥ g := by simp [left_invt]
  right_invt := by
    obtain ‚ü®U, hU‚ü© := œÜ.right_invt
    simp_all only [neg_inj, right_invt]
  trivial_central_char g z := by simp [trivial_central_char]

instance : Neg (WeightTwoAutomorphicForm F D R) where
  neg := neg

@[simp, norm_cast]
theorem neg_apply (œÜ : WeightTwoAutomorphicForm F D R) (x : Dfx F D) :
    (-œÜ : WeightTwoAutomorphicForm F D R) x = -(œÜ x) := rfl

/-- Addition on the space of automorphic forms over a totally definite quaternion algebra. -/
def add (œÜ œà : WeightTwoAutomorphicForm F D R) : WeightTwoAutomorphicForm F D R where
  toFun x := œÜ x + œà x
  left_invt := by simp [left_invt]
  right_invt := by
    obtain ‚ü®U, hU‚ü© := œÜ.right_invt
    obtain ‚ü®V, hV‚ü© := œà.right_invt
    use U ‚äì V
    simp_all only [Subgroup.coe_inf, IsOpen.inter, Subgroup.mem_inf, implies_true, and_self]
  trivial_central_char := by simp [trivial_central_char]

instance : Add (WeightTwoAutomorphicForm F D R) where
  add := add

@[simp, norm_cast]
theorem add_apply (œÜ œà : WeightTwoAutomorphicForm F D R) (x : Dfx F D) :
    (œÜ + œà) x = (œÜ x) + (œà x) := rfl

instance addCommGroup : AddCommGroup (WeightTwoAutomorphicForm F D R) where
  add := (¬∑ + ¬∑)
  add_assoc := by intros; ext; simp [add_assoc];
  zero := 0
  zero_add := by intros; ext; simp
  add_zero := by intros; ext; simp
  nsmul := nsmulRec
  neg := (-¬∑)
  zsmul := zsmulRec
  neg_add_cancel := by intros; ext; simp
  add_comm := by intros; ext; simp [add_comm]

open scoped Pointwise

-- these two should be in mathlib
instance {G} [TopologicalSpace G] [DivInvMonoid G] [ContinuousMul G] :
    ContinuousConstSMul (ConjAct G) G where
  continuous_const_smul _ := IsTopologicalGroup.continuous_conj _

lemma _root_.ConjAct.isOpen_smul {G : Type*} [Group G] [TopologicalSpace G]
    [IsTopologicalGroup G] {U : Subgroup G} (hU : IsOpen (U : Set G)) (g : ConjAct G) :
    IsOpen ((g ‚Ä¢ U : Subgroup G) : Set G) :=
  (Homeomorph.smul g).isOpen_image.mpr hU

open ConjAct

open scoped TensorProduct.RightActions in
/-- The adelic group action on the space of automorphic forms over a totally definite
quaternion algebra. -/
def group_smul (g : Dfx F D) (œÜ : WeightTwoAutomorphicForm F D R) :
    WeightTwoAutomorphicForm F D R where
  toFun x := œÜ (x * g)
  left_invt Œ¥ x := by simp [left_invt, mul_assoc]
  right_invt := by
    obtain ‚ü®U, hU‚ü© := œÜ.right_invt
    refine ‚ü®(toConjAct g) ‚Ä¢ U, ?_, ?_‚ü©
    ¬∑ replace hU := hU.1
      exact isOpen_smul hU (toConjAct g)
    ¬∑ rintro k x ‚ü®u, hu, rfl‚ü©
      simp only [MulDistribMulAction.toMonoidEnd_apply, MulDistribMulAction.toMonoidHom_apply,
        smul_def, ofConjAct_toConjAct, ‚Üê hU.2 (k * g) u hu]
      group
  trivial_central_char z x := by
    simp only [mul_assoc]
    have := range_incl‚ÇÇ_le_center F D ‚ü®z, rfl‚ü©
    rw [Subgroup.mem_center_iff] at this
    rw [‚Üê this, ‚Üê mul_assoc, trivial_central_char]

instance : SMul (Dfx F D) (WeightTwoAutomorphicForm F D R) where
  smul := group_smul

@[simp]
lemma group_smul_apply (g : Dfx F D)
    (œÜ : WeightTwoAutomorphicForm F D R) (x : Dfx F D) :
    (g ‚Ä¢ œÜ) x = œÜ (x * g) := rfl

attribute [instance low] Units.instMulAction

instance mulAction :
    MulAction (Dfx F D) (WeightTwoAutomorphicForm F D R) where
  smul := group_smul
  one_smul œÜ := by ext; simp only [group_smul_apply, mul_one]
  mul_smul g h œÜ := by ext; simp only [group_smul_apply, mul_assoc]

instance distribMulAction : DistribMulAction (Dfx F D)
    (WeightTwoAutomorphicForm F D R) where
  __ := mulAction
  smul_zero g := by ext; simp only [group_smul_apply, zero_apply]
  smul_add g œÜ œà := by ext; simp only [group_smul_apply, add_apply]

end add_comm_group

section comm_ring

variable {R : Type*} [CommRing R]

/-- The scalar action on the space of weight 2 automorphic forms on a totally definite
quaternion algebra. -/
def ring_smul (r : R) (œÜ : WeightTwoAutomorphicForm F D R) :
    WeightTwoAutomorphicForm F D R where
      toFun g := r ‚Ä¢ œÜ g
      left_invt := by simp [left_invt]
      right_invt := by
        obtain ‚ü®U, hU‚ü© := œÜ.right_invt
        use U
        simp_all only [smul_eq_mul, implies_true, and_self]
      trivial_central_char g z := by simp only [trivial_central_char]

instance : SMul R (WeightTwoAutomorphicForm F D R) where
  smul := ring_smul

lemma smul_apply (r : R) (œÜ : WeightTwoAutomorphicForm F D R)
    (g : Dfx F D) :
    (r ‚Ä¢ œÜ) g = r ‚Ä¢ (œÜ g) := rfl

instance module : Module R (WeightTwoAutomorphicForm F D R) where
  one_smul g := by ext; simp [smul_apply]
  mul_smul r s g := by ext; simp [smul_apply, mul_assoc]
  smul_zero r := by ext; simp [smul_apply]
  smul_add r f g := by ext; simp [smul_apply, mul_add]
  add_smul r s g := by ext; simp [smul_apply, add_mul]
  zero_smul g := by ext; simp [smul_apply]

instance : SMulCommClass (Dfx F D) R (WeightTwoAutomorphicForm F D R) where
  smul_comm r g œÜ := by
    ext x
    simp [smul_apply]

end comm_ring

end WeightTwoAutomorphicForm

section finite_level

/--
`WeightTwoAutomorphicFormOfLevel U R` is the `R`-valued weight 2 automorphic forms of a fixed
level `U` for a totally definite quaternion algebra over a totally real field.
-/
def WeightTwoAutomorphicFormOfLevel (U : Subgroup (Dfx F D))
    (R : Type*) [CommRing R] : Type _ := MulAction.FixedPoints U (WeightTwoAutomorphicForm F D R)

namespace WeightTwoAutomorphicFormOfLevel

variable {U : Subgroup (Dfx F D)} {R : Type*} [CommRing R]

/--
Enables coercion of an automorphic forms to a function.
-/
@[coe]
def toFun (f : WeightTwoAutomorphicFormOfLevel U R)
    (x : Dfx F D) : R := f.1.toFun x

instance : CoeFun (WeightTwoAutomorphicFormOfLevel U R) (fun _ ‚Ü¶ Dfx F D ‚Üí R) where
  coe := toFun

@[ext]
lemma ext ‚¶Éf g : WeightTwoAutomorphicFormOfLevel U R‚¶Ñ (h : ‚àÄ x, f x = g x) : f = g :=
  Subtype.ext <| WeightTwoAutomorphicForm.ext _ _ h

lemma left_invt (f : WeightTwoAutomorphicFormOfLevel U R) (Œ¥ : DÀ£)
    (g : Dfx F D) :
    f ((incl‚ÇÅ F D) Œ¥ * g) = f g := f.1.left_invt Œ¥ g

lemma right_invt (f : WeightTwoAutomorphicFormOfLevel U R) (g : Dfx F D)
    (u : U) : f (g * u) = f g :=
  congr($(f.2 u) g)

instance : AddCommGroup (WeightTwoAutomorphicFormOfLevel U R) := inferInstanceAs <|
  AddCommGroup (MulAction.FixedPoints U (WeightTwoAutomorphicForm F D R))

instance : Module R (WeightTwoAutomorphicFormOfLevel U R) := inferInstanceAs <|
  Module R (MulAction.FixedPoints U (WeightTwoAutomorphicForm F D R))

end WeightTwoAutomorphicFormOfLevel

end finite_level

end TotallyDefiniteQuaternionAlgebra
