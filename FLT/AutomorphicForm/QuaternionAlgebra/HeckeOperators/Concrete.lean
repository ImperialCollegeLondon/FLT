/-
Copyright (c) 2025 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Andrew Yang, Matthew Jasper
-/
import FLT.AutomorphicForm.QuaternionAlgebra.HeckeOperators.Abstract -- abstract Hecke ops
import FLT.AutomorphicForm.QuaternionAlgebra.Defs -- definitions of automorphic forms
import FLT.QuaternionAlgebra.NumberField -- rigidifications of quat algs
import Mathlib.NumberTheory.NumberField.InfinitePlace.TotallyRealComplex
import Mathlib.RingTheory.DedekindDomain.FiniteAdeleRing
import FLT.DedekindDomain.FiniteAdeleRing.LocalUnits -- for (π 0; 0 1)
import FLT.Mathlib.Topology.Algebra.RestrictedProduct
/-

# Concrete Hecke operators

Hecke operators for spaces of automorphic forms on totally definite quaternion algebras
of level `U₁(S)`, where `S` is a finite set of finite places of the totally real number
field `F`, and `U₁(S)` is the matrices which are of the form `(a *;0 a)` mod `v` for
all `v ∈ S`.

## Main definitions

All in the `TotallyDefiniteQuaternionAlgebra.WeightTwoAutomorphicForm` namespace.

Let `r : Rigidification F D` be an `𝔸_F^∞`-algebra isomorphism `D ⊗[F] 𝔸_F^∞ = M₂(𝔸_F^∞)`,
needed to interpret the local factors `Dᵥ` as matrix rings so we can define Hecke operators
as matrices.

* `HeckeOperator.T r R v` -- the Hecke operator `Tᵥ` associated to `(ϖᵥ 0; 0 1)` at a (good)
  place `v` (via the rigidification `r`), as an `R`-linear endomorphism of
  `WeightTwoAutomorphicFormOfLevel (U1 r S) R`.
* `HeckeOperator.U r S R a ha` -- the Hecke operator `Uᵥ,ₐ` associated to `(a 0;0 1)` at
  a (bad) place `v`; here `a : v.adicCompletionIntegers F` and `ha` is a proof that `a ≠ 0`,
  as an `R`-linear endomorphism of `WeightTwoAutomorphicFormOfLevel (U1 r S) R`.
* `HeckeAlgebra F D r S R` -- the Hecke algebra generated by the `Tᵥ` and `Uᵥ,ₐ`. Built
  as a type, coerced from a subalgebra of the endomorphism algebra of the forms.
* `HeckeAlgebra.T D r R v hv` -- `Tᵥ` as a term of type `HeckeAlgebra F D r S R`
* `HeckeAlgebra.U D r R v hv a ha` -- `Uᵥ,ₐ` as a term of type `HeckeAlgebra F D r S R`

-/

/-

## A finiteness result

The existence of abstract Hecke operators relies on a certain double coset space being
a finite union of single cosets. In our situation we can supply this finiteness proof
via a topological argument, which we abstract here.

-/

section finiteness

open Topology Set

variable {G : Type*} [Group G] [TopologicalSpace G] [IsTopologicalGroup G]
    {g : G} {U V : Subgroup G}

open scoped Pointwise in
lemma QuotientGroup.mk_image_finite_of_compact_of_open (hU : IsCompact (U : Set G))
    (hV : IsCompact (V : Set G)) (hVopen : IsOpen (V : Set G)) :
    (QuotientGroup.mk '' (U * g • V) : Set (G ⧸ V)).Finite := by
  have : DiscreteTopology (G ⧸ V) := by
    rw [← forall_open_iff_discrete]
    intro s
    rw [← (isQuotientMap_mk V).isOpen_preimage, ← (QuotientGroup.mk_surjective).image_preimage s,
      preimage_image_mk_eq_iUnion_image, iUnion_subtype]
    conv in ⋃ x ∈ _, _ => change ⋃ x ∈ (V : Set G), _
    rw [iUnion_mul_right_image]
    exact IsOpen.mul_left hVopen
  exact ((hU.mul <| hV.image (by fun_prop)).image continuous_mk).finite_of_discrete

end finiteness


open NumberField IsQuaternionAlgebra.NumberField IsDedekindDomain

-- let F be a totally real number field
variable (F : Type*) [Field F] [NumberField F] [IsTotallyReal F]

-- Let D/F be a quaternion algebra
variable (D : Type*) [Ring D] [Algebra F D] [IsQuaternionAlgebra F D]

-- Let r be a rigidification of D, which is a collection of isomorphisms D ⊗ Fᵥ = M₂(Fᵥ)
-- for all finite places v of F, compatible with the adelic structure (i.e. inducing
-- an isomorphism D ⊗_F 𝔸_F^f = M₂(𝔸_F^f))
variable (r : Rigidification F D)

-- Let S be a finite set of finite plaes of F (the level)
variable (S : Finset (HeightOneSpectrum (𝓞 F)))

-- let P be a good prime
variable {P : HeightOneSpectrum (𝓞 F)} (hP : P ∉ S)

open TotallyDefiniteQuaternionAlgebra
-- let's do T_P : S_2^D(U_1(S)) -> S_2^D(U_1(S))
namespace TotallyDefiniteQuaternionAlgebra.WeightTwoAutomorphicForm

open IsDedekindDomain.HeightOneSpectrum

-- attribute [local instance] Algebra.TensorProduct.rightAlgebra in
-- #check Subgroup.map (Units.map r.symm.toMonoidHom) (GL2.TameLevel S)

open scoped TensorProduct

variable {F D} in
attribute [local instance] Algebra.TensorProduct.rightAlgebra in
/-- U1(S) -/
noncomputable abbrev U1 : Subgroup (D ⊗[F] (IsDedekindDomain.FiniteAdeleRing (𝓞 F) F))ˣ :=
  Subgroup.map (Units.map r.symm.toMonoidHom) (GL2.TameLevel S)

variable {F D} in
lemma U1_compact : IsCompact (U1 r S : Set (D ⊗[F] (IsDedekindDomain.FiniteAdeleRing (𝓞 F) F))ˣ) :=
  sorry -- #583, long

variable {F D} in
lemma U1_open : IsOpen (U1 r S : Set (D ⊗[F] (IsDedekindDomain.FiniteAdeleRing (𝓞 F) F))ˣ) :=
  sorry -- #583, long

variable (R : Type*) [CommRing R]

namespace HeckeOperator

variable {F D S} in
set_option maxSynthPendingDepth 1 in
attribute [local instance] Algebra.TensorProduct.rightAlgebra in
/-- The Hecke operator T_v as an R-linear map from R-valued quaternionic weight 2
automorphic forms of level U_1(S).
-/
noncomputable def T (v : HeightOneSpectrum (𝓞 F)) :
    WeightTwoAutomorphicFormOfLevel (U1 r S) R →ₗ[R]
    WeightTwoAutomorphicFormOfLevel (U1 r S) R :=
  letI : DecidableEq (HeightOneSpectrum (𝓞 F)) := Classical.typeDecidableEq _
  let g : (D ⊗[F] (IsDedekindDomain.FiniteAdeleRing (𝓞 F) F))ˣ :=
    Units.map r.symm.toMonoidHom (Matrix.GeneralLinearGroup.diagonal
    ![FiniteAdeleRing.localUniformiserUnit F v, 1])
  AbstractHeckeOperator.HeckeOperator (R := R) g (U1 r S) (U1 r S)
  (QuotientGroup.mk_image_finite_of_compact_of_open (U1_compact r S) (U1_compact r S) (U1_open r S))

variable {F D} in
set_option maxSynthPendingDepth 1 in
attribute [local instance] Algebra.TensorProduct.rightAlgebra in
/-- The Hecke operator U_{v,α} associated to the matrix (α 0;0 1) at v,
considered as an R-linear map from R-valued quaternionic weight 2
automorphic forms of level U_1(S). Here α is a nonzero element of 𝓞ᵥ.
We do not demand the condition v ∈ S, the bad primes, but this operator
should only be used in this setting. See also `T r v` for the good primes.
-/
@[nolint unusedArguments] -- this can be removed when the sorries are filled in
-- but not before because it breaks linting
noncomputable def U {v : HeightOneSpectrum (𝓞 F)}
    (α : v.adicCompletionIntegers F) (hα : α ≠ 0) :
    WeightTwoAutomorphicFormOfLevel (U1 r S) R →ₗ[R]
    WeightTwoAutomorphicFormOfLevel (U1 r S) R :=
  letI : DecidableEq (HeightOneSpectrum (𝓞 F)) := Classical.typeDecidableEq _
  let g : (D ⊗[F] (IsDedekindDomain.FiniteAdeleRing (𝓞 F) F))ˣ :=
    Units.map r.symm.toMonoidHom (Matrix.GeneralLinearGroup.diagonal
    ![FiniteAdeleRing.localUnit F ⟨(α : v.adicCompletion F),
    (α : v.adicCompletion F)⁻¹, by
      rw [mul_inv_cancel₀]
      exact_mod_cast hα, by
      rw [inv_mul_cancel₀]
      exact_mod_cast hα⟩, 1])
  AbstractHeckeOperator.HeckeOperator (R := R) g (U1 r S) (U1 r S)
  (QuotientGroup.mk_image_finite_of_compact_of_open (U1_compact r S) (U1_compact r S) (U1_open r S))

lemma _root_.Ne.mul {M₀ : Type*} [Mul M₀] [Zero M₀] [NoZeroDivisors M₀] {a b : M₀}
  (ha : a ≠ 0) (hb : b ≠ 0) : a * b ≠ 0 := mul_ne_zero ha hb

lemma U_mul {v : HeightOneSpectrum (𝓞 F)}
    {α β : v.adicCompletionIntegers F} (hα : α ≠ 0) (hβ : β ≠ 0) :
    (U r S R α hα ∘ₗ U r S R β hβ) =
    U r S R (α * β) (hα.mul hβ) := by
  sorry -- #584, long

lemma U_comm {v : HeightOneSpectrum (𝓞 F)}
    {α β : v.adicCompletionIntegers F} (hα : α ≠ 0) (hβ : β ≠ 0) :
    U r S R α hα ∘ₗ U r S R β hβ =
    U r S R β hβ ∘ₗ U r S R α hα := by
  rw [U_mul, U_mul]
  congr 1
  rw [mul_comm]

end HeckeOperator

open HeckeOperator

/-- `HeckeAlgebra F D r S R` is the Hecke algebra associated to the weight 2
`R`-valued automorphic forms associated to the discriminant 1 totally definite
quaternion algebra `D` over the totally real field `F`, of level `U₁(S)` where `S` is
a finite set of nonzero primes `v` of `𝓞 F`. To make sense of this definition we choose
a rigidification `r`, that is, a fixed `𝔸_F^∞`-linear
isomorphism `D ⊗[F] 𝔸_F^∞ = M₂(𝔸_F^∞)`, enabling us to define level structures and
Hecke operators `Tᵥ` and `Uᵥ` using 2x2 matrices.

Details: `U₁(S)` is the subgroup of `(D ⊗[F] 𝔸_F^∞)ˣ` associated, via `r`, to the
matrices which are in `GL₂(𝓞ᵥ)` for all `v ∉ S` and which are of the form
`(a *; 0 a)` mod `v` for all `v ∈ S`. The Hecke algebra is defined to be the
sub-`R`-algebra of the weight 2 forms of level `U₁(S)` generated by the following
two kinds of Hecke operators: first there are the operators
`Tᵥ` associated to the matrices `(ϖᵥ 0; 0 1)` for `v ∉ S` (here `ϖᵥ ∈ 𝔸_F^∞` is a local
uniformiser supported at `v`). Second, there are the Hecke operators `Uᵥ,ₐ`
for `v ∈ S` and `0 ≠ αᵥ ∈ 𝓞ᵥ`, associated the matries `(αᵥ 0; 0 1)`.
These slightly nonstandard Hecke operators satisfy `Uᵥ,ₛ * Uᵥ,ₜ = Uᵥ,ₛₜ`
and in particular this Hecke algebra is commutative (Hecke operators supported
at distinct primes commute because the decomposition of the double cosets
into single cosets can be done purely locally).
-/
def HeckeAlgebra : Type _ :=
  (Algebra.adjoin R ({T r R v | v ∉ S} ∪
  {φ | ∃ (v : HeightOneSpectrum (𝓞 F)) (_hv : v ∈ S)
         (α : v.adicCompletionIntegers F) (hα : α ≠ 0), φ = U r S R α hα}) :
    Subalgebra R (WeightTwoAutomorphicFormOfLevel (U1 r S) R →ₗ[R]
      WeightTwoAutomorphicFormOfLevel (U1 r S) R))

namespace HeckeAlgebra

noncomputable instance instRing :
    Ring (HeckeAlgebra F D r S R) := inferInstanceAs <|
  Ring (Algebra.adjoin R _ : Subalgebra R (WeightTwoAutomorphicFormOfLevel (U1 r S) R →ₗ[R]
      WeightTwoAutomorphicFormOfLevel (U1 r S) R))

noncomputable instance instAlgebra :
    Algebra R (HeckeAlgebra F D r S R) := inferInstanceAs <|
  Algebra R (Algebra.adjoin R _ : Subalgebra R (WeightTwoAutomorphicFormOfLevel (U1 r S) R →ₗ[R]
      WeightTwoAutomorphicFormOfLevel (U1 r S) R))

noncomputable instance instCommRing :
    CommRing (HeckeAlgebra F D r S R) where
  __ := instRing F D r S R
  mul_comm := sorry -- #585 -- check on generators

variable {F S} in
/-- The Hecke operator Tᵥ as an element of the Hecke algebra. -/
noncomputable def T (v : HeightOneSpectrum (𝓞 F)) (hv : v ∉ S) : HeckeAlgebra F D r S R :=
  ⟨HeckeOperator.T r R v, by
    apply Algebra.subset_adjoin
    left
    use v⟩



variable {F S} in
/-- The Hecke operator Uᵥ,ₐ as an element of the Hecke algebra. -/
noncomputable def U (v : HeightOneSpectrum (𝓞 F)) (hv : v ∈ S) (α : v.adicCompletionIntegers F)
    (hα : α ≠ 0) : HeckeAlgebra F D r S R :=
  ⟨HeckeOperator.U r S R α hα, by
    apply Algebra.subset_adjoin
    right
    use v, hv, α, hα⟩

end HeckeAlgebra

end TotallyDefiniteQuaternionAlgebra.WeightTwoAutomorphicForm
