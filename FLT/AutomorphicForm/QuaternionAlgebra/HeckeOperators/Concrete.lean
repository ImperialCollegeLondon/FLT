/-
Copyright (c) 2025 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Andrew Yang, Matthew Jasper
-/
import FLT.AutomorphicForm.QuaternionAlgebra.HeckeOperators.Local -- abstract Hecke ops
import FLT.AutomorphicForm.QuaternionAlgebra.HeckeOperators.Abstract -- abstract Hecke ops
import FLT.AutomorphicForm.QuaternionAlgebra.Defs -- definitions of automorphic forms
import FLT.QuaternionAlgebra.NumberField -- rigidifications of quat algs
import Mathlib.NumberTheory.NumberField.InfinitePlace.TotallyRealComplex
import Mathlib.RingTheory.DedekindDomain.FiniteAdeleRing
import FLT.DedekindDomain.FiniteAdeleRing.LocalUnits -- for (Ï€ 0; 0 1)
import FLT.Mathlib.Topology.Algebra.RestrictedProduct.TopologicalSpace
import FLT.Mathlib.LinearAlgebra.Matrix.GeneralLinearGroup.Defs
/-

# Concrete Hecke operators

Hecke operators for spaces of automorphic forms on totally definite quaternion algebras
of level `Uâ‚(S)`, where `S` is a finite set of finite places of the totally real number
field `F`, and `Uâ‚(S)` is the matrices which are of the form `(a *;0 a)` mod `v` for
all `v âˆˆ S`.

## Main definitions

All in the `TotallyDefiniteQuaternionAlgebra.WeightTwoAutomorphicForm` namespace.

Let `r : Rigidification F D` be an `ğ”¸_F^âˆ`-algebra isomorphism `D âŠ—[F] ğ”¸_F^âˆ = Mâ‚‚(ğ”¸_F^âˆ)`,
needed to interpret the local factors `Dáµ¥` as matrix rings so we can define Hecke operators
as matrices.

* `HeckeOperator.T r R v` -- the Hecke operator `Táµ¥` associated to `(Ï–áµ¥ 0; 0 1)` at a (good)
  place `v` (via the rigidification `r`), as an `R`-linear endomorphism of
  `WeightTwoAutomorphicFormOfLevel (U1 r S) R`.
* `HeckeOperator.U r S R a ha` -- the Hecke operator `Uáµ¥,â‚` associated to `(a 0;0 1)` at
  a (bad) place `v`; here `a : v.adicCompletionIntegers F` and `ha` is a proof that `a â‰  0`,
  as an `R`-linear endomorphism of `WeightTwoAutomorphicFormOfLevel (U1 r S) R`.
* `HeckeAlgebra F D r S R` -- the Hecke algebra generated by the `Táµ¥` and `Uáµ¥,â‚`. Built
  as a type, coerced from a subalgebra of the endomorphism algebra of the forms.
* `HeckeAlgebra.T D r R v hv` -- `Táµ¥` as a term of type `HeckeAlgebra F D r S R`
* `HeckeAlgebra.U D r R v hv a ha` -- `Uáµ¥,â‚` as a term of type `HeckeAlgebra F D r S R`

-/

/-

## A finiteness result

The existence of abstract Hecke operators relies on a certain double coset space being
a finite union of single cosets. In our situation we can supply this finiteness proof
via a topological argument, which we abstract here.

-/

section finiteness

open Topology Set

variable {G : Type*} [Group G] [TopologicalSpace G] [IsTopologicalGroup G]
    {g : G} {U V : Subgroup G}

open scoped Pointwise in
lemma QuotientGroup.mk_image_finite_of_compact_of_open
    (hU : IsCompact (U : Set G)) (hVopen : IsOpen (V : Set G)) :
    (QuotientGroup.mk '' (U * {g}) : Set (G â§¸ V)).Finite := by
  have : DiscreteTopology (G â§¸ V) := by
    rw [discreteTopology_iff_forall_isOpen]
    intro s
    rw [â† (isQuotientMap_mk V).isOpen_preimage, â† (QuotientGroup.mk_surjective).image_preimage s,
      preimage_image_mk_eq_iUnion_image, iUnion_subtype]
    conv in â‹ƒ x âˆˆ _, _ => change â‹ƒ x âˆˆ (V : Set G), _
    rw [iUnion_mul_right_image]
    exact IsOpen.mul_left hVopen
  exact ((hU.mul <| isCompact_singleton).image continuous_mk).finite_of_discrete

end finiteness


open NumberField IsQuaternionAlgebra.NumberField IsDedekindDomain

-- let F be a totally real number field
variable (F : Type*) [Field F] [NumberField F] [IsTotallyReal F]

-- Let D/F be a quaternion algebra
variable (D : Type*) [Ring D] [Algebra F D] [IsQuaternionAlgebra F D]

-- Let r be a rigidification of D, which is a collection of isomorphisms D âŠ— Fáµ¥ = Mâ‚‚(Fáµ¥)
-- for all finite places v of F, compatible with the adelic structure (i.e. inducing
-- an isomorphism D âŠ—_F ğ”¸_F^f = Mâ‚‚(ğ”¸_F^f))
variable (r : Rigidification F D)

-- Let S be a finite set of finite plaes of F (the level)
variable (S : Finset (HeightOneSpectrum (ğ“ F)))

-- let P be a good prime
variable {P : HeightOneSpectrum (ğ“ F)} (hP : P âˆ‰ S)

open TotallyDefiniteQuaternionAlgebra
-- let's define T_P : S_2^D(U_1(S)) -> S_2^D(U_1(S))
namespace TotallyDefiniteQuaternionAlgebra.WeightTwoAutomorphicForm

open IsDedekindDomain.HeightOneSpectrum

open scoped TensorProduct

-- Oops! This is also `IsDedekindDomain.HeightOneSpectrum.QuaternionAlgebra.TameLevel`
-- in `FLT.QuaternionAlgebra.NumberField`, defined differently (using `Subgroup.comap` not `map`).
-- Don't care which one survives.
variable {F D} in
open scoped TensorProduct.RightActions in
/-- U1(S) -/
noncomputable abbrev U1 : Subgroup (D âŠ—[F] (IsDedekindDomain.FiniteAdeleRing (ğ“ F) F))Ë£ :=
  Subgroup.map (Units.map r.symm.toMonoidHom) (GL2.TameLevel S)

variable {F D} in
open scoped TensorProduct.RightActions in
lemma U1_compact : IsCompact (U1 r S : Set (D âŠ—[F] (IsDedekindDomain.FiniteAdeleRing (ğ“ F) F))Ë£) :=
  sorry -- #583, long

variable {F D} in
open scoped TensorProduct.RightActions in
lemma U1_open : IsOpen (U1 r S : Set (D âŠ—[F] (IsDedekindDomain.FiniteAdeleRing (ğ“ F) F))Ë£) :=
  sorry -- #583, long

variable (R : Type*) [CommRing R]

namespace HeckeOperator

variable {F D S} in
set_option maxSynthPendingDepth 1 in
open scoped TensorProduct.RightActions in
/-- The Hecke operator T_v as an R-linear map from R-valued quaternionic weight 2
automorphic forms of level U_1(S).
-/
noncomputable def T (v : HeightOneSpectrum (ğ“ F)) :
    WeightTwoAutomorphicFormOfLevel (U1 r S) R â†’â‚—[R]
    WeightTwoAutomorphicFormOfLevel (U1 r S) R :=
  letI : DecidableEq (HeightOneSpectrum (ğ“ F)) := Classical.typeDecidableEq _
  let g : (D âŠ—[F] (IsDedekindDomain.FiniteAdeleRing (ğ“ F) F))Ë£ :=
    Units.map r.symm.toMonoidHom (Matrix.GeneralLinearGroup.diagonal
    ![FiniteAdeleRing.localUniformiserUnit F v, 1])
  AbstractHeckeOperator.HeckeOperator (R := R) g (U1 r S) (U1 r S)
  (QuotientGroup.mk_image_finite_of_compact_of_open (U1_compact r S) (U1_open r S))

section U

variable {F D}

variable {v : HeightOneSpectrum (ğ“ F)} (Î± : v.adicCompletionIntegers F) (hÎ± : Î± â‰  0)

open scoped TensorProduct.RightActions
open scoped Pointwise

noncomputable instance : DecidableEq (HeightOneSpectrum (ğ“ F)) := Classical.typeDecidableEq _

/-- The (global) matrix element `diag[Î±, 1]`. -/
noncomputable abbrev diag :
    (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£ :=
  Units.mapEquiv r.symm.toMulEquiv
    (FiniteAdeleRing.GL2.restrictedProduct.symm
    (RestrictedProduct.mulSingle _ _ (Local.GL2.diag Î± hÎ±)))

/-- The (global) matrix element `(unipotent t) * (diag Î± hÎ±) = !![Î±, t; 0, 1]`.
Here `t âˆˆ ğ’ªáµ¥ / Î±` and we lift it arbitrarily to `ğ’ªáµ¥`. -/
noncomputable def unipotent_mul_diag (t : â†‘(adicCompletionIntegers F v) â§¸ (Ideal.span {Î±})) :
    (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£ :=
  Units.mapEquiv r.symm.toMulEquiv
    (FiniteAdeleRing.GL2.restrictedProduct.symm
    (RestrictedProduct.mulSingle _ _
      (Local.GL2.unipotent_mul_diag Î± hÎ± (Quotient.out t : adicCompletionIntegers F v))))

/-- The set of elements `unipotent_mul_diag`, that is, the elements of `(D âŠ— ğ”¸_F^âˆ)Ë£`
which are `(Î± t;0 1)` at `v` and the identity elsewhere, as `t` runs through a set
of coset reps of `ğ“áµ¥ / Î±`. These will form a set of coset representatives for `U1 diag U1`.
-/
noncomputable def unipotent_mul_diag_image :
    Set (D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£ :=
  (unipotent_mul_diag r Î± hÎ±) '' âŠ¤

omit [IsTotallyReal F] [IsQuaternionAlgebra F D] in
lemma unipotent_mul_diag_inj :
    Set.InjOn (unipotent_mul_diag r Î± hÎ±) âŠ¤ := by
  intro tâ‚ hâ‚ tâ‚‚ hâ‚‚ h
  simp only [unipotent_mul_diag, EmbeddingLike.apply_eq_iff_eq, RestrictedProduct.ext_iff] at h
  let h' := h v; simp only [RestrictedProduct.mulSingle_eq_same, Units.ext_iff] at h'
  rw [â† Matrix.ext_iff] at h'
  let h'' := h' 0 1
  simpa [Local.GL2.unipotent_mul_diag, Matrix.GeneralLinearGroup.GL2.unipotent, Local.GL2.diag,
    Matrix.unitOfDetInvertible, Matrix.GeneralLinearGroup.diagonal] using h''

/-- The double coset space `Uâ‚(S) diag(Î±áµ¥,1) Uâ‚(S)` as a set of left cosets. -/
noncomputable def U1diagU1 :
    Set ((D âŠ—[F] (FiniteAdeleRing (ğ“ F) F))Ë£ â§¸ (U1 r S)) :=
  QuotientGroup.mk '' ((U1 r S) * {diag r Î± hÎ±})

theorem bijOn_unipotent_mul_diagU1_U1diagU1 :
    (unipotent_mul_diag_image r Î± hÎ±).BijOn QuotientGroup.mk (U1diagU1 r S Î± hÎ±) :=
  sorry -- global double coset decomposition

lemma unipotent_mul_diag_image_finite :
    (unipotent_mul_diag_image r Î± hÎ±).Finite := by
  apply (Set.BijOn.finite_iff_finite (bijOn_unipotent_mul_diagU1_U1diagU1 r {v} Î± hÎ±)).mpr
  unfold U1diagU1
  exact (QuotientGroup.mk_image_finite_of_compact_of_open (U1_compact r {v}) (U1_open r {v}))

lemma quot_top_finite (r : Rigidification F D) (Î± : v.adicCompletionIntegers F) (hÎ± : Î± â‰  0) :
    (âŠ¤ : Set ((adicCompletionIntegers F v) â§¸ (Ideal.span {Î±}))).Finite := by
  apply Set.Finite.of_finite_image _ (unipotent_mul_diag_inj r Î± hÎ±)
  apply unipotent_mul_diag_image_finite

set_option maxSynthPendingDepth 1 in -- shaves a little time off compilation!
/-- The Hecke operator U_{v,Î±} associated to the matrix (Î± 0;0 1) at v,
considered as an R-linear map from R-valued quaternionic weight 2
automorphic forms of level U_1(S). Here Î± is a nonzero element of ğ“áµ¥.
We do not demand the condition v âˆˆ S, the bad primes, but this operator
should only be used in this setting. See also `T r v` for the good primes.
-/
noncomputable def U :
    WeightTwoAutomorphicFormOfLevel (U1 r S) R â†’â‚—[R]
    WeightTwoAutomorphicFormOfLevel (U1 r S) R :=
  AbstractHeckeOperator.HeckeOperator (R := R) (diag r Î± hÎ±) (U1 r S) (U1 r S)
  (QuotientGroup.mk_image_finite_of_compact_of_open (U1_compact r S) (U1_open r S))

lemma _root_.Ne.mul {Mâ‚€ : Type*} [Mul Mâ‚€] [Zero Mâ‚€] [NoZeroDivisors Mâ‚€] {a b : Mâ‚€}
  (ha : a â‰  0) (hb : b â‰  0) : a * b â‰  0 := mul_ne_zero ha hb

noncomputable instance :
    DistribSMul (D âŠ—[F] FiniteAdeleRing (ğ“ F) F)Ë£ (WeightTwoAutomorphicForm F D R) :=
  distribMulAction.toDistribSMul

lemma U_apply (a : WeightTwoAutomorphicFormOfLevel (U1 r S) R) :
    ((U r S R Î± hÎ±) a).1 =
    âˆ‘á¶  (gáµ¢ : (D âŠ—[F] FiniteAdeleRing (ğ“ F) F)Ë£) (_ : gáµ¢ âˆˆ Quotient.out '' (U1diagU1 r S Î± hÎ±)),
      gáµ¢ â€¢ a.1 :=
  rfl

open AbstractHeckeOperator in
lemma U_apply_eq_finsum_unipotent_mul_diag_image (a : WeightTwoAutomorphicFormOfLevel (U1 r S) R) :
    ((U r S R Î± hÎ±) a).1 =
    âˆ‘á¶  (g : (D âŠ—[F] FiniteAdeleRing (ğ“ F) F)Ë£) (_ : g âˆˆ unipotent_mul_diag_image r Î± hÎ±),
      g â€¢ a.1 :=
  (eq_finsum_quotient_out_of_bijOn' a (bijOn_unipotent_mul_diagU1_U1diagU1 r S Î± hÎ±)) â–¸
    U_apply r S R Î± hÎ± a

lemma U_mul_aux {v : HeightOneSpectrum (ğ“ F)}
    {Î± Î² : v.adicCompletionIntegers F} (hÎ± : Î± â‰  0) (hÎ² : Î² â‰  0)
    (a : WeightTwoAutomorphicFormOfLevel (U1 r S) R) :
    âˆ‘á¶  (i : (adicCompletionIntegers F v) â§¸ Ideal.span {Î±})
      (j : (adicCompletionIntegers F v) â§¸ Ideal.span {Î²}),
      unipotent_mul_diag r Î± hÎ± i â€¢ unipotent_mul_diag r Î² hÎ² j â€¢ a.1 =
    âˆ‘á¶  (k : (adicCompletionIntegers F v) â§¸ Ideal.span {Î± * Î²}),
      unipotent_mul_diag r (Î± * Î²) (hÎ±.mul hÎ²) k â€¢ a.1 :=
  sorry

open AbstractHeckeOperator in
lemma U_mul {v : HeightOneSpectrum (ğ“ F)}
    {Î± Î² : v.adicCompletionIntegers F} (hÎ± : Î± â‰  0) (hÎ² : Î² â‰  0) :
    (U r S R Î± hÎ± âˆ˜â‚— U r S R Î² hÎ²) =
    U r S R (Î± * Î²) (hÎ±.mul hÎ²) := by
  ext1 a
  apply (Subtype.coe_inj).mp
  simp only [U_apply_eq_finsum_unipotent_mul_diag_image,
    LinearMap.coe_comp, Function.comp_apply,
    smul_finsum_mem (unipotent_mul_diag_image_finite r Î² hÎ²)]
  unfold unipotent_mul_diag_image
  simp only [finsum_mem_image (unipotent_mul_diag_inj _ _ _)]
  simpa using U_mul_aux r S R hÎ± hÎ² a

lemma U_comm {v : HeightOneSpectrum (ğ“ F)}
    {Î± Î² : v.adicCompletionIntegers F} (hÎ± : Î± â‰  0) (hÎ² : Î² â‰  0) :
    U r S R Î± hÎ± âˆ˜â‚— U r S R Î² hÎ² =
    U r S R Î² hÎ² âˆ˜â‚— U r S R Î± hÎ± := by
  rw [U_mul, U_mul]
  congr 1
  rw [mul_comm]

end U

end HeckeOperator

open HeckeOperator

/-- `HeckeAlgebra F D r S R` is the Hecke algebra associated to the weight 2
`R`-valued automorphic forms associated to the discriminant 1 totally definite
quaternion algebra `D` over the totally real field `F`, of level `Uâ‚(S)` where `S` is
a finite set of nonzero primes `v` of `ğ“ F`. To make sense of this definition we choose
a rigidification `r`, that is, a fixed `ğ”¸_F^âˆ`-linear
isomorphism `D âŠ—[F] ğ”¸_F^âˆ = Mâ‚‚(ğ”¸_F^âˆ)`, enabling us to define level structures and
Hecke operators `Táµ¥` and `Uáµ¥` using 2x2 matrices.

Details: `Uâ‚(S)` is the subgroup of `(D âŠ—[F] ğ”¸_F^âˆ)Ë£` associated, via `r`, to the
matrices which are in `GLâ‚‚(ğ“áµ¥)` for all `v âˆ‰ S` and which are of the form
`(a *; 0 a)` mod `v` for all `v âˆˆ S`. The Hecke algebra is defined to be the
sub-`R`-algebra of the weight 2 forms of level `Uâ‚(S)` generated by the following
two kinds of Hecke operators: first there are the operators
`Táµ¥` associated to the matrices `(Ï–áµ¥ 0; 0 1)` for `v âˆ‰ S` (here `Ï–áµ¥ âˆˆ ğ”¸_F^âˆ` is a local
uniformiser supported at `v`). Second, there are the Hecke operators `Uáµ¥,â‚`
for `v âˆˆ S` and `0 â‰  Î±áµ¥ âˆˆ ğ“áµ¥`, associated the matries `(Î±áµ¥ 0; 0 1)`.
These slightly nonstandard Hecke operators satisfy `Uáµ¥,â‚› * Uáµ¥,â‚œ = Uáµ¥,â‚›â‚œ`
and in particular this Hecke algebra is commutative (Hecke operators supported
at distinct primes commute because the decomposition of the double cosets
into single cosets can be done purely locally).
-/
def HeckeAlgebra : Type _ :=
  (Algebra.adjoin R ({T r R v | v âˆ‰ S} âˆª
  {Ï† | âˆƒ (v : HeightOneSpectrum (ğ“ F)) (_hv : v âˆˆ S)
         (Î± : v.adicCompletionIntegers F) (hÎ± : Î± â‰  0), Ï† = U r S R Î± hÎ±}) :
    Subalgebra R (WeightTwoAutomorphicFormOfLevel (U1 r S) R â†’â‚—[R]
      WeightTwoAutomorphicFormOfLevel (U1 r S) R))

namespace HeckeAlgebra

noncomputable instance instRing :
    Ring (HeckeAlgebra F D r S R) := inferInstanceAs <|
  Ring (Algebra.adjoin R _ : Subalgebra R (WeightTwoAutomorphicFormOfLevel (U1 r S) R â†’â‚—[R]
      WeightTwoAutomorphicFormOfLevel (U1 r S) R))

noncomputable instance instAlgebra :
    Algebra R (HeckeAlgebra F D r S R) := inferInstanceAs <|
  Algebra R (Algebra.adjoin R _ : Subalgebra R (WeightTwoAutomorphicFormOfLevel (U1 r S) R â†’â‚—[R]
      WeightTwoAutomorphicFormOfLevel (U1 r S) R))

noncomputable instance instCommRing :
    CommRing (HeckeAlgebra F D r S R) where
  __ := instRing F D r S R
  mul_comm := sorry -- #585 -- check on generators

variable {F S} in
/-- The Hecke operator Táµ¥ as an element of the Hecke algebra. -/
noncomputable def T (v : HeightOneSpectrum (ğ“ F)) (hv : v âˆ‰ S) : HeckeAlgebra F D r S R :=
  âŸ¨HeckeOperator.T r R v, by
    apply Algebra.subset_adjoin
    left
    use vâŸ©

variable {F S} in
/-- The Hecke operator Uáµ¥,â‚ as an element of the Hecke algebra. -/
noncomputable def U (v : HeightOneSpectrum (ğ“ F)) (hv : v âˆˆ S) (Î± : v.adicCompletionIntegers F)
    (hÎ± : Î± â‰  0) : HeckeAlgebra F D r S R :=
  âŸ¨HeckeOperator.U r S R Î± hÎ±, by
    apply Algebra.subset_adjoin
    right
    use v, hv, Î±, hÎ±âŸ©

end HeckeAlgebra

end TotallyDefiniteQuaternionAlgebra.WeightTwoAutomorphicForm
