/-
Copyright (c) 2025 Matthew Jasper. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matthew Jasper
-/

import FLT.Mathlib.Topology.Algebra.RestrictedProduct.TopologicalSpace
import FLT.Mathlib.Topology.Algebra.Module.ModuleTopology
import FLT.Mathlib.Topology.Algebra.MulAction
import FLT.Mathlib.Algebra.Module.Submodule.Basic

/-!
If `R : Œπ ‚Üí Type*` is a family of rings, `B : (i : Œπ) ‚Üí Subring (R i)` is a family of
subrings, `M : Œπ ‚Üí Type*` is a family of types, with `M i` having an `R i`-module structure
and `C : (i : Œπ) ‚Üí Submodule (B i) (M i)`, then `Œ† ≥ i, [M i, C i]_[ùìï]` has a
`Œ† ≥ i, [R i, B i]_[ùìï]`-module structure.

## Main definitions

- `RestrictedProduct.module'` is the module structure defined above.

- `RestrictedProduct.linearMap_component` is the component `M j ‚Üí‚Çó[R j] M' j` given by a linear
  map `Œ† ≥ i, [M i, C i]_[ùìï] ‚Üí‚Çó[Œ† ≥ i, [R i, B i]_[ùìï]] Œ† ≥ i, [M' i, C' i]_[ùìï]`.

- `ContinuousLinearEquiv.restrictedProductPi` is the continuous linear equivalence between
  `Œ† ≥ i, [(R i)^n, (B i)^n]` and `Œ† ≥ i, [(R i), (B i)]^n`.

# Main theorems

- `RestrictedProduct.isOpenMap_linearMap_of_surjective` : a condition for a linear map to be
  surjective based on properties of its components.

- Instances for `ContinuousSMul (Œ† ≥ i, [R i, B i]_[ùìü T]) (Œ† ≥ i, [M i, C i]_[ùìü T])` and
  `ContinuousSMul (Œ† ≥ i, [R i, B i]) (Œ† ≥ i, [M i, C i])`.

- `RestrictedProduct.isModuleTopology` : `Œ† ≥ i, [M i, C i]` has the `Œ† ≥ i, [R i, B i]`-module
  topology if `(M i)` have the `(R i)`-module topology and `(B i)` and `(C i)` are open.
-/

namespace RestrictedProduct

variable {Œπ : Type*}
variable {R : Œπ ‚Üí Type*} [Œ† i, Ring (R i)]
variable {S : Œπ ‚Üí Type*} -- subring type
variable [Œ† i, SetLike (S i) (R i)] [‚àÄ i, SubringClass (S i) (R i)]
variable {B : Œ† i, S i}
variable {‚Ñ± : Filter Œπ}

variable {M : Œπ ‚Üí Type*} [Œ† i, AddCommGroup (M i)] [Œ† i, Module (R i) (M i)]
variable {N : Œπ ‚Üí Type*} -- submodule type
variable [Œ† i, SetLike (N i) (M i)] [‚àÄ i, AddSubgroupClass (N i) (M i)]
variable [‚àÄ i, SMulMemClass (N i) (B i) (M i)]
variable {C : Œ† i, N i}

instance : SMul (Œ† ≥ i, [R i, B i]_[‚Ñ±]) (Œ† ≥ i, [M i, C i]_[‚Ñ±]) where
  smul r m := ‚ü®fun i ‚Ü¶ (r i) ‚Ä¢ (m i), by
    filter_upwards [r.prop, m.prop] with i hr hm using SMulMemClass.smul_mem ‚ü®r i, hr‚ü© hm‚ü©

omit [Œ† i, AddSubgroupClass (N i) (M i)] in
@[simp]
lemma smul_apply' (r : Œ† ≥ i, [R i, B i]_[‚Ñ±]) (m : Œ† ≥ i, [M i, C i]_[‚Ñ±]) (i : Œπ) :
    (r ‚Ä¢ m) i = r i ‚Ä¢ m i :=
  rfl

instance : Module (Œ† ≥ i, [R i, B i]_[‚Ñ±]) (Œ† ≥ i, [M i, C i]_[‚Ñ±]) where
  zero_smul m := by ext; simp
  smul_zero r := by ext; simp
  one_smul m := by ext; simp
  add_smul r s m:= by ext; simp [add_smul]
  smul_add r m n := by ext; simp
  mul_smul r s m := by ext; simp [mul_smul]

@[simp]
lemma single_smul [DecidableEq Œπ] (i : Œπ) (r : R i) (m : Œ† ≥ i, [M i, C i]) :
    single B i r ‚Ä¢ m = single C i (r ‚Ä¢ m i) := by
  ext j
  obtain (rfl | hi) := em (i = j)
  ¬∑ simp
  ¬∑ simp [single_eq_of_ne' _ _ hi]

@[simp]
lemma smul_single [DecidableEq Œπ] (i : Œπ) (r : Œ† ≥ i, [R i, B i]) (m : M i) :
    r ‚Ä¢ single C i m = single C i (r i ‚Ä¢ m) := by
  ext j
  obtain (rfl | hi) := em (i = j)
  ¬∑ simp
  ¬∑ simp [single_eq_of_ne' _ _ hi]

lemma single_eq_smul [DecidableEq Œπ] (i : Œπ) (m : Œ† ≥ i, [M i, C i]) :
    single B i 1 ‚Ä¢ m = single C i (m i) := by
  simp

variable {M‚ÇÇ : Œπ ‚Üí Type*} [Œ† i, AddCommGroup (M‚ÇÇ i)] [Œ† i, Module (R i) (M‚ÇÇ i)]
variable {N‚ÇÇ : Œπ ‚Üí Type*} -- submodule type
variable [Œ† i, SetLike (N‚ÇÇ i) (M‚ÇÇ i)] [‚àÄ i, AddSubgroupClass (N‚ÇÇ i) (M‚ÇÇ i)]
variable [‚àÄ i, SMulMemClass (N‚ÇÇ i) (B i) (M‚ÇÇ i)]
variable {C‚ÇÇ : Œ† i, N‚ÇÇ i}

section components

variable [DecidableEq Œπ]

/-- Components of a linear map. -/
noncomputable def linearMap_component
    (f : Œ† ≥ i, [M i, C i] ‚Üí‚Çó[Œ† ≥ i, [R i, B i]] Œ† ≥ i, [M‚ÇÇ i, C‚ÇÇ i]) (i : Œπ) : M i ‚Üí‚Çó[R i] M‚ÇÇ i where
  toFun x :=
    f (single C i x) i
  map_add' x y := by
    simp [‚Üê add_apply, ‚Üê map_add f]
  map_smul' r m := by
    let r' := single B i r
    have hr : r = r' i := by simp [r']
    rw [hr, ‚Üê smul_single, map_smul, smul_apply', RingHom.id_apply]

lemma linearMap_component_apply (f : Œ† ≥ i, [M i, C i] ‚Üí‚Çó[Œ† ≥ i, [R i, B i]] Œ† ≥ i, [M‚ÇÇ i, C‚ÇÇ i])
    (i : Œπ) (x : M i) : linearMap_component f i x = f (single C i x) i :=
  rfl

lemma linearMap_apply_eq_component (f : Œ† ≥ i, [M i, C i] ‚Üí‚Çó[Œ† ≥ i, [R i, B i]] Œ† ≥ i, [M‚ÇÇ i, C‚ÇÇ i])
    (x : Œ† ≥ i, [M i, C i]) (i : Œπ) : f x i = (linearMap_component f i) (x i):= by
  rw [linearMap_component_apply, ‚Üê single_eq_smul, map_smul, single_smul, single_eq_same, one_smul]

end components

variable [Œ† i, TopologicalSpace (R i)] [Œ† i, TopologicalSpace (M i)]

section continuous_smul

variable [‚àÄ i, ContinuousSMul (R i) (M i)] (T : Set Œπ)

open scoped Filter in
instance : ContinuousSMul (Œ† ≥ i, [R i, B i]_[ùìü T]) (Œ† ≥ i, [M i, C i]_[ùìü T]) :=
  isEmbedding_coe_of_principal.continuousSMul isEmbedding_coe_of_principal.continuous
    (fun {c x} ‚Ü¶ by ext; rfl)

variable [hBopen : Fact (‚àÄ i, IsOpen (B i : Set (R i)))]
variable [hCopen : Fact (‚àÄ i, IsOpen (C i : Set (M i)))]

instance [‚àÄ i, ContinuousSMul (R i) (M i)] :
    ContinuousSMul (Œ† ≥ i, [R i, B i]) (Œ† ≥ i, [M i, C i]) where
  continuous_smul := by
    rw [continuous_dom_prod hBopen.elim hCopen.elim]
    exact fun S hS ‚Ü¶ (continuous_inclusion hS).comp continuous_smul

end continuous_smul

section components

variable [Œ† i, TopologicalSpace (M‚ÇÇ i)]

omit [(i : Œπ) ‚Üí TopologicalSpace (R i)] in
theorem isOpenMap_linearMap_of_surjective [DecidableEq Œπ]
    (hCopen : ‚àÄ i, IsOpen (C i : Set (M i)))
    (hCopen‚ÇÇ : ‚àÄ i, IsOpen (C‚ÇÇ i : Set (M‚ÇÇ i)))
    (f : Œ† ≥ i, [M i, C i] ‚Üí‚Çó[Œ† ≥ i, [R i, B i]] Œ† ≥ i, [M‚ÇÇ i, C‚ÇÇ i])
    (hf : ‚àÄ i, IsOpenMap (linearMap_component f i))
    (hsurj : ‚àÄ·∂† i in Filter.cofinite, Set.SurjOn (linearMap_component f i) (C i) (C‚ÇÇ i)) :
    IsOpenMap f := by
  apply RestrictedProduct.isOpenMap_of_open_components hCopen hCopen‚ÇÇ f
    (fun i ‚Ü¶ linearMap_component f i) (linearMap_apply_eq_component f) hf hsurj

end components

section free_topology

variable (n : Type*) [Fintype n]

variable (B) in
/-- If `B i` is subring of `R i` then `(B i)^n` is a `B i`-submodule of `(R i)^n`. -/
def piSubringSubmodule (i : Œπ) : Submodule (B i) (n ‚Üí R i) :=
  Submodule.pi Set.univ fun (_ : n) ‚Ü¶ Subring.toSubmodule (Subring.ofClass (B i))

/-- Canonical linear equivalence between `Œ†' R^n` and `(Œ†' R)^n` -/
def _root_.LinearEquiv.restrictedProductPi
    : Œ† ≥ i, [n ‚Üí R i, piSubringSubmodule B n i]_[‚Ñ±] ‚âÉ‚Çó[Œ† ≥ i, [R i, B i]_[‚Ñ±]]
      n ‚Üí Œ† ≥ i, [R i, B i]_[‚Ñ±] where
  toFun x j := congrRight (fun i y ‚Ü¶ y j)
    (by
      filter_upwards with i r hr
      rw [piSubringSubmodule, Submodule.coe_pi, Set.mem_univ_pi] at hr
      exact hr j)
    x
  invFun x := ‚ü®fun i j ‚Ü¶ x j i, by simpa [piSubringSubmodule] using fun j ‚Ü¶ (x j).eventually‚ü©
  map_add' x y := rfl
  map_smul' x y := rfl

lemma isOpen_piSubringSubmodule (hOpen : ‚àÄ i, IsOpen (B i : Set (R i))) (i : Œπ) :
    IsOpen (SetLike.coe <| piSubringSubmodule B n i) := by
  rw [piSubringSubmodule, Submodule.coe_pi]
  apply isOpen_set_pi Set.finite_univ
  intro j _
  exact hOpen i

variable [‚àÄ i, IsTopologicalRing (R i)]

/-- Canonical continuous linear equivalence between `Œ†' R^n` and `(Œ†' R)^n` -/
def _root_.ContinuousLinearEquiv.restrictedProductPi (hOpen : ‚àÄ i, IsOpen (B i : Set (R i)))
    : Œ† ≥ i, [n ‚Üí R i, piSubringSubmodule B n i] ‚âÉL[Œ† ≥ i, [R i, B i]] n ‚Üí Œ† ≥ i, [R i, B i] where
  __ := LinearEquiv.restrictedProductPi n
  continuous_toFun := by
    apply continuous_pi
    intro i
    dsimp [LinearEquiv.restrictedProductPi]
    exact Continuous.restrictedProduct_congrRight _ (fun _ ‚Ü¶ continuous_apply i)
  continuous_invFun := by
    have := Fact.mk hOpen
    have := Fact.mk (isOpen_piSubringSubmodule n hOpen)
    exact IsModuleTopology.continuous_of_linearMap
      (LinearEquiv.restrictedProductPi n).symm.toLinearMap

lemma moduleToplogy_of_prod (hOpen : ‚àÄ i, IsOpen (B i : Set (R i))) :
    IsModuleTopology (Œ† ≥ i, [R i, B i]) (Œ† ≥ i, [n ‚Üí R i, piSubringSubmodule B n i]) :=
  have := Fact.mk hOpen
  IsModuleTopology.iso (ContinuousLinearEquiv.restrictedProductPi n hOpen).symm

end free_topology

variable [‚àÄ i, IsTopologicalRing (R i)]
variable [‚àÄ i, IsModuleTopology (R i) (M i)]
variable [Module.Finite (Œ† ≥ i, [R i, B i]) (Œ† ≥ i, [M i, C i])]

theorem isModuleTopology (hBOpen : ‚àÄ i, IsOpen (B i : Set (R i)))
    (hCOpen : ‚àÄ i, IsOpen (C i : Set (M i)))
    : IsModuleTopology (Œ† ≥ i, [R i, B i]) (Œ† ≥ i, [M i, C i]) := by
  have := Fact.mk hBOpen
  have := Fact.mk hCOpen
  obtain ‚ü®n, f, hf‚ü© := Module.Finite.exists_fin' (Œ† ≥ i, [R i, B i]) (Œ† ≥ i, [M i, C i])
  let g' := ContinuousLinearEquiv.restrictedProductPi (Fin n) hBOpen
  let g := f ‚àò‚Çó g'.toLinearMap
  have (i : Œπ) : ContinuousAdd (M i) := IsModuleTopology.toContinuousAdd (R i) (M i)
  have (i : Œπ) : ContinuousSMul (R i) (M i) := IsModuleTopology.toContinuousSMul (R i) (M i)
  have := moduleToplogy_of_prod (Fin n) hBOpen
  have hsurj : Function.Surjective g := hf.comp g'.surjective
  apply IsModuleTopology.of_isOpenMap_surjective _ g _ hsurj
  ¬∑ classical
    apply isOpenMap_linearMap_of_surjective (isOpen_piSubringSubmodule (Fin n) hBOpen) hCOpen
    ¬∑ intro i
      apply IsModuleTopology.isOpenMap_of_surjective
      exact surjective_components_of_surjective _ _ (linearMap_apply_eq_component g) hsurj _
        (Set.Finite.compl_mem_cofinite (Set.finite_singleton i))
    ¬∑ exact eventually_surjOn_of_surjective Filter.comap_id.symm g _
        (linearMap_apply_eq_component g) hsurj

end RestrictedProduct
