import Mathlib.Algebra.Algebra.Bilinear
import Mathlib.LinearAlgebra.Basis.VectorSpace
import Mathlib.LinearAlgebra.FreeModule.Finite.Basic
import Mathlib.Topology.Algebra.Module.ModuleTopology
import Mathlib.Topology.Algebra.Algebra.Equiv
import Mathlib.Topology.Algebra.Algebra.Equiv
import FLT.Mathlib.Algebra.Module.LinearMap.Defs
import FLT.Mathlib.Algebra.Algebra.Tower

theorem ModuleTopology.isModuleTopology (R : Type*) [TopologicalSpace R] (S : Type*) [Add S]
    [SMul R S] : @IsModuleTopology R _ S _ _ (moduleTopology R S) where
  __ := moduleTopology R S
  eq_moduleTopology' := rfl

namespace IsModuleTopology

open ModuleTopology

section semiring_bilinear

-- I need commutativity of R because we don't have bilinear maps for non-commutative rings.
-- **TODO** ask on the Zulip whether this is an issue.
variable {R : Type*} [œÑR : TopologicalSpace R] [CommSemiring R]

variable {A : Type*} [AddCommMonoid A] [Module R A] [aA : TopologicalSpace A] [IsModuleTopology R A]
variable {B : Type*} [AddCommMonoid B] [Module R B] [aB : TopologicalSpace B] [IsModuleTopology R B]
variable {C : Type*} [AddCommMonoid C] [Module R C] [aC : TopologicalSpace C] [IsModuleTopology R C]

-- R^n x B -> C bilinear is continuous for module topologies.
-- Didn't someone give a counterexample when not fg on MO?
-- This works for semirings
theorem Module.continuous_bilinear_of_pi_finite (Œπ : Type*) [Finite Œπ]
    (bil : (Œπ ‚Üí R) ‚Üí‚Çó[R] B ‚Üí‚Çó[R] C) : Continuous (fun ab ‚Ü¶ bil ab.1 ab.2 : ((Œπ ‚Üí R) √ó B ‚Üí C)) := by
  classical
  -- far too long proof that a bilinear map bil : R^n x B -> C
  -- equals the function sending (f,b) to ‚àë i, f(i)*bil(e·µ¢,b)
  have foo : (fun fb ‚Ü¶ bil fb.1 fb.2 : ((Œπ ‚Üí R) √ó B ‚Üí C)) =
      (fun fb ‚Ü¶ ‚àë·∂† i, ((fb.1 i) ‚Ä¢ (bil (Pi.single i 1) fb.2) : C)) := by
    ext ‚ü®f, b‚ü©
    simp_rw [‚Üê LinearMap.smul_apply]
    rw [‚Üê LinearMap.finsum_apply]
    congr
    simp_rw [LinearMap.smul_apply, ‚Üê LinearMap.map_smul]
    convert AddMonoidHom.map_finsum bil.toAddMonoidHom _
    ¬∑ ext j
      simp_rw [‚Üê Pi.single_smul, smul_eq_mul, mul_one]
      symm
      -- Is there a missing delaborator? No ‚àë·∂† notation
      change (‚àë·∂† (i : Œπ), Pi.single i (f i)) j = f j
      -- last tactic has no effect
      rw [finsum_apply (Set.toFinite _)]
      convert finsum_eq_single (fun i ‚Ü¶ Pi.single i (f i) j) j
        (by simp (config := {contextual := true})) using 1
      simp
    ¬∑ apply Set.toFinite _--(Function.support fun x ‚Ü¶ f x ‚Ä¢ Pi.single x 1)
  rw [foo]
  haveI : ContinuousAdd C := toContinuousAdd R C
  exact continuous_finsum (fun i ‚Ü¶ by fun_prop) (locallyFinite_of_finite _)

theorem Module.continuous_bilinear_of_finite_free [IsTopologicalSemiring R] [Module.Finite R A]
    [Module.Free R A] (bil : A ‚Üí‚Çó[R] B ‚Üí‚Çó[R] C) :
    Continuous (fun ab ‚Ü¶ bil ab.1 ab.2 : (A √ó B ‚Üí C)) := by
  let Œπ := Module.Free.ChooseBasisIndex R A
  let hŒπ : Fintype Œπ := Module.Free.ChooseBasisIndex.fintype R A
  let b : Basis Œπ R A := Module.Free.chooseBasis R A
  let elinear : A ‚âÉ‚Çó[R] (Œπ ‚Üí R) := b.equivFun
  let bil' : (Œπ ‚Üí R) ‚Üí‚Çó[R] B ‚Üí‚Çó[R] C := bil.comp elinear.symm.toLinearMap
  have := Module.continuous_bilinear_of_pi_finite Œπ bil'
  have foo : (fun ab ‚Ü¶ (bil ab.1) ab.2 : A √ó B ‚Üí C) = (fun fb ‚Ü¶ bil' fb.1 fb.2) ‚àò
    (fun ab ‚Ü¶ (elinear ab.1, ab.2) : A √ó B ‚Üí (Œπ ‚Üí R) √ó B) := by
    ext ‚ü®a, b‚ü©
    simp [bil']
  rw [foo]
  apply Continuous.comp this
  apply Continuous.prodMk
  ¬∑ exact continuous_of_linearMap (elinear.toLinearMap ‚àò‚Çó (LinearMap.fst R A B))
  ¬∑ fun_prop

end semiring_bilinear

section ring_bilinear

variable {R : Type*} [œÑR : TopologicalSpace R] [CommRing R] [IsTopologicalRing R]

variable {A : Type*} [AddCommGroup A] [Module R A] [aA : TopologicalSpace A] [IsModuleTopology R A]
variable {B : Type*} [AddCommGroup B] [Module R B] [aB : TopologicalSpace B] [IsModuleTopology R B]
variable {C : Type*} [AddCommGroup C] [Module R C] [aC : TopologicalSpace C] [IsModuleTopology R C]

-- This needs rings
theorem Module.continuous_bilinear_of_finite [Module.Finite R A]
    (bil : A ‚Üí‚Çó[R] B ‚Üí‚Çó[R] C) : Continuous (fun ab ‚Ü¶ bil ab.1 ab.2 : (A √ó B ‚Üí C)) := by
  obtain ‚ü®m, f, hf‚ü© := Module.Finite.exists_fin' R A
  let bil' : (Fin m ‚Üí R) ‚Üí‚Çó[R] B ‚Üí‚Çó[R] C := bil.comp f
  let œÜ := f.prodMap (LinearMap.id : B ‚Üí‚Çó[R] B)
  have foo : Function.Surjective (LinearMap.id : B ‚Üí‚Çó[R] B) :=
    Function.RightInverse.surjective (congrFun rfl)
  have hœÜ : Function.Surjective œÜ := Function.Surjective.prodMap hf foo
  have := (isQuotientMap_of_surjective hœÜ).2
  rw [this, continuous_def]
  intro U hU
  rw [isOpen_coinduced, ‚Üê Set.preimage_comp]
  suffices Continuous ((fun ab ‚Ü¶ (bil ab.1) ab.2) ‚àò œÜ : (Fin m ‚Üí R) √ó B ‚Üí C) by
    rw [continuous_def] at this
    convert this _ hU
  rw [show (fun ab ‚Ü¶ (bil ab.1) ab.2 : A √ó B ‚Üí C) ‚àò œÜ = (fun fb ‚Ü¶ bil' fb.1 fb.2) by
    ext ‚ü®a, b‚ü©
    simp [bil', œÜ]]
  apply Module.continuous_bilinear_of_finite_free

end ring_bilinear

section semiring_algebra

open scoped TensorProduct

-- these shouldn't be rings, they should be semirings
variable (R) [CommRing R] [TopologicalSpace R] [IsTopologicalRing R]
variable (D : Type*) [Ring D] [Algebra R D] [Module.Finite R D]
variable [TopologicalSpace D] [IsModuleTopology R D]

open scoped TensorProduct

@[continuity, fun_prop]
theorem continuous_mul'
    (R : Type*) [CommRing R] [TopologicalSpace R] [IsTopologicalRing R]
    (D : Type*) [Ring D] [Algebra R D] [Module.Finite R D] [TopologicalSpace D]
    [IsModuleTopology R D] : Continuous (fun ab ‚Ü¶ ab.1 * ab.2 : D √ó D ‚Üí D) :=
  Module.continuous_bilinear_of_finite (LinearMap.mul R D)

include R in
lemma topologicalSemiring : IsTopologicalSemiring D where
  continuous_add := (toContinuousAdd R D).1
  continuous_mul := continuous_mul' R D

end semiring_algebra

section ring_algebra

-- confusion about whether these are rings or semirings should ideally be resolved
-- Is it: for D finite free R can be a semiring but for D finite it has to be a ring?
variable (R) [CommRing R] [TopologicalSpace R] [IsTopologicalRing R]
variable (D : Type*) [Ring D] [Algebra R D] [Module.Finite R D]
variable [TopologicalSpace D] [IsModuleTopology R D]

open scoped TensorProduct

include R in
@[continuity, fun_prop]
theorem continuous_mul : Continuous (fun ab ‚Ü¶ ab.1 * ab.2 : D √ó D ‚Üí D) := by
  letI : TopologicalSpace (D ‚äó[R] D) := moduleTopology R _
  haveI : IsModuleTopology R (D ‚äó[R] D) := { eq_moduleTopology' := rfl }
  convert Module.continuous_bilinear_of_finite <| (LinearMap.mul R D : D ‚Üí‚Çó[R] D ‚Üí‚Çó[R] D)

include R in
lemma Module.topologicalRing : IsTopologicalRing D where
  continuous_add := (toContinuousAdd R D).1
  continuous_mul := continuous_mul R D
  continuous_neg := continuous_neg R D

end ring_algebra

-- two other results (not needed for FLT but would be
-- independently interesting in the theory)
section trans

variable (R S M : Type*)
  [CommRing R] [TopologicalSpace R] [IsTopologicalRing R]
  [CommRing S] [TopologicalSpace S] [IsTopologicalRing S]
    [Algebra R S] [Module.Finite R S] [IsModuleTopology R S]
  [AddCommGroup M]
    [Module R M]
    [Module S M]
      [IsScalarTower R S M]

example : moduleTopology R M = moduleTopology S M := by
  sorry

/-

Proof: First, it suffices to show that if M has the R-module topology
œÑRM then it's a topological S-module, and that if M has the S-module
topology œÑSM then it's a topological R-module. This is because the former
claim shows œÑSM ‚â§ œÑRM and the latter shows œÑRM ‚â§ œÑSM.

If M has the S-module topology then it's clearly a topological R-module,
because it's a topological S-module so (+ : M √ó M ‚Üí M) is continuous
and (‚Ä¢ : S √ó M ‚Üí M) are continuous, and the map R ‚Üí S is continuous
because it's R-linear and S has the R-module topology, so
R √ó M ‚Üí S √ó M is continuous and thus (‚Ä¢ : R √ó M ‚Üí M) is continuous.

The converse is more subtle and it's here where we need some finiteness
assumptions. If M has the R-module topology then certainly (+ : M √ó M ‚Üí M)
is continuous, so it all rests on showing that (‚Ä¢ : S √ó M ‚Üí M) is
continuous. But everything here is an R-module and ‚Ä¢ is R-bilinear,
and thus if either S or M are module-finite over R the result is
automatic.
-/

-- maybe
end trans

section opensubring
variable (R S : Type*)
  [CommRing R] [TopologicalSpace R] [IsTopologicalRing R]
  [CommRing S] [TopologicalSpace S] [IsTopologicalRing S]
    [Algebra R S]

-- Proved this thinking I could use it to prove `IsModuleTopology K_‚àû L_‚àû`,
-- which application failed, but may as well keep this proof
open scoped Topology in
/-- An `R`-algebra `S` has the `R`-module topology if the embedding `R ‚Üí+* S` is continuous
and open. -/
theorem of_continuous_isOpenMap_algebraMap (hcont : Continuous (algebraMap R S))
    (hopen : IsOpenMap (algebraMap R S)) : IsModuleTopology R S where
  eq_moduleTopology' := by
    -- Let `œÑS` denote the topology on `S`, `œÑRS` denote the `R`-module topology on `S`,
    -- `œÑR` denote the topology on `R`.. This proof consists of pushing fowards and pulling
    -- back open sets between three topological spaces as follows:
    -- ```
    -- (S, œÑRS) <-[hcont_id]- (S, œÑS)
    --       |                 ‚Üó
    -- [hcont_alg]      [hopen]
    --       |          /
    --       |        /
    --       |   [hcont]
    --       ‚Üì   ‚Üô
    --     (R, œÑR)
    -- ```
    -- where the arrows indicate the direction in which open sets are moved, `hopen` and `hcont`
    -- are given hypotheses, and `hcont_id` and `hcont_alg` are the continuity of the identity map
    -- and the algebra map respectively, which are proved below.
    -- ‚Ä¢ : R √ó S ‚Üí S is continuous
    have : ContinuousSMul R S := continuousSMul_of_algebraMap R S hcont
    -- The identity map `(S, œÑRS) ‚Üí (S, œÑS)` is continuous, by minimality of module topology.
    have hcont_id : Continuous[moduleTopology R S, _] id :=
      continuous_id_iff_le.2 <| moduleTopology_le _ _
    -- The algebra map `(R, œÑR) ‚Üí‚Çó[R] (S, œÑRS)` from `R` is continuous, since `œÑR` is the
    -- `R`-module topology on `R`, and any `R`-linear map on this domain is continuous.
    have hcont_alg : Continuous[_, moduleTopology R S] (Algebra.linearMap R S) :=
      -- Give `S` the `R`-module topology
      letI := moduleTopology R S
      letI : ContinuousAdd S := ModuleTopology.continuousAdd _ _
      letI : ContinuousSMul R S := ModuleTopology.continuousSMul _ _
      IsModuleTopology.continuous_of_linearMap _
    -- If `U` is open in `(S, œÑS)`, then it is open in `(S, œÑRS)` by pullback along [hcont_id].
    have hopen_mpr {U : Set S} (h : IsOpen U) : IsOpen[moduleTopology R S] U :=
      @Continuous.isOpen_preimage S S (moduleTopology R S) _ id hcont_id U h
    -- If `U` is open in `(S, œÑRS)` and is contained in the image of `R` inside `S`, then it is
    -- open in `(S, œÑS)`, by pullback along [hcont_alg] and push forward along [hopen].
    have hopen_mp {U : Set S} (h : IsOpen[moduleTopology R S] U)
        (hUS : U ‚äÜ Set.range (algebraMap R S)) : IsOpen U :=
      Set.image_preimage_eq_of_subset hUS ‚ñ∏ hopen _ <|
        @Continuous.isOpen_preimage R S _ (moduleTopology R S) _ hcont_alg U h
    -- To finish the proof, we now show that the neighbourhoods of zero in `œÑS` and `œÑ_R_S` coincide
    rw [IsTopologicalRing.to_topologicalAddGroup.ext_iff <|
      -- `(S, œÑRS)` is a topological add group
      @IsModuleTopology.topologicalAddGroup R _ _ S _ _ (moduleTopology R S) (isModuleTopology R S)]
    -- It is enough to show that the basis of neighbourhoods of zero are contained within each other
    apply (nhds_basis_opens 0).ext (@nhds_basis_opens S (moduleTopology R S) 0)
    ¬∑ -- Assume `U` is open in `(S, œÑS)`, then it is open in `(S, œÑRS)` by `hopen_mpr` above.
      exact fun U hU => ‚ü®U, ‚ü®‚ü®hU.1, hopen_mpr hU.2‚ü©, by simp‚ü©‚ü©
    ¬∑ -- Assume `U` is open in `(S, œÑRS)`
      intro U hU
      -- Intersect `U` with the image of `R` in `(S, œÑRS)`.
      refine ‚ü®Set.range (algebraMap R S) ‚à© U, ‚ü®‚ü®‚ü®‚ü®0, by simp‚ü©, hU.1‚ü©, ?_‚ü©, by simp‚ü©‚ü©
      -- `Set.range (algebraMap R S)` is open in `(S, œÑS)` by hopen, so too in `(S, œÑRS)`
      -- by hopen_mpr.
      let hopen_range := hopen_mpr hopen.isOpen_range
      -- Therefore `U ‚à© Set.range (algebraMap R S)` is open in `(S, œÑRS)`, so too in `(S, œÑS)`
      -- by hopen_mp.
      exact hopen_mp (@IsOpen.inter _ (moduleTopology R S) _ _ hopen_range hU.2) (by simp)

/-
Proof.

First note that `S` is a topological ring so addition and multiplication
on `S` are continuous. Futhermore the hypothesis `Contiuous (algebraMap R S)`
shows that ‚Ä¢ : R √ó S ‚Üí S is continuous, so S is a topological R-module.
In particular the identity map (S,R-module top) -> (S, given top) is continuous.

The algebra map from R to (S,R-module top) is R-linear
and hence also continuous. Furthermore, the composite is open
and I claim that the two topologies on S thus "look the same near 0".
More precisely, the image of R is open in S with the given topology
and hence also with the module topology (by continuity of the identity map above),
and if U ‚äÜ S is a subset of the image of R then we claim that it's open for
the given topology iff it's open for the module topology. Firstly,
continuity of the identity
map shows that if U is open for the given topology it's open for the module
topology. Secondly, if U is open for the module topology then its preimage
in R is open for R's topology, and then the image of this in S is open for
the given topology, and this is U again as U is a subset of the image of R.

-/
end opensubring

/-

Consequence: if one defines the finite adeles of a number field K
as K ‚äó[‚Ñ§] ‚Ñ§-hat and gives it the ‚Ñ§-hat-module topology,
this gives the right answer. Proof: algebraically we have ùî∏_K^f=ùî∏_‚Ñö^f ‚äó[‚Ñö] K
and ùî∏_‚Ñö^f=‚Ñ§hat ‚äó[‚Ñ§] ‚Ñö, so certainly ùî∏_K^f=K ‚äó[‚Ñ§] ‚Ñ§hat algebraically.
It thus suffices to show that the topologies agree. Writing R for the integers
of K we have K = K ‚äó[R] R so ùî∏_K^f = ‚Ñ§hat ‚äó[‚Ñ§] R ‚äó[R] K = Rhat ‚äó[R] K
and because Rhat is open in K with its usual topology this shows that ùî∏_K^f
has the Rhat-module topology by one of the above results. And Rhat=Zhat ‚äó[‚Ñ§] R
is finite over ‚Ñ§hat so we're done if we can check that Rhat with its usual
topology is the ‚Ñ§hat topology and this should be fine, it's finite and free
over a complete thing so I don't think there can be any other possibility
(the argument is weak here)
-/

/-- Given a linear isomorphism between two topological modules with the module topology,
upgrades it to a continuous linear isomorphism using the fact that linear maps between modules
with the module topology are automatically continuous. -/
@[simps!]
def continuousLinearEquiv {A B R : Type*} [TopologicalSpace A]
    [TopologicalSpace B] [TopologicalSpace R] [Semiring R] [AddCommMonoid A] [AddCommMonoid B]
    [Module R A] [Module R B] [IsModuleTopology R A] [IsModuleTopology R B]
    (e : A ‚âÉ‚Çó[R] B) :
    A ‚âÉL[R] B where
  toFun := e
  __ := e
  continuous_toFun :=
    letI := IsModuleTopology.toContinuousAdd
    IsModuleTopology.continuous_of_linearMap e.toLinearMap
  continuous_invFun :=
    letI := IsModuleTopology.toContinuousAdd
    IsModuleTopology.continuous_of_linearMap e.symm.toLinearMap

/--
Given the following
```
e : A <‚Äì‚Äì‚Äì‚Äì‚Äì‚Äì‚Äì‚Äì‚Äì> B
     \     /\    /
      \   /  \  /
       \ /    \/
        S‚ÇÅ    S‚ÇÇ
         \   /
          \ /
           R
```
where `A` and `B` are both `S‚ÇÅ` and `S‚ÇÇ`-algebras, `S‚ÇÅ` and `S‚ÇÇ` are algebras
over a common base ring `R`, and `A` and `B` both have the `S‚ÇÅ`-module topology. If the algebras
form scalar towers and the algebra map from  `S‚ÇÅ` to `B` factors through `e`, and if `A` and `B`
are equivalent as `S‚ÇÇ`-algebras, then they are topologically equivalent as `S‚ÇÇ`-algebras as well
(even though they do not necessarily have the `S‚ÇÇ`-module topologies).

In application this is used for a situation where we have
```
v.Completion    L
         \    /
          \  /
           K
```
for an infinite place `v` of a number field `K`. We have an `L`-algebra equivalence
`L ‚äó[K] v.Completion ‚âÉ‚Çê[L] Œ† (w : v.Extension L), wv.1.Completion`
between `v.Completion`-module topological spaces. And so this allows us to assert that this
is a _continuous_ `L`-algebra equivalence as well.
-/
def continuousAlgEquivOfIsScalarTower {A B : Type*} (R S‚ÇÅ : Type*) {S‚ÇÇ : Type*} [TopologicalSpace A]
    [CommRing S‚ÇÅ] [CommRing S‚ÇÇ] [TopologicalSpace B] [CommRing R] [CommRing A] [CommRing B]
    [Algebra S‚ÇÅ A] [Algebra S‚ÇÅ B] [Algebra S‚ÇÇ A] [Algebra S‚ÇÇ B] [IsTopologicalSemiring B]
    [IsTopologicalSemiring A] [TopologicalSpace S‚ÇÅ] [Algebra R A] [Algebra R B]
    [IsModuleTopology S‚ÇÅ A] [IsModuleTopology S‚ÇÅ B] [Algebra R S‚ÇÅ] [IsScalarTower R S‚ÇÅ A]
    [Algebra R S‚ÇÇ] [IsScalarTower R S‚ÇÇ A] [IsScalarTower R S‚ÇÇ B] (e : A ‚âÉ‚Çê[S‚ÇÇ] B)
    (he : ‚àÄ s, e (algebraMap S‚ÇÅ A s) = algebraMap S‚ÇÅ B s) :
    A ‚âÉA[S‚ÇÇ] B where
  toAlgEquiv := e
  continuous_toFun := by
    -- switch the equivalence scalars of `e` from `S‚ÇÇ` over to `S‚ÇÅ`
    show Continuous (e.changeScalars R S‚ÇÅ he).toLinearEquiv
    -- then this is an `S‚ÇÅ`-linear map on the `S‚ÇÅ`-module topology, so is continuous
    exact IsModuleTopology.continuous_of_linearMap _
  continuous_invFun := by
    show Continuous (e.changeScalars R S‚ÇÅ he).toLinearEquiv.symm
    exact IsModuleTopology.continuous_of_linearMap _

@[simp]
theorem continuousAlgEquivOsIfScalarTower_apply {A B : Type*} (R S‚ÇÅ : Type*) {S‚ÇÇ : Type*}
    [TopologicalSpace A] [CommRing S‚ÇÅ] [CommRing S‚ÇÇ] [TopologicalSpace B] [CommRing R] [CommRing A]
    [CommRing B] [Algebra S‚ÇÅ A] [Algebra S‚ÇÅ B] [Algebra S‚ÇÇ A] [Algebra S‚ÇÇ B]
    [IsTopologicalSemiring B] [IsTopologicalSemiring A] [TopologicalSpace S‚ÇÅ] [Algebra R A]
    [Algebra R B] [IsModuleTopology S‚ÇÅ A] [IsModuleTopology S‚ÇÅ B] [Algebra R S‚ÇÅ]
    [IsScalarTower R S‚ÇÅ A] [Algebra R S‚ÇÇ] [IsScalarTower R S‚ÇÇ A] [IsScalarTower R S‚ÇÇ B]
    (e : A ‚âÉ‚Çê[S‚ÇÇ] B) (he: ‚àÄ s, e (algebraMap S‚ÇÅ A s) = algebraMap S‚ÇÅ B s) (a : A) :
    continuousAlgEquivOfIsScalarTower R S‚ÇÅ e he a = e a :=
  rfl

/-- An algebra isomorphism between two topological algebras over `R` with the
`R`-module topology is automatically an algebra homeomorphism. -/
def continuousAlgEquivOfAlgEquiv {A B R : Type*} [TopologicalSpace A]
    [TopologicalSpace B] [TopologicalSpace R] [CommSemiring R] [Semiring A] [Semiring B]
    [Algebra R A] [Algebra R B] [IsModuleTopology R A] [IsModuleTopology R B]
    (e : A ‚âÉ‚Çê[R] B) :
    A ‚âÉA[R] B where
  __ := e
  continuous_toFun :=
    letI := IsModuleTopology.toContinuousAdd
    IsModuleTopology.continuous_of_linearMap e.toLinearMap
  continuous_invFun :=
    letI := IsModuleTopology.toContinuousAdd
    IsModuleTopology.continuous_of_linearMap e.symm.toLinearMap

/-- A free module with the module topology over a `T2Space` ring is a `T2Space`.
-/
theorem t2Space {R M : Type*} [CommRing R] [AddCommGroup M] [Module R M] [Module.Free R M]
    [TopologicalSpace R] [TopologicalSpace M] [T2Space R]
    [ContinuousAdd R] [ContinuousMul R] [IsModuleTopology R M]
    : T2Space M := by
  have := IsModuleTopology.topologicalAddGroup R M
  rw [IsTopologicalAddGroup.t2Space_iff_zero_closed]
  let f := Module.Free.repr R M |>.toLinearMap
  let g : (Module.Free.ChooseBasisIndex R M ‚Üí‚ÇÄ R) ‚Üí‚Çó[R] (Module.Free.ChooseBasisIndex R M ‚Üí R) := {
    __ := Finsupp.coeFnAddHom
    map_smul' _ _ := rfl
  }
  suffices hpre : (g.comp f) ‚Åª¬π' {0} = {0}  by
    rw [‚Üê hpre]
    apply IsClosed.preimage <| IsModuleTopology.continuous_of_linearMap (g.comp f)
    exact isClosed_singleton
  ext x
  simp [map_eq_zero_iff g DFunLike.coe_injective,
    map_eq_zero_iff f (Module.Free.repr R M).injective]

/-- A vector space with the module topology over a `T2Space` ring is a `T2Space`.
-/
theorem t2Space' {K V : Type*} [Field K] [AddCommGroup V] [Module K V]
    [TopologicalSpace K] [TopologicalSpace V] [T2Space K]
    [ContinuousAdd K] [ContinuousMul K] [mt : IsModuleTopology K V]
    : T2Space V := by
  apply t2Space (R := K)
