import Mathlib.LinearAlgebra.Matrix.Transvection
/-!

Variant of `Matrix.diagonal_transvection_induction_of_det_ne_zero`
that works over a commutative ring. Note that we have to *assume*
that the matrix is a product of transvections and a diagonal matrix;
this is not true in general (I believe; I think there's some obstruction
in SKâ‚(R), the kernel of det:Kâ‚(R) â†’ RË£)

-/

open Matrix TransvectionStruct

namespace Matrix

/-- The claim that a matrix is a product of transvections, a diagonal matrix, and more
transvections. Always true if the base ring is a field. -/
def Pivot.ExistsListTransvecMulDiagonalMulListTransvec {n : Type*} {R : Type*} [CommRing R]
    [Fintype n] [DecidableEq n] (M : Matrix n n R) : Prop :=
  âˆƒ (L L' : List (TransvectionStruct n R)) (D : n â†’ R),
      M = (L.map toMatrix).prod * diagonal D * (L'.map toMatrix).prod

lemma Pivot.exists_list_transvec_mul_diagonal_mul_list_transvec' {n : Type*} {ğ•œ : Type*} [Field ğ•œ]
    [DecidableEq n] [Fintype n] (M : Matrix n n ğ•œ) :
    Pivot.ExistsListTransvecMulDiagonalMulListTransvec M :=
  Pivot.exists_list_transvec_mul_diagonal_mul_list_transvec M

section TransvectionStruct_dot_map

variable {n : Type*} {R S : Type*} [CommRing R] [CommRing S]
    [DecidableEq n] (f : R â†’+* S)

/-- Base change of a TransvectionStruct along a ring homomorphism. -/
def TransvectionStruct.map {n : Type*} {R S : Type*} [CommRing R] [CommRing S]
    (f : R â†’+* S) (t : TransvectionStruct n R) :
    TransvectionStruct n S where
  i := t.i
  j := t.j
  hij := t.hij
  c := f t.c

lemma TransvectionStruct.toMatrix_map [Fintype n] (t : TransvectionStruct n R) :
    (toMatrix âˆ˜ TransvectionStruct.map f) t = f.mapMatrix t.toMatrix := by
  simp [map, transvection, toMatrix, map_add]

@[simp]
lemma TransvectionStruct.map_toMatrix [Finite n] (t : TransvectionStruct n R) :
    (TransvectionStruct.map f t).toMatrix = t.toMatrix.map f :=
  let := Fintype.ofFinite n
  t.toMatrix_map f

lemma TransvectionStruct.mapMatrix_map_toMatrix_prod [Fintype n]
    (L : List (TransvectionStruct n R)) :
    f.mapMatrix (List.map toMatrix L).prod =
    (List.map (toMatrix âˆ˜ TransvectionStruct.map f) L).prod := by
  rw [map_list_prod]
  congr 1
  simp

@[simp]
lemma TransvectionStruct.mapMatrix_map_toMatrix_prod' [Fintype n]
    (L : List (TransvectionStruct n R)) :
    (List.map toMatrix L).prod.map â‡‘f =
    (List.map (toMatrix âˆ˜ TransvectionStruct.map f) L).prod :=
  TransvectionStruct.mapMatrix_map_toMatrix_prod f L

end TransvectionStruct_dot_map

lemma Pivot.baseChange_existsListTransvecEtc {n : Type*} {R : Type*} [CommRing R]
    [Fintype n] [DecidableEq n] (M : Matrix n n R)
    (hM : Pivot.ExistsListTransvecMulDiagonalMulListTransvec M)
    (S : Type*) [CommRing S] (f : R â†’+* S) :
    Pivot.ExistsListTransvecMulDiagonalMulListTransvec (f.mapMatrix M) := by
  obtain âŸ¨L, L', D, rflâŸ© := hM
  exact âŸ¨L.map (TransvectionStruct.map f), L'.map (TransvectionStruct.map f),
    fun i â†¦ f (D i), by simpâŸ©

open Pivot

variable {n : Type*} {R : Type*} [CommRing R] [DecidableEq n] [Fintype n]

-- this proof is literally cut and pasted from mathlib
/-- Variant of `Matrix.diagonal_transvection_induction` for commutative rings
under the extra assumption `ExistsListTransvecMulDiagonalMulListTransvec M`
(which is automatic for matrices over fields). -/
theorem diagonal_transvection_induction' (P : Matrix n n R â†’ Prop) (M : Matrix n n R)
    (hM : ExistsListTransvecMulDiagonalMulListTransvec M)
    (hdiag : âˆ€ D : n â†’ R, det (diagonal D) = det M â†’ P (diagonal D))
    (htransvec : âˆ€ t : TransvectionStruct n R, P t.toMatrix) (hmul : âˆ€ A B, P A â†’ P B â†’ P (A * B)) :
    P M := by
  rcases hM with âŸ¨L, L', D, hâŸ©
  have PD : P (diagonal D) := hdiag D (by simp [h])
  suffices H :
    âˆ€ (Lâ‚ Lâ‚‚ : List (TransvectionStruct n R)) (E : Matrix n n R),
      P E â†’ P ((Lâ‚.map toMatrix).prod * E * (Lâ‚‚.map toMatrix).prod) by
    rw [h]
    apply H L L'
    exact PD
  intro Lâ‚ Lâ‚‚ E PE
  induction Lâ‚ with
  | nil =>
    simp only [Matrix.one_mul, List.prod_nil, List.map]
    induction Lâ‚‚ generalizing E with
    | nil => simpa
    | cons t Lâ‚‚ IH =>
      simp only [â† Matrix.mul_assoc, List.prod_cons, List.map]
      apply IH
      exact hmul _ _ PE (htransvec _)
  | cons t Lâ‚ IH =>
    simp only [Matrix.mul_assoc, List.prod_cons, List.map] at IH âŠ¢
    exact hmul _ _ (htransvec _) IH

-- this proof is literally cut and pasted from mathlib with `â‰  0` changed to `IsUnit`
-- and ğ•œ changed to R and addition of `(hM : ExistsListTransvecMulDiagonalMulListTransvec M)`
theorem diagonal_transvection_induction_of_det_unit (P : Matrix n n R â†’ Prop) (M : Matrix n n R)
    (hM : ExistsListTransvecMulDiagonalMulListTransvec M)
    (hMdet : IsUnit (det M)) (hdiag : âˆ€ D : n â†’ R, IsUnit (det (diagonal D)) â†’ P (diagonal D))
    (htransvec : âˆ€ t : TransvectionStruct n R, P t.toMatrix)
    (hmul : âˆ€ A B, IsUnit (det A) â†’ IsUnit (det B) â†’ P A â†’ P B â†’ P (A * B)) : P M := by
  let Q : Matrix n n R â†’ Prop := fun N => IsUnit (det N) âˆ§ P N
  have : Q M := by
    apply diagonal_transvection_induction' Q M hM
    Â· grind
    Â· intro t
      exact âŸ¨by simp, htransvec tâŸ©
    Â· intro A B QA QB
      exact âŸ¨by simp [QA.1, QB.1], hmul A B QA.1 QB.1 QA.2 QB.2âŸ©
  exact this.2

end Matrix
