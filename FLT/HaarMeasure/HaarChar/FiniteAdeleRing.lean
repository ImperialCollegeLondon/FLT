import FLT.DedekindDomain.FiniteAdeleRing.BaseChange
import FLT.DedekindDomain.FiniteAdeleRing.TensorProduct
import FLT.HaarMeasure.HaarChar.FiniteDimensional
import FLT.Mathlib.Algebra.Central.TensorProduct
import FLT.Mathlib.MeasureTheory.Constructions.BorelSpace.AdicCompletion
import FLT.Mathlib.NumberTheory.NumberField.FiniteAdeleRing
import FLT.Mathlib.MeasureTheory.Constructions.BorelSpace.FiniteAdeleRing

/-!

# A result related to the Haar character of the finite adele ring of a number field

We prove the crucial result that left and right multiplication by an element of `D âŠ—[K] ğ”¸_K^f`
scale Haar measure by the same factor, if D is a finite-dimensional central simple `K`-alegbra.

The proof looks simple on paper. We know the analogous result for finite-dimensional
central simple algebras over local fields, but the proof essentially uses fields.
I had thought that the adelic case would follow easily from this and from the fact
that the haar character of a restricted product of maps was the product of the haar
characters, but `D âŠ—[K] ğ”¸_K^f` is not a restricted product, and furthermore making it
a restricted product is not completely formal because the compact open subrings ğ“áµ¥
used in the restricted product are not `K`-modules. One could either choose a lattice
in D and work with that, or choose a basis of D and reduce first to to (ğ”¸_K^f)^n
and then to `Î Ê³[Káµ¥^n,ğ“áµ¥^n]`, which is what we do.
-/

open NumberField

open scoped TensorProduct

variable (K : Type*) [Field K] [NumberField K]

variable (B : Type*) [Ring B] [Algebra K B] [FiniteDimensional K B]

open MeasureTheory IsDedekindDomain HeightOneSpectrum RestrictedProduct

-- this horrible instance causes timeouts and we don't need it because we're never
-- changing number field here.
attribute [-instance] instIsScalarTowerFiniteAdeleRing_fLT_1

/-- We give ğ”¸_K^f âŠ— B the ğ”¸_K^f-module topology in this file (it's the only sensible topology). -/
local instance : TopologicalSpace (FiniteAdeleRing (ğ“ K) K âŠ—[K] B) :=
  moduleTopology (FiniteAdeleRing (ğ“ K) K) _

local instance : IsModuleTopology (FiniteAdeleRing (ğ“ K) K) (FiniteAdeleRing (ğ“ K) K âŠ—[K] B) :=
  âŸ¨rflâŸ©

local instance : IsTopologicalRing (FiniteAdeleRing (ğ“ K) K âŠ—[K] B) :=
  IsModuleTopology.isTopologicalRing (FiniteAdeleRing (ğ“ K) K) _

local instance : LocallyCompactSpace (FiniteAdeleRing (ğ“ K) K âŠ—[K] B) :=
  IsModuleTopology.locallyCompactSpaceOfFinite (FiniteAdeleRing (ğ“ K) K)

/-- We put the Borel measurable space structure on ğ”¸_K^f âŠ— B (because it's the only
sensible one). -/
local instance : MeasurableSpace ((FiniteAdeleRing (ğ“ K) K) âŠ—[K] B) := borel _

local instance : BorelSpace ((FiniteAdeleRing (ğ“ K) K) âŠ—[K] B) := âŸ¨rflâŸ©

section moving_from_tensor_B_to_Pi

/-- If `Ï† : ğ”¸_K^f âŠ—[K] B â‰ƒ ğ”¸_K^f âŠ—[K] B` is continuous and ğ”¸_K^f-linear then `f Ï†` is the
associated continuous linear isomorphism `(ğ”¸_K^f)^n â‰ƒ (ğ”¸_K^f)^n` coming from the "canonical"
K-basis of B. -/
noncomputable def FiniteAdeleRing.Aux.f
    (Ï† : ((FiniteAdeleRing (ğ“ K) K) âŠ—[K] B) â‰ƒL[FiniteAdeleRing (ğ“ K) K]
      (FiniteAdeleRing (ğ“ K) K) âŠ—[K] B) :
    (Module.Free.ChooseBasisIndex K B â†’ (FiniteAdeleRing (ğ“ K) K)) â‰ƒL[FiniteAdeleRing (ğ“ K) K]
    (Module.Free.ChooseBasisIndex K B â†’ (FiniteAdeleRing (ğ“ K) K)) := by
  let bâ‚€ := Module.Free.chooseBasis K B
  let b := Module.Basis.baseChange (FiniteAdeleRing (ğ“ K) K) bâ‚€
  refine (ContinuousLinearEquiv.chooseBasis_piScalarRight' K
    (FiniteAdeleRing (ğ“ K) K) B).symm.trans ?_
  refine Ï†.trans ?_
  exact (ContinuousLinearEquiv.chooseBasis_piScalarRight' K (FiniteAdeleRing (ğ“ K) K) B)

lemma FiniteAdeleRing.Aux.f_commSq
    (Ï† : ((FiniteAdeleRing (ğ“ K) K) âŠ—[K] B) â‰ƒL[FiniteAdeleRing (ğ“ K) K]
      (FiniteAdeleRing (ğ“ K) K) âŠ—[K] B) :
    addEquivAddHaarChar (Ï†.toContinuousAddEquiv) =
    addEquivAddHaarChar (FiniteAdeleRing.Aux.f K B Ï†).toContinuousAddEquiv := by
  refine MeasureTheory.addEquivAddHaarChar_eq_addEquivAddHaarChar_of_continuousAddEquiv
    (ContinuousLinearEquiv.chooseBasis_piScalarRight' K
      (FiniteAdeleRing (ğ“ K) K) B).toContinuousAddEquiv _ _ ?_
  intro x
  let g := (ContinuousLinearEquiv.chooseBasis_piScalarRight' K (FiniteAdeleRing (ğ“ K) K) B)
  change g (Ï† x) = g (Ï† (g.symm (g x)))
  simp

end moving_from_tensor_B_to_Pi

local instance {Î¹ : Type*} [Fintype Î¹] :
    Fact (âˆ€ (v : HeightOneSpectrum (ğ“ K)), IsOpen
      (â†‘(AddSubgroup.pi (Set.univ : Set Î¹)
      (fun _ â†¦ (v.adicCompletionIntegers K).toAddSubgroup)) :
    Set (Î¹ â†’ v.adicCompletion K))) := âŸ¨fun _ â†¦
  isOpen_set_pi Set.finite_univ (fun _ _ â†¦ isOpenAdicCompletionIntegers K _)âŸ©

local instance :
    Fact (âˆ€ (v : HeightOneSpectrum (ğ“ K)), IsOpen
      (â†‘(v.adicCompletionIntegers K).toAddSubgroup :
    Set (v.adicCompletion K))) :=
  âŸ¨isOpenAdicCompletionIntegers KâŸ©

local instance (v : HeightOneSpectrum (ğ“ K)) :
    CompactSpace (AddSubgroup.pi (Set.univ : Set (Module.Free.ChooseBasisIndex K B))
      fun x â†¦ (adicCompletionIntegers K v).toAddSubgroup) := by
  change CompactSpace (Set.pi Set.univ fun x â†¦ _)
  rw [â† isCompact_iff_compactSpace]
  refine isCompact_univ_pi (fun i â†¦ ?_)
  change IsCompact (v.adicCompletionIntegers K : Set (v.adicCompletion K))
  sorry -- "integers are compact"

variable {Î¹ : Type*} [Fintype Î¹] in
local instance : LocallyCompactSpace
    Î Ê³ (v : HeightOneSpectrum (ğ“ K)), [Î¹ â†’ adicCompletion K v,
      (â†‘(AddSubgroup.pi (Set.univ : Set Î¹) fun x â†¦ (adicCompletionIntegers K v).toAddSubgroup) :
      Set ((Î¹ â†’ adicCompletion K v)))] := by
  refine RestrictedProduct.locallyCompactSpace_of_addGroup _ ?_
  filter_upwards
  intro v
  refine isCompact_univ_pi (fun i â†¦ ?_)
  change IsCompact (v.adicCompletionIntegers K : Set (v.adicCompletion K))
  sorry -- "integers are compact"

local instance : LocallyCompactSpace
    Î Ê³ (v : HeightOneSpectrum (ğ“ K)), [adicCompletion K v,
      ((adicCompletionIntegers K v).toAddSubgroup : Set (adicCompletion K v))] := by
  refine RestrictedProduct.locallyCompactSpace_of_addGroup _ ?_
  filter_upwards
  intro v
  change IsCompact (v.adicCompletionIntegers K : Set (v.adicCompletion K))
  sorry -- "integers are compact"

local instance : SecondCountableTopology Î Ê³ (v : HeightOneSpectrum (ğ“ K)),
    [v.adicCompletion K, v.adicCompletionIntegers K] := inferInstanceAs <|
  SecondCountableTopology (FiniteAdeleRing (ğ“ K) K)

section moving_from_pi_restrictedproduct_to_restrictedproduct_pi

/-- If `Ïˆ : (ğ”¸_K^f)^n â‰ƒ (ğ”¸_K^f)^n` is continuous and ğ”¸_K^f-linear then `g Ï†` is the
associated continuous additive isomorphism `Î Ê³[Káµ¥^n, ğ“áµ¥^n] â†’ Î Ê³[Káµ¥^n,ğ“áµ¥^n]`.
-/
noncomputable def FiniteAdeleRing.Aux.g {Î¹ : Type*} [Fintype Î¹]
    (Ïˆ : (Î¹ â†’ (FiniteAdeleRing (ğ“ K) K)) â‰ƒL[FiniteAdeleRing (ğ“ K) K]
      (Î¹ â†’ (FiniteAdeleRing (ğ“ K) K))) :
    Î Ê³ (v : HeightOneSpectrum (ğ“ K)), [Î¹ â†’ v.adicCompletion K,
      (AddSubgroup.pi (Set.univ : Set Î¹) (fun _ â†¦ (v.adicCompletionIntegers K).toAddSubgroup))] â‰ƒâ‚œ+
    Î Ê³ (v : HeightOneSpectrum (ğ“ K)), [Î¹ â†’ v.adicCompletion K,
      (AddSubgroup.pi (Set.univ : Set Î¹) (fun _ â†¦ (v.adicCompletionIntegers K).toAddSubgroup))] :=
  letI f := ContinuousAddEquiv.restrictedProductPi
    (C := fun (_ : Î¹) (v : HeightOneSpectrum (ğ“ K)) â†¦ (v.adicCompletionIntegers K).toAddSubgroup)
    (fun _ v â†¦ isOpenAdicCompletionIntegers K v)
  f.trans (Ïˆ.toContinuousAddEquiv.trans f.symm)

lemma FiniteAdeleRing.Aux.g_commSq {Î¹ : Type*} [Fintype Î¹]
    (Ïˆ : (Î¹ â†’ (FiniteAdeleRing (ğ“ K) K)) â‰ƒL[FiniteAdeleRing (ğ“ K) K]
      (Î¹ â†’ (FiniteAdeleRing (ğ“ K) K))) :
    addEquivAddHaarChar (Ïˆ.toContinuousAddEquiv) =
    addEquivAddHaarChar (FiniteAdeleRing.Aux.g K Ïˆ) := by
  symm
  let f := (ContinuousAddEquiv.restrictedProductPi
    (C := fun (i : Î¹) (v : HeightOneSpectrum (ğ“ K)) â†¦
      (v.adicCompletionIntegers K).toAddSubgroup) (fun _ â†¦ isOpenAdicCompletionIntegers K))
  refine MeasureTheory.addEquivAddHaarChar_eq_addEquivAddHaarChar_of_continuousAddEquiv f _ _ ?_
  intro x
  change f (f.symm (Ïˆ (f x))) = Ïˆ (f x)
  simp at f -- why??
  simp

end moving_from_pi_restrictedproduct_to_restrictedproduct_pi

local instance (v : HeightOneSpectrum (ğ“ K)) : TopologicalSpace (adicCompletion K v âŠ—[K] B) :=
  moduleTopology (adicCompletion K v) _

local instance (v : HeightOneSpectrum (ğ“ K)) :
    IsModuleTopology (adicCompletion K v) (adicCompletion K v âŠ—[K] B) :=
  âŸ¨rflâŸ©

local instance (v : HeightOneSpectrum (ğ“ K)) :
    IsTopologicalAddGroup (adicCompletion K v âŠ—[K] B) :=
  IsModuleTopology.topologicalAddGroup (adicCompletion K v) _

local instance (v : HeightOneSpectrum (ğ“ K)) :
    IsTopologicalRing (adicCompletion K v âŠ—[K] B) :=
  IsModuleTopology.isTopologicalRing (adicCompletion K v) _

/-- If `Ï† : Káµ¥ âŠ—[K] B â‰ƒ Káµ¥ âŠ—[K] B` is continuous and additive then `f Ï†` is the
associated continuous additive isomorphism `Káµ¥^n â‰ƒ Káµ¥^n` coming from the "canonical"
K-basis of B. -/
noncomputable def FiniteAdeleRing.Aux.e (v : HeightOneSpectrum (ğ“ K))
    (Î± : v.adicCompletion K âŠ—[K] B â‰ƒL[K] v.adicCompletion K âŠ—[K] B) :
    (Module.Free.ChooseBasisIndex K B â†’ (v.adicCompletion K)) â‰ƒâ‚œ+
    (Module.Free.ChooseBasisIndex K B â†’ (v.adicCompletion K)) := by
  let bâ‚€ := Module.Free.chooseBasis K B
  let b := Module.Basis.baseChange (v.adicCompletion K) bâ‚€
  let Î² := (ContinuousLinearEquiv.chooseBasis_piScalarRight' K
    (v.adicCompletion K) B).toContinuousAddEquiv
  refine Î².symm.trans ?_
  refine Î±.toContinuousAddEquiv.trans ?_
  exact Î²

local instance (v : HeightOneSpectrum (ğ“ K)) :
  MeasurableSpace (adicCompletion K v âŠ—[K] B) := borel _

local instance (v : HeightOneSpectrum (ğ“ K)) :
  BorelSpace (adicCompletion K v âŠ—[K] B) := âŸ¨rflâŸ©

local instance (v : HeightOneSpectrum (ğ“ K)) :
    LocallyCompactSpace (adicCompletion K v âŠ—[K] B) :=
  IsModuleTopology.locallyCompactSpaceOfFinite (adicCompletion K v)

lemma FiniteAdeleRing.Aux.e_commSq (v : HeightOneSpectrum (ğ“ K))
    (Î± : v.adicCompletion K âŠ—[K] B â‰ƒL[K] v.adicCompletion K âŠ—[K] B) :
    addEquivAddHaarChar (Î±.toContinuousAddEquiv) =
    addEquivAddHaarChar (FiniteAdeleRing.Aux.e K B v Î±) := by
  refine MeasureTheory.addEquivAddHaarChar_eq_addEquivAddHaarChar_of_continuousAddEquiv
    (ContinuousLinearEquiv.chooseBasis_piScalarRight' K
      (v.adicCompletion K) B).toContinuousAddEquiv _ _ ?_
  intro x
  let g := (ContinuousLinearEquiv.chooseBasis_piScalarRight' K (v.adicCompletion K) B)
  change g (Î± x) = g (Î± (g.symm (g x)))
  simp

open FiniteAdeleRing.Aux

/-- Needed in some argument below; there will never be a constructive one in this
generality so no harm in making it classical. -/
noncomputable local instance : DecidableEq (HeightOneSpectrum (ğ“ K)) := Classical.decEq _

-- A (continuous) ğ”¸_K^f-linear automorphism of ğ”¸_K^f âŠ— B is "integral" at all but
-- finitely many places
lemma FiniteAdeleRing.Aux.almost_always_integral
    (Ï† : FiniteAdeleRing (ğ“ K) K âŠ—[K] B â‰ƒL[FiniteAdeleRing (ğ“ K) K]
    FiniteAdeleRing (ğ“ K) K âŠ—[K] B) :
    let Î¹ := Module.Free.ChooseBasisIndex K B
    âˆ€á¶  (i : HeightOneSpectrum (ğ“ K)) in Filter.cofinite,
      Set.BijOn â‡‘((fun v â†¦ e K B v
        (FiniteAdeleRing.TensorProduct.localcomponentEquiv (ğ“ K) K B v Ï†)) i)
      â†‘(AddSubgroup.pi (Set.univ : Set Î¹) fun x â†¦ (adicCompletionIntegers K i).toAddSubgroup)
      â†‘(AddSubgroup.pi (Set.univ : Set Î¹) fun x â†¦ (adicCompletionIntegers K i).toAddSubgroup) :=
  sorry -- this needs some thought

/-- A diagram which obviously commutes, commutes. -/
lemma FiniteAdeleRing.Aux.f_g_local_global
    (Ï† : ((FiniteAdeleRing (ğ“ K) K) âŠ—[K] B) â‰ƒL[FiniteAdeleRing (ğ“ K) K]
      (FiniteAdeleRing (ğ“ K) K) âŠ—[K] B) :
    g K (f K B Ï†) = ContinuousAddEquiv.restrictedProductCongrRight
    (fun v â†¦ e _ _ _ (FiniteAdeleRing.TensorProduct.localcomponentEquiv (ğ“ K) K B v Ï†))
    (FiniteAdeleRing.Aux.almost_always_integral _ _ _) := by
  sorry -- this is hopefully close to being true by ext but I didn't think about it.

lemma localcomponent_mulLeft (u : ((FiniteAdeleRing (ğ“ K) K) âŠ—[K] B)Ë£)
    (v : HeightOneSpectrum (ğ“ K)) :
    (FiniteAdeleRing.TensorProduct.localcomponentEquiv (ğ“ K) K B v
    (ContinuousLinearEquiv.mulLeft (FiniteAdeleRing (ğ“ K) K) u)).toContinuousAddEquiv =
    (ContinuousAddEquiv.mulLeft (u.map (Algebra.TensorProduct.rTensor B
      (IsDedekindDomain.FiniteAdeleRing.evalContinuousAlgebraMap
        (ğ“ K) K v).toAlgHom).toMonoidHom)) := by
  ext u
  -- should hopefully follow from localcomponent_eval
  sorry

lemma localcomponent_mulRight (u : ((FiniteAdeleRing (ğ“ K) K) âŠ—[K] B)Ë£)
    (v : HeightOneSpectrum (ğ“ K)) :
    (FiniteAdeleRing.TensorProduct.localcomponentEquiv (ğ“ K) K B v
    (ContinuousLinearEquiv.mulRight (FiniteAdeleRing (ğ“ K) K) u)).toContinuousAddEquiv =
    (ContinuousAddEquiv.mulRight (u.map (Algebra.TensorProduct.rTensor B
      (IsDedekindDomain.FiniteAdeleRing.evalContinuousAlgebraMap
        (ğ“ K) K v).toAlgHom).toMonoidHom)) := by
  ext u
  -- should hopefully follow from localcomponent_eval
  sorry

/-- left multiplication and right multiplication by a unit have the same Haar character
on `ğ”¸_K^f âŠ— B`. See also
`NumberField.FiniteAdeleRing.isCentralSimple_addHaarScalarFactor_left_mul_eq_right_mul`
which proves it for `B âŠ— ğ”¸_K^f`.
-/
lemma NumberField.FiniteAdeleRing.tensor_isCentralSimple_addHaarScalarFactor_left_mul_eq_right_mul
    [IsSimpleRing B] [Algebra.IsCentral K B] (u : ((FiniteAdeleRing (ğ“ K) K) âŠ—[K] B)Ë£) :
    addEquivAddHaarChar (ContinuousAddEquiv.mulLeft u) =
    addEquivAddHaarChar (ContinuousAddEquiv.mulRight u) := by
  change addEquivAddHaarChar
      (ContinuousLinearEquiv.mulLeft ((FiniteAdeleRing (ğ“ K) K)) u).toContinuousAddEquiv =
    addEquivAddHaarChar
      (ContinuousLinearEquiv.mulRight ((FiniteAdeleRing (ğ“ K) K)) u).toContinuousAddEquiv
  rw [FiniteAdeleRing.Aux.f_commSq, FiniteAdeleRing.Aux.f_commSq]
  rw [FiniteAdeleRing.Aux.g_commSq, FiniteAdeleRing.Aux.g_commSq]
  rw [FiniteAdeleRing.Aux.f_g_local_global, FiniteAdeleRing.Aux.f_g_local_global]
  rw [addEquivAddHaarChar_restrictedProductCongrRight,
    addEquivAddHaarChar_restrictedProductCongrRight]
  congr
  ext v
  rw [â† FiniteAdeleRing.Aux.e_commSq, â† FiniteAdeleRing.Aux.e_commSq]
  rw [localcomponent_mulLeft, localcomponent_mulRight]
  congr 1
  let w : (adicCompletion K v âŠ—[K] B)Ë£ := ((Units.map (Algebra.TensorProduct.rTensor B
    (FiniteAdeleRing.evalContinuousAlgebraMap (ğ“ K) K v).toAlgHom).toMonoidHom) u)
  exact IsSimpleRing.ringHaarChar_eq_addEquivAddHaarChar_mulRight (F := v.adicCompletion K) w

/-!

We've proved the result for ğ”¸ âŠ— B, we now deduce it for B âŠ— ğ”¸

-/
open scoped TensorProduct.RightActions in
instance (k A B : Type*) [Field k] [Field A] [Ring B]
    [Algebra k A] [Algebra k B]
    [Algebra.IsCentral k B] :
    Algebra.IsCentral A (B âŠ—[k] A) :=
  Algebra.IsCentral.of_algEquiv _ _ _ {
    __ := (Algebra.TensorProduct.comm k A B)
    commutes' := by simp }

open scoped TensorProduct.RightActions in
/-- B âŠ— ğ”¸_K^f â‰ƒ ğ”¸_K^f âŠ— B as ğ”¸_K^f-modules. -/
noncomputable def FiniteAdeleRing.TensorProduct.commLinearMap :
    (B âŠ—[K] (FiniteAdeleRing (ğ“ K) K)) â‰ƒâ‚—[FiniteAdeleRing (ğ“ K) K]
    (FiniteAdeleRing (ğ“ K) K) âŠ—[K] B := {
  __ := TensorProduct.comm K B (FiniteAdeleRing (ğ“ K) K)
  map_smul' m x := by simp
  }

open scoped TensorProduct.RightActions in
/-- B âŠ— ğ”¸_K^f â‰ƒ ğ”¸_K^f âŠ— B as topological additive groups. -/
noncomputable def FiniteAdeleRing.TensorProduct.commContinuousAddMonoidHom :
    (B âŠ—[K] (FiniteAdeleRing (ğ“ K) K)) â‰ƒâ‚œ+
    (FiniteAdeleRing (ğ“ K) K) âŠ—[K] B := {
  __ := FiniteAdeleRing.TensorProduct.commLinearMap K B
  continuous_toFun := IsModuleTopology.continuous_of_linearMap _
  continuous_invFun := IsModuleTopology.continuous_of_linearMap
    (FiniteAdeleRing.TensorProduct.commLinearMap K B).symm.toLinearMap
  }

open IsDedekindDomain HeightOneSpectrum RestrictedProduct in
open scoped TensorProduct.RightActions in
variable
  [MeasurableSpace (B âŠ—[K] (FiniteAdeleRing (ğ“ K) K))]
  [BorelSpace (B âŠ—[K] (FiniteAdeleRing (ğ“ K) K))] in
/-- left multiplication and right multiplication by a unit have the same Haar character
on `B âŠ— ğ”¸_K^f`. See also
`NumberField.FiniteAdeleRing.tensor_isCentralSimple_addHaarScalarFactor_left_mul_eq_right_mul`
which proves it for `ğ”¸_K^f âŠ— B`.
-/
lemma NumberField.FiniteAdeleRing.isCentralSimple_addHaarScalarFactor_left_mul_eq_right_mul
    [IsSimpleRing B] [Algebra.IsCentral K B] (u : (B âŠ—[K] (FiniteAdeleRing (ğ“ K) K))Ë£) :
    addEquivAddHaarChar (ContinuousAddEquiv.mulLeft u) =
    addEquivAddHaarChar (ContinuousAddEquiv.mulRight u) := by
  let v : ((FiniteAdeleRing (ğ“ K) K) âŠ—[K] B)Ë£:=
    u.map (Algebra.TensorProduct.comm K B (FiniteAdeleRing (ğ“ K) K))
  have := MeasureTheory.addEquivAddHaarChar_eq_addEquivAddHaarChar_of_continuousAddEquiv
      (FiniteAdeleRing.TensorProduct.commContinuousAddMonoidHom K B)
      (ContinuousAddEquiv.mulLeft u)
      (ContinuousAddEquiv.mulLeft v) <| fun _ â†¦
    map_mul (Algebra.TensorProduct.comm K B (FiniteAdeleRing (ğ“ K) K)) _ _
  rw [this]
  have := MeasureTheory.addEquivAddHaarChar_eq_addEquivAddHaarChar_of_continuousAddEquiv
      (FiniteAdeleRing.TensorProduct.commContinuousAddMonoidHom K B)
      (ContinuousAddEquiv.mulRight u)
      (ContinuousAddEquiv.mulRight v) <| fun _ â†¦
    map_mul (Algebra.TensorProduct.comm K B (FiniteAdeleRing (ğ“ K) K)) _ _
  rw [this]
  apply NumberField.FiniteAdeleRing.tensor_isCentralSimple_addHaarScalarFactor_left_mul_eq_right_mul
