import FLT.HaarMeasure.HaarChar.Ring
import FLT.Mathlib.MeasureTheory.Constructions.BorelSpace.AdicCompletion
import FLT.Mathlib.NumberTheory.NumberField.AdeleRing
import FLT.NumberField.AdeleRing
import FLT.HaarMeasure.HaarChar.RealComplex
import FLT.HaarMeasure.HaarChar.Padic
import Mathlib.NumberTheory.NumberField.ProductFormula
import FLT.Mathlib.LinearAlgebra.Lattice
/-!

# Global units are in the determinant of the adelic Haar character

If `K` is a number field and `B` is a finite-dimensional `K`-algebra
then `B âŠ— ğ”¸_K` is a locally compact topological ring, so it admits
a Haar character `(B âŠ— ğ”¸_K)Ë£ â†’ â„>0`. In this file we show
that the global units `BË£` are in the kernel of this character.

-/

open NumberField

open scoped TensorProduct

variable (K L : Type*) [Field K] [Field L] [Algebra K L] [NumberField K] [NumberField L]

open scoped NumberField.AdeleRing -- for ğ”¸ K notation

variable (V : Type*) [AddCommGroup V] [Module L V] [Module K V] [IsScalarTower K L V]
  [FiniteDimensional L V] [FiniteDimensional K V] -- the latter can be proved but
  -- can't be an instance as it uses L

variable (B : Type*) [Ring B] [Algebra K B] [FiniteDimensional K B]

open scoped TensorProduct

open NumberField MeasureTheory

open scoped TensorProduct.RightActions in
variable
  [MeasurableSpace (B âŠ—[K] ğ”¸ K)]
  [BorelSpace (B âŠ—[K] ğ”¸ K)] in
lemma NumberField.AdeleRing.isCentralSimple_addHaarScalarFactor_left_mul_eq_right_mul
    [IsSimpleRing B] [Algebra.IsCentral K B] (u : (B âŠ—[K] (ğ”¸ K))Ë£) :
    addEquivAddHaarChar (ContinuousAddEquiv.mulLeft u) =
    addEquivAddHaarChar (ContinuousAddEquiv.mulRight u) := by
  sorry

lemma MeasureTheory.ringHaarChar_adeles_rat (x : (ğ”¸ â„š)Ë£) :
  ringHaarChar x = ringHaarChar (MulEquiv.prodUnits x).1 *
    (âˆá¶  p, ringHaarChar (MulEquiv.restrictedProductUnits (MulEquiv.prodUnits x).2 p)) := by
  unfold AdeleRing
  rw [ringHaarChar_prod' x]
  congr
  have := Fact.mk <| NumberField.isOpenAdicCompletionIntegers â„š
  have := NumberField.instCompactSpaceAdicCompletionIntegers â„š
  convert addEquivAddHaarChar_restrictedProductCongrRight
    (C := fun p â†¦ (p.adicCompletionIntegers â„š).toAddSubgroup)
    (fun p â†¦
      (ContinuousAddEquiv.mulLeft (MulEquiv.restrictedProductUnits (MulEquiv.prodUnits x).2 p))) _
  exact (MulEquiv.restrictedProductUnits (MulEquiv.prodUnits x).2).2.mono
    (fun p hp â†¦ Equiv.bijOn' _
      (fun x hx â†¦ Subring.mul_mem _ ((Submonoid.mem_units_iff _ _).mp hp).1 hx)
      (fun x hx â†¦ Subring.mul_mem _ ((Submonoid.mem_units_iff _ _).mp hp).2 hx))

-- depends on `IsDedekindDomain.HeightOneSpectrum.padicEquiv`, from pending mathlib PR #30576
lemma padicEquiv_norm_eq (v : IsDedekindDomain.HeightOneSpectrum (ğ“ â„š)) (x : v.adicCompletion â„š) :
    â€–(Rat.HeightOneSpectrum.adicCompletion.padicEquiv v) xâ€– = â€–xâ€– := by
  sorry

lemma MeasureTheory.ringHaarChar_adeles_units_rat_eq_one (x : â„šË£) :
  ringHaarChar (Units.map (algebraMap â„š (ğ”¸ â„š)) x : (ğ”¸ â„š)Ë£) = 1 := by
  rw [ringHaarChar_adeles_rat (Units.map (algebraMap â„š (ğ”¸ â„š)) x : (ğ”¸ â„š)Ë£)]
  ext; simp only [NNReal.coe_mul, NNReal.coe_one]
  rw [â† NumberField.prod_abs_eq_one (K := â„š) (x := x) (Units.ne_zero x)]; congr
  Â· -- infinite place
    simp only [InfiniteAdeleRing, ringHaarChar_pi', NNReal.coe_prod, Rat.infinitePlace_apply,
      Rat.cast_abs]
    congr; ext v; rw [Subsingleton.elim v Rat.infinitePlace]
    let : Algebra â„¤ Rat.infinitePlace.Completion := Ring.toIntAlgebra _
    simp [InfinitePlace.mult, Rat.isReal_infinitePlace,
      ringHaarChar_eq_ringHaarChar_of_continuousAlgEquiv {
        __ := Rat.infinitePlace_completion_continuousAlgEquiv
        commutes' := by simp},
      ringHaarChar_real, â† Rat.infinitePlace_completion_continuousAlgEquiv_apply_algebraMap,
      -eq_ratCast]
    rfl
  Â· -- finite places
    rw [â† finprod_comp_equiv FinitePlace.equivHeightOneSpectrum.symm]
    conv_lhs =>
      apply NNReal.toRealHom.map_finprod_of_injective (injective_of_le_imp_le _ fun {x y} a â†¦ a)
    apply finprod_congr; intro p
    let : Algebra â„¤ (p.adicCompletion â„š) := Ring.toIntAlgebra _
    simp [FinitePlace.equivHeightOneSpectrum,
      ringHaarChar_eq_ringHaarChar_of_continuousAlgEquiv {
        __ := (Rat.HeightOneSpectrum.adicCompletion.padicEquiv p)
        commutes' := by simp},
      padicEquiv_norm_eq]
    rfl

-- TODO: need TensorProduct.RightActions.LinearEquiv.baseChange
open scoped TensorProduct.RightActions in
/-- The continuous A-linear map (A a topological ring, tensor products have the module
topology) A âŠ—[R] M â‰ƒ A âŠ—[R] N associated to an abstract R-linear isomorphism M â‰ƒ N. -/
noncomputable def ContinuousLinearEquiv.baseChange (R : Type*) [CommRing R]
    (A : Type*) [CommRing A] [Algebra R A] [TopologicalSpace A]
    (M N : Type*) [AddCommGroup M] [Module R M] [AddCommGroup N] [Module R N]
    [Module.Finite R M] [Module.Finite R N]
    (Ï† : M â‰ƒâ‚—[R] N) : (M âŠ—[R] A) â‰ƒL[A] (N âŠ—[R] A) where
  __ := TensorProduct.RightActions.LinearEquiv.baseChange _ _ _ _ Ï†
  continuous_toFun := IsModuleTopology.continuous_of_linearMap _
  continuous_invFun := IsModuleTopology.continuous_of_linearMap _

@[simp]
lemma ContinuousLinearEquiv.baseChange_apply (R : Type*) [CommRing R]
    (A : Type*) [CommRing A] [Algebra R A] [TopologicalSpace A]
    (M N : Type*) [AddCommGroup M] [Module R M] [AddCommGroup N] [Module R N]
    [Module.Finite R M] [Module.Finite R N]
    (Ï† : M â‰ƒâ‚—[R] N) (m : M) (a : A) :
    ContinuousLinearEquiv.baseChange R A M N Ï† (m âŠ—â‚œ a) = (Ï† m) âŠ—â‚œ a := rfl

open scoped TensorProduct.RightActions in
lemma MeasureTheory.addHaarScalarFactor_tensor_adeles_rat_eq_one [Module â„š V]
    [FiniteDimensional â„š V] (Ï† : V â‰ƒâ‚—[â„š] V)
    [MeasurableSpace (V âŠ—[â„š] ğ”¸ â„š)] [BorelSpace (V âŠ—[â„š] ğ”¸ â„š)] :
    addEquivAddHaarChar
      (ContinuousLinearEquiv.baseChange â„š (ğ”¸ â„š) V V Ï†).toContinuousAddEquiv = 1 := by
  --let foo : (IntegralLattice â„¤ â„š V) âŠ—[â„¤]
  sorry

open scoped TensorProduct.RightActions in
lemma MeasureTheory.addHaarScalarFactor_tensor_adeles_eq_one (Ï† : V â‰ƒâ‚—[K] V)
    [MeasurableSpace (V âŠ—[K] ğ”¸ K)] [BorelSpace (V âŠ—[K] ğ”¸ K)] :
    addEquivAddHaarChar
      (ContinuousLinearEquiv.baseChange K (ğ”¸ K) V V Ï†).toContinuousAddEquiv = 1 := by
  -- we deduce this from the corresponding statement for `K = â„š`.
  -- A K-module is a â„š-module
  let : Module â„š V := Module.compHom V (algebraMap â„š K)
  have : Module.Finite â„š V := FiniteDimensional.trans â„š K V
  let : Module (AdeleRing (ğ“ â„š) â„š) (V âŠ—[K] AdeleRing (ğ“ K) K) :=
    Module.compHom _ (algebraMap (AdeleRing (ğ“ â„š) â„š) (AdeleRing (ğ“ K) K))
  have : IsScalarTower (AdeleRing (ğ“ â„š) â„š) (AdeleRing (ğ“ K) K) (V âŠ—[K] AdeleRing (ğ“ K) K) :=
    IsScalarTower.of_algebraMap_smul fun r â†¦ congrFun rfl
  -- and V âŠ—[K] ğ”¸_K â‰ƒ V âŠ—[â„š] ğ”¸_â„š
  let f := NumberField.AdeleRing.ModuleBaseChangeContinuousAddEquiv â„š K V
  borelize (V âŠ—[â„š] AdeleRing (ğ“ â„š) â„š)
  have Ï†â„š : V â‰ƒâ‚—[â„š] V := by exact Function.invFun (fun a â†¦ Ï†) Ï†
  -- and the obvious diagram commutes
  have := MeasureTheory.addEquivAddHaarChar_eq_addEquivAddHaarChar_of_continuousAddEquiv f
    (ContinuousLinearEquiv.baseChange â„š (ğ”¸ â„š) V V (Ï†.restrictScalars â„š)).toContinuousAddEquiv
    (ContinuousLinearEquiv.baseChange K (ğ”¸ K) V V Ï†).toContinuousAddEquiv
  rw [â† this]
  -- so the result follows from the case K=â„š
  Â· apply MeasureTheory.addHaarScalarFactor_tensor_adeles_rat_eq_one
  Â· intro x
    induction x with
    | zero => simp
    | tmul x y => rfl
    | add x y hx hy => simp [hx, hy]

open scoped TensorProduct.RightActions in
/-- Left multiplication by an element of BË£ on B âŠ— ğ”¸_K does not scale additive
Haar measure. In other words, BË£ is in the kernel of the `ringHaarChar` of `B âŠ— ğ”¸_K`.
-/
lemma NumberField.AdeleRing.units_mem_ringHaarCharacter_ker
    [MeasurableSpace (B âŠ—[K] AdeleRing (ğ“ K) K)] [BorelSpace (B âŠ—[K] AdeleRing (ğ“ K) K)]
    (b : BË£) :
    (Units.map Algebra.TensorProduct.includeLeftRingHom.toMonoidHom b :
      (B âŠ—[K] AdeleRing (ğ“ K) K)Ë£) âˆˆ
    ringHaarChar_ker (B âŠ—[K] AdeleRing (ğ“ K) K) := by
  rw [mem_ringHaarChar_ker, ringHaarChar_apply]
  convert MeasureTheory.addHaarScalarFactor_tensor_adeles_eq_one K B (LinearEquiv.mulLeft K b)
  ext c
  change _ = (ContinuousLinearEquiv.baseChange K _ _ _ _) c
  induction c with
  | zero => simp
  | tmul x y => simp [LinearEquiv.mulLeft]
  | add x y hx hy => simp_all [mul_add]

open scoped TensorProduct.RightActions in
/-- Right multiplication by an element of BË£ on B âŠ— ğ”¸_K does not scale additive
Haar measure.
-/
lemma NumberField.AdeleRing.addEquivAddHaarChar_mulRight_unit_eq_one
    [MeasurableSpace (B âŠ—[K] AdeleRing (ğ“ K) K)] [BorelSpace (B âŠ—[K] AdeleRing (ğ“ K) K)]
    (b : BË£) :
    addEquivAddHaarChar
      (ContinuousAddEquiv.mulRight
        (Units.map Algebra.TensorProduct.includeLeftRingHom.toMonoidHom b :
      (B âŠ—[K] AdeleRing (ğ“ K) K)Ë£)) = 1 := by
  convert addHaarScalarFactor_tensor_adeles_eq_one K B (LinearEquiv.mulRight K b)
  ext c
  change _ = (ContinuousLinearEquiv.baseChange K _ _ _ _) c
  induction c with
  | zero => simp
  | tmul x y => simp [LinearEquiv.mulRight]
  | add x y hx hy => simp_all [add_mul]
