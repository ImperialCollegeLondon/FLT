import FLT.Patching.Utils.AdicTopology
import FLT.Patching.Ultraproduct

variable (Î› : Type*) {Î¹ : Type*} [CommRing Î›] (R : Î¹ â†’ Type*)
variable [âˆ€ i, CommRing (R i)] [âˆ€ i, IsLocalRing (R i)] [âˆ€ i, Algebra Î› (R i)]
variable [âˆ€ i, TopologicalSpace (R i)] [âˆ€ i, IsTopologicalRing (R i)]
variable [âˆ€ i, CompactSpace (R i)] [âˆ€ i, IsLocalRing.IsAdicTopology (R i)]
variable (F : Ultrafilter Î¹)
variable [TopologicalSpace Î›] [IsTopologicalRing Î›] [âˆ€ i, ContinuousSMul Î› (R i)]
variable [Algebra.TopologicallyFG â„¤ Î›]

open IsLocalRing

-- This is true when `R i` is topologically fg over `ğ’ª` by a bounded number of generators, and
-- for each `k`, the index of `m_Î› âŠ” máµ¢áµ` in `Ráµ¢` is uniformly bounded.
-- The latter is true when all `Ráµ¢/m_Î›` are isomorphic.
class Algebra.UniformlyBoundedRank : Prop where
  cond : âˆ€ k, âˆƒ n : â„•, âˆ€ i, Nat.card (R i â§¸ maximalIdeal (R i) ^ k) < n

variable [Algebra.UniformlyBoundedRank R]

abbrev PatchingAlgebra.Component (k : â„•) := UltraProduct (fun i â†¦ R i â§¸ maximalIdeal (R i) ^ k) F

instance (k) : T2Space (PatchingAlgebra.Component R F k) := DiscreteTopology.toT2Space

lemma PatchingAlgebra.componentEquiv (k) : âˆ€á¶  j in F,
    Nonempty (PatchingAlgebra.Component R F k â‰ƒâ‚[Î›] R j â§¸ maximalIdeal (R j) ^ k) := by
  obtain âŸ¨n, hnâŸ© := Algebra.UniformlyBoundedRank.cond (R := R) k
  refine UltraProduct.exists_algEquiv_of_bddAbove_card F n (.of_forall ?_) (.of_forall ?_)
  Â· exact fun x â†¦ âŸ¨AddSubgroup.quotient_finite_of_isOpen _ (isOpen_maximalIdeal_pow _ _), hn _âŸ©
  Â· exact fun i â†¦ ((maximalIdeal (R i) ^ k).restrictScalars Î›).continuousSMul_quotient

instance (k : â„•) : Finite (PatchingAlgebra.Component R F k) :=
  (PatchingAlgebra.componentEquiv â„¤ R F k).exists.choose_spec.some.finite_iff.mpr
    (AddSubgroup.quotient_finite_of_isOpen _ (isOpen_maximalIdeal_pow _ _))

instance (k : â„•) [NeZero k] : Nontrivial (PatchingAlgebra.Component R F k) := by
  obtain âŸ¨i, âŸ¨eâŸ©âŸ© := (PatchingAlgebra.componentEquiv â„¤ R F k).exists
  have : Nontrivial (R i â§¸ maximalIdeal (R i) ^ k) := by
    refine Ideal.Quotient.nontrivial ?_
    exact ((Ideal.pow_le_self (NeZero.ne k)).trans_lt
      (lt_top_iff_ne_top.mpr (maximalIdeal.isMaximal (R i)).ne_top)).ne
  exact e.toRingHom.domain_nontrivial

instance : Subsingleton (PatchingAlgebra.Component R F 0) := by
  obtain âŸ¨i, âŸ¨eâŸ©âŸ© := (PatchingAlgebra.componentEquiv â„¤ R F 0).exists
  have : Subsingleton (R i â§¸ maximalIdeal (R i) ^ 0) := by
    simp [Submodule.subsingleton_quotient_iff_eq_top]
  exact e.symm.toRingHom.codomain_trivial

instance (k : â„•) [NeZero k] : IsLocalRing (PatchingAlgebra.Component R F k) :=
  let âŸ¨i, âŸ¨eâŸ©âŸ© := (PatchingAlgebra.componentEquiv â„¤ R F k).exists
  .of_surjective' (e.symm.toRingHom.comp (Ideal.Quotient.mk (maximalIdeal (R i) ^ k))) <|
    e.symm.surjective.comp Ideal.Quotient.mk_surjective

instance : Nontrivial (Î  k, PatchingAlgebra.Component R F k) :=
  (Pi.evalRingHom _ 1).domain_nontrivial

abbrev PatchingAlgebra.componentMap (j k : â„•) (hjk : k â‰¤ j) : Component R F j â†’+* Component R F k :=
  UltraProduct.mapRingHom (R := fun i â†¦ R i â§¸ maximalIdeal (R i) ^ j) F fun _ â†¦
    Ideal.quotientMap _ (.id _) (Ideal.pow_le_pow_right hjk)

instance (j k : â„•) (hjk : k â‰¤ j) [NeZero k] : IsLocalHom (PatchingAlgebra.componentMap R F j k hjk) := by
  suffices âˆ€ (i : Î¹), IsLocalHom (Ideal.quotientMap (maximalIdeal (R i) ^ k)
      (.id (R i)) (Ideal.pow_le_pow_right hjk)) by
    delta PatchingAlgebra.componentMap; infer_instance
  intro i
  have : Nontrivial (R i â§¸ maximalIdeal (R i) ^ k) := Ideal.Quotient.nontrivial
    ((Ideal.pow_le_self (NeZero.ne k)).trans_lt
      (lt_top_iff_ne_top.mpr (maximalIdeal.isMaximal (R i)).ne_top)).ne
  have : Nontrivial (R i â§¸ maximalIdeal (R i) ^ j) := Ideal.Quotient.nontrivial
    ((Ideal.pow_le_self ((Nat.pos_of_neZero k).trans_le hjk).ne').trans_lt
      (lt_top_iff_ne_top.mpr (maximalIdeal.isMaximal (R i)).ne_top)).ne
  have : IsLocalRing (R i â§¸ maximalIdeal (R i) ^ j) :=
    .of_surjective' _ Ideal.Quotient.mk_surjective
  exact .of_surjective _ (Ideal.quotientMap_surjective RingHomSurjective.is_surjective)

def PatchingAlgebra.subring : Subring (Î  i, Component R F i) where
  carrier := { v | âˆ€ j k hjk, componentMap R F j k hjk (v j) = v k }
  mul_mem' := by simp +contextual only [Set.mem_setOf, Pi.mul_apply, map_mul, implies_true]
  add_mem' := by simp +contextual only [Set.mem_setOf, Pi.add_apply, RingHom.map_add, implies_true]
  one_mem' := by simp
  zero_mem' := by simp
  neg_mem' := by simp +contextual only [Set.mem_setOf, Pi.neg_apply, RingHom.map_neg, implies_true]

def PatchingAlgebra.subalgebra : Subalgebra Î› (Î  i, Component R F i) where
  __ := subring R F
  algebraMap_mem' _ _ _ _ := rfl

omit [âˆ€ i, TopologicalSpace (R i)] [âˆ€ i, IsTopologicalRing (R i)]
  [âˆ€ i, CompactSpace (R i)] [âˆ€ i, IsLocalRing.IsAdicTopology (R i)]
  [Algebra.UniformlyBoundedRank R] in
lemma PatchingAlgebra.isClosed_subring :
    IsClosed (X := Î  i, Component R F i) (subring R F) := by
  have (j k h) : IsClosed { v : Î  i, Component R F i | componentMap R F j k h (v j) = v k } := by
    exact isClosed_eq (by continuity) (by continuity)
  convert isClosed_iInter fun j â†¦ isClosed_iInter fun k â†¦ isClosed_iInter (this j k)
  ext; simp; rfl

def PatchingAlgebra : Type _ := PatchingAlgebra.subring R F

instance : CommRing (PatchingAlgebra R F) :=
  inferInstanceAs (CommRing (PatchingAlgebra.subring R F))

instance : Algebra Î› (PatchingAlgebra R F) :=
  inferInstanceAs (Algebra Î› (PatchingAlgebra.subalgebra Î› R F))

instance : TopologicalSpace (PatchingAlgebra R F) :=
  inferInstanceAs (TopologicalSpace (PatchingAlgebra.subring R F))

instance : IsTopologicalRing (PatchingAlgebra R F) :=
  inferInstanceAs (IsTopologicalRing (PatchingAlgebra.subring R F))

instance : T2Space (PatchingAlgebra R F) :=
  (Topology.IsEmbedding.subtypeVal).t2Space

instance : CompactSpace (PatchingAlgebra R F) :=
  (IsClosed.isClosedEmbedding_subtypeVal (PatchingAlgebra.isClosed_subring R F)).compactSpace

instance : Nontrivial (PatchingAlgebra R F) := (PatchingAlgebra.subring R F).subtype.domain_nontrivial

instance : IsLocalHom (PatchingAlgebra.subring R F).subtype := by
  constructor
  intro a H
  rw [IsUnit.pi_iff] at H
  refine (isUnit_iff_exists_inv.mpr âŸ¨âŸ¨fun i â†¦ â†‘((H i).unitâ»Â¹), fun i j hij â†¦ ?_âŸ©,
    Subtype.ext (funext fun i â†¦ (H i).mul_val_inv)âŸ©)
  dsimp only
  rw [â† MonoidHom.coe_coe, â† RingHom.toMonoidHom_eq_coe, â† Units.coe_map_inv]
  congr
  ext
  simpa using a.2 i j hij

instance : IsLocalRing (PatchingAlgebra R F) := by
  constructor
  intro a b e
  wlog H : âˆ€ i, IsUnit (a.1 i) generalizing a b
  Â· obtain âŸ¨i, hiâŸ© := not_forall.mp H
    have hb : âˆ€ j > i, IsUnit (b.1 j) := by
      intro j hj
      have : NeZero j := NeZero.of_gt hj
      exact (isUnit_or_isUnit_of_add_one (congr_fun (congr_arg Subtype.val e) j)).resolve_left
        fun H â†¦ hi (a.2 _ _ hj.le â–¸ H.map (PatchingAlgebra.componentMap R F j i hj.le))
    replace hb : âˆ€ j, IsUnit (b.1 j) := by
      intro j
      by_cases hij : i < j
      Â· exact hb j hij
      rw [â† b.2 _ _ ((le_of_not_lt hij).trans i.le_succ)]
      exact (hb _ i.lt_succ_self).map _
    exact (this ((add_comm b a).trans e) hb).symm
  exact .inl (IsUnit.of_map (PatchingAlgebra.subring R F).subtype _ (IsUnit.pi_iff.mpr H))

instance (i) [NeZero i] : IsLocalHom ((Pi.evalRingHom _ i).comp (PatchingAlgebra.subring R F).subtype) := by
  constructor
  intro a H
  apply isUnit_of_map_unit (PatchingAlgebra.subring R F).subtype
  rw [IsUnit.pi_iff]
  intro j
  obtain hij | hij := le_total i j
  Â· apply isUnit_of_map_unit (PatchingAlgebra.componentMap R F _ _ hij)
    dsimp
    rwa [a.2 _ _ hij]
  Â· dsimp
    rw [â† a.2 _ _ hij]
    exact H.map (PatchingAlgebra.componentMap R F _ _ hij)

instance : TotallyDisconnectedSpace (PatchingAlgebra R F) :=
  Subtype.totallyDisconnectedSpace

variable {Râ‚’â‚’} [CommRing Râ‚’â‚’] (f : âˆ€ i, Râ‚’â‚’ â†’+* R i) [TopologicalSpace Râ‚’â‚’]
variable [IsTopologicalRing Râ‚’â‚’] [Algebra.TopologicallyFG â„¤ Râ‚’â‚’]
variable (hf : âˆ€ i, Continuous (f i))

def PatchingAlgebra.lift : Râ‚’â‚’ â†’+* PatchingAlgebra R F :=
  (Pi.ringHom fun i â†¦ (UltraProduct.Ï€ _ _).comp
    (Pi.ringHom fun j â†¦ (Ideal.Quotient.mk _).comp (f j))).codRestrict _ <| by
  intro x i j hij
  simp [componentMap, UltraProduct.mapRingHom_Ï€]

include hf in
lemma PatchingAlgebra.continuous_lift : Continuous (lift R F f) := by
  refine continuous_induced_rng.mpr ?_
  refine continuous_pi fun k â†¦ ?_
  obtain âŸ¨n, hnâŸ© := Algebra.UniformlyBoundedRank.cond (R := R) k
  refine UltraProduct.continuous_of_bddAbove_card F n (.of_forall ?_) _ (.of_forall ?_)
  Â· exact fun x â†¦ âŸ¨AddSubgroup.quotient_finite_of_isOpen _ (isOpen_maximalIdeal_pow _ _), hn _âŸ©
  Â· exact fun i â†¦ (continuous_algebraMap _ _).comp (hf i)

variable [CompactSpace Râ‚’â‚’]

include hf in
lemma PatchingAlgebra.lift_surjective (hf' : âˆ€ i, Function.Surjective (f i)) :
    Function.Surjective (lift R F f) := by
  suffices DenseRange (lift R F f) by
    rw [â† Set.range_eq_univ, â† this.closure_eq,
      (isCompact_range (continuous_lift R F f hf)).isClosed.closure_eq]
  refine denseRange_inverseLimit (Î¹ := â„•áµ’áµˆ) _ _ (fun _ _ _ â†¦ continuous_of_discreteTopology) _ ?_
  refine fun i â†¦ denseRange_discrete.mpr ?_
  obtain âŸ¨n, hnâŸ© := Algebra.UniformlyBoundedRank.cond (R := R) i
  refine UltraProduct.surjective_of_bddAbove_card F n (.of_forall ?_) _ (.of_forall ?_) (.of_forall ?_)
  Â· exact fun x â†¦ âŸ¨AddSubgroup.quotient_finite_of_isOpen _ (isOpen_maximalIdeal_pow _ _), hn _âŸ©
  Â· exact fun x â†¦ (continuous_algebraMap _ _).comp (hf x)
  Â· exact fun x â†¦ Ideal.Quotient.mk_surjective.comp (hf' x)

def PatchingAlgebra.ofPi :
    (â„• â†’ Î  i, R i) â†’+* Î  k, Component R F k :=
  Pi.ringHom fun k â†¦ (RingHom.comp ((Ideal.Quotient.mk
    (eventuallyProd (R := fun i â†¦ R i â§¸ maximalIdeal (R i) ^ k)
    (M := fun i â†¦ R i â§¸ maximalIdeal (R i) ^ k) âŠ¥ F)).comp
    (Pi.ringHom fun j â†¦ (Ideal.Quotient.mk _).comp (Pi.evalRingHom _ j))) (Pi.evalRingHom _ k))

variable {R F} in
omit
  [âˆ€ (i : Î¹), TopologicalSpace (R i)]
  [âˆ€ (i : Î¹), IsTopologicalRing (R i)]
  [âˆ€ (i : Î¹), CompactSpace (R i)]
  [âˆ€ (i : Î¹), IsAdicTopology (R i)]
  [Algebra.UniformlyBoundedRank R] in
lemma PatchingAlgebra.ofPi_surjective :
    Function.Surjective (ofPi R F) := by
  intro x
  have (k : â„•) := Ideal.Quotient.mk_surjective
    (I := (eventuallyProd (R := fun i â†¦ R i â§¸ maximalIdeal (R i) ^ k)
      (M := fun i â†¦ R i â§¸ maximalIdeal (R i) ^ k) âŠ¥ F)) (x k)
  choose y hy using this
  choose z hz using fun k i â†¦ Ideal.Quotient.mk_surjective (y k i)
  refine âŸ¨z, funext fun k â†¦ ?_âŸ©
  rw [â† hy]
  show Ideal.Quotient.mk _ _ = _
  congr 1
  ext i
  simp only [Pi.evalRingHom_apply, Pi.ringHom_apply, RingHom.coe_comp, Function.comp_apply, hz]

omit
  [âˆ€ (i : Î¹), TopologicalSpace (R i)]
  [âˆ€ (i : Î¹), IsTopologicalRing (R i)]
  [âˆ€ (i : Î¹), CompactSpace (R i)]
  [âˆ€ (i : Î¹), IsAdicTopology (R i)]
  [Algebra.UniformlyBoundedRank R] in
@[simp]
lemma PatchingAlgebra.ofPi_apply (x k) :
  ofPi R F x k = UltraProduct.Ï€ (fun i â†¦ R i â§¸ maximalIdeal (R i) ^ k) F
    (fun i â†¦ Ideal.Quotient.mk _ (x k i)) := rfl

def PatchingAlgebra.incl :
    (Î  i, R i) â†’+* PatchingAlgebra R F :=
  RingHom.codRestrict ((ofPi R F).comp (Pi.ringHom fun _ â†¦ .id _)) (subring R F) <| by
    intro x j k h
    simp [UltraProduct.mapRingHom_Ï€]

section Functorial

variable {R' : Î¹ â†’ Type*} [âˆ€ i, CommRing (R' i)] [âˆ€ i, IsLocalRing (R' i)] --[âˆ€ i, Module R' (N i)]
variable {R'' : Î¹ â†’ Type*} [âˆ€ i, CommRing (R'' i)] [âˆ€ i, IsLocalRing (R'' i)] --[âˆ€ i, Module R (N' i)]
variable (f : âˆ€ i, R i â†’+* R' i) (g : âˆ€ i, R' i â†’+* R'' i)
variable [âˆ€ i, IsLocalHom (f i)] [âˆ€ i, IsLocalHom (g i)]

abbrev PatchingAlgebra.componentMapRingHom (k : â„•) :
    Component R F k â†’+* Component R' F k :=
  UltraProduct.mapRingHom F
    (R := fun i â†¦ R i â§¸ maximalIdeal (R i) ^ k)
    (S := fun i â†¦ R' i â§¸ maximalIdeal (R' i) ^ k)
    fun i â†¦ Ideal.quotientMap _ (f i) <| by
      rw [â† Ideal.map_le_iff_le_comap, Ideal.map_pow]
      apply Ideal.pow_right_mono
      exact ((local_hom_TFAE (f i)).out 0 2).mp (by infer_instance)

omit
  [âˆ€ (i : Î¹), TopologicalSpace (R i)]
  [âˆ€ (i : Î¹), IsTopologicalRing (R i)]
  [âˆ€ (i : Î¹), CompactSpace (R i)]
  [âˆ€ (i : Î¹), IsAdicTopology (R i)]
  [Algebra.UniformlyBoundedRank R] in
lemma PatchingAlgebra.componentMapRingHom_surjective
    (hf : âˆ€ i, Function.Surjective (f i)) (k : â„•) :
    Function.Surjective (componentMapRingHom R F f k) := by
  apply UltraProduct.mapRingHom_surjective
  intro i x
  obtain âŸ¨x, rflâŸ© := Ideal.Quotient.mk_surjective.comp (hf i) x
  refine âŸ¨Ideal.Quotient.mk _ x, by simpâŸ©

variable {R} in
def PatchingAlgebra.map :
    PatchingAlgebra R F â†’+* PatchingAlgebra R' F :=
  RingHom.restrict (Pi.ringHom fun i â†¦ (componentMapRingHom R F f i).comp (Pi.evalRingHom _ _))
    _ _ <| by
    intro x hx i j hij
    obtain âŸ¨a, haâŸ© := UltraProduct.Ï€_surjective (x i)
    simp only [Pi.ringHom_apply, RingHom.coe_comp, Function.comp_apply, Pi.evalRingHom_apply, â†
      hx i j hij, â† ha, UltraProduct.mapRingHom_Ï€, UltraProduct.Ï€_eq_iff]
    filter_upwards with k
    obtain âŸ¨b, hbâŸ© := Ideal.Quotient.mk_surjective (a k)
    simp only [â† hb, Ideal.quotientMap_mk, RingHomCompTriple.comp_apply, RingHom.id_apply]

-- omit [IsTopologicalRing R] [CompactSpace R] in
omit
  [âˆ€ (i : Î¹), TopologicalSpace (R i)]
  [âˆ€ (i : Î¹), IsTopologicalRing (R i)]
  [âˆ€ (i : Î¹), CompactSpace (R i)]
  [âˆ€ (i : Î¹), IsAdicTopology (R i)]
  [Algebra.UniformlyBoundedRank R] in
@[simp]
lemma PatchingAlgebra.map_apply (x : PatchingAlgebra R F) (k) :
    (map F f x).1 k = componentMapRingHom R F f k (x.1 k) := rfl

-- omit [IsTopologicalRing R] [CompactSpace R] in
omit
  [âˆ€ (i : Î¹), TopologicalSpace (R i)]
  [âˆ€ (i : Î¹), IsTopologicalRing (R i)]
  [âˆ€ (i : Î¹), CompactSpace (R i)]
  [âˆ€ (i : Î¹), IsAdicTopology (R i)]
  [Algebra.UniformlyBoundedRank R] in
lemma PatchingAlgebra.map_comp_apply (x) :
    map F (fun i â†¦ (g i).comp (f i)) x = map F g (map F f x) := by
  refine Subtype.ext (funext fun k â†¦ ?_)
  obtain âŸ¨y, hyâŸ© := ofPi_surjective x.1
  simp [â† hy, UltraProduct.mapRingHom_Ï€]

omit
  [âˆ€ (i : Î¹), TopologicalSpace (R i)]
  [âˆ€ (i : Î¹), IsTopologicalRing (R i)]
  [âˆ€ (i : Î¹), CompactSpace (R i)]
  [âˆ€ (i : Î¹), IsAdicTopology (R i)]
  [Algebra.UniformlyBoundedRank R] in
lemma PatchingAlgebra.map_comp :
    map F (fun i â†¦ (g i).comp (f i)) = (map F g).comp (map F f) :=
  RingHom.ext (map_comp_apply R F f g)

omit
  [âˆ€ (i : Î¹), TopologicalSpace (R i)]
  [âˆ€ (i : Î¹), IsTopologicalRing (R i)]
  [âˆ€ (i : Î¹), CompactSpace (R i)]
  [âˆ€ (i : Î¹), IsAdicTopology (R i)]
  [Algebra.UniformlyBoundedRank R] in
@[simp]
lemma PatchingAlgebra.map_id :
    map F (fun i â†¦ RingHom.id (R i)) = RingHom.id _ := by
  ext x
  obtain âŸ¨y, hyâŸ© := ofPi_surjective x.1
  refine Subtype.ext (funext fun Î± â†¦ ?_)
  simp [â† hy, UltraProduct.mapRingHom_Ï€]

instance {R S} [Semiring R] [Semiring S] (e : R â‰ƒ+* S) : IsLocalHom e.toRingHom :=
  âŸ¨fun x hx â†¦ by convert hx.map e.symm; simpâŸ©

@[simps! apply symm_apply]
def PatchingAlgebra.mapEquiv (f : âˆ€ i, R i â‰ƒ+* R' i) :
    PatchingAlgebra R F â‰ƒ+* PatchingAlgebra R' F where
  __ := map F fun i â†¦ (f i).toRingHom
  invFun := map F fun i â†¦ (f i).symm.toRingHom
  left_inv x := by simp [â† map_comp_apply]
  right_inv x := by simp [â† map_comp_apply]

lemma RingHom.continuous_of_finite_of_compact {R H : Type*} [CommRing R] [Semiring H]
    (f : R â†’+* H) [TopologicalSpace R] [TopologicalSpace H] [CompactSpace R] [_root_.Finite H]
    [IsTopologicalRing R] [T2Space R] [IsNoetherianRing R] [ContinuousAdd H] :
    Continuous f := by
  suffices IsOpen (X := R) (RingHom.ker f) by
    apply continuous_of_continuousAt_zero
    rw [ContinuousAt, map_zero]
    refine (Filter.tendsto_zero.mpr (this.mem_nhds (map_zero f))).trans ?_
    simp +contextual [le_nhds_iff]
  have : (RingHom.ker f).toAddSubgroup.FiniteIndex := by
    have : _root_.Finite (R â§¸ (RingHom.ker f).toAddSubgroup) :=
      _root_.Finite.of_injective _ f.kerLift_injective
    exact AddSubgroup.finiteIndex_of_finite_quotient _
  have := (isCompact_of_isNoetherianRing (RingHom.ker f)).isClosed
  exact AddSubgroup.isOpen_of_isClosed_of_finiteIndex (RingHom.ker f).toAddSubgroup this

open IsLocalRing in
lemma PatchingAlgebra.map_surjective
    (hf : âˆ€ i, Function.Surjective (f i)) :
    Function.Surjective (map F f) := by
  intro x
  let s (k : â„•) : Set (Component R F k) :=
    componentMapRingHom R F f k â»Â¹' {x.1 k}
  let fs (kâ‚ kâ‚‚ : â„•áµ’áµˆ) (h : kâ‚ â‰¤ kâ‚‚) (a : s kâ‚) : s kâ‚‚ :=
    âŸ¨componentMap R F _ _ h a.1, by
      have : _ = _ := a.2
      simp only [Set.mem_preimage, Set.mem_singleton_iff, s, â† x.2 _ _ h, â† this]
      obtain âŸ¨b, hbâŸ© := UltraProduct.Ï€_surjective a.1
      simp only [Pi.ringHom_apply, RingHom.coe_comp, Function.comp_apply, Pi.evalRingHom_apply,
        â† hb, UltraProduct.mapRingHom_Ï€, UltraProduct.Ï€_eq_iff]
      filter_upwards with k
      obtain âŸ¨c, hcâŸ© := Ideal.Quotient.mk_surjective (b k)
      simp only [â† hc, Ideal.quotientMap_mk, RingHomCompTriple.comp_apply, RingHom.id_apply]âŸ©
  have (k) : Nonempty (s k) := by
    simp only [nonempty_subtype, Set.mem_preimage, Set.mem_singleton_iff, s]
    exact PatchingAlgebra.componentMapRingHom_surjective R F f hf k (x.1 k)
  obtain âŸ¨v, hvâŸ© := nonempty_inverseLimit_of_finite (Î¹ := â„•áµ’áµˆ) (s Â·) fs (by
      intro i
      ext âŸ¨x, hxâŸ©
      obtain âŸ¨x, rflâŸ© := UltraProduct.Ï€_surjective x
      simp only [Pi.ringHom_apply, RingHom.coe_comp, Function.comp_apply, Pi.evalRingHom_apply,
         UltraProduct.mapRingHom_Ï€, UltraProduct.Ï€_eq_iff, id_eq, fs]
      filter_upwards with k
      obtain âŸ¨b, hbâŸ© := Ideal.Quotient.mk_surjective (x k)
      simp only [â† hb, Ideal.quotientMap_mk, RingHom.id_apply]) (by
      intro i j k hij hjk
      ext âŸ¨x, hxâŸ©
      obtain âŸ¨x, rflâŸ© := UltraProduct.Ï€_surjective x
      simp only [Pi.ringHom_apply, RingHom.coe_comp, Function.comp_apply, Pi.evalRingHom_apply,
         UltraProduct.mapRingHom_Ï€, UltraProduct.Ï€_eq_iff, id_eq, fs]
      filter_upwards with k
      obtain âŸ¨b, hbâŸ© := Ideal.Quotient.mk_surjective (x k)
      simp only [â† hb, Ideal.quotientMap_mk, RingHom.id_apply])
      (l := id) (fun _ _ â†¦ id) (fun a â†¦ âŸ¨a, le_rflâŸ©)
  refine âŸ¨âŸ¨fun i â†¦ (v i).1, fun Î± Î² h â†¦ congr_arg Subtype.val (hv Î± Î² h)âŸ©, ?_âŸ©
  refine Subtype.ext (funext fun Î± â†¦ ?_)
  have : _ = _ := (v Î±).2
  simpa using this

end Functorial

lemma PatchingAlgebra.algebraMap_continuous
    (R : Type*) [CommRing R] [IsLocalRing R] [TopologicalSpace R] [IsTopologicalRing R]
    [CompactSpace R] [IsNoetherianRing R] [IsAdicTopology R] :
    Continuous (algebraMap R (PatchingAlgebra (fun _ â†¦ R) F)) := by
  refine continuous_induced_rng.mpr ?_
  refine continuous_pi fun k â†¦ ?_
  let f : R â†’+* Component (fun x â†¦ R) F k := (UltraProduct.Ï€ _ _).comp
    (Pi.ringHom fun i â†¦ Ideal.Quotient.mk _)
  have : Algebra.UniformlyBoundedRank fun _ : Î¹ â†¦ R :=
    âŸ¨fun k â†¦ âŸ¨(Nat.card (R â§¸ maximalIdeal R ^ k)).succ, fun _ â†¦ Nat.lt_succ_self _âŸ©âŸ©
  have : Finite (Component (fun x â†¦ R) F k) := instFiniteComponent _ _ k
  exact RingHom.continuous_of_finite_of_compact f

lemma PatchingAlgebra.algebraMap_surjective
    (R : Type*) [CommRing R] [IsLocalRing R] [TopologicalSpace R] [IsTopologicalRing R]
    [CompactSpace R] [IsNoetherianRing R] [IsAdicTopology R] :
    Function.Surjective (algebraMap R (PatchingAlgebra (fun _ â†¦ R) F)) := by
  suffices DenseRange (algebraMap R (PatchingAlgebra (fun _ â†¦ R) F)) by
    rw [â† Set.range_eq_univ, â† this.closure_eq,
      (isCompact_range (algebraMap_continuous F R)).isClosed.closure_eq]
  refine denseRange_inverseLimit (Î¹ := â„•áµ’áµˆ) _ _
    (fun _ _ _ â†¦ continuous_of_discreteTopology) _
    fun k â†¦ denseRange_discrete.mpr ?_
  have : Finite (R â§¸ maximalIdeal R ^ k) := AddSubgroup.quotient_finite_of_isOpen _
    (isOpen_maximalIdeal_pow _ _)
  have := UltraProduct.surjective_of_eventually_surjective
    (f := fun i : Î¹ â†¦ LinearMap.id (R := R) (M := R â§¸ maximalIdeal R ^ k)) F
    (.of_forall fun _ _ â†¦ âŸ¨_, rflâŸ©)
  exact this.comp Ideal.Quotient.mk_surjective

noncomputable
def PatchingAlgebra.constEquiv
    (R : Type*) [CommRing R] [IsLocalRing R] [TopologicalSpace R] [IsTopologicalRing R]
    [CompactSpace R] [IsNoetherianRing R] [IsAdicTopology R] :
    R â‰ƒâ‚[R] PatchingAlgebra (fun _ â†¦ R) F := by
  refine .ofBijective (Algebra.ofId R (PatchingAlgebra (fun _ â†¦ R) F))
    âŸ¨?_, algebraMap_surjective F RâŸ©
  rw [injective_iff_map_eq_zero]
  intro a ha
  have (k : â„•) : a âˆˆ maximalIdeal R ^ k  := by
    have := UltraProduct.Ï€â‚—_eq_zero.mp (congr_fun (congr_arg Subtype.val ha) k)
    simp only [Pi.algebraMap_apply, Ideal.Quotient.algebraMap_eq,
      Ideal.Quotient.eq_zero_iff_mem] at this
    exact this.exists.choose_spec
  rwa [â† Ideal.mem_bot (R := R), â† Ideal.iInf_pow_eq_bot_of_isLocalRing _
    (IsLocalRing.maximalIdeal.isMaximal R).ne_top, Ideal.mem_iInf]
