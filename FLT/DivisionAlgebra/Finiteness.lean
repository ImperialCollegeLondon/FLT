/-
Copyright (c) 2024 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, William Coram
-/
import Mathlib.NumberTheory.NumberField.Basic
import Mathlib.RingTheory.DedekindDomain.FiniteAdeleRing
import Mathlib.Algebra.Group.Subgroup.Pointwise
import FLT.Mathlib.Topology.Algebra.Module.ModuleTopology
import Mathlib.GroupTheory.DoubleCoset
import Mathlib.Algebra.Central.Defs
import Mathlib.Tactic.LinearCombination'
import FLT.NumberField.AdeleRing
import FLT.HaarMeasure.HaarChar.Ring
import FLT.HaarMeasure.HaarChar.AdeleRing

/-

# Fujisaki's lemma

We prove a lemma which Voight (in his quaternion algebra book) attributes to Fujisaki:
if `D` is a finite-dimensional division algebra over a number field `K`
and if `U ‚äÜ (D ‚äó[K] ùî∏_K^infty)À£` is a compact open subgroup then the double coset
space `DÀ£ \ (D ‚äó[K] ùî∏_K^infty)À£ / U` is finite.

-/

suppress_compilation

open IsDedekindDomain MeasureTheory

open scoped NumberField TensorProduct

variable (K : Type*) [Field K] [NumberField K]
variable (D : Type*) [DivisionRing D] [Algebra K D] [FiniteDimensional K D]

namespace NumberField.AdeleRing.DivisionAlgebra

set_option quotPrecheck false in
/-- `D_ùî∏` is notation for `D ‚äó[K] ùî∏_K`. -/
notation "D_ùî∏" => (D ‚äó[K] AdeleRing (ùìû K) K)

open scoped TensorProduct.RightActions

variable [MeasurableSpace (D ‚äó[K] AdeleRing (ùìû K) K)] [BorelSpace (D ‚äó[K] AdeleRing (ùìû K) K)]

/-- The inclusion DÀ£ ‚Üí D_ùî∏À£ as a group homomorphism. -/
noncomputable abbrev incl : DÀ£ ‚Üí* D_ùî∏À£ :=
  Units.map Algebra.TensorProduct.includeLeftRingHom.toMonoidHom

namespace Aux

lemma existsE : ‚àÉ E : Set (D_ùî∏), IsCompact E ‚àß
    ‚àÄ œÜ : D_ùî∏ ‚âÉ‚Çú+ D_ùî∏, addEquivAddHaarChar œÜ = 1 ‚Üí ‚àÉ e‚ÇÅ ‚àà E, ‚àÉ e‚ÇÇ ‚àà E,
    e‚ÇÅ ‚â† e‚ÇÇ ‚àß œÜ e‚ÇÅ - œÜ e‚ÇÇ ‚àà Set.range (Algebra.TensorProduct.includeLeft : D ‚Üí‚Çê[K] D_ùî∏) :=
  sorry

/-- An auxiliary set E used in the proof of Fukisaki's lemma. -/
def E : Set D_ùî∏ := (existsE K D).choose

lemma E_compact : IsCompact (E K D) := (existsE K D).choose_spec.1

lemma E_noninjective_left {x : D_ùî∏À£} (h : x ‚àà ringHaarChar_ker D_ùî∏) :
    ‚àÉ e‚ÇÅ ‚àà E K D, ‚àÉ e‚ÇÇ ‚àà E K D, e‚ÇÅ ‚â† e‚ÇÇ ‚àß
    x * e‚ÇÅ - x * e‚ÇÇ ‚àà Set.range (Algebra.TensorProduct.includeLeft : D ‚Üí‚Çê[K] D_ùî∏) :=
  (existsE K D).choose_spec.2 (ContinuousAddEquiv.mulLeft x) h

lemma E_noninjective_right {x : D_ùî∏À£} (h : x ‚àà ringHaarChar_ker D_ùî∏) :
    ‚àÉ e‚ÇÅ ‚àà E K D, ‚àÉ e‚ÇÇ ‚àà E K D, e‚ÇÅ ‚â† e‚ÇÇ ‚àß
    e‚ÇÅ * x‚Åª¬π - e‚ÇÇ * x‚Åª¬π  ‚àà Set.range (Algebra.TensorProduct.includeLeft : D ‚Üí‚Çê[K] D_ùî∏) := by
  let œÜ : D_ùî∏ ‚âÉ‚Çú+ D_ùî∏ := ContinuousAddEquiv.mulRight x‚Åª¬π
  have hœÜ : addEquivAddHaarChar œÜ = 1 := sorry
  exact (existsE K D).choose_spec.2 œÜ hœÜ

open scoped Pointwise in
/-- An auxiliary set X used in the proof of Fukisaki's lemma. Defined as E - E. -/
def X : Set D_ùî∏ := E K D - E K D

open scoped Pointwise in
/-- An auxiliary set Y used in the proof of Fukisaki's lemma. Defined as X * X. -/
def Y : Set D_ùî∏ := X K D * X K D

lemma X_compact : IsCompact (X K D) := by
  simpa only [Set.image_prod, Set.image2_sub] using (IsCompact.image_of_continuousOn
    ((E_compact K D).prod (E_compact K D)) ((continuous_fst.sub continuous_snd).continuousOn))

lemma Y_compact : IsCompact (Y K D) := by
  simpa only [Set.image_prod, Set.image2_mul] using (IsCompact.image_of_continuousOn
    ((X_compact K D).prod (X_compact K D)) ((continuous_fst.mul continuous_snd).continuousOn))

lemma X_meets_kernel {Œ≤ : D_ùî∏À£} (hŒ≤ : Œ≤ ‚àà ringHaarChar_ker D_ùî∏) :
    ‚àÉ x ‚àà X K D, ‚àÉ d ‚àà Set.range (incl K D : DÀ£ ‚Üí D_ùî∏À£), Œ≤ * x = d := by
  obtain ‚ü®e1, he1, e2, he2, noteq, b, hb‚ü© := E_noninjective_left K D hŒ≤
  refine ‚ü®e1 - e2, by simpa only using (Set.sub_mem_sub he1 he2), ?_‚ü©
  obtain ‚ü®b1, rfl‚ü© : IsUnit b := by
    simp_rw [‚Üê mul_sub_left_distrib, Algebra.TensorProduct.includeLeft_apply] at hb
    have h1 : ‚ÜëŒ≤ * (e1 - e2) ‚â† 0 := by
      simpa only [ne_eq, not_not, Units.mul_right_eq_zero] using (sub_ne_zero_of_ne noteq)
    simp only [isUnit_iff_ne_zero, ne_eq]
    rintro rfl
    simp only [‚Üê hb, TensorProduct.zero_tmul, ne_eq, not_true_eq_false] at h1
  exact ‚ü®incl K D b1, ‚ü®b1, rfl‚ü©, by simpa [mul_sub] using hb.symm‚ü©

lemma X_meets_kernel' {Œ≤ : D_ùî∏À£} (hŒ≤ : Œ≤ ‚àà ringHaarChar_ker D_ùî∏) :
    ‚àÉ x ‚àà X K D, ‚àÉ d ‚àà Set.range (incl K D : DÀ£ ‚Üí D_ùî∏À£), x * Œ≤‚Åª¬π = d := by
  obtain ‚ü®e1, he1, e2, he2, noteq, b, hb‚ü© := E_noninjective_right K D hŒ≤
  refine ‚ü®e1 - e2, by simpa only using (Set.sub_mem_sub he1 he2), ?_‚ü©
  obtain ‚ü®b1, rfl‚ü© : IsUnit b := by
    simp_rw [‚Üê mul_sub_right_distrib, Algebra.TensorProduct.includeLeft_apply] at hb
    have h1 : (e1 - e2) * ‚ÜëŒ≤‚Åª¬π ‚â† 0 := by
      simpa only [ne_eq, Units.mul_left_eq_zero] using (sub_ne_zero_of_ne noteq)
    simp only [isUnit_iff_ne_zero, ne_eq]
    rintro rfl
    simp only [‚Üê hb, TensorProduct.zero_tmul, ne_eq, not_true_eq_false] at h1
  exact ‚ü®incl K D b1, ‚ü®b1, rfl‚ü©, by simpa [sub_mul] using hb.symm‚ü©

/-- An auxiliary set T used in the proof of Fukisaki's lemma. Defined as Y ‚à© DÀ£. -/
def T : Set D_ùî∏À£ := ((‚Üë) : D_ùî∏À£ ‚Üí D_ùî∏) ‚Åª¬π' (Y K D) ‚à© Set.range ((incl K D : DÀ£ ‚Üí D_ùî∏À£))

-- Need something saying D ‚äÜ D_ùî∏ is discrete

lemma T_finite : Set.Finite (T K D) := by
  have h : Set.Finite ((Y K D) ‚à© (Set.range (Algebra.TensorProduct.includeLeft : D ‚Üí‚Çê[K] D_ùî∏)))
      := by
    apply IsCompact.finite
    ¬∑ refine IsCompact.inter_right (Y_compact K D) ?_

      -- Subgroup.isClosed_of_discrete
      sorry
    ¬∑ -- follows form D being discrete

      sorry
  have h1 : Units.val '' T K D ‚äÜ (Y K D) ‚à©
      (Set.range (Algebra.TensorProduct.includeLeft : D ‚Üí‚Çê[K] D_ùî∏)) := by
    rintro _ ‚ü®t, ‚ü®ht1, d, rfl‚ü©, rfl‚ü©
    exact ‚ü®ht1, d, rfl‚ü©
  exact Set.Finite.of_finite_image (Set.Finite.subset h h1) (Function.Injective.injOn Units.ext)

open scoped Pointwise in
/-- An auxiliary set C used in the proof of Fukisaki's lemma. Defined as T‚Åª¬πX √ó X. -/
def C : Set (D_ùî∏ √ó D_ùî∏) := ((((‚Üë) : D_ùî∏À£ ‚Üí D_ùî∏) '' (T K D)‚Åª¬π) * X K D) √óÀ¢ X K D

lemma C_compact : IsCompact (C K D) := by
  refine IsCompact.prod ?_ (X_compact K D)
  simpa only [Set.image_prod, Set.image2_mul] using
    (IsCompact.image_of_continuousOn (IsCompact.prod (IsCompact.image_of_continuousOn
    (IsCompact.inv (Set.Finite.isCompact (T_finite K D))) (Continuous.comp_continuousOn'
    (Units.continuous_val) (continuousOn_id' (T K D)‚Åª¬π)))
    (X_compact K D)) ((continuous_fst.mul continuous_snd).continuousOn))

lemma antidiag_mem_C {Œ≤ : D_ùî∏À£} (hŒ≤ : Œ≤ ‚àà ringHaarChar_ker D_ùî∏) :
    ‚àÉ b ‚àà Set.range (incl K D : DÀ£ ‚Üí D_ùî∏À£),
    ‚àÉ ŒΩ ‚àà ringHaarChar_ker D_ùî∏,
    Œ≤ = b * ŒΩ ‚àß ((ŒΩ : D_ùî∏), ((ŒΩ‚Åª¬π : D_ùî∏À£) : D_ùî∏)) ‚àà C K D := by
  obtain ‚ü®x1, hx1, b1, ‚ü®b1, rfl‚ü©, eq1‚ü© := X_meets_kernel K D hŒ≤
  obtain ‚ü®x2, hx2, b2, ‚ü®b2, rfl‚ü©, eq2‚ü© := X_meets_kernel' K D hŒ≤
  obtain ‚ü®x1, rfl‚ü© : IsUnit x1 := ‚ü®‚ÜëŒ≤‚Åª¬π * incl K D b1,
    ((Units.eq_inv_mul_iff_mul_eq Œ≤).mpr eq1).symm‚ü©
  obtain ‚ü®x2, rfl‚ü© : IsUnit x2 := ‚ü®incl K D b2 * Œ≤, ((Units.mul_inv_eq_iff_eq_mul Œ≤).mp eq2).symm‚ü©
  have h : x2 * x1 ‚àà T K D := ‚ü®by simpa only [Y] using (Set.mul_mem_mul hx2 hx1), b2 * b1,
    by simpa using Units.eq_iff.mp (id (Eq.symm (by simpa [mul_assoc] using
    (Mathlib.Tactic.LinearCombination'.mul_pf eq2 eq1))))‚ü©
  refine ‚ü®incl K D b1, by simp only [Set.mem_range, exists_apply_eq_apply],  x1‚Åª¬π, ?_,
    eq_mul_inv_of_mul_eq (Units.eq_iff.mp eq1), ?_, hx1‚ü©
  ¬∑ rw [(Eq.symm (inv_mul_eq_of_eq_mul (eq_mul_inv_of_mul_eq (Units.eq_iff.mp eq1))))]
    exact (Subgroup.mul_mem_cancel_right (ringHaarChar_ker (D ‚äó[K] AdeleRing (ùìû K) K)) hŒ≤).mpr
      ((Subgroup.inv_mem_iff (ringHaarChar_ker (D ‚äó[K] AdeleRing (ùìû K) K))).mpr
      (NumberField.AdeleRing.units_mem_ringHaarCharacter_ker K D b1))
  ¬∑ obtain ‚ü®t, ht, ht1‚ü© := exists_eq_right'.mpr h
    simp_rw [(Eq.symm (inv_mul_eq_of_eq_mul (eq_mul_inv_of_mul_eq ht1)))]
    exact Set.mem_mul.mpr ‚ü®‚Üët‚Åª¬π, Set.mem_image_of_mem Units.val ht, x2, hx2, rfl‚ü©

end Aux

/-- The inclusion of `ringHaarChar_ker D_ùî∏` into the product space `D_ùî∏ √ó D_ùî∏·µê·µí·µñ`. -/
def incl‚ÇÇ : ringHaarChar_ker D_ùî∏ ‚Üí Prod D_ùî∏ D_ùî∏·µê·µí·µñ :=
  fun u => Units.embedProduct D_ùî∏ (Subgroup.subtype (ringHaarChar_ker D_ùî∏) u)

/-- An auxillary set used in the proof of compact_quotient'. -/
def M : Set (ringHaarChar_ker D_ùî∏) := Set.preimage (incl‚ÇÇ K D)
  (Set.image (fun p => (p.1, MulOpposite.op p.2)) (Aux.C K D))

/-- The map from `ringHaarChar_ker D_ùî∏` to the quotient `DÀ£ \ ringHaarChar_ker D_ùî∏`. -/
abbrev toQuot (a : ringHaarChar_ker D_ùî∏) : (_root_.Quotient (QuotientGroup.rightRel
    ((MonoidHom.range (incl K D)).comap (ringHaarChar_ker D_ùî∏).subtype))) :=
  (Quotient.mk (QuotientGroup.rightRel ((MonoidHom.range (incl K D)).comap
  (ringHaarChar_ker D_ùî∏).subtype)) a)

lemma toQuot_cont : Continuous (toQuot K D) := by exact { isOpen_preimage := fun s a ‚Ü¶ a }

lemma toQuot_surjective : (toQuot K D) '' (M K D) = Set.univ := by
  rw [Set.eq_univ_iff_forall]
  rintro ‚ü®a, ha‚ü©
  obtain ‚ü®c, hc, ŒΩ, hŒΩ, rfl, h31‚ü© := Aux.antidiag_mem_C K D ha
  simp only [toQuot, Subgroup.comap_subtype, Set.mem_image, Subtype.exists]
  refine ‚ü®ŒΩ, hŒΩ, ?_, ?_ ‚ü©
  ¬∑ simp only [M, Set.mem_preimage, Set.mem_image, Prod.exists]
    exact ‚ü®ŒΩ, Units.val (ŒΩ‚Åª¬π), h31, rfl‚ü©
  ¬∑ have : Quot.mk ‚áë(QuotientGroup.rightRel ((incl K D).range.subgroupOf
        (ringHaarChar_ker (D ‚äó[K] AdeleRing (ùìû K) K)))) ‚ü®c * ŒΩ, ha‚ü© =
        Quot.mk ‚áë(QuotientGroup.rightRel ((incl K D).range.subgroupOf
        (ringHaarChar_ker (D ‚äó[K] AdeleRing (ùìû K) K))))
        ‚ü®ŒΩ, hŒΩ‚ü© := by
      refine Quot.sound ?_
      rw [@QuotientGroup.rightRel_apply]
      refine Subgroup.mem_subgroupOf.mpr ?_
      simp only [@Subgroup.coe_mul, InvMemClass.coe_inv, mul_inv_rev, mul_inv_cancel_left,
        inv_mem_iff, MonoidHom.mem_range]
      exact hc
    rw [this]
    rfl

/- The following is part of the proof of 12.11 on the blueprint - perhaps this can be moved there
  in more generality later
-/

variable (L : Type*) [Monoid L]

/-- Auxillary map used in `embedProduct_preimageOf`. -/
def p : Prod L L·µê·µí·µñ ‚Üí L :=
  fun p => p.1 * MulOpposite.unop p.2

/-- Auxillary map used in `embedProduct_preimageOf`. -/
def q : Prod L L·µê·µí·µñ ‚Üí L :=
  fun p => MulOpposite.unop p.2 * p.1

lemma p_cont [TopologicalSpace L] [ContinuousMul L] : Continuous (p L) :=
    Continuous.mul (continuous_fst) (Continuous.comp (MulOpposite.continuous_unop) continuous_snd)

lemma q_cont [TopologicalSpace L] [ContinuousMul L] : Continuous (q L) :=
    Continuous.mul (Continuous.comp (MulOpposite.continuous_unop) continuous_snd) (continuous_fst)

lemma embedProduct_preimageOf : (Set.range ‚áë(Units.embedProduct L)) =
    Set.preimage (p L) {1} ‚à© Set.preimage (q L) {1} := by
  ext x
  simp only [Set.mem_range, Units.embedProduct_apply, Set.mem_inter_iff, Set.mem_preimage,
    Set.mem_singleton_iff]
  constructor
  ¬∑ rintro ‚ü®y, ‚ü®x1, x2‚ü©‚ü©
    exact ‚ü®by simp only [p , MulOpposite.unop_op, Units.mul_inv],
      by simp only [q, MulOpposite.unop_op, Units.inv_mul]‚ü©
  ¬∑ rintro ‚ü®hp, hq‚ü©
    use ‚ü®x.1, MulOpposite.unop x.2, hp, hq‚ü©
    rfl

lemma embedProduct_closed [TopologicalSpace L] [ContinuousMul L] [T1Space L] :
    IsClosed (Set.range ‚áë(Units.embedProduct L))
    := by
  rw [embedProduct_preimageOf]
  exact IsClosed.inter (IsClosed.preimage (p_cont L) (isClosed_singleton))
    (IsClosed.preimage (q_cont L) (isClosed_singleton))

-- elsewhere
local instance : T2Space (D ‚äó[K] AdeleRing (ùìû K) K) := by

  sorry

lemma incl‚ÇÇ_isClosedEmbedding : Topology.IsClosedEmbedding (incl‚ÇÇ K D) := by
  ¬∑ apply Topology.IsClosedEmbedding.comp
    ¬∑ exact { toIsEmbedding := Units.isEmbedding_embedProduct, isClosed_range :=
        embedProduct_closed D_ùî∏}
    ¬∑ refine Topology.IsClosedEmbedding.of_continuous_injective_isClosedMap
        (continuous_iff_le_induced.mpr fun U a ‚Ü¶ a)
        (Subgroup.subtype_injective (ringHaarChar_ker (D ‚äó[K] AdeleRing (ùìû K) K))) ?_
      simp only [Subgroup.coe_subtype]
      refine Topology.IsInducing.isClosedMap ({ eq_induced := rfl }) ?_
      simp only [Subtype.range_coe_subtype, SetLike.setOf_mem_eq]
      exact IsClosed.preimage (continuous_id')
        (IsClosed.preimage (map_continuous ringHaarChar) (by simp))

lemma ImAux_isCompact : IsCompact ((fun p ‚Ü¶ (p.1, MulOpposite.op p.2)) '' Aux.C K D) := by
  refine IsCompact.image (Aux.C_compact K D) (Continuous.prodMk (continuous_fst) ?_)
  refine Continuous.comp ?_ (continuous_snd)
  rw [continuous_induced_rng]
  exact { isOpen_preimage := fun s a ‚Ü¶ a }

lemma M_compact : IsCompact (M K D) := Topology.IsClosedEmbedding.isCompact_preimage
  (incl‚ÇÇ_isClosedEmbedding K D) (ImAux_isCompact K D)

lemma compact_quotient : CompactSpace (_root_.Quotient (QuotientGroup.rightRel
    ((MonoidHom.range (incl K D)).comap (ringHaarChar_ker D_ùî∏).subtype))) :=
  isCompact_univ_iff.mp (by simpa only [toQuot_surjective, Set.image_univ] using
    (((IsCompact.image (M_compact K D) (toQuot_cont K D)))))

end NumberField.AdeleRing.DivisionAlgebra

section FiniteAdeleRing

variable [FiniteDimensional K D]

-- Instance to help speed up instance synthesis
instance : NonUnitalNonAssocRing (D ‚äó[K] (FiniteAdeleRing (ùìû K) K)) :=
  let r := Algebra.TensorProduct.instRing.toNonUnitalRing
  r.toNonUnitalNonAssocRing

-- Instance to help speed up instance synthesis
instance : NonAssocSemiring (D ‚äó[K] (FiniteAdeleRing (ùìû K) K)) :=
  Algebra.TensorProduct.instRing.toNonAssocSemiring

variable [Algebra.IsCentral K D]

/-- Dfx is notation for (D ‚äó ùî∏_K^‚àû)À£. -/
abbrev Dfx := (D ‚äó[K] (FiniteAdeleRing (ùìû K) K))À£

/-- The inclusion DÀ£ ‚Üí (D ‚äó ùî∏_K^‚àû)À£ as a group homomorphism. -/
noncomputable abbrev incl‚ÇÅ : DÀ£ ‚Üí* Dfx K D :=
  Units.map Algebra.TensorProduct.includeLeftRingHom.toMonoidHom

open scoped TensorProduct.RightActions in
theorem NumberField.FiniteAdeleRing.DivisionAlgebra.units_cocompact :
    CompactSpace (Dfx K D ‚ß∏ (incl‚ÇÅ K D).range) := by
  sorry

-- Voight "Main theorem 27.6.14(b) (Fujisaki's lemma)"
/-!
If `D` is a finite-dimensional division algebra over a number field `K`
then the double coset space `DÀ£ \ (D ‚äó ùî∏_K^infty)À£ / U` is finite for any compact open subgroup `U`
of `(D ‚äó ùî∏_F^infty)À£`.
-/
open scoped TensorProduct.RightActions in
theorem NumberField.FiniteAdeleRing.DivisionAlgebra.finiteDoubleCoset
    {U : Subgroup (Dfx K D)} (hU : IsOpen (U : Set (Dfx K D))) :
    Finite (Doset.Quotient (Set.range (incl‚ÇÅ K D)) U) := by
  sorry


end FiniteAdeleRing
