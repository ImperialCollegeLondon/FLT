/-
Copyright (c) 2024 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, William Coram
-/
import FLT.HaarMeasure.HaarChar.AdeleRing
import FLT.Mathlib.GroupTheory.DoubleCoset
import FLT.Mathlib.Topology.HomToDiscrete
import FLT.HaarMeasure.HaarChar.RealComplex
import FLT.Mathlib.LinearAlgebra.TensorProduct.Basis
import FLT.Mathlib.MeasureTheory.Haar.Extension
import FLT.Mathlib.MeasureTheory.Measure.Haar.MulEquivHaarChar
import FLT.Mathlib.LinearAlgebra.TensorProduct.Basis
import FLT.Mathlib.Topology.HomToDiscrete
import FLT.Mathlib.Topology.Polish
import Mathlib.Topology.Metrizable.Urysohn
import Mathlib.Topology.MetricSpace.Polish
import Mathlib.Topology.UniformSpace.Completion
import Mathlib.Topology.MetricSpace.Completion
/-

# Fujisaki's lemma

We prove a lemma which Voight (in his quaternion algebra book) attributes to Fujisaki:
if `D` is a finite-dimensional division algebra over a number field `K`
and if `U âŠ† (D âŠ—[K] ğ”¸_K^infty)Ë£` is a compact open subgroup then the double coset
space `DË£ \ (D âŠ—[K] ğ”¸_K^infty)Ë£ / U` is finite.

## Main definitions

Most of the definitions in this file are auxiliary definitions, in an `Aux` namespace.

## Main theorem

Fujisaki's lemma:

* NumberField.FiniteAdeleRing.DivisionAlgebra.finiteDoubleCoset

-/

suppress_compilation

open IsDedekindDomain MeasureTheory NumberField

-- this instance creates a nasty diamond for
-- `IsScalarTower K (FiniteAdeleRing A K) (FiniteAdeleRing B L)` when K = L and A = B, and
-- should probably be scoped (or even removed and statements changed so that they
-- don't need it).
attribute [-instance] instIsScalarTowerFiniteAdeleRing_fLT_1

-- this instance creates a nasty diamond for `IsScalarTower K K_âˆ L_âˆ when K = L and
-- should probably be scoped (or even removed and statements changed so that they
-- don't need it).
attribute [-instance] InfiniteAdeleRing.instIsScalarTower_fLT_1

open scoped TensorProduct

variable (K : Type*) [Field K] [NumberField K]
variable (D : Type*) [DivisionRing D] [Algebra K D]

-- notation for this file

set_option quotPrecheck false in
/-- `D_ğ”¸` is notation for `D âŠ—[K] ğ”¸_K`. -/
notation "D_ğ”¸" => (D âŠ—[K] AdeleRing (ğ“ K) K)

-- abbrevs for this file, in an Aux namespace (as is most of this file;
-- it is local definitions and lemmas which we don't need. All we need
-- is the big result at the end.)

namespace NumberField.AdeleRing.DivisionAlgebra.Aux

/-- Df is notation for D âŠ— ğ”¸_K^âˆ -/
abbrev Df := D âŠ—[K] (FiniteAdeleRing (ğ“ K) K)

/-- Dfx is notation for (D âŠ— ğ”¸_K^âˆ)Ë£. -/
abbrev Dfx := (Df K D)Ë£

/-- Dinf is notation for D âŠ— ğ”¸_K^âˆ -/
abbrev Dinf := D âŠ—[K] (NumberField.InfiniteAdeleRing K)

/-- Dinfx is notation for (D âŠ— ğ”¸_K^âˆ)Ë£ -/
abbrev Dinfx := (Dinf K D)Ë£

/-- The inclusion DË£ â†’ D_ğ”¸Ë£ as a group homomorphism. -/
abbrev incl : DË£ â†’* D_ğ”¸Ë£ :=
  Units.map Algebra.TensorProduct.includeLeftRingHom.toMonoidHom

/-- The inclusion DË£ â†’ (D âŠ— ğ”¸_K^âˆ)Ë£ as a group homomorphism. -/
noncomputable abbrev inclâ‚ : DË£ â†’* Dfx K D :=
  Units.map Algebra.TensorProduct.includeLeftRingHom.toMonoidHom

/-- The inclusion of K^n into ğ”¸^n. -/
abbrev incl_Kn_ğ”¸Kn : (Fin (Module.finrank K D) â†’ K) â†’
    (Fin (Module.finrank K D) â†’ AdeleRing (ğ“ K) K) :=
  fun x i â†¦ algebraMap K (AdeleRing (ğ“ K) K) (x i)

-- instances for Df, Dinf, D_ğ”¸

open scoped TensorProduct.RightActions

/-- The â„-algebra structure on Dinf K D. -/
instance : Algebra â„ (Dinf K D) :=
  RingHom.toAlgebra' ((algebraMap (InfiniteAdeleRing K) (Dinf K D)).comp
  (algebraMap â„ (InfiniteAdeleRing K))) <| by
    intro c x
    rw [RingHom.comp_apply, Algebra.commutes]

instance : IsScalarTower â„ (InfiniteAdeleRing K) (Dinf K D) :=
  IsScalarTower.of_algebraMap_eq (fun _ â†¦ rfl)

variable [FiniteDimensional K D]

/-- We put the Borel measurable space structure on D_ğ”¸ in this file. -/
instance : MeasurableSpace D_ğ”¸ := borel _

instance : BorelSpace D_ğ”¸ := âŸ¨rflâŸ©

instance : Module.Finite â„ (Dinf K D) :=
  Module.Finite.trans (InfiniteAdeleRing K) (Dinf K D)

/-- Dinf K D has the â„-module topology. -/
instance : IsModuleTopology â„ (Dinf K D) := by
  /- (InfiniteAdeleRing K) has the â„-module topology.
    Now since (Dinf K D) has the (InfiniteAdeleRing K)-module topolology it also has the
    â„-module topology.
  -/
  rw [IsModuleTopology.trans â„ (InfiniteAdeleRing K)]
  infer_instance

/-- Dinf K D is given the borel sigma algebra (for Haar measure). -/
instance : MeasurableSpace (Dinf K D) := borel (Dinf K D)

instance : BorelSpace (Dinf K D) := {measurable_eq := rfl }

/-- Df K D is given the borel sigma algebra (for Haar measure). -/
instance : MeasurableSpace (Df K D) := borel (Df K D)

instance : BorelSpace (Df K D) := { measurable_eq := rfl }

-- D âŠ— K_âˆ is second countable because it's a finite â„-module
instance : SecondCountableTopology (Dinf K D) :=
  Module.Finite.secondCountabletopology â„ (Dinf K D)

-- discreteness of K^n in ğ”¸_K^n (which will be used to show discreteness of D in D_ğ”¸)
omit [FiniteDimensional K D] in
theorem Kn_discrete : âˆ€ x : (Fin (Module.finrank K D) â†’ K),
    âˆƒ U : Set (Fin (Module.finrank K D) â†’ AdeleRing (ğ“ K) K),
    IsOpen U âˆ§ (incl_Kn_ğ”¸Kn K D)â»Â¹' U = {x} := by
  exact (DiscretePi (algebraMap K (AdeleRing (ğ“ K) K)) (Module.finrank K D))
    (NumberField.AdeleRing.discrete K)

/-- The K-algebra equivalence of D and K^n. -/
abbrev D_iso : (D â‰ƒâ‚—[K] ((Fin (Module.finrank K D) â†’ K))) := Module.Finite.equivPi K D

-- Mathlib#29315....
attribute [local instance 1100] IsTopologicalSemiring.toIsModuleTopology

-- ...makes this work
example : IsModuleTopology (AdeleRing (ğ“ K) K)
    ((Fin (Module.finrank K D) â†’ AdeleRing (ğ“ K) K)) := inferInstance

/-- The ğ”¸_K-algebra equivalence of D_ğ”¸ and ğ”¸_K^d. -/
abbrev Dğ”¸_iso : (D_ğ”¸ â‰ƒâ‚—[(AdeleRing (ğ“ K) K)] ((Fin (Module.finrank K D) â†’ AdeleRing (ğ“ K) K))) :=
  ((TensorProduct.RightActions.Module.TensorProduct.comm _ _ _).symm).trans
    (TensorProduct.AlgebraTensorModule.finiteEquivPi K D (AdeleRing (ğ“ K) K))

/-- The topological ğ”¸_K-linear equivalence D_ğ”¸ â‰ƒ ğ”¸_K^d. -/
abbrev Dğ”¸_iso_top : D_ğ”¸ â‰ƒL[(AdeleRing (ğ“ K) K)]
    ((Fin (Module.finrank K D) â†’ AdeleRing (ğ“ K) K)) :=
  IsModuleTopology.continuousLinearEquiv (Dğ”¸_iso K D)

theorem D_discrete_aux (U : Set (Fin (Module.finrank K D) â†’ AdeleRing (ğ“ K) K)) :
    incl_Kn_ğ”¸Kn K D â»Â¹' U  =
    (D_iso K D) ''
      (â‡‘(Dğ”¸_iso_top K D) âˆ˜ (Algebra.TensorProduct.includeLeft : D â†’â‚[K] D_ğ”¸) â»Â¹' U) := by
  ext x
  constructor
  Â· intro hx
    use (D_iso K D).symm x
    simpa [â† Algebra.algebraMap_eq_smul_one] using hx
  Â· intro âŸ¨y, hy1, hy2âŸ©
    have : (Dğ”¸_iso_top K D) âˆ˜ (Algebra.TensorProduct.includeLeft : D â†’â‚[K] D_ğ”¸) =
        (incl_Kn_ğ”¸Kn K D) âˆ˜ (D_iso K D) := by
      ext d n
      simp [â† Algebra.algebraMap_eq_smul_one]
    simpa [â† hy2, this] using hy1

theorem D_discrete : âˆ€ x : D, âˆƒ U : Set D_ğ”¸,
    IsOpen U âˆ§ (Algebra.TensorProduct.includeLeft : D â†’â‚[K] D_ğ”¸) â»Â¹' U = {x} := by
  apply Discrete_of_HomeoDiscrete (Y' := ((Fin (Module.finrank K D) â†’ AdeleRing (ğ“ K) K)))
    (Algebra.TensorProduct.includeLeft : D â†’â‚[K] D_ğ”¸) (Dğ”¸_iso_top K D)
  apply Discrete_of_HomDiscrete (X' := Fin (Module.finrank K D) â†’ K)
    ((Dğ”¸_iso_top K D) âˆ˜ (Algebra.TensorProduct.includeLeft : D â†’â‚[K] D_ğ”¸)) (D_iso K D)
  simpa [D_discrete_aux] using Kn_discrete K D

/-- The additive subgroup D of D_ğ”¸. -/
def includeLeft_subgroup : AddSubgroup D_ğ”¸ :=
  AddMonoidHom.range (G := D) (Algebra.TensorProduct.includeLeft : D â†’â‚[K] D_ğ”¸)

instance discrete_includeLeft_subgroup :
    DiscreteTopology (includeLeft_subgroup K D).carrier := by
  rw [includeLeft_subgroup, discreteTopology_iff_isOpen_singleton]
  rintro âŸ¨a, a', rflâŸ©
  obtain âŸ¨U, hUopen, hUeqâŸ© := (D_discrete K D) a'
  refine isOpen_mk.mpr âŸ¨U, hUopen, Set.image_val_inj.mp ?_âŸ©
  simp only [Subtype.image_preimage_coe, Set.image_singleton]
  let f : D â†’ D âŠ—[K] AdeleRing (ğ“ K) K :=
    (Algebra.TensorProduct.includeLeft : D â†’â‚[K] D âŠ—[K] AdeleRing (ğ“ K) K)
  change Set.range f âˆ© U = {f a'}
  change f â»Â¹' U = {a'} at hUeq
  ext d
  simp [Set.ext_iff] at hUeq
  grind

instance : T2Space (D âŠ—[K] AdeleRing (ğ“ K) K) := IsModuleTopology.t2Space (AdeleRing (ğ“ K) K)

instance discrete_principalSubgroup :
    DiscreteTopology (principalSubgroup (ğ“ K) K : Set (AdeleRing (ğ“ K) K)) := by
  rw [discreteTopology_iff_isOpen_singleton]
  rintro âŸ¨-, y, rflâŸ©
  obtain âŸ¨U, hUopen, hUâŸ© := NumberField.AdeleRing.discrete K y
  refine isOpen_mk.mpr âŸ¨U, hUopen, Set.image_val_inj.mp ?_âŸ©
  simp only [Subtype.image_preimage_coe, Set.image_singleton]
  let f : K â†’ AdeleRing (ğ“ K) K := algebraMap K (AdeleRing (ğ“ K) K)
  change Set.range f âˆ© U = {f y}
  change f â»Â¹' U = {y} at hU
  ext d
  simp [Set.ext_iff] at hU
  grind

-- we seem to have this twice?
instance compact_includeLeft_subgroup :
    CompactSpace (D_ğ”¸ â§¸ (includeLeft_subgroup K D)) := by
  let H := includeLeft_subgroup K D
  change CompactSpace (D_ğ”¸ â§¸ H)
  have key := NumberField.AdeleRing.cocompact K
  let Ï€ : (Fin (Module.finrank K D) â†’ AdeleRing (ğ“ K) K) â†’+
      (Fin (Module.finrank K D) â†’ AdeleRing (ğ“ K) K â§¸ principalSubgroup (ğ“ K) K) :=
    AddMonoidHom.compLeft (QuotientAddGroup.mk' _) _
  have hÏ€1 : Continuous Ï€ := by
    simp only [Ï€, AddMonoidHom.compLeft, QuotientAddGroup.coe_mk', AddMonoidHom.coe_mk,
      ZeroHom.coe_mk]
    fun_prop
  have hÏ€2 : IsOpenQuotientMap Ï€ := by
    have : DiscreteTopology (principalSubgroup (ğ“ K) K : Set (AdeleRing (ğ“ K) K)) :=
      discrete_principalSubgroup K
    have key := TopologicalAddGroup.IsSES.ofClosedAddSubgroup (principalSubgroup (ğ“ K) K)
    exact IsOpenQuotientMap.piMap (fun _ â†¦ key.isOpenQuotientMap)
  let Ï† : (Fin (Module.finrank K D) â†’ AdeleRing (ğ“ K) K) â†’+ (D_ğ”¸ â§¸ H) :=
    AddMonoidHom.comp (QuotientAddGroup.mk' _) (Dğ”¸_iso_top K D).symm.toAddMonoidHom
  have hÏ†0 : Ï€.ker â‰¤ Ï†.ker := by
    intro x hx
    replace hx : âˆ€ i, x i âˆˆ Set.range (algebraMap K (AdeleRing (ğ“ K) K)) := by
      simpa [Ï€, funext_iff] using hx
    choose q hq using hx
    let d := (D_iso K D).symm q
    simp only [Algebra.algebraMap_eq_smul_one] at hq
    simp only [Ï†, AddMonoidHom.mem_ker, AddMonoidHom.comp_apply,
      QuotientAddGroup.mk'_apply, QuotientAddGroup.eq_zero_iff]
    use d
    simp only [LinearMap.toAddMonoidHom_coe, ContinuousLinearEquiv.toLinearEquiv_symm,
      LinearEquiv.coe_coe, LinearEquiv.eq_symm_apply]
    simp [d, hq]
  have hÏ†1 : Continuous Ï† := by
    simp only [Ï†, AddMonoidHom.coe_comp, QuotientAddGroup.coe_mk', LinearMap.toAddMonoidHom_coe]
    fun_prop
  have hÏ†2 : Function.Surjective Ï† :=
    (QuotientAddGroup.mk'_surjective _).comp (Dğ”¸_iso_top K D).symm.surjective
  let f : (Fin (Module.finrank K D) â†’ AdeleRing (ğ“ K) K â§¸ principalSubgroup (ğ“ K) K) â†’+
    (D_ğ”¸ â§¸ H) := AddMonoidHom.liftOfSurjective Ï€ hÏ€2.surjective âŸ¨Ï†, hÏ†0âŸ©
  have hf0 : f âˆ˜ Ï€ = Ï† := by ext; simp [f]
  have hf1 : Continuous f := by rwa [â† hÏ€2.continuous_comp_iff, hf0]
  have hf2 : Function.Surjective f := by rwa [â† hÏ€2.surjective.of_comp_iff, hf0]
  rw [â† isCompact_univ_iff, â† Set.image_univ_of_surjective hf2]
  exact isCompact_univ.image hf1

open scoped NNReal in
lemma not_injective_of_large_measure : âˆƒ B : â„â‰¥0, âˆ€ U : Set D_ğ”¸,
   IsOpen U â†’ B < MeasureTheory.Measure.addHaar U â†’
    Â¬ U.InjOn (QuotientAddGroup.mk : D_ğ”¸ â†’
        D_ğ”¸ â§¸ (Algebra.TensorProduct.includeLeftRingHom : D â†’+* D_ğ”¸).range.toAddSubgroup) := by
  let H := includeLeft_subgroup K D
  have : DiscreteTopology H := discrete_includeLeft_subgroup K D
  have : SecondCountableTopology (D âŠ—[K] AdeleRing (ğ“ K) K) :=
    Homeomorph.secondCountableTopology (Dğ”¸_iso_top K D).toHomeomorph
  have : PolishSpace (D âŠ—[K] AdeleRing (ğ“ K) K) := polish_of_locally_compact_second_countable _
  exact TopologicalAddGroup.IsSES.not_injOn_of_measure_gt H

section FiniteAdeleRing

/-- The K-algebra isomorphism `D_ğ”¸ â‰ƒ D_âˆ Ã— D_f` -- adelic D is infinite adele D times
finite adele D. -/
abbrev Dğ”¸_prodRight : D_ğ”¸ â‰ƒâ‚[K] Dinf K D Ã— Df K D :=
  (Algebra.TensorProduct.prodRight K K D (InfiniteAdeleRing K) (FiniteAdeleRing (ğ“ K) K))

/-- The (InfiniteAdeleRing K Ã— FiniteAdeleRing (ğ“ K) K)-module structure on (Dinf K D Ã— Df K D). -/
instance : Module (AdeleRing (ğ“ K) K) (Dinf K D Ã— Df K D) where
  smul rs mn := (rs.1 â€¢ mn.1, rs.2 â€¢ mn.2)
  one_smul mn := by cases mn; ext; exacts [one_smul _ _, one_smul _ _]
  mul_smul rs rs' mn := by
    cases rs; cases rs'; cases mn
    ext <;>
    exact mul_smul _ _ _
  smul_zero rs := by cases rs; ext <;> exact smul_zero _
  smul_add rs mn mn' := by
    cases rs; cases mn; cases mn'
    ext <;>
    exact smul_add _ _ _
  add_smul rs rs' mn := by
    cases rs; cases rs'; cases mn
    ext <;>
    exact add_smul _ _ _
  zero_smul mn := by cases mn; ext <;> exact zero_smul _ _

/-- (Dinf K D Ã— Df K D) has the ğ”¸_K module topology. -/
instance [FiniteDimensional K D] :
    IsModuleTopology (AdeleRing (ğ“ K) K) (Dinf K D Ã— Df K D) :=
  IsModuleTopology.instProd'

/-- The ğ”¸_K linear map `D_ğ”¸ â‰ƒ D_âˆ Ã— D_f`. -/
abbrev Dğ”¸_prodRight' : D_ğ”¸ â‰ƒâ‚—[AdeleRing (ğ“ K) K] (Dinf K D Ã— Df K D) := {
  toFun := Dğ”¸_prodRight K D
  __ := Dğ”¸_prodRight K D
  map_add' a b := by
    exact RingHom.map_add (Dğ”¸_prodRight K D).toRingHom a b
  map_smul' m x := by
    simp only [RingHom.id_apply]
    obtain âŸ¨s, hxâŸ© := TensorProduct.exists_finset x
    simp_rw [hx, Finset.smul_sum, map_sum, TensorProduct.RightActions.smul_def,
      TensorProduct.comm_tmul, TensorProduct.smul_tmul', TensorProduct.comm_symm_tmul,
      Finset.smul_sum]
    -- missing lemma probably
    rfl
}

/-- The continuous additive isomorphism `D_ğ”¸ â‰ƒ D_âˆ Ã— D_f`. -/
abbrev Dğ”¸_prodRight'' : D_ğ”¸ â‰ƒâ‚œ+ Dinf K D Ã— Df K D where
  __ := Dğ”¸_prodRight K D
  continuous_toFun := IsModuleTopology.continuous_of_linearMap (Dğ”¸_prodRight' K D).toLinearMap
  continuous_invFun := IsModuleTopology.continuous_of_linearMap (Dğ”¸_prodRight' K D).symm.toLinearMap

/-- The equivalence of the units of D_ğ”¸ and the product of the
units of (D âŠ— ğ”¸_K^f) and (D âŠ— ğ”¸_K^âˆ). -/
abbrev Dğ”¸_prodRight_units : D_ğ”¸Ë£ â‰ƒ* (Dinfx K D) Ã— (Dfx K D) :=
  (Units.mapEquiv (Dğ”¸_prodRight K D)).trans (MulEquiv.prodUnits)

omit [FiniteDimensional K D] in
lemma smul_Dğ”¸_prodRight_symm (a : (Dinf K D)Ë£) (b : (Df K D)Ë£)
    (di : Dinf K D) (df : Df K D) :
  ((Dğ”¸_prodRight_units K D).symm (a, b)) â€¢ ((Dğ”¸_prodRight K D).symm (di, df)) =
    (Dğ”¸_prodRight K D).symm (a â€¢ di, b â€¢ df) :=
  (map_mul _ _ _).symm


lemma Dğ”¸_prodRight_units_cont : Continuous (Dğ”¸_prodRight_units K D) := by
  rw [ MulEquiv.coe_trans]
  -- annoying that fun_prop and continuity can't seem to do this
  -- it's about monoid and ring homs, it's the usual problem.
  apply Continuous.comp ?_ ?_
  Â· apply Continuous.prodMk
    Â· apply Continuous.units_map
      exact continuous_fst
    Â· apply Continuous.units_map
      exact continuous_snd
  Â· apply Continuous.units_map
    exact IsModuleTopology.continuous_of_linearMap (Dğ”¸_prodRight' K D).toLinearMap

lemma ringHaarChar_Dğ”¸ (a : Dinfx K D) (b : Dfx K D) :
    ringHaarChar ((Dğ”¸_prodRight_units K D).symm (a, b)) =
    ringHaarChar (MulEquiv.prodUnits.symm (a, b)) := by
  apply MeasureTheory.addEquivAddHaarChar_eq_addEquivAddHaarChar_of_continuousAddEquiv
    (Dğ”¸_prodRight'' K D)
  simp [MulEquivClass.map_mul]

/-- For any positive real `r`, there's some `Ï âˆˆ â„Ë£` such that the haar character of
`(Ï, 1) âˆˆ D_f Ã— D_âˆ` is `r`. -/
lemma ringHaarChar_Dğ”¸_real_surjective (r : â„) (h : r > 0) :
    âˆƒ (Ï : â„Ë£), ringHaarChar
      ((Dğ”¸_prodRight_units K D).symm (((Units.map (algebraMap â„ (Dinf K D))) Ï),1)) = r := by
  have a : IsUnit (r ^ (1 / Module.finrank â„ (Dinf K D) : â„)) := by
    simp only [one_div, isUnit_iff_ne_zero, ne_eq]
    refine (Real.rpow_ne_zero (by positivity) ?_).mpr (by positivity)
    simp [Nat.ne_zero_iff_zero_lt, Module.finrank_pos]
  have := ringHaarChar_ModuleFinite_unit (K := â„) (R := Dinf K D) (a.unit)
  use a.unit
  rw [ringHaarChar_Dğ”¸, ringHaarChar_prod, map_one, mul_one]
  simp_all only [gt_iff_lt, RingHom.toMonoidHom_eq_coe, NNReal.coe_pow]
  have t : (ringHaarChar a.unit) = r ^ ((1 / Module.finrank â„ (Dinf K D) : â„)) := by
    simp_rw [MeasureTheory.ringHaarChar_real, IsUnit.unit_spec, coe_nnnorm, Real.norm_eq_abs,
      one_div, abs_eq_self]
    positivity
  simp_rw [t, one_div]
  exact Real.rpow_inv_natCast_pow (by positivity) (Nat.ne_zero_iff_zero_lt.mpr Module.finrank_pos)

end FiniteAdeleRing

section AdeleRing

instance (vi : InfinitePlace K) : SecondCountableTopology (D âŠ—[K] vi.Completion) :=
  Module.Finite.secondCountabletopology vi.Completion _

open scoped TensorProduct.RightActions in
variable
  [(vi : InfinitePlace K) â†’ MeasurableSpace (D âŠ—[K] vi.Completion)]
  [(vi : InfinitePlace K) â†’ BorelSpace (D âŠ—[K] vi.Completion)] in
lemma isCentralSimple_infinite_addHaarScalarFactor_left_mul_eq_right_mul_aux
    [Algebra.IsCentral K D] (u : (Î  vi : InfinitePlace K, (D âŠ—[K] vi.Completion))Ë£) :
    addEquivAddHaarChar (ContinuousAddEquiv.mulLeft u) =
    addEquivAddHaarChar (ContinuousAddEquiv.mulRight u) := by
  open MeasureTheory in
  have : BorelSpace (Î  vi : InfinitePlace K, (D âŠ—[K] vi.Completion)) := Pi.borelSpace
  let u' := MulEquiv.piUnits u
  have hl :
      addEquivAddHaarChar (ContinuousAddEquiv.mulLeft u)
      = âˆ vi, addEquivAddHaarChar (ContinuousAddEquiv.mulLeft (u' vi)) := by
    rw [â† addEquivAddHaarChar_piCongrRight (fun vi â†¦ ContinuousAddEquiv.mulLeft (u' vi))]
    congr
  have hr :
      addEquivAddHaarChar (ContinuousAddEquiv.mulRight u)
      = âˆ vi, addEquivAddHaarChar (ContinuousAddEquiv.mulRight (u' vi)) := by
    rw [â† addEquivAddHaarChar_piCongrRight (fun vi â†¦ ContinuousAddEquiv.mulRight (u' vi))]
    congr
  rw [hl, hr]; congr; funext vi
  apply
    IsSimpleRing.ringHaarChar_eq_addEquivAddHaarChar_mulRight (F := vi.Completion) (u' vi)

open Classical in
/-- The canonical `â„`-algebra structure on `InfinitePlace.Completion`. -/
def real_to_completion (vi : InfinitePlace K) : â„ â†’+* vi.Completion :=
  if h : vi.IsReal
  then (InfinitePlace.Completion.ringEquivRealOfIsReal h).symm.toRingHom
  else
    (InfinitePlace.Completion.ringEquivComplexOfIsComplex (by simpa using h)).symm.toRingHom.comp
    Complex.ofRealHom

-- TODO: fix this approach in view of the diamond created with things like
-- `instAlgebraRealInfiniteAdeleRing_fLT`
-- (but everything below works, so I'm hesitant to touch it for now)

instance (vi : InfinitePlace K) : Algebra â„ vi.Completion :=
  (real_to_completion K vi).toAlgebra

omit [NumberField K] in
lemma algebraMap_completion_def (vi : InfinitePlace K) :
    (algebraMap â„ vi.Completion) = (real_to_completion K vi) := rfl

instance (vi : InfinitePlace K) : Module.Finite â„ vi.Completion := by
  by_cases h : vi.IsReal
  Â· let e : vi.Completion â‰ƒâ‚—[â„] â„ := {
      __ := InfinitePlace.Completion.ringEquivRealOfIsReal h
      map_smul' r x := by
        simp_all [Algebra.smul_def, algebraMap_completion_def, real_to_completion, â†“reduceDIte]
    }
    exact Module.Finite.of_injective _ e.injective
  Â· let e : vi.Completion â‰ƒâ‚—[â„] â„‚ := {
      __ := InfinitePlace.Completion.ringEquivComplexOfIsComplex (by simpa using h)
      map_smul' r x := by
        simp_all [Algebra.smul_def, algebraMap_completion_def, real_to_completion, â†“reduceDIte]
    }
    exact Module.Finite.of_injective _ e.injective

instance (vi : InfinitePlace K) : ContinuousSMul â„ vi.Completion := by
  refine continuousSMul_of_algebraMap â„ vi.Completion ?_
  rw [algebraMap_completion_def]
  by_cases h : vi.IsReal
  Â· convert (InfinitePlace.Completion.isometryEquivRealOfIsReal h).symm.isometry_toFun.continuous
    funext x; simp_all [real_to_completion, â†“reduceDIte]; rfl
  Â· convert (InfinitePlace.Completion.isometryEquivComplexOfIsComplex
      (by simpa using h)).symm.isometry_toFun.continuous.comp Complex.continuous_ofReal
    funext x; simp_all [real_to_completion, â†“reduceDIte]; rfl

instance (vi : InfinitePlace K) : IsModuleTopology â„ vi.Completion :=
  isModuleTopologyOfFiniteDimensional

open scoped TensorProduct.RightActions in
instance (vi : InfinitePlace K) : Algebra â„ (D âŠ—[K] vi.Completion) :=
  Algebra.compHom _ <| real_to_completion K vi

open scoped TensorProduct.RightActions in
instance (vi : InfinitePlace K) : IsScalarTower â„ vi.Completion (D âŠ—[K] vi.Completion) :=
  IsScalarTower.of_compHom ..

open scoped TensorProduct.RightActions in
instance (vi : InfinitePlace K) : IsModuleTopology â„ (D âŠ—[K] vi.Completion) := by
  rw [IsModuleTopology.trans â„ vi.Completion]
  infer_instance

open scoped TensorProduct.RightActions in
instance : IsModuleTopology â„ (Î  vi : InfinitePlace K, (D âŠ—[K] vi.Completion)) :=
  IsModuleTopology.instPi

omit [NumberField K] in
lemma algebraMap_completion {vi : InfinitePlace K} {x : â„} :
    (algebraMap â„ (InfiniteAdeleRing K)) x vi = (algebraMap â„ vi.Completion) x := by
  change
    ((InfiniteAdeleRing.ringEquiv_mixedSpace K).symm.toRingHom.comp (algebraMap â„ _)) x vi
    = real_to_completion K vi x
  by_cases h : vi.IsReal
  Â· simp_all [real_to_completion, â†“reduceDIte,
      RingEquiv.piEquivPiSubtypeProd, Equiv.piEquivPiSubtypeProd]
    rfl
  Â· simp_all [-InfinitePlace.not_isReal_iff_isComplex, real_to_completion, â†“reduceDIte,
      RingEquiv.piEquivPiSubtypeProd, Equiv.piEquivPiSubtypeProd]
    rfl

lemma tensorPi_equiv_piTensor_map_mul {x y : Dinf K D} :
    tensorPi_equiv_piTensor K D InfinitePlace.Completion (x * y)
    = tensorPi_equiv_piTensor K D InfinitePlace.Completion x
      * tensorPi_equiv_piTensor K D InfinitePlace.Completion y :=
  -- we need that `tensorPi_equiv_piTensor` is a ring hom
  sorry

open scoped TensorProduct.RightActions in
/-- `tensorPi_equiv_piTensor` applied to `Dinf`, as a `â„`-linear map. -/
def Dinf_tensorPi_equiv_piTensor_aux :
    (Dinf K D) â‰ƒâ‚—[â„] Î  vi : InfinitePlace K, (D âŠ—[K] vi.Completion) := {
  __ := tensorPi_equiv_piTensor K D InfinitePlace.Completion
  map_smul' x y := by
    change tensorPi_equiv_piTensor K D InfinitePlace.Completion (x â€¢ y)
      = x â€¢ tensorPi_equiv_piTensor K D InfinitePlace.Completion y
    simp only [Algebra.smul_def, tensorPi_equiv_piTensor_map_mul];
    congr
    have hâ‚ : (algebraMap â„ (Dinf K D)) x = 1 âŠ—â‚œ[K] (algebraMap â„ (InfiniteAdeleRing K) x) := rfl
    have hâ‚‚ :
        (algebraMap â„ ((i : InfinitePlace K) â†’ D âŠ—[K] i.Completion)) x
        = fun (i : InfinitePlace K) â†¦ 1 âŠ—â‚œ[K] (algebraMap â„ i.Completion x) := rfl
    rw [hâ‚, hâ‚‚, tensorPi_equiv_piTensor_apply]
    funext vi
    congr
    exact algebraMap_completion _
}

open scoped TensorProduct.RightActions in
/-- `tensorPi_equiv_piTensor` applied to `Dinf`, as a continuous `â„`-linear equiv. -/
def Dinf_tensorPi_equiv_piTensor :
    (Dinf K D) â‰ƒL[â„] Î  vi : InfinitePlace K, (D âŠ—[K] vi.Completion) := {
  __ := Dinf_tensorPi_equiv_piTensor_aux ..
  continuous_toFun :=
    IsModuleTopology.continuous_of_linearMap (Dinf_tensorPi_equiv_piTensor_aux K D).toLinearMap
  continuous_invFun :=
    IsModuleTopology.continuous_of_linearMap (Dinf_tensorPi_equiv_piTensor_aux K D).symm.toLinearMap
}

open scoped TensorProduct.RightActions in
/-- `tensorPi_equiv_piTensor` applied to `Dinf`, as a monoid hom. -/
def Dinf_tensorPi_equiv_piTensor_monoidHom :
    (Dinf K D) â†’* Î  vi : InfinitePlace K, (D âŠ—[K] vi.Completion) := {
  __ := Dinf_tensorPi_equiv_piTensor K D
  map_one' := by
    rw [Algebra.TensorProduct.one_def]
    simp [Dinf_tensorPi_equiv_piTensor, Dinf_tensorPi_equiv_piTensor_aux,
      Dinf, InfiniteAdeleRing, tensorPi_equiv_piTensor_apply]
    rfl
  map_mul' _ _ := tensorPi_equiv_piTensor_map_mul ..
}

open scoped TensorProduct.RightActions in
lemma isCentralSimple_infinite_addHaarScalarFactor_left_mul_eq_right_mul
    [Algebra.IsCentral K D] (u : (Dinf K D)Ë£) :
    addEquivAddHaarChar (ContinuousAddEquiv.mulLeft u) =
    addEquivAddHaarChar (ContinuousAddEquiv.mulRight u) := by
  -- infinite places
  open MeasureTheory in
  let (vi : InfinitePlace K) : MeasurableSpace (D âŠ—[K] vi.Completion) := borel _
  have (vi : InfinitePlace K) : BorelSpace (D âŠ—[K] vi.Completion) := âŸ¨rflâŸ©
  let e := Dinf_tensorPi_equiv_piTensor K D
  let u' := Units.map (Dinf_tensorPi_equiv_piTensor_monoidHom K D) u
  have hl :
      addEquivAddHaarChar (ContinuousAddEquiv.mulLeft u)
      = addEquivAddHaarChar (ContinuousAddEquiv.mulLeft u') := by
    apply addEquivAddHaarChar_eq_addEquivAddHaarChar_of_continuousAddEquiv {__ := e}
    intro x; have : e (u * x) = u' * e x := tensorPi_equiv_piTensor_map_mul ..
    simpa
  have hr :
      addEquivAddHaarChar (ContinuousAddEquiv.mulRight u)
      = addEquivAddHaarChar (ContinuousAddEquiv.mulRight u') := by
    apply addEquivAddHaarChar_eq_addEquivAddHaarChar_of_continuousAddEquiv {__ := e}
    intro x; have : e (x * u) = e x * u' := tensorPi_equiv_piTensor_map_mul ..
    simpa
  rw [hl, hr]
  apply isCentralSimple_infinite_addHaarScalarFactor_left_mul_eq_right_mul_aux

open scoped TensorProduct.RightActions in
lemma isCentralSimple_addHaarScalarFactor_left_mul_eq_right_mul
    [Algebra.IsCentral K D] (u : D_ğ”¸Ë£) :
    addEquivAddHaarChar (ContinuousAddEquiv.mulLeft u) =
    addEquivAddHaarChar (ContinuousAddEquiv.mulRight u) := by
  open IsDedekindDomain MeasureTheory in
  let u' := Dğ”¸_prodRight_units K D u
  have hl :
      addEquivAddHaarChar (ContinuousAddEquiv.mulLeft u)
      = addEquivAddHaarChar (ContinuousAddEquiv.mulLeft u'.1)
      * addEquivAddHaarChar (ContinuousAddEquiv.mulLeft u'.2) := by
    rw [â† addEquivAddHaarChar_prodCongr
      (ContinuousAddEquiv.mulLeft u'.1) (ContinuousAddEquiv.mulLeft u'.2)]
    apply addEquivAddHaarChar_eq_addEquivAddHaarChar_of_continuousAddEquiv (Dğ”¸_prodRight'' K D)
    intro x; simp; rfl
  have hr :
      addEquivAddHaarChar (ContinuousAddEquiv.mulRight u)
      = addEquivAddHaarChar (ContinuousAddEquiv.mulRight u'.1)
      * addEquivAddHaarChar (ContinuousAddEquiv.mulRight u'.2) := by
    rw [â† addEquivAddHaarChar_prodCongr
      (ContinuousAddEquiv.mulRight u'.1) (ContinuousAddEquiv.mulRight u'.2)]
    apply addEquivAddHaarChar_eq_addEquivAddHaarChar_of_continuousAddEquiv (Dğ”¸_prodRight'' K D)
    intro x; simp; rfl
  simp [hl, hr, Dinfx, Dfx, Df,
    isCentralSimple_infinite_addHaarScalarFactor_left_mul_eq_right_mul _,
    isCentralSimple_finite_addHaarScalarFactor_left_mul_eq_right_mul K D _]

end AdeleRing

section auxiliary_defs
-- We need a subset of D âŠ—[K] ğ”¸_K^f with positive finite measure
-- and a subset of D âŠ—[K] K_âˆ with positive finite measure. We build them
-- as compact neighbourhoods of 0.

/-- An auxiliary compact subset of D_ğ”¸^f with nonempty interior -/
def Uf : Set (D âŠ—[K] (FiniteAdeleRing (ğ“ K) K)) := (exists_compact_mem_nhds 0).choose

theorem Uf.spec : IsCompact (Uf K D) âˆ§ (Uf K D) âˆˆ nhds 0 := (exists_compact_mem_nhds 0).choose_spec

/-- An auxiliary compact subset of D_âˆ with nonempty interior -/
def Ui : Set (D âŠ—[K] (InfiniteAdeleRing K)) := (exists_compact_mem_nhds 0).choose

theorem Ui.spec : IsCompact (Ui K D) âˆ§ (Ui K D) âˆˆ nhds 0 := (exists_compact_mem_nhds 0).choose_spec

end auxiliary_defs

open scoped Pointwise

open InfinitePlace.Completion Set Rat RestrictedProduct in
/-- An auxiliary definition of an increasing family of compact
subsets of D_ğ”¸, defined as the product of a compact neighbourhood of 0
at the finite places and a compact neighbourhood of 0, scaled by `r`,
at the infinite places.
-/
def Efamily (r : â„) : Set (D_ğ”¸) :=
  (Dğ”¸_prodRight'' K D).symm '' (r â€¢ Ui K D) Ã—Ë¢ (Uf K D)

lemma E_family_compact (r : â„) : IsCompact (Efamily K D r) := by
  refine IsCompact.image ?_ (by fun_prop)
  refine IsCompact.prod ?_ (Uf.spec K D).1
  exact IsCompact.image (Ui.spec K D).1 (show Continuous (fun x â†¦ r â€¢ x) by fun_prop)

lemma E_family_nonempty_interior : (interior (Efamily K D 1)).Nonempty := by
  unfold Efamily
  rw [one_smul]
  change (interior ((Dğ”¸_prodRight'' K D).toHomeomorph.symm '' Ui K D Ã—Ë¢ Uf K D)).Nonempty
  rw [â† Homeomorph.image_interior, Set.image_nonempty]
  use 0
  rw [mem_interior_iff_mem_nhds, Prod.zero_eq_mk, mem_nhds_prod_iff]
  exact âŸ¨Ui K D, (Ui.spec K D).2, Uf K D, (Uf.spec K D).2, subset_rflâŸ©

open NNReal ENNReal in
lemma E_family_unbounded (B : â„â‰¥0) :
    âˆƒ r, MeasureTheory.Measure.addHaar (Efamily K D r) > B := by
  -- For r a nonzero real, let di(r) be the image of r in D_âˆË£
  let di (r : â„Ë£) : (Dinf K D)Ë£ := ((Units.map (algebraMap â„ (Dinf K D))) r)
  -- For r a nonzero real, let d(r) be the element (r,1)=(di(r),i) in D_ğ”¸Ë£, so it's
  -- r at the infinite places.
  let d : â„Ë£ â†’ D_ğ”¸Ë£ :=
    fun r â†¦ (Dğ”¸_prodRight_units K D).symm (di r, 1)
  -- By definition, scaling a set by `u` changes its measure by the Haar character of u.
  have hscale := ringHaarChar_mul_volume
    (MeasureTheory.Measure.addHaar : Measure (D âŠ—[K] AdeleRing (ğ“ K) K))
    (X := Efamily K D 1)
  -- We now make the obvious claim that the r'th element of the family
  -- is what you get by scaling the 1st element by d(r) -- this is
  -- true by definition.
  have hfamily (r : â„Ë£) : Efamily K D r = (d r) â€¢ Efamily K D 1 := by
    ext x
    -- this is just unfolding definitions
    unfold Efamily
    rw [Set.mem_smul_set, Set.mem_image]
    constructor
    Â· rintro âŸ¨âŸ¨xi, xfâŸ©, âŸ¨âŸ¨a, ha, rflâŸ©, hfâŸ©, rflâŸ©
      use (Dğ”¸_prodRight'' K D).symm (a, xf)
      simp only [one_smul, Set.mem_image, Set.mem_prod, EmbeddingLike.apply_eq_iff_eq,
        exists_eq_right]
      refine âŸ¨âŸ¨ha, hfâŸ©, ?_âŸ©
      simp only [d, Units.smul_def]
      nth_rw 2 [â† one_smul (Df K D) xf]
      convert smul_Dğ”¸_prodRight_symm K D ((Units.map â†‘(algebraMap â„ (Dinf K D))) r) 1 a xf
    Â· rintro âŸ¨-, âŸ¨âŸ¨a, bâŸ©, âŸ¨hzi, hzfâŸ©, rflâŸ©, rflâŸ©
      use (r â€¢ a, b)
      simp only [one_smul] at hzi
      refine âŸ¨âŸ¨âŸ¨a, hzi, rflâŸ©, hzfâŸ©, ?_âŸ©
      convert (smul_Dğ”¸_prodRight_symm K D ((Units.map â†‘(algebraMap â„ (Dinf K D))) r) 1 a b).symm
      simp
  -- Now Efamily 1 is a compact neighbourhood of 1 so it has Haar measure which isn't 0 or âˆ
  have hpos : Measure.addHaar (Efamily K D 1) â‰  0 := by
    refine (MeasureTheory.Measure.measure_pos_of_nonempty_interior _ ?_).ne'
    apply E_family_nonempty_interior
  have hfin : Measure.addHaar (Efamily K D 1) â‰  âˆ :=
    IsCompact.measure_ne_top (E_family_compact K D 1)
  have hm : (Measure.addHaar (Efamily K D 1)).toNNReal â‰  0 := by
    rw [toNNReal_ne_zero]
    exact âŸ¨hpos, hfinâŸ©
  -- Choose Ï such that the Haar charaacter of d(Ï) is (B + 1) / Î¼ (Efamily 1)
  obtain âŸ¨Ï, hÏâŸ© := ringHaarChar_Dğ”¸_real_surjective K D
    ((B + 1 : â„â‰¥0) / (Measure.addHaar (Efamily K D 1)).toNNReal) (by positivity)
  -- this Ï works
  use Ï
  rw [hfamily, hscale, â† coe_toNNReal hfin]
  norm_cast
  change (ringHaarChar (d Ï) : â„) = _ at hÏ
  -- indeed we need to show B < haar_char(d(Ï))*Î¼(Efamily 1)
  suffices ((B : â„) < ringHaarChar (d Ï) * (Measure.addHaar (Efamily K D 1)).toNNReal) by
    exact_mod_cast this
  rw [hÏ]
  -- which simplifies to B < (B + 1)
  convert_to (B : â„) < (B + 1)
  Â· push_cast
    convert div_mul_cancelâ‚€ _ _
    exact mt (NNReal.eq (m := 0)) hm
  Â· linarith

lemma existsE : âˆƒ E : Set (D_ğ”¸), IsCompact E âˆ§
    âˆ€ Ï† : D_ğ”¸ â‰ƒâ‚œ+ D_ğ”¸, addEquivAddHaarChar Ï† = 1 â†’ âˆƒ eâ‚ âˆˆ E, âˆƒ eâ‚‚ âˆˆ E,
    eâ‚ â‰  eâ‚‚ âˆ§ Ï† eâ‚ - Ï† eâ‚‚ âˆˆ Set.range (Algebra.TensorProduct.includeLeft : D â†’â‚[K] D_ğ”¸) := by
  obtain âŸ¨B, hBâŸ© := not_injective_of_large_measure K D
  obtain âŸ¨r, hrâŸ© := E_family_unbounded K D B
  let E := Efamily K D r
  obtain âŸ¨U, hU, hKU, hU'âŸ© := exists_isOpen_superset_and_isCompact_closure (E_family_compact K D r)
  use closure U, hU'
  intro Ï† hÏ†
  specialize hB (Ï†.symm â»Â¹' U) (hU.preimage Ï†.symm.continuous)
  replace hr : B < Measure.addHaar U := hr.trans_le (measure_mono hKU)
  replace hÏ† : addEquivAddHaarChar Ï†.symm = 1 := by
    simpa [hÏ†] using (addEquivAddHaarChar_trans (Ï† := Ï†) (Ïˆ := Ï†.symm)).symm
  have foo : Measure.addHaar U = Measure.addHaar (â‡‘Ï†.symm â»Â¹' U) := by
    rw [â† one_smul NNReal (Measure.addHaar (Ï†.symm â»Â¹' U)), â† hÏ†,
      addEquivAddHaarChar_smul_preimage]
  rw [foo] at hr
  specialize hB hr
  simp only [Set.InjOn, not_forall] at hB
  obtain âŸ¨x, hx, y, hy, h, hneâŸ© := hB
  rw [QuotientAddGroup.eq_iff_sub_mem] at h
  exact âŸ¨Ï†.symm x, subset_closure hx, Ï†.symm y, subset_closure hy, by simpa, by simpaâŸ©

/-- An auxiliary set E used in the proof of Fujisaki's lemma. -/
def E : Set D_ğ”¸ := (existsE K D).choose

lemma E_compact : IsCompact (E K D) := (existsE K D).choose_spec.1

lemma E_noninjective_left {x : D_ğ”¸Ë£} (h : x âˆˆ ringHaarChar_ker D_ğ”¸) :
    âˆƒ eâ‚ âˆˆ E K D, âˆƒ eâ‚‚ âˆˆ E K D, eâ‚ â‰  eâ‚‚ âˆ§
    x * eâ‚ - x * eâ‚‚ âˆˆ Set.range (Algebra.TensorProduct.includeLeft : D â†’â‚[K] D_ğ”¸) :=
  (existsE K D).choose_spec.2 (ContinuousAddEquiv.mulLeft x) h

lemma E_noninjective_right [Algebra.IsCentral K D] {x : D_ğ”¸Ë£} (h : x âˆˆ ringHaarChar_ker D_ğ”¸) :
    âˆƒ eâ‚ âˆˆ E K D, âˆƒ eâ‚‚ âˆˆ E K D, eâ‚ â‰  eâ‚‚ âˆ§
    eâ‚ * xâ»Â¹ - eâ‚‚ * xâ»Â¹  âˆˆ Set.range (Algebra.TensorProduct.includeLeft : D â†’â‚[K] D_ğ”¸) := by
  let Ï† : D_ğ”¸ â‰ƒâ‚œ+ D_ğ”¸ := ContinuousAddEquiv.mulRight xâ»Â¹
  have hÏ† : addEquivAddHaarChar Ï† = 1 := by
    rwa [ â† inv_mem_iff, mem_ringHaarChar_ker, ringHaarChar_apply,
      isCentralSimple_addHaarScalarFactor_left_mul_eq_right_mul K D xâ»Â¹] at h
  exact (existsE K D).choose_spec.2 Ï† hÏ†

open scoped Pointwise in
/-- An auxiliary set X used in the proof of Fukisaki's lemma. Defined as E - E. -/
def X : Set D_ğ”¸ := E K D - E K D

open scoped Pointwise in
/-- An auxiliary set Y used in the proof of Fukisaki's lemma. Defined as X * X. -/
def Y : Set D_ğ”¸ := X K D * X K D

lemma X_compact : IsCompact (X K D) := by
  simpa only [Set.image_prod, Set.image2_sub] using (IsCompact.image_of_continuousOn
    ((E_compact K D).prod (E_compact K D)) ((continuous_fst.sub continuous_snd).continuousOn))

lemma Y_compact : IsCompact (Y K D) := by
  simpa only [Set.image_prod, Set.image2_mul] using (IsCompact.image_of_continuousOn
    ((X_compact K D).prod (X_compact K D)) ((continuous_fst.mul continuous_snd).continuousOn))

lemma X_meets_kernel {Î² : D_ğ”¸Ë£} (hÎ² : Î² âˆˆ ringHaarChar_ker D_ğ”¸) :
    âˆƒ x âˆˆ X K D, âˆƒ d âˆˆ Set.range (incl K D : DË£ â†’ D_ğ”¸Ë£), Î² * x = d := by
  obtain âŸ¨e1, he1, e2, he2, noteq, b, hbâŸ© := E_noninjective_left K D hÎ²
  refine âŸ¨e1 - e2, by simpa only using (Set.sub_mem_sub he1 he2), ?_âŸ©
  obtain âŸ¨b1, rflâŸ© : IsUnit b := by
    simp_rw [â† mul_sub_left_distrib, Algebra.TensorProduct.includeLeft_apply] at hb
    have h1 : â†‘Î² * (e1 - e2) â‰  0 := by
      simpa only [ne_eq, not_not, Units.mul_right_eq_zero] using (sub_ne_zero_of_ne noteq)
    simp only [isUnit_iff_ne_zero, ne_eq]
    rintro rfl
    simp only [â† hb, TensorProduct.zero_tmul, ne_eq, not_true_eq_false] at h1
  exact âŸ¨incl K D b1, âŸ¨b1, rflâŸ©, by simpa [mul_sub] using hb.symmâŸ©

lemma X_meets_kernel' [Algebra.IsCentral K D] {Î² : D_ğ”¸Ë£} (hÎ² : Î² âˆˆ ringHaarChar_ker D_ğ”¸) :
    âˆƒ x âˆˆ X K D, âˆƒ d âˆˆ Set.range (incl K D : DË£ â†’ D_ğ”¸Ë£), x * Î²â»Â¹ = d := by
  obtain âŸ¨e1, he1, e2, he2, noteq, b, hbâŸ© := E_noninjective_right K D hÎ²
  refine âŸ¨e1 - e2, by simpa only using (Set.sub_mem_sub he1 he2), ?_âŸ©
  obtain âŸ¨b1, rflâŸ© : IsUnit b := by
    simp_rw [â† mul_sub_right_distrib, Algebra.TensorProduct.includeLeft_apply] at hb
    have h1 : (e1 - e2) * â†‘Î²â»Â¹ â‰  0 := by
      simpa only [ne_eq, Units.mul_left_eq_zero] using (sub_ne_zero_of_ne noteq)
    simp only [isUnit_iff_ne_zero, ne_eq]
    rintro rfl
    simp only [â† hb, TensorProduct.zero_tmul, ne_eq, not_true_eq_false] at h1
  exact âŸ¨incl K D b1, âŸ¨b1, rflâŸ©, by simpa [sub_mul] using hb.symmâŸ©

/-- An auxiliary set T used in the proof of Fukisaki's lemma. Defined as Y âˆ© DË£. -/
def T : Set D_ğ”¸Ë£ := ((â†‘) : D_ğ”¸Ë£ â†’ D_ğ”¸) â»Â¹' (Y K D) âˆ© Set.range ((incl K D : DË£ â†’ D_ğ”¸Ë£))

lemma T_finite_extracted1 : IsCompact (Y K D âˆ©
    Set.range (Algebra.TensorProduct.includeLeft : D â†’â‚[K] D_ğ”¸)) := by
  refine IsCompact.inter_right (Y_compact K D) ?_
  have : DiscreteTopology (includeLeft_subgroup K D).carrier := by
    infer_instance
  simpa [includeLeft_subgroup] using AddSubgroup.isClosed_of_discrete
    (H := includeLeft_subgroup K D)

lemma T_finite : Set.Finite (T K D) := by
  have h := IsCompact.finite (T_finite_extracted1 K D)
    âŸ¨(inter_Discrete (includeLeft_subgroup K D).carrier (Y K D))âŸ©
  have h1 : Units.val '' T K D âŠ† (Y K D) âˆ©
      (Set.range (Algebra.TensorProduct.includeLeft : D â†’â‚[K] D_ğ”¸)) := by
    rintro _ âŸ¨t, âŸ¨ht1, d, rflâŸ©, rflâŸ©
    exact âŸ¨ht1, d, rflâŸ©
  exact Set.Finite.of_finite_image (Set.Finite.subset h h1)
    (Function.Injective.injOn Units.val_injective)

open scoped Pointwise in
/-- An auxiliary set C used in the proof of Fukisaki's lemma. Defined as Tâ»Â¹X Ã— X. -/
def C : Set (D_ğ”¸ Ã— D_ğ”¸) := ((((â†‘) : D_ğ”¸Ë£ â†’ D_ğ”¸) '' (T K D)â»Â¹) * X K D) Ã—Ë¢ X K D

lemma C_compact : IsCompact (C K D) := by
  refine IsCompact.prod ?_ (X_compact K D)
  simpa only [Set.image_prod, Set.image2_mul] using
    (IsCompact.image_of_continuousOn (IsCompact.prod (IsCompact.image_of_continuousOn
    (IsCompact.inv (Set.Finite.isCompact (T_finite K D))) (Continuous.comp_continuousOn'
    (Units.continuous_val) (continuousOn_id' (T K D)â»Â¹)))
    (X_compact K D)) ((continuous_fst.mul continuous_snd).continuousOn))

lemma antidiag_mem_C [Algebra.IsCentral K D] {Î² : D_ğ”¸Ë£} (hÎ² : Î² âˆˆ ringHaarChar_ker D_ğ”¸) :
    âˆƒ b âˆˆ Set.range (incl K D : DË£ â†’ D_ğ”¸Ë£),
    âˆƒ Î½ âˆˆ ringHaarChar_ker D_ğ”¸,
    Î² = b * Î½ âˆ§ ((Î½ : D_ğ”¸), ((Î½â»Â¹ : D_ğ”¸Ë£) : D_ğ”¸)) âˆˆ C K D := by
  obtain âŸ¨x1, hx1, b1, âŸ¨b1, rflâŸ©, eq1âŸ© := X_meets_kernel K D hÎ²
  obtain âŸ¨x2, hx2, b2, âŸ¨b2, rflâŸ©, eq2âŸ© := X_meets_kernel' K D hÎ²
  obtain âŸ¨x1, rflâŸ© : IsUnit x1 := âŸ¨â†‘Î²â»Â¹ * incl K D b1,
    ((Units.eq_inv_mul_iff_mul_eq Î²).mpr eq1).symmâŸ©
  obtain âŸ¨x2, rflâŸ© : IsUnit x2 := âŸ¨incl K D b2 * Î², ((Units.mul_inv_eq_iff_eq_mul Î²).mp eq2).symmâŸ©
  have h : x2 * x1 âˆˆ T K D := âŸ¨by simpa only [Y] using Set.mul_mem_mul hx2 hx1,
    b2 * b1, by norm_cast at eq1 eq2; rw [map_mul, â† eq2, â† eq1]; groupâŸ©
  refine âŸ¨incl K D b1, by simp only [Set.mem_range, exists_apply_eq_apply],  x1â»Â¹, ?_,
    eq_mul_inv_of_mul_eq (Units.val_inj.mp eq1), ?_, hx1âŸ©
  Â· rw [(Eq.symm (inv_mul_eq_of_eq_mul (eq_mul_inv_of_mul_eq (Units.val_inj.mp eq1))))]
    exact (Subgroup.mul_mem_cancel_right (ringHaarChar_ker (D âŠ—[K] AdeleRing (ğ“ K) K)) hÎ²).mpr
      ((Subgroup.inv_mem_iff (ringHaarChar_ker (D âŠ—[K] AdeleRing (ğ“ K) K))).mpr
      (NumberField.AdeleRing.units_mem_ringHaarCharacter_ker K D b1))
  Â· obtain âŸ¨t, ht, ht1âŸ© := exists_eq_right'.mpr h
    simp_rw [(Eq.symm (inv_mul_eq_of_eq_mul (eq_mul_inv_of_mul_eq ht1)))]
    exact Set.mem_mul.mpr âŸ¨â†‘tâ»Â¹, Set.mem_image_of_mem Units.val ht, x2, hx2, rflâŸ©



/-- The inclusion of `ringHaarChar_ker D_ğ”¸` into the product space `D_ğ”¸ Ã— D_ğ”¸áµáµ’áµ–`. -/
def inclâ‚‚ : ringHaarChar_ker D_ğ”¸ â†’ Prod D_ğ”¸ D_ğ”¸áµáµ’áµ– :=
  fun u => Units.embedProduct D_ğ”¸ (Subgroup.subtype (ringHaarChar_ker D_ğ”¸) u)

/-- An auxiliary set used in the proof of compact_quotient'. -/
def M : Set (ringHaarChar_ker D_ğ”¸) := Set.preimage (inclâ‚‚ K D)
  (Set.image (fun p => (p.1, MulOpposite.op p.2)) (Aux.C K D))

/-- The map from `ringHaarChar_ker D_ğ”¸` to the quotient `DË£ \ ringHaarChar_ker D_ğ”¸`. -/
abbrev toQuot (a : ringHaarChar_ker D_ğ”¸) : (_root_.Quotient (QuotientGroup.rightRel
    ((MonoidHom.range (incl K D)).comap (ringHaarChar_ker D_ğ”¸).subtype))) :=
  (Quotient.mk (QuotientGroup.rightRel ((MonoidHom.range (incl K D)).comap
  (ringHaarChar_ker D_ğ”¸).subtype)) a)

lemma toQuot_cont : Continuous (toQuot K D) where
  isOpen_preimage := fun _ a â†¦ a

lemma toQuot_surjective [Algebra.IsCentral K D] : (toQuot K D) '' (M K D) = Set.univ := by
  rw [Set.eq_univ_iff_forall]
  rintro âŸ¨a, haâŸ©
  obtain âŸ¨c, hc, Î½, hÎ½, rfl, h31âŸ© := Aux.antidiag_mem_C K D ha
  simp only [toQuot, Subgroup.comap_subtype, Set.mem_image, Subtype.exists]
  refine âŸ¨Î½, hÎ½, ?_, ?_ âŸ©
  Â· simp only [M, Set.mem_preimage, Set.mem_image, Prod.exists]
    exact âŸ¨Î½, Units.val (Î½â»Â¹), h31, rflâŸ©
  Â· have : Quot.mk â‡‘(QuotientGroup.rightRel ((incl K D).range.subgroupOf
        (ringHaarChar_ker (D âŠ—[K] AdeleRing (ğ“ K) K)))) âŸ¨c * Î½, haâŸ© =
        Quot.mk â‡‘(QuotientGroup.rightRel ((incl K D).range.subgroupOf
        (ringHaarChar_ker (D âŠ—[K] AdeleRing (ğ“ K) K))))
        âŸ¨Î½, hÎ½âŸ© := by
      refine Quot.sound ?_
      rw [@QuotientGroup.rightRel_apply]
      refine Subgroup.mem_subgroupOf.mpr ?_
      simp only [@Subgroup.coe_mul, InvMemClass.coe_inv, mul_inv_rev, mul_inv_cancel_left,
        inv_mem_iff, MonoidHom.mem_range]
      exact hc
    rw [this]
    rfl

lemma inclâ‚‚_isClosedEmbedding : Topology.IsClosedEmbedding (inclâ‚‚ K D) := by
  apply Units.isClosedEmbedding_embedProduct.comp
  refine Topology.IsClosedEmbedding.of_continuous_injective_isClosedMap
    (continuous_iff_le_induced.mpr fun U a â†¦ a)
    (Subgroup.subtype_injective (ringHaarChar_ker (D âŠ—[K] AdeleRing (ğ“ K) K))) ?_
  simp only [Subgroup.coe_subtype]
  refine Topology.IsInducing.isClosedMap ({ eq_induced := rfl }) ?_
  simp only [Subtype.range_coe_subtype, SetLike.setOf_mem_eq]
  exact IsClosed.preimage (continuous_id')
    (IsClosed.preimage (map_continuous ringHaarChar) (by simp))

lemma ImAux_isCompact : IsCompact ((fun p â†¦ (p.1, MulOpposite.op p.2)) '' Aux.C K D) :=
  IsCompact.image (Aux.C_compact K D) <| by fun_prop

lemma M_compact : IsCompact (M K D) := Topology.IsClosedEmbedding.isCompact_preimage
  (inclâ‚‚_isClosedEmbedding K D) (ImAux_isCompact K D)

/-- The restriction of ringHaarChar_ker D_ğ”¸ to (D âŠ— ğ”¸_K^âˆ)Ë£ via Dğ”¸_iso_prod_units. -/
abbrev restâ‚ : ringHaarChar_ker D_ğ”¸ â†’ Dfx K D :=
  fun a => (Dğ”¸_prodRight_units K D) a.val |>.2

lemma restâ‚_continuous : Continuous (restâ‚ K D) :=
  Continuous.comp continuous_snd
  (Continuous.comp (Dğ”¸_prodRight_units_cont K D) continuous_subtype_val)

lemma ringHaarChar_Dğ”¸_prodRight_units_aux (r : â„) (h : r > 0) :
    âˆƒ y, ringHaarChar ((Dğ”¸_prodRight_units K D).symm (y,1)) = r := by
  obtain âŸ¨Ï, hÏâŸ© := ringHaarChar_Dğ”¸_real_surjective K D r h
  use ((Units.map (algebraMap â„ (Dinf K D))) Ï)

open scoped NNReal in
lemma restâ‚_surjective : Function.Surjective (restâ‚ K D) := by
  intro x
  simp only [Subtype.exists]
  set r : â„â‰¥0 := ringHaarChar ((Dğ”¸_prodRight_units K D).symm (1,x)) with hr_def
  have hr : r > 0 := addEquivAddHaarChar_pos _
  obtain âŸ¨y, hyâŸ© : âˆƒ y, ringHaarChar ((Dğ”¸_prodRight_units K D).symm (y,1)) = r := by
    obtain âŸ¨y, hyâŸ© := ringHaarChar_Dğ”¸_prodRight_units_aux K D r hr
    use y
    exact_mod_cast hy
  use (Dğ”¸_prodRight_units K D).symm (yâ»Â¹, x)
  constructor
  Â· rw [restâ‚]
    refine Units.val_inj.mp ?_
    simp only [MulEquiv.apply_symm_apply]
  Â· ext
    simp only [ContinuousMonoidHom.coe_toMonoidHom, MonoidHom.coe_coe, NNReal.coe_one,
      NNReal.coe_eq_one]
    have : (yâ»Â¹, x) = (y, 1)â»Â¹ * (1, x) := by
      ext <;> simp
    simp_rw [this, map_mul, map_inv, hy, â† hr_def, inv_mul_cancelâ‚€ hr.ne']

-- the goal that comes up when you define the map `DË£ \ D_ğ”¸^(1) to DË£ \ (Dfx K D)`
-- below using Quot.lift
lemma incl_Dğ”¸quot_equivariant : âˆ€ (a b : â†¥(ringHaarChar_ker D_ğ”¸)),
    (QuotientGroup.rightRel (Subgroup.comap (ringHaarChar_ker D_ğ”¸).subtype
    (AdeleRing.DivisionAlgebra.Aux.incl K D).range)) a b â†’
    (Quotient.mk (QuotientGroup.rightRel (inclâ‚ K D).range) (restâ‚ K D a) =
     Quotient.mk (QuotientGroup.rightRel (inclâ‚ K D).range) (restâ‚ K D b)) := by
  refine fun a b hab â†¦ Quotient.eq''.mpr ?_
  obtain âŸ¨âŸ¨t, t', htâŸ©, rflâŸ© := hab
  simp_rw [QuotientGroup.rightRel, MulAction.orbitRel, MulAction.orbit, Set.mem_range,
    Subtype.exists, Subgroup.mk_smul, smul_eq_mul, MonoidHom.mem_range, exists_prop,
    exists_exists_eq_and]
  use t'
  have : inclâ‚ K D t' =
      ((Dğ”¸_prodRight_units K D) (AdeleRing.DivisionAlgebra.Aux.incl K D t')).2 := by
    rfl
  simp_rw [this, ht, â† Prod.snd_mul, Subgroup.subtype_apply, Subgroup.comap_subtype, â† map_mul]
  rfl

/-- The obvious map DË£ \ D_ğ”¸^(1) to DË£ \ (Dfx K D). -/
abbrev incl_Dğ”¸quot : Quotient (QuotientGroup.rightRel
    ((MonoidHom.range (NumberField.AdeleRing.DivisionAlgebra.Aux.incl K D)).comap
    (ringHaarChar_ker D_ğ”¸).subtype)) â†’
    Quotient (QuotientGroup.rightRel (inclâ‚ K D).range) :=
  Quot.lift
    (fun a => Quotient.mk (QuotientGroup.rightRel (inclâ‚ K D).range) (restâ‚ K D a))
    (incl_Dğ”¸quot_equivariant K D)

lemma incl_Dğ”¸quot_continuous : Continuous (incl_Dğ”¸quot K D) := by
  refine Continuous.quotient_lift ?_ (incl_Dğ”¸quot_equivariant K D)
  exact Continuous.comp' ({isOpen_preimage := fun s a â†¦ a}) (restâ‚_continuous K D)

lemma incl_Dğ”¸quot_surjective : Function.Surjective (incl_Dğ”¸quot K D) := by
  refine (Quot.surjective_lift (f := fun a => Quotient.mk (QuotientGroup.rightRel (inclâ‚ K D).range)
    (restâ‚ K D a)) (incl_Dğ”¸quot_equivariant K D)).mpr ?_
  refine Set.range_eq_univ.mp ?_
  ext x
  simp only [Set.mem_range, Subtype.exists, Set.mem_univ, iff_true]
  obtain âŸ¨a, haâŸ© : âˆƒ a : (ringHaarChar_ker D_ğ”¸),
      (restâ‚ K D) a = x.out := restâ‚_surjective K D _
  use a
  simp [ha]

end Aux

open Aux

variable [FiniteDimensional K D]

open scoped TensorProduct.RightActions

/-- DË£ \ D_ğ”¸^{(1)} is compact. -/
lemma compact_quotient [Algebra.IsCentral K D] :
    CompactSpace (_root_.Quotient (QuotientGroup.rightRel
    ((MonoidHom.range (incl K D)).comap (ringHaarChar_ker D_ğ”¸).subtype))) :=
  isCompact_univ_iff.mp (by simpa only [toQuot_surjective, Set.image_univ] using
    (((IsCompact.image (M_compact K D) (toQuot_cont K D)))))

variable [Algebra.IsCentral K D]

/-- DË£ \ D_ğ”¸^fË£ is compact. -/
theorem _root_.NumberField.FiniteAdeleRing.DivisionAlgebra.units_cocompact :
    CompactSpace (_root_.Quotient (QuotientGroup.rightRel (inclâ‚ K D).range)) := by
  have := isCompact_univ_iff.mpr (NumberField.AdeleRing.DivisionAlgebra.compact_quotient K D)
  apply isCompact_univ_iff.mp
  have := IsCompact.image (this) (incl_Dğ”¸quot_continuous K D)
  rw [Set.image_univ_of_surjective (incl_Dğ”¸quot_surjective K D)] at this
  exact this

-- Voight "Main theorem 27.6.14(b) (Fujisaki's lemma)"
/-!
If `D` is a finite-dimensional division `K`-algebra with centre a number field `K`
then the double coset space `DË£ \ (D âŠ— ğ”¸_K^infty)Ë£ / U` is finite for any open subgroup `U`
of `(D âŠ— ğ”¸_K^infty)Ë£`.
-/
open scoped TensorProduct.RightActions in
theorem _root_.NumberField.FiniteAdeleRing.DivisionAlgebra.finiteDoubleCoset
    {U : Subgroup (Dfx K D)} (hU : IsOpen (U : Set (Dfx K D))) :
    Finite (DoubleCoset.Quotient (Set.range (inclâ‚ K D)) U) := by
  have ToFinCover := IsCompact.elim_finite_subcover
    (Î¹ := (DoubleCoset.Quotient (Set.range (inclâ‚ K D)) U))
    (U := fun q â†¦ Quot.mk â‡‘(QuotientGroup.rightRel (inclâ‚ K D).range) ''
    DoubleCoset.doubleCoset (Quotient.out q) (Set.range â‡‘(inclâ‚ K D)) U) (isCompact_univ_iff.mpr
    (NumberField.FiniteAdeleRing.DivisionAlgebra.units_cocompact K D))
  have âŸ¨t, FinCover_descendedâŸ© := ToFinCover (DoubleCoset.isOpen_doubleCoset_rightrel_mk
    ((inclâ‚ K D).range) U hU) (DoubleCoset.union_image_mk_rightRel (inclâ‚ K D).range U
    â–¸ Set.Subset.rfl)
  apply (DoubleCoset.iUnion_finset_quotTodoubleCoset ((inclâ‚ K D).range) U).mp
  exact âŸ¨t, DoubleCoset.union_finset_rightrel_cover ((inclâ‚ K D).range) U t FinCover_descendedâŸ©

end DivisionAlgebra

end AdeleRing

end NumberField
