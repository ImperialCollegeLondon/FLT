import FLT.DedekindDomain.FiniteAdeleRing.BaseChange
import FLT.NumberField.InfiniteAdeleRing
import FLT.Mathlib.Algebra.Algebra.Tower
import FLT.Mathlib.LinearAlgebra.Dimension.Constructions
import FLT.Mathlib.NumberTheory.NumberField.Basic
import FLT.Mathlib.RingTheory.TensorProduct.Pi
import FLT.Mathlib.Algebra.Algebra.Bilinear
import FLT.Mathlib.Topology.Algebra.ContinuousAlgEquiv
import FLT.Mathlib.Topology.Algebra.ContinuousMonoidHom
import FLT.Mathlib.Topology.Algebra.Group.Quotient
import FLT.Mathlib.Topology.Algebra.Module.ModuleTopology
import Mathlib.NumberTheory.NumberField.AdeleRing
import Mathlib.LinearAlgebra.TensorProduct.Prod
import FLT.NumberField.FiniteAdeleRing

open scoped TensorProduct

universe u

open NumberField

section LocallyCompact

variable (K : Type*) [Field K] [NumberField K]

open IsDedekindDomain.HeightOneSpectrum in
instance NumberField.AdeleRing.locallyCompactSpace : LocallyCompactSpace (AdeleRing (ğ“ K) K) :=
  Prod.locallyCompactSpace _ _

end LocallyCompact

section BaseChange

namespace NumberField.AdeleRing

open IsDedekindDomain

variable (K L : Type*) [Field K] [NumberField K] [Field L] [NumberField L] [Algebra K L]

/-- `ğ”¸ K` for `K` a number field, is notation for `AdeleRing (ğ“ K) K`. -/
scoped notation:100 "ğ”¸" K => AdeleRing (ğ“ K) K

-- I am not mad keen on this instance. But we don't have continuous semialgebra maps I don't think.
noncomputable instance : Algebra K (ğ”¸ L) :=
  inferInstanceAs (Algebra K (InfiniteAdeleRing L Ã— FiniteAdeleRing (ğ“ L) L))

instance : IsScalarTower K L (ğ”¸ L) :=
  IsScalarTower.of_algebraMap_eq fun _ â†¦ rfl

/-- The canonical map from the adeles of K to the adeles of L -/
noncomputable def baseChange :
    (ğ”¸ K) â†’A[K] ğ”¸ L :=
  let finite : FiniteAdeleRing (ğ“ K) K â†’A[K] FiniteAdeleRing (ğ“ L) L := {
    __ := Algebra.algHom _ _ _
    cont := FiniteAdeleRing.mapSemialgHom_continuous (ğ“ K) K L (ğ“ L)
  }
  let infinite : InfiniteAdeleRing K â†’A[K] InfiniteAdeleRing L := {
    __ := Algebra.algHom _ _ _
    cont := NumberField.InfiniteAdeleRing.baseChange_cont K L
  }
  ContinuousAlgHom.prod
    (infinite.comp <| ContinuousAlgHom.fst K (InfiniteAdeleRing K) _)
    (finite.comp <| ContinuousAlgHom.snd K (InfiniteAdeleRing K) _)

/-- `baseChange` as a `SemialgHom` -/
noncomputable def baseChangeSemialgHom :
  (ğ”¸ K) â†’â‚›â‚[algebraMap K L] ğ”¸ L where
    __ := baseChange K L
    map_smul' x y := by simp

open scoped TensorProduct

noncomputable instance : Algebra (ğ”¸ K) (ğ”¸ L) :=
  (baseChangeSemialgHom K L).toAlgebra

instance instPiIsModuleTopology : IsModuleTopology (ğ”¸ K) (Fin (Module.finrank K L) â†’ ğ”¸ K) :=
  IsModuleTopology.instPi

instance instBaseChangeIsModuleTopology : IsModuleTopology (ğ”¸ K) (ğ”¸ L) := by
  have := BaseChange.isModuleTopology (ğ“ K) K L (ğ“ L)
  exact IsModuleTopology.instProd' (A := InfiniteAdeleRing K)
    (B := FiniteAdeleRing (ğ“ K) K) (M := InfiniteAdeleRing L) (N := FiniteAdeleRing (ğ“ L) L)

open scoped TensorProduct.RightActions in
/-- The canonical `ğ”¸ K`-algebra homomorphism `(L âŠ—_K ğ”¸ K) â†’ ğ”¸ L` induced
by the maps from `L` and `ğ”¸ K` into `ğ”¸ L`. -/
noncomputable def baseChangeAdeleAlgHom : (L âŠ—[K] ğ”¸ K) â†’â‚[ğ”¸ K] ğ”¸ L :=
  (baseChangeSemialgHom K L).baseChangeRightOfAlgebraMap

-- do we not have this?? Move! PR! TODO
/-- Product of algebra equivalences; the maps come from Equiv.prodCongr. -/
def _root_.AlgEquiv.prodCongr {R A Aâ‚‚ Aâ‚ƒ Aâ‚„ : Type*} [CommSemiring R]
    [Semiring A] [Semiring Aâ‚‚] [Semiring Aâ‚ƒ] [Semiring Aâ‚„] [Algebra R A]
    [Algebra R Aâ‚‚] [Algebra R Aâ‚ƒ] [Algebra R Aâ‚„]
    (eâ‚ : A â‰ƒâ‚[R] Aâ‚‚) (eâ‚‚ : Aâ‚ƒ â‰ƒâ‚[R] Aâ‚„) :
    (A Ã— Aâ‚ƒ) â‰ƒâ‚[R] (Aâ‚‚ Ã— Aâ‚„) where
  __ := LinearEquiv.prodCongr eâ‚.toLinearEquiv eâ‚‚.toLinearEquiv
  map_mul' := by simp
  commutes' := by simp

/-- The L-algebra isomorphism `L âŠ—[K] ğ”¸_K = ğ”¸_L`. -/
noncomputable def baseChangeAdeleAlgEquiv : (L âŠ—[K] ğ”¸ K) â‰ƒâ‚[L] ğ”¸ L :=
  let tensor :=
    Algebra.TensorProduct.prodRight K L L (InfiniteAdeleRing K) (FiniteAdeleRing (ğ“ K) K)
  let prod := AlgEquiv.prodCongr
    (NumberField.InfiniteAdeleRing.baseChangeEquivAux K L)
    (FiniteAdeleRing.baseChangeAlgEquiv (ğ“ K) K L (ğ“ L))
  tensor.trans prod

@[simp] lemma baseChangeAdeleAlgEquiv_apply (l : L) (a : ğ”¸ K) :
    baseChangeAdeleAlgEquiv K L (l âŠ—â‚œ a) = algebraMap _ _ l * algebraMap _ _ a := by
  rfl

open scoped TensorProduct.RightActions in
lemma baseChangeAdeleAlgHom_bijective : Function.Bijective (baseChangeAdeleAlgHom K L) := by
  -- There's a linear equivlance `(L âŠ—_K ğ”¸ K) â‰… ğ”¸ L`
  let linearEquiv : (L âŠ—[K] ğ”¸ K) â‰ƒâ‚—[L] ğ”¸ L :=
    let tensor := TensorProduct.prodRight K L L (InfiniteAdeleRing K) (FiniteAdeleRing (ğ“ K) K)
    let prod := LinearEquiv.prodCongr (InfiniteAdeleRing.baseChangeEquiv K L).toLinearEquiv
      (FiniteAdeleRing.baseChangeAlgEquiv (ğ“ K) K L (ğ“ L)).toLinearEquiv
    tensor.trans prod
  -- and it's given by an equal function to the algebra homomorphism we've defined.
  have eqEquiv : â‡‘(baseChangeAdeleAlgHom K L) = â‡‘(linearEquiv) := by
    show â‡‘((baseChangeAdeleAlgHom K L).toLinearMap.restrictScalars K) =
      â‡‘(linearEquiv.toLinearMap.restrictScalars K)
    exact congr_arg DFunLike.coe (TensorProduct.ext' fun x y â†¦ rfl)
  rw [eqEquiv]
  exact linearEquiv.bijective

open scoped TensorProduct.RightActions in
/-- The canonical `ğ”¸_K`-algebra isomorphism from `L âŠ—_K ğ”¸_K` to `ğ”¸_L` induced by the
base change map `ğ”¸_K â†’ ğ”¸_L`. -/
noncomputable def baseChangeAdeleEquiv : (L âŠ—[K] ğ”¸ K) â‰ƒA[ğ”¸ K] ğ”¸ L :=
  IsModuleTopology.continuousAlgEquivOfAlgEquiv <|
    AlgEquiv.ofBijective (baseChangeAdeleAlgHom K L) (baseChangeAdeleAlgHom_bijective K L)

open scoped TensorProduct.RightActions in
/-- The canonical `L`-algebra isomorphism from `L âŠ—_K ğ”¸_K` to `ğ”¸_L` induced by the
`K`-algebra base change map `ğ”¸_K â†’ ğ”¸_L`. -/
noncomputable def baseChangeEquiv :
    (L âŠ—[K] ğ”¸ K) â‰ƒA[L] ğ”¸ L where
  __ := (baseChangeSemialgHom K L).baseChange_of_algebraMap
  __ := baseChangeAdeleEquiv K L

-- this isn't rfl. Explanation below
example (x : L âŠ—[K] ğ”¸ K) : baseChangeEquiv K L x = baseChangeAdeleAlgEquiv K L x := by
  induction x with
  | zero => rfl
  | tmul x y => rfl
  | add x y _ _ => simp_all

/-

We have two isomorphisms `(L âŠ—[K] ğ”¸ K) = ğ”¸ L`.

1)
`baseChangeEquiv` is
  `(baseChangeSemialgHom K L).baseChange_of_algebraMap` *and
  `baseChangeAdeleEquiv`. The latter is `baseChangeAdeleAlgHom` which is
  `(baseChangeSemialgHom K L).baseChangeRightOfAlgebraMap`

Note:
```
example (x : L âŠ—[K] ğ”¸ K) :
    (baseChangeSemialgHom K L).baseChange_of_algebraMap x =
    (baseChangeSemialgHom K L).baseChangeRightOfAlgebraMap x := by
  rfl
```

This map is defined as "there is a commutative square `K â†’ L â†’ ğ”¸ L` and `K â†’ ğ”¸ K â†’ ğ”¸ L`
so there's an induced map `L âŠ—[K] ğ”¸ K â†’ ğ”¸ L`; this is a bijection"

But `baseChangeAdeleAlgEquiv` is `tensor.trans prod` i.e.

`(L âŠ—[K] ğ”¸ K) = L âŠ—[K] (ğ”¸^âˆ x A_âˆ) â‰… (L âŠ—[K] ğ”¸^âˆ) x (L âŠ—[K] ğ”¸_âˆ) â‰… ğ”¸_L^âˆ x ğ”¸_L_âˆ

-/

variable {L}

theorem baseChangeEquiv_tsum_apply_right (l : L) :
    baseChangeEquiv K L (l âŠ—â‚œ[K] 1) = algebraMap L (ğ”¸ L) l := by
  have h : (l âŠ—â‚œ[K] (1 : ğ”¸ K)) = l â€¢ 1 := by
    simp [Algebra.TensorProduct.one_def, TensorProduct.smul_tmul']
  simp [h, Algebra.algebraMap_eq_smul_one]

variable (L)

open scoped TensorProduct.RightActions in
open TensorProduct.AlgebraTensorModule in
/-- A continuous `K`-linear isomorphism `L âŠ—[K] ğ”¸_K = (ğ”¸_K)â¿` for `n = [L:K]` -/
noncomputable abbrev tensorProductEquivPi :
    L âŠ—[K] (ğ”¸ K) â‰ƒL[K] (Fin (Module.finrank K L) â†’ ğ”¸ K) :=
  letI := instPiIsModuleTopology K L
  -- `ğ”¸ K âŠ—[K] L â‰ƒâ‚—[ğ”¸ K] L âŠ—[K] ğ”¸ K`
  -- Note: needs to be this order to avoid instance clash with inferred leftAlgebra
  let comm := (TensorProduct.RightActions.Algebra.TensorProduct.comm K (ğ”¸ K) L) |>.toLinearEquiv
  -- `ğ”¸ K âŠ—[K] L â‰ƒâ‚—[ğ”¸ K] âŠ• ğ”¸ K`
  let Ï€ := finiteEquivPi K L (ğ”¸ K)
  -- Stitch together to get `L âŠ—[K] ğ”¸ K â‰ƒâ‚—[ğ”¸ K] âŠ• ğ”¸ K`, which is automatically
  -- continuous due to `ğ”¸ K` module topologies on both sides, then restrict scalars to `K`
  IsModuleTopology.continuousLinearEquiv (comm.symm.trans Ï€) |>.restrictScalars K

open scoped TensorProduct.RightActions in
/-- A continuous `K`-linear isomorphism `(ğ”¸_K)â¿ â‰ƒ ğ”¸_L` for `n = [L:K]` -/
noncomputable abbrev piEquiv :
    (Fin (Module.finrank K L) â†’ ğ”¸ K) â‰ƒL[K] ğ”¸ L :=
  -- `âŠ• ğ”¸ K â‰ƒL[K] L âŠ—[K] ğ”¸ K` from previous def
  let Ï€ := (tensorProductEquivPi K L).symm
  -- `L âŠ—[K] ğ”¸ K â‰ƒL[K] ğ”¸ L` base change  restricted to `K` as a continuous linear equiv
  let BC := baseChangeEquiv K L |>.toContinuousLinearEquiv |>.restrictScalars K
  Ï€.trans BC

variable {K L}

open TensorProduct.AlgebraTensorModule in
theorem piEquiv_apply_of_algebraMap
    {x : Fin (Module.finrank K L) â†’ ğ”¸ K}
    {y : Fin (Module.finrank K L) â†’ K}
    (h : âˆ€ i, algebraMap K (ğ”¸ K) (y i) = x i) :
    piEquiv K L x = algebraMap L _ (Module.Finite.equivPi _ _ |>.symm y) := by
  simp only [â† funext h, ContinuousLinearEquiv.trans_apply,
    ContinuousLinearEquiv.restrictScalars_symm_apply, AlgEquiv.toAlgHom_eq_coe,
    AlgHom.toRingHom_eq_coe, AlgEquiv.toLinearEquiv_symm,
    ContinuousLinearEquiv.restrictScalars_apply, IsModuleTopology.continuousLinearEquiv_symm_apply]
  rw [LinearEquiv.trans_symm, LinearEquiv.trans_apply, finiteEquivPi_symm_apply]
  simp [AlgEquiv.extendScalars, ContinuousAlgEquiv.toContinuousLinearEquiv_apply,
    baseChangeEquiv_tsum_apply_right]

theorem piEquiv_mem_principalSubgroup
    {x : Fin (Module.finrank K L) â†’ ğ”¸ K}
    (h : x âˆˆ AddSubgroup.pi Set.univ (fun _ => principalSubgroup (ğ“ K) K)) :
    piEquiv K L x âˆˆ principalSubgroup (ğ“ L) L := by
  simp only [AddSubgroup.mem_pi, Set.mem_univ, forall_const] at h
  choose y hy using h
  exact piEquiv_apply_of_algebraMap hy â–¸ âŸ¨Module.Finite.equivPi _ _ |>.symm y, rflâŸ©

variable (K L)

theorem piEquiv_map_principalSubgroup :
    (AddSubgroup.pi Set.univ (fun (_ : Fin (Module.finrank K L)) => principalSubgroup (ğ“ K) K)).map
      (piEquiv K L).toAddMonoidHom
      = principalSubgroup (ğ“ L) L := by
  ext x
  simp only [AddSubgroup.mem_map, LinearMap.toAddMonoidHom_coe, LinearEquiv.coe_coe,
    ContinuousLinearEquiv.coe_toLinearEquiv]
  refine âŸ¨fun âŸ¨a, h, haâŸ© => ha â–¸ piEquiv_mem_principalSubgroup h, ?_âŸ©
  rintro âŸ¨a, rflâŸ©
  use fun i => algebraMap K (ğ”¸ K) (Module.Finite.equivPi _ _ a i)
  refine âŸ¨fun i _ => âŸ¨Module.Finite.equivPi _ _ a i, rflâŸ©, ?_âŸ©
  rw [piEquiv_apply_of_algebraMap (fun i => rfl), LinearEquiv.symm_apply_apply]

theorem comap_piEquiv_principalSubgroup :
    (AddSubgroup.pi Set.univ (fun (_ : Fin (Module.finrank K L)) => principalSubgroup (ğ“ K) K))
      = (principalSubgroup (ğ“ L) L).comap (piEquiv K L).toAddMonoidHom := by
  rw [â† piEquiv_map_principalSubgroup K L,
    AddSubgroup.comap_map_eq_self_of_injective (piEquiv K L).injective]

/-- A continuous additive isomorphism `(ğ”¸_K / K)â¿ = ğ”¸_L / L` where `n = [L:K]`. -/
noncomputable def piQuotientEquiv :
    (Fin (Module.finrank K L) â†’ (ğ”¸ K) â§¸ principalSubgroup (ğ“ K) K) â‰ƒâ‚œ+
      (ğ”¸ L) â§¸ principalSubgroup (ğ“ L) L :=
  -- The map `âŠ• ğ”¸ K â‰ƒL[K] ğ”¸ L` reduces to quotients `âŠ• ğ”¸ K / K â‰ƒâ‚œ+ ğ”¸ L / L`
  (ContinuousAddEquiv.quotientPi _).symm.trans <|
    QuotientAddGroup.continuousAddEquiv _ _ (piEquiv K L).toContinuousAddEquiv
      (comap_piEquiv_principalSubgroup K L)

end NumberField.AdeleRing

end BaseChange

section Discrete

open IsDedekindDomain

theorem Rat.AdeleRing.zero_discrete : âˆƒ U : Set (AdeleRing (ğ“ â„š) â„š),
    IsOpen U âˆ§ (algebraMap â„š (AdeleRing (ğ“ â„š) â„š)) â»Â¹' U = {0} := by
  let integralAdeles := {f : FiniteAdeleRing (ğ“ â„š) â„š |
    âˆ€ v , f v âˆˆ IsDedekindDomain.HeightOneSpectrum.adicCompletionIntegers â„š v}
  use {f | âˆ€ v, f v âˆˆ (Metric.ball 0 1)} Ã—Ë¢ integralAdeles
  refine âŸ¨?_, ?_âŸ©
  Â· apply IsOpen.prod
    Â· rw [Set.setOf_forall]
      apply isOpen_iInter_of_finite
      intro v
      exact Metric.isOpen_ball.preimage (continuous_apply v)
    Â· exact RestrictedProduct.isOpen_forall_mem fun v â†¦ Valued.isOpen_integer _
  Â· apply subset_antisymm
    Â· intro x hx
      rw [Set.mem_preimage] at hx
      simp only [Set.mem_singleton_iff]
      rw [show (algebraMap â„š (AdeleRing (ğ“ â„š) â„š)) x =
        (algebraMap â„š (InfiniteAdeleRing â„š) x, algebraMap â„š (FiniteAdeleRing (ğ“ â„š) â„š) x)
        from rfl] at hx
      rw [Set.mem_prod] at hx
      obtain âŸ¨h1, h2âŸ© := hx
      dsimp only at h1 h2
      simp only [Metric.mem_ball, dist_zero_right, Set.mem_setOf_eq,
        InfiniteAdeleRing.algebraMap_apply, UniformSpace.Completion.norm_coe] at h1
      simp only [integralAdeles, Set.mem_setOf_eq] at h2
      specialize h1 Rat.infinitePlace
      change â€–(x : â„‚)â€– < 1 at h1
      simp only [Complex.norm_ratCast, integralAdeles] at h1
      have intx: âˆƒ (y:â„¤), y = x := by
        obtain âŸ¨z, hzâŸ© := IsDedekindDomain.HeightOneSpectrum.mem_integers_of_valuation_le_one
            â„š x <| fun v â†¦ by
          specialize h2 v
          letI : UniformSpace â„š := v.adicValued.toUniformSpace
          rw [IsDedekindDomain.HeightOneSpectrum.mem_adicCompletionIntegers] at h2
          rwa [â† IsDedekindDomain.HeightOneSpectrum.valuedAdicCompletion_eq_valuation']
        use Rat.ringOfIntegersEquiv z
        rw [â† hz]
        apply Rat.ringOfIntegersEquiv_eq_algebraMap
      obtain âŸ¨y, rflâŸ© := intx
      simp only [abs_lt] at h1
      norm_cast at h1 âŠ¢
      -- We need the next line because `norm_cast` is for some reason producing a `negSucc 0`.
      -- I haven't been able to isolate this behaviour even in a standalone lemma.
      -- We could also make `omega` more robust against accidental appearances of `negSucc`.
      rw [Int.negSucc_eq] at h1
      omega
    Â· intro x
      simp only [Set.mem_singleton_iff, Set.mem_preimage]
      rintro rfl
      simp only [map_zero]
      change (0, 0) âˆˆ _
      simp only [Prod.mk_zero_zero, Set.mem_prod, Prod.fst_zero, Prod.snd_zero]
      constructor
      Â· simp only [Metric.mem_ball, dist_zero_right, Set.mem_setOf_eq]
        intro v
        have : â€–(0:InfiniteAdeleRing â„š) vâ€– = 0 := by
          simp only [norm_eq_zero]
          rfl
        simp [this, zero_lt_one]
      Â· simp only [integralAdeles, Set.mem_setOf_eq]
        intro v
        apply zero_mem

variable (K : Type*) [Field K] [NumberField K]

theorem NumberField.AdeleRing.zero_discrete : âˆƒ U : Set (AdeleRing (ğ“ K) K),
    IsOpen U âˆ§ (algebraMap K (AdeleRing (ğ“ K) K)) â»Â¹' U = {0} := by
  obtain âŸ¨V, hV, hV0âŸ© := Rat.AdeleRing.zero_discrete
  use (piEquiv â„š K) '' {f | âˆ€i, f i âˆˆ V }
  constructor
  Â· rw [â† (piEquiv â„š K).coe_toHomeomorph, Homeomorph.isOpen_image, Set.setOf_forall]
    apply isOpen_iInter_of_finite
    intro i
    exact hV.preimage (continuous_apply i)
  rw [Set.eq_singleton_iff_unique_mem]
  constructor
  Â· rw [Set.eq_singleton_iff_unique_mem, Set.mem_preimage, map_zero] at hV0
    simp only [Set.mem_preimage, map_zero, Set.mem_image,
      EmbeddingLike.map_eq_zero_iff, exists_eq_right, Pi.zero_apply]
    exact fun _ => hV0.left
  intro x âŸ¨y, hy, hyxâŸ©
  apply (Module.Finite.equivPi â„š K).injective
  set f := Module.Finite.equivPi â„š K x
  let g := fun i => algebraMap â„š (AdeleRing (ğ“ â„š) â„š) (f i)
  have hfg : âˆ€ i, (algebraMap _ _) (f i) = g i := fun i => rfl
  have hg := piEquiv_apply_of_algebraMap hfg
  simp only [LinearEquiv.symm_apply_apply, f, â† hyx, EquivLike.apply_eq_iff_eq] at hg
  subst hg
  ext i
  rw [map_zero, Pi.zero_apply, â† Set.mem_singleton_iff, â† hV0, Set.mem_preimage]
  exact hy i

-- Maybe this discreteness isn't even stated in the best way?
-- I'm ambivalent about how it's stated
open Pointwise in
theorem NumberField.AdeleRing.discrete : âˆ€ x : K, âˆƒ U : Set (AdeleRing (ğ“ K) K),
    IsOpen U âˆ§ (algebraMap K (AdeleRing (ğ“ K) K)) â»Â¹' U = {x} := by
  obtain âŸ¨V, hV, hV0âŸ© := zero_discrete K
  intro x
  let Î¹  := algebraMap K (AdeleRing (ğ“ K) K)
  set xâ‚ := Î¹ x                           with hxâ‚
  set f  := Homeomorph.subLeft xâ‚         with hf
  use f â»Â¹' V, f.isOpen_preimage.mpr hV
  have : f âˆ˜ Î¹ = Î¹ âˆ˜ Equiv.subLeft x := by ext; simp [hf, hxâ‚]
  rw [â† Set.preimage_comp, this, Set.preimage_comp, hV0]
  ext
  simp only [Set.mem_preimage, Equiv.subLeft_apply, Set.mem_singleton_iff, sub_eq_zero, eq_comm]

end Discrete

section Compact

open NumberField

theorem Rat.AdeleRing.cocompact :
    CompactSpace (AdeleRing (ğ“ â„š) â„š â§¸ AdeleRing.principalSubgroup (ğ“ â„š) â„š) :=
  sorry -- issue #258

variable (K L : Type*) [Field K] [Field L] [NumberField K] [NumberField L] [Algebra K L]

theorem NumberField.AdeleRing.cocompact :
    CompactSpace (AdeleRing (ğ“ K) K â§¸ principalSubgroup (ğ“ K) K) :=
  letI := Rat.AdeleRing.cocompact
  (piQuotientEquiv â„š K).compactSpace

end Compact
