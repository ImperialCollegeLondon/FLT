import Mathlib
import FLT.Mathlib.RingTheory.TensorProduct.Pi
import FLT.Mathlib.Topology.Algebra.Group.Quotient
import FLT.Mathlib.Topology.Algebra.ContinuousAlgEquiv

open scoped TensorProduct

universe u

section LocallyCompact

-- see https://github.com/smmercuri/adele-ring_locally-compact
-- for a proof of this

variable (K : Type*) [Field K] [NumberField K]

instance NumberField.AdeleRing.locallyCompactSpace : LocallyCompactSpace (AdeleRing K) :=
  sorry -- issue #253

end LocallyCompact

section BaseChange

namespace NumberField.AdeleRing

variable (K L : Type*) [Field K] [Field L] [NumberField K] [NumberField L] [Algebra K L]

noncomputable instance : Algebra K (NumberField.AdeleRing L) :=
  RingHom.toAlgebra <| (algebraMap L (NumberField.AdeleRing L)).comp <| algebraMap K L

noncomputable abbrev tensorProductLinearEquivPi : AdeleRing K âŠ—[K] L â‰ƒâ‚—[K]
    (Fin (Module.finrank K L) â†’ AdeleRing K) :=
  LinearEquiv.trans (TensorProduct.congr (LinearEquiv.refl K (AdeleRing K))
      (Basis.equivFun (Module.finBasis K L)))
    (TensorProduct.piScalarRight _ _ _ _)

variable {L}

theorem tensorProductLinearEquivPi_tsum_apply (l : L) :
    tensorProductLinearEquivPi K L (1 âŠ—â‚œ[K] l) =
      fun i => algebraMap _ _ (Module.finBasis K L |>.equivFun l i) := by
  simp [tensorProductLinearEquivPi, Algebra.algebraMap_eq_smul_one]

variable {K}

theorem tensorProductLinearEquivPi_symm_apply_of_algebraMap
    (x : Fin (Module.finrank K L) â†’ K) :
    (tensorProductLinearEquivPi K L).symm (fun i => algebraMap _ _ (x i)) =
      1 âŠ—â‚œ[K] ((Module.finBasis K L).equivFun.symm x) := by
  simp only [LinearEquiv.trans_symm, LinearEquiv.trans_apply,
    Algebra.TensorProduct.piScalarRight_symm_apply_of_algebraMap, TensorProduct.congr_symm_tmul,
    LinearEquiv.refl_symm, LinearEquiv.refl_apply, map_sum, Basis.equivFun_symm_apply]
  rw [Finset.sum_comm]
  simp only [â† Finset.sum_smul, Fintype.sum_pi_single]

/-
smmercuri: A possible alternative using `moduleTopology` is
```
instance :
    TopologicalSpace (NumberField.AdeleRing K âŠ—[K] L) :=
  letI := TopologicalSpace.induced (algebraMap K (AdeleRing K)) inferInstance
  moduleTopology K _
```
However it is not clear to me how the inverse function is of `tensorProductLinearEquivPi`
is continuous in that case. Additionally,
https://math.mit.edu/classes/18.785/2017fa/LectureNotes25.pdf (just above Prop 25.10)
for an informal source where the tensor product is given the product topology. Maybe they
coincide!
-/
instance : TopologicalSpace (NumberField.AdeleRing K âŠ—[K] L) :=
  TopologicalSpace.induced (NumberField.AdeleRing.tensorProductLinearEquivPi K L) inferInstance

variable (K L)

noncomputable def tensorProductContinuousLinearEquivPi :
    AdeleRing K âŠ—[K] L â‰ƒL[K] (Fin (Module.finrank K L) â†’ AdeleRing K) where
  toLinearEquiv := tensorProductLinearEquivPi K L
  continuous_toFun := continuous_induced_dom
  continuous_invFun := by
    convert (tensorProductLinearEquivPi K L).toEquiv.coinduced_symm â–¸ continuous_coinduced_rng

variable {K L}

theorem tensorProductContinuousLinearEquivPi_symm_apply_of_algebraMap
    (x : Fin (Module.finrank K L) â†’ K) :
    (tensorProductContinuousLinearEquivPi K L).symm (fun i => algebraMap _ _ (x i)) =
      1 âŠ—â‚œ[K] ((Module.finBasis K L).equivFun.symm x) := by
  exact tensorProductLinearEquivPi_symm_apply_of_algebraMap _

variable (K L)

/-
smmercuri : The tensor product here is in a different order to the one
appearing in the finite adele ring base change, where `L âŠ—[K] FiniteAdeleRing A K`
is used. Is there a preference between these orderings? One benefit
to using `AdeleRing K âŠ—[K] L` is that it automatically gets
a `K` algebra instance via the instance `Algebra.TensorProduct.leftAlgebra`
(while `rightAlgebra` is a def), but maybe there are other reasons to
prefer the `rightAlgebra` set up.
-/
def baseChange : (AdeleRing K âŠ—[K] L) â‰ƒA[K] AdeleRing L := sorry

variable {L}

theorem baseChange_tsum_apply_right (l : L) :
  baseChange K L (1 âŠ—â‚œ[K] l) = algebraMap L (AdeleRing L) l := sorry

variable (L)

noncomputable def baseChangePi :
    (Fin (Module.finrank K L) â†’ AdeleRing K) â‰ƒL[K] AdeleRing L :=
  (tensorProductContinuousLinearEquivPi K L).symm.trans (baseChange K L).toContinuousLinearEquiv

variable {K L}

theorem baseChangePi_apply (x : Fin (Module.finrank K L) â†’ AdeleRing K) :
    baseChangePi K L x = baseChange K L ((tensorProductContinuousLinearEquivPi K L).symm x) := rfl

theorem baseChangePi_apply_eq_algebraMap_comp
    {x : Fin (Module.finrank K L) â†’ AdeleRing K}
    {y : Fin (Module.finrank K L) â†’ K}
    (h : âˆ€ i, algebraMap K (AdeleRing K) (y i) = x i) :
    baseChangePi K L x = algebraMap L (AdeleRing L) ((Module.finBasis K L).equivFun.symm y) := by
  rw [â† funext h, baseChangePi_apply, tensorProductContinuousLinearEquivPi_symm_apply_of_algebraMap,
    baseChange_tsum_apply_right]

theorem baseChangePi_mem_principalSubgroup
    {x : Fin (Module.finrank K L) â†’ AdeleRing K}
    (h : x âˆˆ AddSubgroup.pi Set.univ (fun _ => principalSubgroup K)) :
    baseChangePi K L x âˆˆ principalSubgroup L := by
  simp only [AddSubgroup.mem_pi, Set.mem_univ, forall_const] at h
  choose y hy using h
  exact baseChangePi_apply_eq_algebraMap_comp hy â–¸ âŸ¨(Module.finBasis K L).equivFun.symm y, rflâŸ©

variable (K L)

theorem baseChangePi_map_principalSubgroup :
    (AddSubgroup.pi Set.univ (fun (_ : Fin (Module.finrank K L)) => principalSubgroup K)).map
      (baseChangePi K L).toAddMonoidHom = principalSubgroup L := by
  ext x
  simp only [AddSubgroup.mem_map, LinearMap.toAddMonoidHom_coe, LinearEquiv.coe_coe,
    ContinuousLinearEquiv.coe_toLinearEquiv]
  refine âŸ¨fun âŸ¨a, h, haâŸ© => ha â–¸ baseChangePi_mem_principalSubgroup h, ?_âŸ©
  rintro âŸ¨a, rflâŸ©
  use fun i => algebraMap K (AdeleRing K) ((Module.finBasis K L).equivFun a i)
  refine âŸ¨fun i _ => âŸ¨(Module.finBasis K L).equivFun a i, rflâŸ©, ?_âŸ©
  rw [baseChangePi_apply_eq_algebraMap_comp (fun i => rfl), LinearEquiv.symm_apply_apply]
  rfl

noncomputable def baseChangeQuotientPi :
    (Fin (Module.finrank K L) â†’ AdeleRing K â§¸ principalSubgroup K) â‰ƒâ‚œ+
      AdeleRing L â§¸ principalSubgroup L :=
  (ContinuousAddEquiv.quotientPi _).symm.trans <|
    QuotientAddGroup.continuousAddEquiv _ _ _ _ (baseChangePi K L).toContinuousAddEquiv
      (baseChangePi_map_principalSubgroup K L)

end NumberField.AdeleRing

end BaseChange

section Discrete

open NumberField DedekindDomain

theorem Rat.AdeleRing.zero_discrete : âˆƒ U : Set (AdeleRing â„š),
    IsOpen U âˆ§ (algebraMap â„š (AdeleRing â„š)) â»Â¹' U = {0} := by
  use {f | âˆ€ v, f v âˆˆ (Metric.ball 0 1)} Ã—Ë¢
    {f | âˆ€ v , f v âˆˆ IsDedekindDomain.HeightOneSpectrum.adicCompletionIntegers â„š v}
  refine âŸ¨?_, ?_âŸ©
  Â· dsimp
    sorry -- issue #252 -- should be easy (product of opens is open, product of integers is surely
          -- known to be open)
  Â· apply subset_antisymm
    Â· intro x hx
      rw [Set.mem_preimage] at hx
      simp only [Set.mem_singleton_iff]
      have : (algebraMap â„š (AdeleRing â„š)) x =
        (algebraMap â„š (InfiniteAdeleRing â„š) x, algebraMap â„š (FiniteAdeleRing (ð“ž â„š) â„š) x)
      Â· rfl
      rw [this] at hx
      clear this
      rw [Set.mem_prod] at hx
      obtain âŸ¨h1, h2âŸ© := hx
      dsimp only at h1 h2
      simp only [Metric.mem_ball, dist_zero_right, Set.mem_setOf_eq,
        InfiniteAdeleRing.algebraMap_apply, UniformSpace.Completion.norm_coe] at h1
      simp only [Set.mem_setOf_eq] at h2
      specialize h1 Rat.infinitePlace
      change â€–(x : â„‚)â€– < 1 at h1
      simp at h1
      have intx: âˆƒ (y:â„¤), y = x
      Â· clear h1 -- not needed
        -- mathematically this is trivial:
        -- h2 says that no prime divides the denominator of x
        -- so x is an integer
        -- and the goal is that there exists an integer `y` such that `y = x`.
        suffices âˆ€ p : â„•, p.Prime â†’ Â¬(p âˆ£ x.den) by
          use x.num
          rw [â† den_eq_one_iff]
          contrapose! this
          exact âŸ¨x.den.minFac, Nat.minFac_prime this, Nat.minFac_dvd _âŸ©
        sorry -- issue #254
      obtain âŸ¨y, rflâŸ© := intx
      simp only [abs_lt] at h1
      norm_cast at h1 âŠ¢
      -- We need the next line because `norm_cast` is for some reason producing a `negSucc 0`.
      -- I haven't been able to isolate this behaviour even in a standalone lemma.
      -- We could also make `omega` more robust against accidental appearances of `negSucc`.
      rw [Int.negSucc_eq] at h1
      omega
    Â· intro x
      simp only [Set.mem_singleton_iff, Set.mem_preimage]
      rintro rfl
      simp only [map_zero]
      change (0, 0) âˆˆ _
      simp only [Prod.mk_zero_zero, Set.mem_prod, Prod.fst_zero, Prod.snd_zero]
      constructor
      Â· simp only [Metric.mem_ball, dist_zero_right, Set.mem_setOf_eq]
        intro v
        have : â€–(0:InfiniteAdeleRing â„š) vâ€– = 0
        Â· simp only [norm_eq_zero]
          rfl
        simp [this, zero_lt_one]
      Â· simp only [Set.mem_setOf_eq]
        intro v
        apply zero_mem

-- Maybe this discreteness isn't even stated in the best way?
-- I'm ambivalent about how it's stated
open Pointwise in
theorem Rat.AdeleRing.discrete : âˆ€ q : â„š, âˆƒ U : Set (AdeleRing â„š),
    IsOpen U âˆ§ (algebraMap â„š (AdeleRing â„š)) â»Â¹' U = {q} := by
  obtain âŸ¨V, hV, hV0âŸ© := zero_discrete
  intro q
  set Î¹  := algebraMap â„š (AdeleRing â„š)    with hÎ¹
  set qâ‚ := Î¹ q                           with hqâ‚
  set f  := Homeomorph.subLeft qâ‚         with hf
  use f â»Â¹' V, f.isOpen_preimage.mpr hV
  have : f âˆ˜ Î¹ = Î¹ âˆ˜ Homeomorph.subLeft q := by ext; simp [hf, hqâ‚]
  rw [â† Set.preimage_comp, this, Set.preimage_comp, hV0]
  ext
  simp only [Set.mem_preimage, Homeomorph.subLeft_apply, Set.mem_singleton_iff, sub_eq_zero, eq_comm]

variable (K : Type*) [Field K] [NumberField K]

theorem NumberField.AdeleRing.discrete : âˆ€ k : K, âˆƒ U : Set (AdeleRing K),
    IsOpen U âˆ§ (algebraMap K (AdeleRing K)) â»Â¹' U = {k} := sorry -- issue #257

end Discrete

section Compact

open NumberField

theorem Rat.AdeleRing.cocompact :
    CompactSpace (AdeleRing â„š â§¸ AdeleRing.principalSubgroup â„š) :=
  sorry -- issue #258

variable (K L : Type*) [Field K] [Field L] [NumberField K] [NumberField L] [Algebra K L]

theorem NumberField.AdeleRing.cocompact :
    CompactSpace (AdeleRing K â§¸ principalSubgroup K) :=
  letI := Rat.AdeleRing.cocompact
  (baseChangeQuotientPi â„š K).compactSpace

end Compact
