/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.CategoryTheory.Monoidal.Transport
import FLT.Proj3.CommAlgebraCat.Basic
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Basic
import Mathlib.RingTheory.TensorProduct

/-!
# The monoidal category structure on R-algebras
-/

open CategoryTheory
open scoped MonoidalCategory

universe v u

variable {R : Type u} [CommRing R]

namespace CommAlgebraCat

noncomputable section

namespace instMonoidalCategory

open scoped TensorProduct

/-- Auxiliary definition used to fight a timeout when building
`CommAlgebraCat.instMonoidalCategory`. -/
@[simps!]
noncomputable abbrev tensorObj (X Y : CommAlgebraCat.{u} R) : CommAlgebraCat.{u} R :=
  of R (X âŠ—[R] Y)

/-- Auxiliary definition used to fight a timeout when building
`CommAlgebraCat.instMonoidalCategory`. -/
noncomputable abbrev tensorHom {W X Y Z : CommAlgebraCat.{u} R} (f : W âŸ¶ X) (g : Y âŸ¶ Z) :
    tensorObj W Y âŸ¶ tensorObj X Z :=
  Algebra.TensorProduct.map f g

open MonoidalCategory

end instMonoidalCategory

open instMonoidalCategory

instance : MonoidalCategoryStruct (CommAlgebraCat.{u} R) where
  tensorObj := instMonoidalCategory.tensorObj
  whiskerLeft X _ _ f := tensorHom (ðŸ™ X) f
  whiskerRight {Xâ‚ Xâ‚‚} (f : Xâ‚ âŸ¶ Xâ‚‚) Y := tensorHom f (ðŸ™ Y)
  tensorHom := tensorHom
  tensorUnit := of R R
  associator X Y Z := (Algebra.TensorProduct.assoc R X Y Z).toCommAlgebraIso
  leftUnitor X := (Algebra.TensorProduct.lid R X).toCommAlgebraIso
  rightUnitor X := (Algebra.TensorProduct.rid R R X).toCommAlgebraIso

theorem forgetâ‚‚_map_associator_hom (X Y Z : CommAlgebraCat.{u} R) :
    (forgetâ‚‚ (CommAlgebraCat R) (ModuleCat R)).map (Î±_ X Y Z).hom =
      (Î±_
        (forgetâ‚‚ _ (ModuleCat R) |>.obj X)
        (forgetâ‚‚ _ (ModuleCat R) |>.obj Y)
        (forgetâ‚‚ _ (ModuleCat R) |>.obj Z)).hom := by
  rfl

set_option maxHeartbeats 800000 in
theorem forgetâ‚‚_map_associator_inv (X Y Z : CommAlgebraCat.{u} R) :
    (forgetâ‚‚ (CommAlgebraCat R) (ModuleCat R)).map (Î±_ X Y Z).inv =
      (Î±_
        (forgetâ‚‚ _ (ModuleCat R) |>.obj X)
        (forgetâ‚‚ _ (ModuleCat R) |>.obj Y)
        (forgetâ‚‚ _ (ModuleCat R) |>.obj Z)).inv := by
  rfl

set_option maxHeartbeats 800000 in
noncomputable instance instMonoidalCategory : MonoidalCategory (CommAlgebraCat.{u} R) :=
  Monoidal.induced
    (forgetâ‚‚ (CommAlgebraCat R) (ModuleCat R))
    { Î¼Iso := fun X Y => Iso.refl _
      ÎµIso := Iso.refl _
      associator_eq := fun X Y Z => by
        dsimp only [forgetâ‚‚_module_obj, forgetâ‚‚_map_associator_hom]
        simp only [eqToIso_refl, Iso.refl_trans, Iso.refl_symm, Iso.trans_hom, tensorIso_hom,
          Iso.refl_hom, MonoidalCategory.tensor_id]
        erw [Category.id_comp, Category.comp_id, MonoidalCategory.tensor_id, Category.id_comp]
      leftUnitor_eq := fun X => by
        dsimp only [forgetâ‚‚_module_obj, forgetâ‚‚_module_map, Iso.refl_symm, Iso.trans_hom,
          Iso.refl_hom, tensorIso_hom]
        erw [Category.id_comp, MonoidalCategory.tensor_id, Category.id_comp]
        rfl
      rightUnitor_eq := fun X => by
        dsimp
        erw [Category.id_comp, MonoidalCategory.tensor_id, Category.id_comp]
        exact congr_arg LinearEquiv.toLinearMap <|
          TensorProduct.AlgebraTensorModule.rid_eq_rid R X }

variable (R) in
/-- `forgetâ‚‚ (CommAlgebraCat R) (ModuleCat R)` as a monoidal functor. -/
def toModuleCatMonoidalFunctor : MonoidalFunctor (CommAlgebraCat.{u} R) (ModuleCat.{u} R) := by
  unfold instMonoidalCategory
  exact Monoidal.fromInduced (forgetâ‚‚ (CommAlgebraCat R) (ModuleCat R)) _

instance : Faithful (toModuleCatMonoidalFunctor R).toFunctor :=
  forgetâ‚‚_faithful _ _

end

end CommAlgebraCat
