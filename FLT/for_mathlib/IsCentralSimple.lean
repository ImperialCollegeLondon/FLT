/-
Copyright (c) 2024 Kevin Buzzard. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard
-/

import Mathlib.Algebra.Quaternion -- probably get away with less
import FLT.for_mathlib.Con
import FLT.for_mathlib.CrazySimple
import Mathlib.RingTheory.Flat.Basic
import Mathlib.LinearAlgebra.TensorProduct.RightExactness

/-!
# Characteristic predicate for central simple algebras

In this file we define the predicate `IsCentralSimple K D` where `K` is a field
and `D` is a (noncommutative) `K`-algebra.

Note that the predicate makes sense just for `K` a `CommRing` but it doesn't give the
right definition; for a commutative ring base one should use the theory of Azumaya algebras.
This adds an extra layer of complication which we don't need. In fact ideals of `K`
immediately give rise to nontrivial quotients of `D` so there are no central simple
algebras in this case according to our definition.

-/

universe u v w

open Classical
open scoped BigOperators

class IsCentralSimple
    (K : Type u) [Field K] (D : Type v) [Ring D] [Algebra K D] : Prop where
  is_central : ‚àÄ d : D, d ‚àà Subring.center D ‚Üí ‚àÉ k : K, d = algebraMap K D k
  is_simple : IsSimpleOrder (RingCon D)

variable (K : Type u) [Field K]

open Matrix in
theorem MatrixRing.isCentralSimple (Œπ : Type v) (hŒπ : Fintype Œπ) (hnonempty : Nonempty Œπ) [DecidableEq Œπ] :
    IsCentralSimple K (Matrix Œπ Œπ K) where
  is_central d hd := by
    rw [Subring.mem_center_iff] at hd
    convert mem_range_scalar_of_commute_stdBasisMatrix (M := d) fun i j _ => hd _
    simp_rw [Set.mem_range, eq_comm, algebraMap_eq_diagonal, Pi.algebraMap_def,
      Algebra.id.map_eq_self, scalar_apply]
  is_simple := inferInstance

namespace IsCentralSimple

variable (D : Type v) [Ring D] [Algebra K D] (h : IsCentralSimple K D)

/-
\begin{lemma}
    \label{IsCentralSimple.baseChange}
    If DD is a central simple algebra over~KK and L/KL/K is a field extension, then L‚äóKDL\otimes_KD
    is a central simple algebra over~LL.
\end{lemma}
\begin{proof}
    This is not too hard: it's lemma b of section 12.4 in Peirce's "Associative algebras".
    Will maybe write more on Saturday.
\end{proof}
-/
open scoped TensorProduct

section should_be_elsewhere

instance (B : Type*) [Ring B] [Algebra K B]: Algebra K (Subring.center B) :=
  RingHom.toAlgebra <| (algebraMap K B).codRestrict _ <| fun x => by
    rw [Subring.mem_center_iff]
    exact fun y => Algebra.commutes x y |>.symm

lemma TensorProduct.eq_repr_basis_right
    (B : Type*) [Ring B] [Algebra K B]
    (C : Type*) [Ring C] [Algebra K C]
    {ŒπC : Type*} (ùíû : Basis ŒπC K C)
    (x : B ‚äó[K] C) :
    ‚àÉ (b : ŒπC ‚Üí B) (s : Finset ŒπC), ‚àë i ‚àà s, b i ‚äó‚Çú[K] ùíû i = x := by
  let ‚Ñ¨ := Basis.ofVectorSpace K B
  let ùíØ := Basis.tensorProduct ‚Ñ¨ ùíû
  have eq1 := calc x
      _ = ‚àë ij ‚àà (ùíØ.repr x).support, (ùíØ.repr x) ij ‚Ä¢ ùíØ ij := ùíØ.total_repr x |>.symm
      _ = ‚àë ij ‚àà (ùíØ.repr x).support, (ùíØ.repr x) (ij.1, ij.2) ‚Ä¢ ùíØ (ij.1, ij.2) :=
          Finset.sum_congr rfl <| by simp
      _ = ‚àë i ‚àà (ùíØ.repr x).support.image Prod.fst, ‚àë j ‚àà (ùíØ.repr x).support.image Prod.snd,
            ùíØ.repr x (i, j) ‚Ä¢ ùíØ (i, j) := by
          rw [‚Üê Finset.sum_product']
          apply Finset.sum_subset
          ¬∑ rintro ‚ü®i, j‚ü© hij
            simp only [Finsupp.mem_support_iff, ne_eq, Finset.mem_product, Finset.mem_image,
              Prod.exists, exists_and_right, exists_eq_right, Subtype.exists, ùíØ] at hij ‚ä¢
            exact ‚ü®‚ü®j, hij‚ü©, ‚ü®i.1, ‚ü®i.2, hij‚ü©‚ü©‚ü©
          ¬∑ rintro ‚ü®i, j‚ü© hij1 hij2
            simp only [Finset.mem_product, Finset.mem_image, Finsupp.mem_support_iff, ne_eq,
              Prod.exists, exists_and_right, exists_eq_right, Subtype.exists, Decidable.not_not,
              Basis.tensorProduct_apply, smul_eq_zero, ùíØ] at hij1 hij2 ‚ä¢
            rw [hij2]
            tauto
      _ = ‚àë j ‚àà (ùíØ.repr x).support.image Prod.snd, ‚àë i ‚àà (ùíØ.repr x).support.image Prod.fst,
            ùíØ.repr x (i, j) ‚Ä¢ ùíØ (i, j) := Finset.sum_comm
      _ = ‚àë j ‚àà (ùíØ.repr x).support.image Prod.snd, ‚àë i ‚àà (ùíØ.repr x).support.image Prod.fst,
            ùíØ.repr x (i, j) ‚Ä¢ (‚Ñ¨ i ‚äó‚Çú[K] ùíû j) := by
          refine Finset.sum_congr rfl fun _ _ => ?_
          simp only [ùíØ, Basis.tensorProduct_apply]
      _ =  ‚àë j ‚àà (ùíØ.repr x).support.image Prod.snd, ‚àë i ‚àà (ùíØ.repr x).support.image Prod.fst,
            (ùíØ.repr x (i, j) ‚Ä¢ ‚Ñ¨ i) ‚äó‚Çú[K] ùíû j := by
          refine Finset.sum_congr rfl fun _ _ => Finset.sum_congr rfl fun _ _ => ?_
          rw [TensorProduct.smul_tmul']
      _ =  ‚àë j ‚àà (ùíØ.repr x).support.image Prod.snd, (‚àë i ‚àà (ùíØ.repr x).support.image Prod.fst,
            (ùíØ.repr x (i, j) ‚Ä¢ ‚Ñ¨ i)) ‚äó‚Çú[K] ùíû j := by
          refine Finset.sum_congr rfl fun _ _ => ?_
          rw [TensorProduct.sum_tmul]
  exact ‚ü®_, _, eq1.symm‚ü©


lemma TensorProduct.sum_tmul_basis_right_eq_zero
    (B : Type*) [Ring B] [Algebra K B]
    (C : Type*) [Ring C] [Algebra K C]
    {ŒπC : Type*} (ùíû : Basis ŒπC K C)
    (s : Finset ŒπC) (b : ŒπC ‚Üí B)
    (h : ‚àë i ‚àà s, b i ‚äó‚Çú[K] ùíû i = 0) :
    ‚àÄ i ‚àà s, b i = 0 := by
  let ‚Ñ¨ := Basis.ofVectorSpace K B
  let ùíØ := Basis.tensorProduct ‚Ñ¨ ùíû
  let I := s.biUnion fun i => (‚Ñ¨.repr (b i)).support
  have eq1 := calc (0 : B ‚äó[K] C)
      _ = ‚àë i ‚àà s, b i ‚äó‚Çú[K] ùíû i := h.symm
      _ = ‚àë i ‚àà s, (‚àë k ‚àà (‚Ñ¨.repr (b i)).support, (‚Ñ¨.repr (b i)) k ‚Ä¢ ‚Ñ¨ k) ‚äó‚Çú[K] ùíû i := by
          refine Finset.sum_congr rfl fun z _ => ?_
          congr
          exact ‚Ñ¨.total_repr (b z) |>.symm
      _ = ‚àë i ‚àà s, ‚àë k ‚àà (‚Ñ¨.repr (b i)).support, (‚Ñ¨.repr (b i)) k ‚Ä¢ (‚Ñ¨ k ‚äó‚Çú[K] ùíû i) := by
          refine Finset.sum_congr rfl fun z _ => ?_
          rw [TensorProduct.sum_tmul]
          refine Finset.sum_congr rfl fun _ _ => ?_
          rw [TensorProduct.smul_tmul']
      _ = ‚àë i ‚àà s, ‚àë k ‚àà I, (‚Ñ¨.repr (b i)) k ‚Ä¢ (‚Ñ¨ k ‚äó‚Çú[K] ùíû i) := by
          refine Finset.sum_congr rfl fun j h => ?_
          apply Finset.sum_subset
          ¬∑ intro i hi
            simp only [Finsupp.mem_support_iff, ne_eq, Finset.mem_biUnion, I] at hi ‚ä¢
            exact ‚ü®_, h, hi‚ü©
          ¬∑ intro i hi1 hi2
            simp only [Finsupp.mem_support_iff, ne_eq, Decidable.not_not, smul_eq_zero]
              at hi1 hi2 ‚ä¢
            tauto
      _ = ‚àë k ‚àà I, ‚àë i ‚àà s, (‚Ñ¨.repr (b i)) k ‚Ä¢ (‚Ñ¨ k ‚äó‚Çú[K] ùíû i) := Finset.sum_comm
      _ = ‚àë ij ‚àà I √óÀ¢ s, (‚Ñ¨.repr (b ij.2)) ij.1 ‚Ä¢ (‚Ñ¨ ij.1 ‚äó‚Çú[K] ùíû ij.2) := by
          rw [Finset.sum_product]
      _ = ‚àë ij ‚àà I √óÀ¢ s, (‚Ñ¨.repr (b ij.2)) ij.1 ‚Ä¢ ùíØ ij := by
          refine Finset.sum_congr rfl fun ij _ => ?_
          rw [Basis.tensorProduct_apply]
  have LI := ùíØ.linearIndependent
  rw [linearIndependent_iff'] at LI
  specialize LI (I √óÀ¢ s) _ eq1.symm
  intro i hi
  rw [‚Üê ‚Ñ¨.total_repr (b i)]
  change ‚àë _ ‚àà _, _ = 0
  simp only [LinearMap.coe_smulRight, LinearMap.id_coe, id_eq]
  refine Finset.sum_eq_zero fun j hj => ?_
  specialize LI ‚ü®j, i‚ü© (by
    simp only [Finset.mem_product, Finset.mem_biUnion, Finsupp.mem_support_iff, ne_eq, I] at hj ‚ä¢
    refine ‚ü®‚ü®_, hi, hj‚ü©, hi‚ü©)
  simp [LI]

lemma Subalgebra.centralizer_sup (K B : Type*) [CommRing K] [Ring B] [Algebra K B]
    (S T : Subalgebra K B) :
    Subalgebra.centralizer K ((S ‚äî T : Subalgebra K B) : Set B) =
    Subalgebra.centralizer K S ‚äì Subalgebra.centralizer K T := by
  ext x
  simp only [Subalgebra.mem_centralizer_iff, SetLike.mem_coe, Algebra.mem_inf]
  constructor
  ¬∑ intro h
    exact ‚ü®fun g hg => h g <| (le_sup_left : S ‚â§ S ‚äî T) hg,
      fun g hg => h g <| (le_sup_right : T ‚â§ S ‚äî T) hg‚ü©
  ¬∑ rintro ‚ü®h1, h2‚ü© g hg
    change g ‚àà Algebra.adjoin _ _ at hg
    refine Algebra.adjoin_induction hg ?_ ?_ ?_ ?_
    ¬∑ rintro y (hy | hy)
      ¬∑ exact h1 y hy
      ¬∑ exact h2 y hy
    ¬∑ intro k
      exact Algebra.commutes k x
    ¬∑ intro y1 y2 hy1 hy2
      simp [add_mul, hy1, hy2, mul_add]
    ¬∑ intro y1 y2 hy1 hy2
      rw [mul_assoc, hy2, ‚Üê mul_assoc, hy1, mul_assoc]

lemma TensorProduct.left_tensor_base_sup_base_tensor_right
    (K B C : Type*) [CommRing K] [Ring B] [Algebra K B] [Ring C] [Algebra K C] :
    (Algebra.TensorProduct.map (AlgHom.id K B) (Algebra.ofId K C)).range ‚äî
    (Algebra.TensorProduct.map (Algebra.ofId K B) (AlgHom.id K C)).range = ‚ä§ := by
  rw [eq_top_iff]
  rintro x -
  induction x using TensorProduct.induction_on with
  | zero => exact Subalgebra.zero_mem _
  | tmul b c =>
    rw [show b ‚äó‚Çú[K] c = b ‚äó‚Çú[K] 1 * 1 ‚äó‚Çú[K] c by simp]
    exact Algebra.mul_mem_sup ‚ü®b ‚äó‚Çú 1, by simp‚ü© ‚ü®1 ‚äó‚Çú c, by simp‚ü©
  | add x y hx hy =>
    exact Subalgebra.add_mem _ hx hy

-- We need to restrict the universe, because we used properties of flatness.
lemma TensorProduct.submodule_tensor_inf_tensor_submodule [Small.{v, u} K]
    (B C : Type v) [AddCommGroup B] [Module K B] [AddCommGroup C] [Module K C]
    (b : Submodule K B) (c : Submodule K C) :
    LinearMap.range (TensorProduct.map b.subtype .id) ‚äì
    LinearMap.range (TensorProduct.map .id c.subtype) =
    LinearMap.range (TensorProduct.map b.subtype c.subtype) := by

  refine le_antisymm ?_ ?_
  ¬∑ letI : Module.Flat K (B ‚ß∏ b) := Module.Flat.of_free _ _

    let u : b ‚äó[K] c ‚Üí‚Çó[K] B ‚äó[K] c := TensorProduct.map b.subtype LinearMap.id
    let v : B ‚äó[K] c ‚Üí‚Çó[K] (B ‚ß∏ b) ‚äó[K] c :=
      TensorProduct.map (Submodule.mkQ _) LinearMap.id
    have exactuv : Function.Exact u v := by
      apply rTensor_exact
      rw [LinearMap.exact_iff]
      simp only [Submodule.ker_mkQ, Submodule.range_subtype]
      exact Submodule.mkQ_surjective _

    let u' : b ‚äó[K] C ‚Üí‚Çó[K] B ‚äó[K] C := TensorProduct.map b.subtype LinearMap.id
    let v' : B ‚äó[K] C ‚Üí‚Çó[K] (B ‚ß∏ b) ‚äó[K] C := TensorProduct.map (Submodule.mkQ _) LinearMap.id
    have exactu'v' : Function.Exact u' v' := by
      apply rTensor_exact
      rw [LinearMap.exact_iff]
      simp only [Submodule.ker_mkQ, Submodule.range_subtype]
      exact Submodule.mkQ_surjective _

    let Œ± : b ‚äó[K] c ‚Üí‚Çó[K] b ‚äó[K] C := TensorProduct.map LinearMap.id c.subtype
    let Œ≤ : B ‚äó[K] c ‚Üí‚Çó[K] B ‚äó[K] C := TensorProduct.map LinearMap.id c.subtype
    let Œ≥ : (B ‚ß∏ b) ‚äó[K] c ‚Üí‚Çó[K] (B ‚ß∏ b) ‚äó[K] C := TensorProduct.map LinearMap.id c.subtype

    have Œ≥_inj : Function.Injective Œ≥ :=
      Module.Flat.iff_lTensor_preserves_injective_linearMap K (B ‚ß∏ b) |>.1 inferInstance
        c.subtype c.injective_subtype

    rintro z (hz : z ‚àà LinearMap.range u' ‚äì LinearMap.range Œ≤)
    obtain ‚ü®z, rfl‚ü© := hz.2
    have comm0 : v' ‚àò‚Çó Œ≤ = Œ≥ ‚àò‚Çó v := by
      ext
      simp [v', Œ≤, Œ≥, v]
    have hz1 : v' (Œ≤ z) = Œ≥ (v z) := congr($comm0 z)
    have hz2 : v' (Œ≤ z) = 0 := by
      rw [‚Üê LinearMap.mem_ker]
      rw [LinearMap.exact_iff] at exactu'v'
      rw [exactu'v']
      exact hz.1
    rw [hz2] at hz1
    have hz3 : v z ‚àà LinearMap.ker Œ≥ := by rw [LinearMap.mem_ker]; exact hz1.symm
    replace hz3 : v z = 0 := by
      rw [‚Üê LinearMap.ker_eq_bot] at Œ≥_inj; rw [Œ≥_inj] at hz3; exact hz3
    replace hz3 : z ‚àà LinearMap.ker v := hz3
    replace hz3 : z ‚àà LinearMap.range u := by
      rw [LinearMap.exact_iff] at exactuv
      rwa [‚Üê exactuv]
    obtain ‚ü®z, rfl‚ü© := hz3
    change (Œ≤ ‚àò‚Çó u) z ‚àà _

    have comm1 : Œ≤ ‚àò‚Çó u = u' ‚àò‚Çó Œ± := by
      ext
      simp [Œ≤, u, u', Œ±]

    rw [comm1, LinearMap.comp_apply]
    refine ‚ü®z, ?_‚ü©
    simp only [u', Œ±]
    change _ = (TensorProduct.map b.subtype .id ‚àò‚Çó TensorProduct.map .id c.subtype) z
    rw [‚Üê TensorProduct.map_comp, LinearMap.comp_id, LinearMap.id_comp]

  ¬∑ rintro _ ‚ü®x, rfl‚ü©
    refine ‚ü®‚ü®TensorProduct.map LinearMap.id c.subtype x, ?_‚ü©,
      ‚ü®TensorProduct.map b.subtype LinearMap.id x, ?_‚ü©‚ü©
    ¬∑ change (TensorProduct.map b.subtype LinearMap.id ‚àò‚Çó
        TensorProduct.map LinearMap.id c.subtype) x = _
      rw [‚Üê TensorProduct.map_comp]
      rfl
    ¬∑ change (TensorProduct.map LinearMap.id c.subtype ‚àò‚Çó
        TensorProduct.map b.subtype LinearMap.id) x = _
      rw [‚Üê TensorProduct.map_comp]
      rfl

end should_be_elsewhere

lemma centralizer_tensorProduct_eq_center_tensorProduct_right
    (B : Type*) [Ring B] [Algebra K B]
    (C : Type*) [Ring C] [Algebra K C] :
    Subalgebra.centralizer K
      (Algebra.TensorProduct.map (AlgHom.id K B) (Algebra.ofId K C)).range =
      (Algebra.TensorProduct.map (Subalgebra.center K B).val (AlgHom.id K C)).range := by
  ext w; constructor
  ¬∑ intro hw
    rw [Subalgebra.mem_centralizer_iff] at hw
    let ùíû := Basis.ofVectorSpace K C
    obtain ‚ü®b, S, rfl‚ü© := TensorProduct.eq_repr_basis_right K B C ùíû w

    have aux (i) (hi : i ‚àà S) : b i ‚àà Subalgebra.center K B := by
      rw [Subalgebra.mem_center_iff]
      intro x
      specialize hw (x ‚äó‚Çú[K] 1) (by
        simp only [AlgHom.coe_range, Set.mem_range]
        exact ‚ü®x ‚äó‚Çú[K] 1, by simp‚ü©)
      simp only [Finset.mul_sum, Algebra.TensorProduct.tmul_mul_tmul, one_mul, Finset.sum_mul,
        mul_one] at hw
      rw [‚Üê sub_eq_zero, ‚Üê Finset.sum_sub_distrib] at hw
      simp_rw [‚Üê TensorProduct.sub_tmul] at hw
      simpa [sub_eq_zero] using TensorProduct.sum_tmul_basis_right_eq_zero K B C ùíû S _ hw i hi
    exact Subalgebra.sum_mem _ fun j hj => ‚ü®‚ü®b j, aux _ hj‚ü© ‚äó‚Çú[K] ùíû j, by simp‚ü©
  ¬∑ rintro ‚ü®w, rfl‚ü©
    rw [Subalgebra.mem_centralizer_iff]
    rintro _ ‚ü®x, rfl‚ü©
    induction w using TensorProduct.induction_on with
    | zero => simp
    | tmul b c =>
      simp only [AlgHom.toRingHom_eq_coe, RingHom.coe_coe, Algebra.TensorProduct.map_tmul,
        Subalgebra.coe_val, AlgHom.coe_id, id_eq]
      induction x using TensorProduct.induction_on with
      | zero => simp
      | tmul x0 x1 =>
        simp only [Algebra.TensorProduct.map_tmul, AlgHom.coe_id, id_eq,
          Algebra.TensorProduct.tmul_mul_tmul]
        rcases b with ‚ü®b, hb‚ü©
        congr 1
        ¬∑ rw [Subalgebra.mem_center_iff] at hb
          exact hb _
        ¬∑ exact Algebra.commutes _ _
      | add x x' hx hx' =>
        rw [map_add, add_mul, hx, hx', mul_add]
    | add y z hy hz =>
      rw [map_add, mul_add, hy, hz, add_mul]

lemma centralizer_tensorProduct_eq_left_tensorProduct_center
    (B : Type*) [Ring B] [Algebra K B]
    (C : Type*) [Ring C] [Algebra K C] :
    Subalgebra.centralizer K
      (Algebra.TensorProduct.map (Algebra.ofId K B) (AlgHom.id K C)).range =
      (Algebra.TensorProduct.map (AlgHom.id K B) (Subalgebra.center K C).val).range := by
  have H1 := centralizer_tensorProduct_eq_center_tensorProduct_right K C B
  ext z
  have h1 :
      z ‚àà Subalgebra.centralizer K
        (Algebra.TensorProduct.map (Algebra.ofId K B) (AlgHom.id K C)).range  ‚Üî
      (Algebra.TensorProduct.comm K B C z) ‚àà Subalgebra.centralizer K
        (Algebra.TensorProduct.map (AlgHom.id K C) (Algebra.ofId K B)).range := by
    rw [Subalgebra.mem_centralizer_iff, Subalgebra.mem_centralizer_iff]
    constructor
    ¬∑ rintro h _ ‚ü®x, rfl‚ü©
      specialize h (Algebra.TensorProduct.comm K C B
        (Algebra.TensorProduct.map (AlgHom.id K C) (Algebra.ofId K B) x))
        (by
          simp only [AlgHom.coe_range, Set.mem_range]
          refine ‚ü®Algebra.TensorProduct.comm K C K x, ?_‚ü©
          change (AlgHom.comp (Algebra.TensorProduct.map (Algebra.ofId K B) (AlgHom.id K C))
            (Algebra.TensorProduct.comm K C K)) x =
            (AlgHom.comp (Algebra.TensorProduct.comm K C B)
              (Algebra.TensorProduct.map (AlgHom.id K C) (Algebra.ofId K B))) x
          refine DFunLike.congr_fun ?_ x
          ext
          simp)

      apply_fun Algebra.TensorProduct.comm K C B
      simp only [AlgHom.toRingHom_eq_coe, RingHom.coe_coe, map_mul]
      convert h  <;>
      rw [‚Üê Algebra.TensorProduct.comm_symm] <;>
      simp only [AlgEquiv.symm_apply_apply]
    ¬∑ rintro h _ ‚ü®x, rfl‚ü©
      specialize h (Algebra.TensorProduct.comm K B C
        (Algebra.TensorProduct.map (Algebra.ofId K B) (AlgHom.id K C) x))
        (by
          simp only [AlgHom.coe_range, Set.mem_range]
          refine ‚ü®Algebra.TensorProduct.comm K K C x, ?_‚ü©
          change (AlgHom.comp (Algebra.TensorProduct.map (AlgHom.id K C) (Algebra.ofId K B))
            (Algebra.TensorProduct.comm K K C)) x =
            (AlgHom.comp (Algebra.TensorProduct.comm K B C)
              (Algebra.TensorProduct.map (Algebra.ofId K B) (AlgHom.id K C))) x
          refine DFunLike.congr_fun ?_ x
          ext
          simp)
      apply_fun Algebra.TensorProduct.comm K B C
      simp only [AlgHom.toRingHom_eq_coe, RingHom.coe_coe, map_mul]
      convert h
  rw [h1, H1]
  simp only [AlgHom.mem_range]
  constructor
  ¬∑ rintro ‚ü®x, hx‚ü©
    apply_fun (Algebra.TensorProduct.comm K B C).symm at hx
    simp only [AlgEquiv.symm_apply_apply] at hx
    refine ‚ü®(Algebra.TensorProduct.comm K B _).symm x, Eq.trans ?_ hx‚ü©
    simp only [Algebra.TensorProduct.comm_symm]
    change (AlgHom.comp (Algebra.TensorProduct.map (AlgHom.id K B) (Subalgebra.center K C).val)
      (Algebra.TensorProduct.comm K (Subalgebra.center K C) B)) x =
      (AlgHom.comp (Algebra.TensorProduct.comm K C B)
      (Algebra.TensorProduct.map (Subalgebra.center K C).val (AlgHom.id K B))) x
    refine DFunLike.congr_fun ?_ x
    ext x
    simp only [AlgHom.coe_comp, AlgHom.coe_coe, Function.comp_apply,
      Algebra.TensorProduct.includeLeft_apply, Algebra.TensorProduct.comm_tmul,
      Algebra.TensorProduct.map_tmul, map_one, Subalgebra.coe_val]
    rfl
  ¬∑ rintro ‚ü®x, hx‚ü©
    refine ‚ü®Algebra.TensorProduct.comm _ _ _ x, ?_‚ü©
    apply_fun (Algebra.TensorProduct.comm K B C).symm
    simp only [AlgEquiv.symm_apply_apply]
    rw [‚Üê hx]
    change AlgHom.comp (Algebra.TensorProduct.comm K B C).symm
      (AlgHom.comp (Algebra.TensorProduct.map (Subalgebra.center K C).val (AlgHom.id K B))
        (Algebra.TensorProduct.comm K B ‚Ü•(Subalgebra.center K C))) x =
      (Algebra.TensorProduct.map (AlgHom.id K B) (Subalgebra.center K C).val) x
    refine DFunLike.congr_fun ?_ x
    ext x
    simp only [AlgHom.coe_comp, AlgHom.coe_coe, Function.comp_apply,
      Algebra.TensorProduct.includeLeft_apply, Algebra.TensorProduct.comm_tmul,
      Algebra.TensorProduct.map_tmul, map_one, AlgHom.coe_id, id_eq,
      Algebra.TensorProduct.comm_symm_tmul, Algebra.TensorProduct.map_comp_includeLeft,
      AlgHom.comp_id]
    rfl

-- We need to restrict the universe, because we used properties of flatness.
lemma center_tensorProduct [Small.{v, u} K]
    (B C : Type v) [Ring B] [Algebra K B] [Ring C] [Algebra K C] :
    Subalgebra.center K (B ‚äó[K] C) =
      (Algebra.TensorProduct.map (Subalgebra.center K B).val
        (Subalgebra.center K C).val).range := by
  rw [show Subalgebra.center K (B ‚äó[K] C) = Subalgebra.centralizer K (‚ä§ : Subalgebra K (B ‚äó[K] C))
    by simp, ‚Üê TensorProduct.left_tensor_base_sup_base_tensor_right K B C,
    Subalgebra.centralizer_sup, centralizer_tensorProduct_eq_center_tensorProduct_right,
    centralizer_tensorProduct_eq_left_tensorProduct_center]
  suffices eq :
    Subalgebra.toSubmodule (Algebra.TensorProduct.map (Subalgebra.center K B).val (AlgHom.id K C)).range ‚äì
    Subalgebra.toSubmodule (Algebra.TensorProduct.map (AlgHom.id K B) (Subalgebra.center K C).val).range =
    Subalgebra.toSubmodule (Algebra.TensorProduct.map (Subalgebra.center K B).val (Subalgebra.center K C).val).range by
    apply_fun Subalgebra.toSubmodule using Subalgebra.toSubmodule_injective
    rw [‚Üê eq]
    ext x
    simp only [Algebra.inf_toSubmodule, Submodule.mem_inf, Subalgebra.mem_toSubmodule,
      AlgHom.mem_range]

  have eq1:
      Subalgebra.toSubmodule (Algebra.TensorProduct.map (Subalgebra.center K B).val (AlgHom.id K C)).range =
      LinearMap.range (TensorProduct.map (Subalgebra.center K B).val.toLinearMap (LinearMap.id)) := by
    rfl
  rw [eq1]

  have eq2 :
      Subalgebra.toSubmodule (Algebra.TensorProduct.map (AlgHom.id K B) (Subalgebra.center K C).val).range =
      LinearMap.range (TensorProduct.map (LinearMap.id) (Subalgebra.center K C).val.toLinearMap) := by
    rfl
  rw [eq2]

  have eq3 :
      Subalgebra.toSubmodule (Algebra.TensorProduct.map (Subalgebra.center K B).val (Subalgebra.center K C).val).range =
      LinearMap.range (TensorProduct.map (Subalgebra.center K B).val.toLinearMap
        (Subalgebra.center K C).val.toLinearMap) := by
    rfl
  rw [eq3]

  have := TensorProduct.submodule_tensor_inf_tensor_submodule K B C
    (Subalgebra.toSubmodule <| .center K B)
    (Subalgebra.toSubmodule <| .center K C)

  have eq4 : (Subalgebra.toSubmodule (Subalgebra.center K B)).subtype =
    (Subalgebra.center K B).val.toLinearMap := by rfl
  rw [eq4] at this

  have eq5 : (Subalgebra.toSubmodule (Subalgebra.center K C)).subtype =
    (Subalgebra.center K C).val.toLinearMap := by rfl
  rw [eq5] at this
  rw [this]

-- lemma baseChange (L : Type w) [Field L] [Algebra K L] : IsCentralSimple L (L ‚äó[K] D) := sorry
-- We can't have `L` to have different universe level of `D` in this proof, again due that we used
-- `flatness`
instance baseChange [Small.{v, u} K] (L : Type v) [Field L] [Algebra K L] :
    IsCentralSimple L (L ‚äó[K] D) where
  is_central:= by
    intro z hz
    replace hz : z ‚àà Subalgebra.center K (L ‚äó[K] D) := by
      rw [Subalgebra.mem_center_iff]
      intro x
      rw [Subring.mem_center_iff] at hz
      exact hz x

    rw [center_tensorProduct K L] at hz
    obtain ‚ü®x, rfl‚ü© := hz
    induction x using TensorProduct.induction_on with
    | zero => exact ‚ü®0, by simp‚ü©
    | tmul l d =>
      obtain ‚ü®k, hk‚ü© := h.is_central (d := d.1)
        (Subring.mem_center_iff.2 <| fun x ‚Ü¶ Subalgebra.mem_center_iff.mp d.2 x)
      simp only [AlgHom.toRingHom_eq_coe, RingHom.coe_coe, Algebra.TensorProduct.map_tmul,
        Subalgebra.coe_val, Algebra.TensorProduct.algebraMap_apply, Algebra.id.map_eq_id,
        RingHom.id_apply]
      simp_rw [hk]
      refine ‚ü®k ‚Ä¢ l, ?_‚ü©
      rw [TensorProduct.smul_tmul, Algebra.algebraMap_eq_smul_one]
    | add x y hx hy =>
      obtain ‚ü®kx, hkx‚ü© := hx
      obtain ‚ü®ky, hky‚ü© := hy
      simp only [AlgHom.toRingHom_eq_coe, RingHom.coe_coe, Algebra.TensorProduct.algebraMap_apply,
        Algebra.id.map_eq_id, RingHom.id_apply] at hkx hky
      refine ‚ü®kx + ky, ?_‚ü©
      simp only [AlgHom.toRingHom_eq_coe, map_add, RingHom.coe_coe, hkx, hky,
        Algebra.TensorProduct.algebraMap_apply, Algebra.id.map_eq_id, RingHom.id_apply]
  is_simple := by sorry
end IsCentralSimple

section CSA_implies_CSA
variable (K : Type u) [Field K]
variable (B : Type*) [Ring B]

lemma top_eq_ring (R :Type*)[Ring R] : (‚ä§ : RingCon R) = (‚ä§ : Set R) := by
  aesop

lemma _root_.AlgEquiv.isCentralSimple {K B C : Type*}
    [Field K] [Ring B] [Algebra K B] [hcs : IsCentralSimple K B]
    [Ring C] [Algebra K C] (e : B ‚âÉ‚Çê[K] C):
    IsCentralSimple K C where
  is_central := by
    intro z hz
    obtain ‚ü®k, hk‚ü© := hcs.is_central (e.symm z) (by
      simp only [Subring.mem_center_iff] at hz ‚ä¢
      exact fun x => by simpa using congr(e.symm $(hz (e x))))
    exact ‚ü®k, by simpa using congr(e $hk)‚ü©
  is_simple := by
    haveI := hcs.is_simple
    exact RingCon.orderIsoOfRingEquiv e.symm.toRingEquiv |>.isSimpleOrder


theorem CSA_implies_CSA (K : Type*) (B : Type*) [Field K] [Ring B] [Algebra K B]
    [IsSimpleOrder (RingCon B)] (n : ‚Ñï)
    (D : Type*) (hn : 0 < n) (h : DivisionRing D) [Algebra K D]
    (Wdb: B ‚âÉ‚Çê[K] (Matrix (Fin n) (Fin n) D)):
    IsCentralSimple K B ‚Üí IsCentralSimple K D := by
  intro BCS
  letI inst1 := Wdb.isCentralSimple
  let hnone : Nonempty (Fin n) := ‚ü®0, hn‚ü©
  constructor
  ¬∑ intro d hd
    obtain ‚ü®k, hk‚ü© := inst1.is_central (Matrix.diagonal fun _ => d) (by
      rw [Matrix.mem_center_iff]
      refine ‚ü®‚ü®d, hd‚ü©, ?_‚ü©
      simp only [Submonoid.mk_smul]
      ext i j
      rw [Matrix.diagonal_apply, Matrix.smul_apply, Matrix.one_apply]
      split_ifs <;> simp)
    refine ‚ü®k, ?_‚ü©
    apply_fun (¬∑ ‚ü®0, by omega‚ü© ‚ü®0, by omega‚ü©) at hk
    simp only [Matrix.diagonal_apply_eq] at hk
    rw [hk]
    rfl
  ¬∑ exact RingCon.equivRingConMatrix' D (Œπ := (Fin n)) ‚ü®0, hn‚ü© |>.isSimpleOrder


-- restrict to 4d case
-- theorem exists_quaternionAlgebra_iso (hK : (2 : K) ‚â† 0) :
--     ‚àÉ a b : K, Nonempty (D ‚âÉ‚Çê[K] ‚Ñç[K, a, b]) := sorry
