import FLT.ForMathlib.MiscLemmas
import Mathlib.Topology.Algebra.Ring.Basic
import Mathlib.LinearAlgebra.FreeModule.Finite.Basic

/-!
# An "action topology" for modules over a topological ring

If `R` is a topological space acting on an additive abelian group `A`, we define
the *action topology* to be the finest topology on `A` making `‚Ä¢ : R √ó A ‚Üí A`
and `+ : A √ó A ‚Üí A` continuous (with all the products having the product topology).

This topology was suggested by Will Sawin [here](https://mathoverflow.net/a/477763/1384).

## Mathematical details

I (buzzard) don't know of any reference for this other than Sawin's mathoverflow answer,
so I expand some of the details here.

First note that there *is* a finest topology with this property! Indeed, topologies on a fixed
type form a complete lattice (infinite infs and sups exist). So if `œÑ` is the Inf of all
the topologies on `A` which make `+` and `‚Ä¢` continuous, then the claim is that `+` and `‚Ä¢`
are still continuous for `œÑ` (note that topologies are ordered so that finer topologies
are smaller). To show `+ : A √ó A ‚Üí A` is continuous we equivalently need to show
that the pushforward of the product topology `œÑ √ó œÑ` along `+` is `‚â§ œÑ`, and because `œÑ` is
the greatest lower bound of the topologies making `‚Ä¢` and `+` continuous,
it suffices to show that it's `‚â§ œÉ` for any topology `œÉ` on `A` which makes `+` and `‚Ä¢` continuous.
However pushforward and products are monotone, so `œÑ √ó œÑ ‚â§ œÉ √ó œÉ`, and the pushforward of
`œÉ √ó œÉ` is `‚â§ œÉ` because that's precisely the statement that `+` is continuous for `œÉ`.
The proof for `‚Ä¢` is similar.

A *topological module* for a topological ring `R` is an `R`-module `A` with a topology
making `+` and `‚Ä¢` continuous. A crucial observation is that if `M` is a topological `R`-module,
if `A` is an `R`-module with no topology, and if `œÜ : A ‚Üí M` is linear, then the pullback of
`M`'s topology to `A` is a topology making `A` into a topological module. Let's for example
check that `‚Ä¢` is continuous. If `U ‚äÜ A` is open
then by definition of the pullback topology, `U = œÜ‚Åª¬π(V)` for some open `V ‚äÜ M`, and
now the pullback of `U` under `‚Ä¢` is just the pullback along the continuous map
`id √ó œÜ : R √ó A ‚Üí R √ó M` of the preimage of `V` under the continuous map `‚Ä¢ : R √ó M ‚Üí M`,
so it's open. The proof for `+` is similar.

As a consequence of this, we see that all linear maps are automatically continuous for
the action topology. Indeed the argument above shows that if `A ‚Üí M` is linear then the action
topology on `A` is `‚â§` the pullback of the action topology on `M` (because it's the inf of a set
containing this topology) which is the definition of continuity.

We also deduce that the action topology is a functor from the category of `R`-modules
(`R` a topological ring) to the category of topological `R`-modules, and it is perhaps
unsurprising that this is an adjoint to the forgetful functor. Indeed, if `A` is an `R`-module
and `M` is a topological `R`-module, then the linear maps `A ‚Üí M` are precisely the continuous
linear maps from `A` with its action topology, to `M`, so the action topology is a left adjoint
to the forgetful functor.

This file develops the theory of the action topology. We prove that the action topology on
`R` as a module over itself is `R`'s original topology, that the action topology on a product
of modules is the product of the action topologies, and that the action topology on a quotient
module is the quotient topology.

We also show the slightly more subtle result that if `M`, `N` and `P` are `R`-modules
equipped with the action topology and if furthermore `M` is finite as an `R`-module,
then any bilinear map `M √ó N ‚Üí P` is continuous.

As a consequence of this, we deduce that if `R` is a commutative topological ring
and `A` is an `R`-algebra of finite type as `R`-module, then `A` with its module
topology becomes a topological ring (i.e., multiplication is continuous).

## TODO

1) add the statement that the action topology is a functor from the category of `R`-modules
to the category of topological `R`-modules, and prove it's an adjoint

2) PRs to mathlib:

2a) weaken ring to semiring in some freeness statements in mathlib and then weaken
the corresponding statements in this file

2b) PR `induced_sInf`, `induced_continuous_smul`, `induced_continuous_add`,
  `isOpenMap_of_coinduced`, `LinearEquiv.sumPiEquivProdPi` and whatever else I use here.

-/

section basics

/-
This section is just boilerplate, defining the action topology and making
some infrastructure.
-/
variable (R : Type*) [TopologicalSpace R] (A : Type*) [Add A] [SMul R A]

/-- The action topology, for a module `A` over a topological ring `R`. It's the finest topology
making addition and the `R`-action continuous, or equivalently the finest topology making `A`
into a topological `R`-module. More precisely it's the Inf of the set of
topologies with these properties; theorems `continuousSMul` and `continuousAdd` show
that the action topology also has these properties. -/
abbrev actionTopology : TopologicalSpace A :=
  sInf {t | @ContinuousSMul R A _ _ t ‚àß @ContinuousAdd A t _}

/-- A class asserting that the topology on a module over a topological ring `R` is
the action topology. See `actionTopology` for more discussion of the action topology. -/
class IsActionTopology [œÑA : TopologicalSpace A] : Prop where
  isActionTopology' : œÑA = actionTopology R A

theorem isActionTopology [œÑA : TopologicalSpace A] [IsActionTopology R A] :
    œÑA = actionTopology R A :=
  IsActionTopology.isActionTopology' (R := R) (A := A)

/-- Scalar multiplication `‚Ä¢ : R √ó A ‚Üí A` is continuous if `R` is a topological
ring, and `A` is an `R` module with the action topology. -/
theorem ActionTopology.continuousSMul : @ContinuousSMul R A _ _ (actionTopology R A) :=
  -- Proof: We need to prove that the product topology is finer than the pullback
  -- of the action topology. But the action topology is an Inf and thus a limit,
  -- and pullback is a right adjoint, so it preserves limits.
  -- We must thus show that the product topology is finer than an Inf, so it suffices
  -- to show it's a lower bound, which is not hard. All this is wrapped into
  -- `continuousSMul_sInf`.
  continuousSMul_sInf <| fun _ _ ‚Ü¶ by simp_all only [Set.mem_setOf_eq]

/-- Addition `+ : A √ó A ‚Üí A` is continuous if `R` is a topological
ring, and `A` is an `R` module with the action topology. -/
theorem ActionTopology.continuousAdd : @ContinuousAdd A (actionTopology R A) _ :=
  continuousAdd_sInf <| fun _ _ ‚Ü¶ by simp_all only [Set.mem_setOf_eq]

instance instIsActionTopology_continuousSMul [TopologicalSpace A] [IsActionTopology R A] :
    ContinuousSMul R A := isActionTopology R A ‚ñ∏ ActionTopology.continuousSMul R A

theorem isActionTopology_continuousAdd [TopologicalSpace A] [IsActionTopology R A] :
    ContinuousAdd A := isActionTopology R A ‚ñ∏ ActionTopology.continuousAdd R A

theorem actionTopology_le [œÑA : TopologicalSpace A] [ContinuousSMul R A] [ContinuousAdd A] :
    actionTopology R A ‚â§ œÑA := sInf_le ‚ü®‚ÄπContinuousSMul R A‚Ä∫, ‚ÄπContinuousAdd A‚Ä∫‚ü©

end basics

namespace ActionTopology

section zero

instance subsingleton (R : Type*) [TopologicalSpace R] (A : Type*) [Add A] [SMul R A]
    [Subsingleton A] [TopologicalSpace A] : IsActionTopology R A where
  isActionTopology' := by
    ext U
    simp only [isOpen_discrete]

end zero


section one

/-

We now fix once and for all a topological semiring `R`.

We first prove that the action topology on `R` considered as a module over itself,
is `R`'s topology.

-/
instance instId (R : Type*) [Semiring R] [œÑR : TopologicalSpace R] [TopologicalSemiring R] :
    IsActionTopology R R := by
  constructor
  /- Let `R` be a topological ring with topology œÑR. To prove that `œÑR` is the action
  topology, it suffices to prove inclusions in both directions.
  One way is obvious: addition and multiplication are continuous for `R`, so the
  action topology is finer than `R` because it's the finest topology with these properties.-/
  refine le_antisymm ?_ (actionTopology_le R R)
  /- The other way is more interesting. We can interpret the problem as proving that
  the identity function is continuous from `R` with the action topology to `R` with
  its usual topology to `R` with the action topology.
  -/
  rw [‚Üê continuous_id_iff_le]
  /-
  The idea needed here is to rewrite the identity function as the composite of `r ‚Ü¶ (r,1)`
  from `R` to `R √ó R`, and multiplication on `R √ó R`, where we topologise `R √ó R`
  by giving the first `R` the usual topology and the second `R` the action topology.
  -/
  rw [show (id : R ‚Üí R) = (fun rs ‚Ü¶ rs.1 ‚Ä¢ rs.2) ‚àò (fun r ‚Ü¶ (r, 1)) by ext; simp]
  /-
  It thus suffices to show that each of these maps are continuous.
  -/
  apply @Continuous.comp R (R √ó R) R œÑR (@instTopologicalSpaceProd R R œÑR (actionTopology R R))
      (actionTopology R R)
  ¬∑ /-
    The map R √ó R ‚Üí R is `‚Ä¢`, so by a fundamental property of the action topology,
    this is continuous. -/
    exact @continuous_smul _ _ _ _ (actionTopology R R) <| ActionTopology.continuousSMul ..
  ¬∑ /-
    The map `R ‚Üí R √ó R` sending `r` to `(r,1)` is a map into a product, so it suffices to show
    that each of the two factors is continuous. But the first is the identity function and the
    second is a constant function. -/
    exact @Continuous.prod_mk _ _ _ _ (actionTopology R R) _ _ _ continuous_id <|
      @continuous_const _ _ _ (actionTopology R R) _

end one

section iso

variable {R : Type*} [œÑR : TopologicalSpace R] [Semiring R] --[TopologicalSemiring R]
variable {A : Type*} [AddCommMonoid A] [Module R A] [œÑA : TopologicalSpace A] [IsActionTopology R A]
variable {B : Type*} [AddCommMonoid B] [Module R B] [œÑB : TopologicalSpace B]

-- this is horrible. Why isn't it easy?
-- One reason: we are rolling our own continuous linear equivs!
-- **TODO** Ask about making continuous linear equivs properly
theorem iso (ehomeo : A ‚âÉ‚Çú B) (elinear : A ‚âÉ‚Çó[R] B) (he : ‚àÄ a, ehomeo a = elinear a) :
    IsActionTopology R B where
  isActionTopology' := by
    simp_rw [ehomeo.symm.inducing.1, isActionTopology R A, actionTopology, induced_sInf]
    congr 1
    ext œÑ
    rw [Set.mem_image]
    -- it's the same picture
    constructor
    ¬∑ rintro ‚ü®œÉ, ‚ü®hœÉ1, hœÉ2‚ü©, rfl‚ü©
      refine ‚ü®?_, ?_‚ü©
      ¬∑ convert @induced_continuous_smul (f := @id R) (hf := continuous_id)
          (g := elinear.symm.toMulActionHom) (œÑA := œÉ) _ _ _ _ _
        ext x
        rw [@Homeomorph.symm_apply_eq, he, ‚Üê LinearEquiv.symm_apply_eq elinear]
        rfl
      ¬∑ convert @induced_continuous_add (h := elinear.symm.toAddMonoidHom) œÉ _
        ext x
        rw [@Homeomorph.symm_apply_eq, he, ‚Üê LinearEquiv.symm_apply_eq elinear]
        rfl
    ¬∑ rintro ‚ü®h1, h2‚ü©
      use œÑ.induced elinear
      rw [induced_compose]
      refine ‚ü®‚ü®?_, ?_‚ü©, ?_‚ü©
      ¬∑ exact @induced_continuous_smul (f := @id R) (hf := continuous_id)
          (g := elinear.toMulActionHom) (œÑA := œÑ) _ _ _ _ _
      ¬∑ exact @induced_continuous_add (h := elinear.toAddMonoidHom) œÑ _
      ¬∑ nth_rw 2 [‚Üê induced_id (t := œÑ)]
        congr
        ext x
        simp [(he _).symm]

end iso

section function

variable {R : Type*} [œÑR : TopologicalSpace R] [Semiring R]
variable {A : Type*} [AddCommMonoid A] [Module R A] [aA : TopologicalSpace A] [IsActionTopology R A]
variable {B : Type*} [AddCommMonoid B] [Module R B] [aB : TopologicalSpace B] [IsActionTopology R B]

/-- Every `R`-linear map between two `R`-modules with the canonical topology is continuous. -/
@[fun_prop, continuity]
theorem continuous_of_distribMulActionHom (œÜ : A ‚Üí+[R] B) : Continuous œÜ := by
  -- the proof: We know that `+ : B √ó B ‚Üí B` and `‚Ä¢ : R √ó B ‚Üí B` are continuous for the action
  -- topology on `B`, and two earlier theorems (`induced_continuous_smul` and
  -- `induced_continuous_add`) say that hence `+` and `‚Ä¢` on `A` are continuous if `A`
  -- is given the topology induced from `œÜ`. Hence the action topology is finer than
  -- the induced topology, and so the function is continuous.
  rw [isActionTopology R A, continuous_iff_le_induced]
  haveI : ContinuousAdd B := isActionTopology_continuousAdd R B
  exact sInf_le <| ‚ü®induced_continuous_smul (œÜ.toMulActionHom) continuous_id,
    induced_continuous_add œÜ.toAddMonoidHom‚ü©

@[fun_prop, continuity]
theorem continuous_of_linearMap (œÜ : A ‚Üí‚Çó[R] B) : Continuous œÜ :=
  continuous_of_distribMulActionHom œÜ.toDistribMulActionHom

variable (R) in
theorem continuous_neg (C : Type*) [AddCommGroup C] [Module R C] [TopologicalSpace C]
    [IsActionTopology R C] : Continuous (fun a ‚Ü¶ -a : C ‚Üí C) :=
  continuous_of_linearMap (LinearEquiv.neg R).toLinearMap

end function

section surjection

variable {R : Type*} [œÑR : TopologicalSpace R] [Ring R] [TopologicalRing R]
variable {A : Type*} [AddCommGroup A] [Module R A] [aA : TopologicalSpace A] [IsActionTopology R A]
variable {B : Type*} [AddCommGroup B] [Module R B] [aB : TopologicalSpace B] [IsActionTopology R B]

-- Here I need the lemma about how quotients are open so I do need groups
-- because this relies on translates of an open being open
theorem coinduced_of_surjective {œÜ : A ‚Üí‚Çó[R] B} (hœÜ : Function.Surjective œÜ) :
    TopologicalSpace.coinduced œÜ (actionTopology R A) = actionTopology R B := by
  have : Continuous œÜ := continuous_of_linearMap œÜ
  rw [continuous_iff_coinduced_le, isActionTopology R A, isActionTopology R B] at this
  apply le_antisymm this
  have : ContinuousAdd A := isActionTopology_continuousAdd R A
  refine sInf_le ‚ü®?_, ?_‚ü©
  ¬∑ apply @ContinuousSMul.mk R B _ _ (_)
    obtain ‚ü®foo‚ü© : ContinuousSMul R A := inferInstance
    rw [continuous_def] at foo ‚ä¢
    intro U hU
    rw [isOpen_coinduced, ‚Üê isActionTopology R A] at hU
    specialize foo _ hU; clear hU
    rw [‚Üê Set.preimage_comp, show œÜ ‚àò (fun p ‚Ü¶ p.1 ‚Ä¢ p.2 : R √ó A ‚Üí A) =
      (fun p ‚Ü¶ p.1 ‚Ä¢ p.2 : R √ó B ‚Üí B) ‚àò
      (Prod.map id ‚áëœÜ.toAddMonoidHom) by ext; simp, Set.preimage_comp] at foo
    clear! aB -- easiest to just remove topology on B completely now so typeclass inference
    -- never sees it
    convert isOpenMap_of_coinduced (AddMonoidHom.prodMap (AddMonoidHom.id R) œÜ.toAddMonoidHom)
      (_) (_) (_) foo
    ¬∑ -- aesop would do this if `Function.surjective_id : Surjective ‚áë(AddMonoidHom.id R)`
      -- was known by it
      apply (Set.image_preimage_eq _ _).symm
      rw [AddMonoidHom.coe_prodMap, Prod.map_surjective]
      exact ‚ü®Function.surjective_id, by simp_all‚ü©
    ¬∑ -- should `apply continuousprodmap ctrl-space` find `Continuous.prod_map`?
      apply @Continuous.prod_map _ _ _ _ (_) (_) (_) (_) id œÜ continuous_id
      rw [continuous_iff_coinduced_le, isActionTopology R A]
    ¬∑ rw [‚Üê isActionTopology R A]
      exact coinduced_prod_eq_prod_coinduced (AddMonoidHom.id R) œÜ.toAddMonoidHom
       (Function.surjective_id) hœÜ
  ¬∑ apply @ContinuousAdd.mk _ (_)
    obtain ‚ü®bar‚ü© := isActionTopology_continuousAdd R A
    rw [continuous_def] at bar ‚ä¢
    intro U hU
    rw [isOpen_coinduced, ‚Üê isActionTopology R A] at hU
    specialize bar _ hU; clear hU
    rw [‚Üê Set.preimage_comp, show œÜ ‚àò (fun p ‚Ü¶ p.1 + p.2 : A √ó A ‚Üí A) =
      (fun p ‚Ü¶ p.1 + p.2 : B √ó B ‚Üí B) ‚àò
      (Prod.map ‚áëœÜ.toAddMonoidHom ‚áëœÜ.toAddMonoidHom) by ext; simp, Set.preimage_comp] at bar
    clear! aB -- easiest to just remove topology on B completely now
    convert isOpenMap_of_coinduced (AddMonoidHom.prodMap œÜ.toAddMonoidHom œÜ.toAddMonoidHom)
      (_) (_) (_) bar
    ¬∑ aesop
    ¬∑ apply @Continuous.prod_map _ _ _ _ (_) (_) (_) (_) <;>
      ¬∑ rw [continuous_iff_coinduced_le, isActionTopology R A]; rfl
    ¬∑ rw [‚Üê isActionTopology R A]
      exact coinduced_prod_eq_prod_coinduced œÜ œÜ hœÜ hœÜ


end surjection


section prod

variable {R : Type*} [TopologicalSpace R] [Semiring R] [TopologicalSemiring R]
variable {M : Type*} [AddCommMonoid M] [Module R M] [TopologicalSpace M] [IsActionTopology R M]
variable {N : Type*} [AddCommMonoid N] [Module R N] [TopologicalSpace N] [IsActionTopology R N]

instance prod : IsActionTopology R (M √ó N) := by
  constructor
  haveI : ContinuousAdd M := isActionTopology_continuousAdd R M
  haveI : ContinuousAdd N := isActionTopology_continuousAdd R N
  refine le_antisymm ?_ <| sInf_le ‚ü®Prod.continuousSMul, Prod.continuousAdd‚ü©
  rw [‚Üê continuous_id_iff_le]
  rw [show (id : M √ó N ‚Üí M √ó N) =
       (fun abcd ‚Ü¶ abcd.1 + abcd.2 : (M √ó N) √ó (M √ó N) ‚Üí M √ó N) ‚àò
       (fun ab ‚Ü¶ ((ab.1, 0),(0, ab.2))) by
       ext ‚ü®a, b‚ü© <;> simp]
  -- The rest of the proof is a massive fight against typeclass inference, which is desperate
  -- to always put the product topology on M √ó N, when we sometimes want the action topology
  -- (they are equal, but that's exactly what we're proving so we can't assume it yet).
  -- This issue stops the standard continuity tactics from working.
  obtain ‚ü®this‚ü© : @ContinuousAdd (M √ó N) (actionTopology R (M √ó N)) _ :=
    ActionTopology.continuousAdd _ _
  refine @Continuous.comp _ ((M √ó N) √ó (M √ó N)) _ (_) (_) (_) _ _ this ?_
  refine (@continuous_prod_mk _ _ _ (_) (_) (_) _ _).2 ‚ü®?_, ?_‚ü©
  ¬∑ refine @Continuous.comp _ _ _ (_) (_) (_) _ ((LinearMap.inl R M N)) ?_ continuous_fst
    apply @continuous_of_linearMap _ _ _ _ _ _ _ _ _ _ _ (actionTopology _ _) (?_)
    exact @IsActionTopology.mk _ _ _ _ _ (_) rfl
  ¬∑ refine @Continuous.comp _ _ _ (_) (_) (_) _ ((LinearMap.inr R M N)) ?_ continuous_snd
    apply @continuous_of_linearMap _ _ _ _ _ _ _ _ _ _ _ (actionTopology _ _) (?_)
    exact @IsActionTopology.mk _ _ _ _ _ (_) rfl

end prod

section Pi

variable {R : Type*} [œÑR : TopologicalSpace R] [Semiring R] [TopologicalSemiring R]

variable {Œπ : Type*} [Finite Œπ] {A : Œπ ‚Üí Type*} [‚àÄ i, AddCommGroup (A i)]
  [‚àÄ i, Module R (A i)] [‚àÄ i, TopologicalSpace (A i)]
  [‚àÄ i, IsActionTopology R (A i)]

/-
-- ActionTopology.iso.{u_1, u_2, u_3} {R : Type u_1} [œÑR : TopologicalSpace R] [Semiring R] [TopologicalSemiring R]
--   {A : Type u_2} [AddCommMonoid A] [Module R A] [œÑA : TopologicalSpace A] [IsActionTopology R A] {B : Type u_3}
--   [AddCommMonoid B] [Module R B] [œÑB : TopologicalSpace B] (ehomeo : A ‚âÉ‚Çú B) (elinear : A ‚âÉ‚Çó[R] B)
--   (he : ‚àÄ (a : A), ehomeo a = elinear a) [IsActionTopology R A] : IsActionTopology R B

-/
instance pi : IsActionTopology R (‚àÄ i, A i) := by
  induction Œπ using Finite.induction_empty_option
  ¬∑ case of_equiv X Y e _ _ _ _ _ =>
    exact iso (Homeomorph.piCongrLeft e) (LinearEquiv.piCongrLeft R A e) (fun _ ‚Ü¶ rfl)
  ¬∑ apply subsingleton
  ¬∑ case h_option X _ hind _ _ _ _ =>
    let e : Option X ‚âÉ X ‚äï Unit := Equiv.optionEquivSumPUnit X
    refine @iso (ehomeo := Homeomorph.piCongrLeft e.symm)
        (elinear := LinearEquiv.piCongrLeft R A e.symm)
        (he := fun f ‚Ü¶ rfl) _ _ _ _ _ ?_
    apply @iso (ehomeo := (Homeomorph.sumPiEquivProdPi X Unit _).symm)
        (elinear := (LinearEquiv.sumPiEquivProdPi R X Unit _).symm)
        (he := fun f ‚Ü¶ rfl) _ _ _ _ _ ?_
    refine @prod _ _ _ _ _ _ (_) (hind) _ _ _ (_) (?_)
    let œÜ : Unit ‚Üí Option X := fun t ‚Ü¶ e.symm (Sum.inr t)
    exact iso (Homeomorph.pUnitPiEquiv (fun t ‚Ü¶ A (œÜ t))).symm
      (LinearEquiv.pUnitPiEquiv R (fun t ‚Ü¶ A (œÜ t))).symm (fun a ‚Ü¶ rfl)

end Pi

section semiring_bilinear

-- I need rings not semirings here, because ` ChooseBasisIndex.fintype` incorrectly(?) needs
-- a ring instead of a semiring. This should be fixed.
-- I also need commutativity because we don't have bilinear maps for non-commutative rings.
-- **TODO** ask on the Zulip whether this is an issue.
variable {R : Type*} [œÑR : TopologicalSpace R] [CommRing R]

-- similarly these don't need to be groups
variable {A : Type*} [AddCommGroup A] [Module R A] [aA : TopologicalSpace A] [IsActionTopology R A]
variable {B : Type*} [AddCommGroup B] [Module R B] [aB : TopologicalSpace B] [IsActionTopology R B]
variable {C : Type*} [AddCommGroup C] [Module R C] [aC : TopologicalSpace C] [IsActionTopology R C]

theorem Module.continuous_bilinear_of_pi_finite (Œπ : Type*) [Finite Œπ]
    (bil : (Œπ ‚Üí R) ‚Üí‚Çó[R] B ‚Üí‚Çó[R] C) : Continuous (fun ab ‚Ü¶ bil ab.1 ab.2 : ((Œπ ‚Üí R) √ó B ‚Üí C)) := by
  classical
  have foo : (fun fb ‚Ü¶ bil fb.1 fb.2 : ((Œπ ‚Üí R) √ó B ‚Üí C)) =
      (fun fb ‚Ü¶ ‚àë·∂† i, ((fb.1 i) ‚Ä¢ (bil (Pi.single i 1) fb.2) : C)) := by
    ext ‚ü®f, b‚ü©
    simp_rw [‚Üê LinearMap.smul_apply]
    rw [‚Üê LinearMap.finsum_apply]
    congr
    simp_rw [LinearMap.smul_apply, ‚Üê LinearMap.map_smul]
    convert AddMonoidHom.map_finsum bil.toAddMonoidHom _
    ¬∑ ext j
      simp_rw [‚Üê Pi.single_smul, smul_eq_mul, mul_one]
      symm
      -- Is there a missing delaborator? No ‚àë·∂† notation
      change (‚àë·∂† (i : Œπ), Pi.single i (f i)) j = f j
      -- last tactic has no effect
      rw [finsum_apply]
      convert finsum_eq_single (fun i ‚Ü¶ Pi.single i (f i) j) j
        (by simp (config := {contextual := true})) using 1
      simp
    ¬∑ apply Set.toFinite _--(Function.support fun x ‚Ü¶ f x ‚Ä¢ Pi.single x 1)
  rw [foo]
  haveI : ContinuousAdd C := isActionTopology_continuousAdd R C
  apply continuous_finsum (fun i ‚Ü¶ by fun_prop)
  intro x
  use Set.univ
  simp [Set.toFinite _]

-- Probably this can be beefed up to semirings.
theorem Module.continuous_bilinear_of_finite_free [TopologicalSemiring R] [Module.Finite R A] [Module.Free R A]
    (bil : A ‚Üí‚Çó[R] B ‚Üí‚Çó[R] C) : Continuous (fun ab ‚Ü¶ bil ab.1 ab.2 : (A √ó B ‚Üí C)) := by
  let Œπ := Module.Free.ChooseBasisIndex R A
  let hŒπ : Fintype Œπ := Module.Free.ChooseBasisIndex.fintype R A
  let b : Basis Œπ R A := Module.Free.chooseBasis R A
  let elinear : A ‚âÉ‚Çó[R] (Œπ ‚Üí R) := b.equivFun
  let bil' : (Œπ ‚Üí R) ‚Üí‚Çó[R] B ‚Üí‚Çó[R] C := bil.comp elinear.symm.toLinearMap
  have := Module.continuous_bilinear_of_pi_finite Œπ bil'
  have foo : (fun ab ‚Ü¶ (bil ab.1) ab.2 : A √ó B ‚Üí C) = (fun fb ‚Ü¶ bil' fb.1 fb.2) ‚àò
    (fun ab ‚Ü¶ (elinear ab.1, ab.2) : A √ó B ‚Üí (Œπ ‚Üí R) √ó B) := by
    ext ‚ü®a, b‚ü©
    simp [bil']
  rw [foo]
  apply Continuous.comp this
  apply Continuous.prod_mk
  ¬∑ exact continuous_of_linearMap (elinear.toLinearMap ‚àò‚Çó (LinearMap.fst R A B))
  ¬∑ fun_prop

end semiring_bilinear

section ring_bilinear

variable {R : Type*} [œÑR : TopologicalSpace R] [CommRing R] [TopologicalRing R]

variable {A : Type*} [AddCommGroup A] [Module R A] [aA : TopologicalSpace A] [IsActionTopology R A]
variable {B : Type*} [AddCommGroup B] [Module R B] [aB : TopologicalSpace B] [IsActionTopology R B]
variable {C : Type*} [AddCommGroup C] [Module R C] [aC : TopologicalSpace C] [IsActionTopology R C]

-- This needs rings though
theorem Module.continuous_bilinear_of_finite [Module.Finite R A]
    (bil : A ‚Üí‚Çó[R] B ‚Üí‚Çó[R] C) : Continuous (fun ab ‚Ü¶ bil ab.1 ab.2 : (A √ó B ‚Üí C)) := by
  obtain ‚ü®m, f, hf‚ü© := Module.Finite.exists_fin' R A
  let bil' : (Fin m ‚Üí R) ‚Üí‚Çó[R] B ‚Üí‚Çó[R] C := bil.comp f
  have := Module.continuous_bilinear_of_pi_finite (Fin m) bil'
  let œÜ := f.prodMap (LinearMap.id : B ‚Üí‚Çó[R] B)
  have foo : Function.Surjective (LinearMap.id : B ‚Üí‚Çó[R] B) :=
    Function.RightInverse.surjective (congrFun rfl)
  have hœÜ : Function.Surjective œÜ := Function.Surjective.prodMap hf foo
  have := coinduced_of_surjective hœÜ
  rw [isActionTopology R (A √ó B), ‚Üê this, continuous_def]
  intro U hU
  rw [isOpen_coinduced, ‚Üê Set.preimage_comp]
  suffices Continuous ((fun ab ‚Ü¶ (bil ab.1) ab.2) ‚àò œÜ : (Fin m ‚Üí R) √ó B ‚Üí C) by
    rw [continuous_def] at this
    convert this _ hU
    rw [‚Üê prod.isActionTopology']
  rw [show (fun ab ‚Ü¶ (bil ab.1) ab.2 : A √ó B ‚Üí C) ‚àò œÜ = (fun fb ‚Ü¶ bil' fb.1 fb.2) by
    ext ‚ü®a, b‚ü©
    simp [bil', œÜ]]
  apply Module.continuous_bilinear_of_finite_free

end ring_bilinear

section semiring_algebra

-- these shouldn't be rings, they should be semirings
variable (R) [CommRing R] [TopologicalSpace R] [TopologicalRing R]
variable (D : Type*) [Ring D] [Algebra R D] [Module.Finite R D] [Module.Fre(D ‚äó[F] (FiniteAdeleRing (ùìû F) F)) e R D]
variable [TopologicalSpace D] [IsActionTopology R D]

open scoped TensorProduct

@[continuity, fun_prop]
theorem continuous_mul'
    (R) [CommSemiring R] [TopologicalSpace R] [TopologicalSemiring R]
    (D : Type*) [Ring D] [Algebra R D] [Module.Finite R D] [Module.Free R D] [TopologicalSpace D]
    [IsActionTopology R D]: Continuous (fun ab ‚Ü¶ ab.1 * ab.2 : D √ó D ‚Üí D) := by
  letI : TopologicalSpace (D ‚äó[R] D) := actionTopology R _
  haveI : IsActionTopology R (D ‚äó[R] D) := { isActionTopology' := rfl }
  convert Module.continuous_bilinear_of_finite_free <| LinearMap.mul R D

-- this should be about top semirings
def Module.topologicalSemiring : TopologicalSemiring D where
  continuous_add := (isActionTopology_continuousAdd R D).1
  continuous_mul := continuous_mul' R D

end semiring_algebra

section ring_algebra

-- these shouldn't be rings, they should be semirings
variable (R) [CommRing R] [TopologicalSpace R] [TopologicalRing R]
variable (D : Type*) [Ring D] [Algebra R D] [Module.Finite R D]
variable [TopologicalSpace D] [IsActionTopology R D]

open scoped TensorProduct

-- @[continuity, fun_prop]
-- lemma continuous_mul : Continuous (fun ab ‚Ü¶ ab.1 * ab.2 : D √ó D ‚Üí D) := by
--   letI : TopologicalSpace (D ‚äó[R] D) := actionTopology R _
--   haveI : IsActionTopology R (D ‚äó[R] D) := { isActionTopology' := rfl }
--   convert Module.continuous_bilinear_of_finite <| LinearMap.mul R D

-- def Module.topologicalRing : TopologicalRing D where
--   continuous_add := (isActionTopology_continuousAdd R D).1
--   continuous_mul := continuous_mul' R D
--   continuous_neg := continuous_neg R D

end ring_algebra
