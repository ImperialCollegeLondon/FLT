import FLT.Deformation.IsProartinianRing
import FLT.Deformation.IsResidueAlgebra
import FLT.Mathlib.Algebra.Group.Units.Hom

set_option linter.unusedSectionVars false

universe v u

open CategoryTheory Function Limits

namespace Deformation

variable (ğ“ : Type u)
  [CommRing ğ“] [IsLocalRing ğ“] [IsNoetherianRing ğ“]

scoped notation3:max "ğ“´" ğ“ => (IsLocalRing.ResidueField ğ“)

structure BaseCat where
  private mk ::
  carrier : Type v
  [isCommRing : CommRing carrier]
  [isAlgebra : Algebra ğ“ carrier]
  [isLocalRing : IsLocalRing carrier]
  [isLocalHom : IsLocalHom (algebraMap ğ“ carrier)]
  [isResidueAlgebra : IsResidueAlgebra ğ“ carrier]
  [isProartinianRing : IsProartinianRing carrier]

scoped notation3:max "ğ“’" ğ“ => BaseCat ğ“

-- Porting note: typemax hack to fix universe complaints
/-- An alias for `BaseCat.{max uâ‚ uâ‚‚}`, to deal around unification issues.
Since the universe the ring lives in can be inferred, we put that last. -/
@[nolint checkUnivs]
abbrev BaseCatMax.{vâ‚, vâ‚‚, uâ‚} (ğ“ : Type uâ‚) [CommRing ğ“] [IsLocalRing ğ“] [IsNoetherianRing ğ“] :=
  BaseCat.{max vâ‚ vâ‚‚} ğ“

attribute [instance] BaseCat.isCommRing BaseCat.isAlgebra BaseCat.isLocalRing BaseCat.isLocalHom
  BaseCat.isResidueAlgebra BaseCat.isProartinianRing

initialize_simps_projections BaseCat (-isCommRing, -isAlgebra, -isLocalRing, -isLocalHom,
  -isResidueAlgebra, -isProartinianRing)

namespace BaseCat

instance : CoeSort (BaseCat ğ“) (Type v) :=
  âŸ¨BaseCat.carrierâŸ©

attribute [coe] BaseCat.carrier

abbrev of (X : Type v) [CommRing X] [Algebra ğ“ X] [IsLocalRing X] [IsLocalHom (algebraMap ğ“ X)]
  [IsResidueAlgebra ğ“ X] [IsProartinianRing X] : BaseCat.{v} ğ“ :=
  âŸ¨XâŸ©

lemma coe_of (X : Type v) [CommRing X] [Algebra ğ“ X] [IsLocalRing X] [IsLocalHom (algebraMap ğ“ X)]
  [IsResidueAlgebra ğ“ X] [IsProartinianRing X] : (of ğ“ X : Type v) = X := rfl

variable {ğ“} in
/-- The type of morphisms in `BaseCat ğ“`. -/
@[ext]
structure Hom (A B : BaseCat.{v} ğ“) where
  private mk ::
  /-- The underlying algebra map. -/
  hom : A â†’A[ğ“] B
  [isLocalHom : IsLocalHom hom.toRingHom]

attribute [instance] Hom.isLocalHom

-- TODO(jlcontreras): find home
instance ContinuousAlgHom.isLocalHom_id {R A : Type*}
    [CommRing R] [CommRing A] [Algebra R A] [TopologicalSpace A] [IsTopologicalRing A]:
    IsLocalHom (ContinuousAlgHom.id R A).toRingHom :=
  sorry

-- TODO(jlcontreras): find home
instance ContinuousAlgHom.isLocalHom_comp {R A B C : Type*}
    [CommRing R] [Ring A] [Ring B] [Ring C]
    [Algebra R A] [Algebra R B] [Algebra R C]
    [TopologicalSpace A] [TopologicalSpace B] [TopologicalSpace C]
    {g : B â†’A[R] C} {f : A â†’A[R] B}
    [IsLocalHom g.toRingHom] [IsLocalHom f.toRingHom] :
    IsLocalHom (ContinuousAlgHom.comp g f).toRingHom :=
  sorry

instance : Category (BaseCat.{v} ğ“) where
  Hom A B := Hom A B
  id A := âŸ¨ContinuousAlgHom.id ğ“ AâŸ©
  comp f g := âŸ¨ContinuousAlgHom.comp g.hom f.homâŸ©

variable {ğ“} in
/-- Typecheck an `ContinuousAlgHom` as a morphism in `BaseCat`. -/
abbrev ofHom {A B : Type v}
  [CommRing A] [Algebra ğ“ A] [IsLocalRing A] [IsLocalHom (algebraMap ğ“ A)]
  [IsResidueAlgebra ğ“ A] [IsProartinianRing A]
  [CommRing B] [Algebra ğ“ B] [IsLocalRing B] [IsLocalHom (algebraMap ğ“ B)]
  [IsResidueAlgebra ğ“ B] [IsProartinianRing B]
  (f : A â†’A[ğ“] B) [IsLocalHom f.toRingHom]:
    of ğ“ A âŸ¶ of ğ“ B := âŸ¨fâŸ©

instance {A B : BaseCat.{v} ğ“} : CoeFun (A âŸ¶ B) fun _ â†¦ (A â†’ B) where
  coe f := f.hom

------------------------------------------------------------
variable {ğ“}

variable {X Y Z : Type v}
  [CommRing X] [Algebra ğ“ X] [IsLocalRing X] [IsLocalHom (algebraMap ğ“ X)]
  [IsResidueAlgebra ğ“ X] [IsProartinianRing X]
  [CommRing Y] [Algebra ğ“ Y] [IsLocalRing Y] [IsLocalHom (algebraMap ğ“ Y)]
  [IsResidueAlgebra ğ“ Y] [IsProartinianRing Y]
  [CommRing Z] [Algebra ğ“ Z] [IsLocalRing Z] [IsLocalHom (algebraMap ğ“ Z)]
  [IsResidueAlgebra ğ“ Z] [IsProartinianRing Z]

variable {A B C : BaseCat.{v} ğ“}

@[simp]
lemma hom_id : (ğŸ™ A : A âŸ¶ A).hom = ContinuousAlgHom.id ğ“ A := rfl

/- Provided for rewriting. -/
lemma id_apply (a : A) : (ğŸ™ A : A âŸ¶ A) a = a := by simp

@[simp]
lemma hom_comp (f : A âŸ¶ B) (g : B âŸ¶ C) : (f â‰« g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply (f : A âŸ¶ B) (g : B âŸ¶ C) (a : A) :
    (f â‰« g) a = g (f a) := by simp

@[ext]
lemma hom_ext {f g : A âŸ¶ B} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

@[simp]
lemma hom_ofHom (f : X â†’A[ğ“] Y) [IsLocalHom f.toRingHom] : (ofHom f).hom = f := rfl

@[simp]
lemma ofHom_hom (f : A âŸ¶ B) : ofHom (Hom.hom f) = f := rfl

@[simp]
lemma ofHom_id : ofHom (ContinuousAlgHom.id ğ“ X) = ğŸ™ (of ğ“ X) := rfl

@[simp]
lemma ofHom_comp (f : X â†’A[ğ“] Y) (g : Y â†’A[ğ“] Z) [IsLocalHom f.toRingHom] [IsLocalHom g.toRingHom]:
    ofHom (g.comp f) = ofHom f â‰« ofHom g :=
  rfl

lemma ofHom_apply (f : X â†’A[ğ“] Y) [IsLocalHom f.toRingHom] (x : X) : ofHom f x = f x := rfl

@[simp]
lemma inv_hom_apply (e : A â‰… B) (x : A) : e.inv (e.hom x) = x := by
  rw [â† comp_apply]
  simp

@[simp]
lemma hom_inv_apply (e : A â‰… B) (x : B) : e.hom (e.inv x) = x := by
  rw [â† comp_apply]
  simp

-- TODO(jlcontreras): why is ğ“ in BaseCat ğ“. Is it?
-- instance : Inhabited (BaseCat ğ“) := âŸ¨of ğ“ ğ“âŸ©

/-- Forgetting to the underlying type and then building the bundled object returns the original
algebra. -/
@[simps]
def ofSelfIso : of ğ“ A â‰… A where
  hom := ğŸ™ A
  inv := ğŸ™ A

def quotient (a : Ideal A) [Nontrivial (A â§¸ a)] : BaseCat.{v} ğ“ where
  carrier := A â§¸ a
  isCommRing := by infer_instance
  isAlgebra := by infer_instance
  isLocalRing := by infer_instance
  isLocalHom := by
    have h := isLocalHom_of_quotient (algebraMap ğ“ A) a
    simp only [Ideal.Quotient.mk_comp_algebraMap] at h
    exact h
  isResidueAlgebra := by infer_instance
  isProartinianRing := by infer_instance

end BaseCat

variable {ğ“}
variable {X Y : Type u}
  [CommRing X] [Algebra ğ“ X] [IsLocalRing X] [IsLocalHom (algebraMap ğ“ X)]
  [IsResidueAlgebra ğ“ X] [IsProartinianRing X]
  [CommRing Y] [Algebra ğ“ Y] [IsLocalRing Y] [IsLocalHom (algebraMap ğ“ Y)]
  [IsResidueAlgebra ğ“ Y] [IsProartinianRing Y]
variable {A B : BaseCat ğ“}

/-- Build an isomorphism in the category `BaseCat R` from a `ContinuousAlgEquiv` between `Algebra`s. -/
@[simps]
def _root_.ContinuousAlgEquiv.toContinuousAlgebraIso (e : X â‰ƒA[ğ“] Y)
  [IsLocalHom e.toContinuousAlgHom.toRingHom] [IsLocalHom e.symm.toContinuousAlgHom.toRingHom] : BaseCat.of ğ“ X â‰… BaseCat.of ğ“ Y where
  hom := BaseCat.ofHom (e : X â†’A[ğ“] Y)
  inv := BaseCat.ofHom (e.symm : Y â†’A[ğ“] X)

/-- Build a `ContinuousAlgEquiv` from an isomorphism in the category `BaseCat R`. -/
@[simps]
def _root_.CategoryTheory.Iso.toContinuousAlgEquiv (i : A â‰… B) : A â‰ƒA[ğ“] B :=
  {i.hom.hom  with
    toFun := i.hom
    invFun := i.inv
    left_inv := fun x â†¦ by simp
    right_inv := fun x â†¦ by simp
    continuous_toFun := by continuity}

section Noetherian -- Proposition 2.4 of Smit&Lenstra

variable (A : ğ“’ ğ“) [IsNoetherianRing A]

instance noetherian_topology
    : IsAdic (IsLocalRing.maximalIdeal A) := by
  exact IsProartinianRing.noetherian_topology A

instance noetherian_isAdic
    : IsAdicComplete (IsLocalRing.maximalIdeal A) A := by
  exact IsProartinianRing.noetherian_isAdic A

lemma noetherian_continuous (A' : ğ“’ ğ“) (f : A â†’â‚[ğ“] A')
    : Continuous f := by
  exact IsProartinianRing.noetherian_continuous A A' f

end Noetherian

end Deformation
